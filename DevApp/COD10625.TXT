OBJECT Codeunit 10625 E-Invoice Check Serv. Cr. Memo
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO8.00;
  }
  PROPERTIES
  {
    TableNo=5994;
    OnRun=VAR
            EInvoiceCheckCommon@1080000 : Codeunit 10629;
          BEGIN
            CheckCompanyInfo;
            CheckServiceMgtSetup;
            EInvoiceCheckCommon.CheckCurrencyCode("Currency Code","No.","Posting Date");
          END;

  }
  CODE
  {
    VAR
      EInvoiceDocumentEncode@1080001 : Codeunit 10610;
      InvalidPathErr@1080000 : TextConst 'ENU=does not contain a valid path';

    LOCAL PROCEDURE CheckCompanyInfo@1080002();
    VAR
      CompanyInfo@1080000 : Record 79;
    BEGIN
      CompanyInfo.GET;
      CompanyInfo.TESTFIELD(Name);
      CompanyInfo.TESTFIELD(Address);
      CompanyInfo.TESTFIELD(City);
      CompanyInfo.TESTFIELD("Post Code");
      CompanyInfo.TESTFIELD("Country/Region Code");
      EInvoiceDocumentEncode.GetEInvoiceCountryRegionCode(CompanyInfo."Country/Region Code");

      CompanyInfo.TESTFIELD("VAT Registration No.");

      IF CompanyInfo.IBAN = '' THEN
        CompanyInfo.TESTFIELD("Bank Account No.");
      CompanyInfo.TESTFIELD("Bank Branch No.");
    END;

    LOCAL PROCEDURE CheckServiceMgtSetup@1080000();
    VAR
      ServiceMgtSetup@1080000 : Record 5911;
      FileMgt@1080001 : Codeunit 419;
    BEGIN
      ServiceMgtSetup.GET;

      ServiceMgtSetup."E-Invoice Serv. Cr. Memo Path" := DELCHR(ServiceMgtSetup."E-Invoice Serv. Cr. Memo Path",'>','\');
      ServiceMgtSetup.TESTFIELD("E-Invoice Serv. Cr. Memo Path");
      IF NOT FileMgt.DirectoryExistsOnDotNetClient(ServiceMgtSetup."E-Invoice Serv. Cr. Memo Path") THEN
        ServiceMgtSetup.FIELDERROR("E-Invoice Serv. Cr. Memo Path",InvalidPathErr);
    END;

    BEGIN
    END.
  }
}

