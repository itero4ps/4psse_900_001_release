OBJECT Codeunit 11012018 Import IBIS-Trad/Infra TXT
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1210190001 : TextConst 'ENU=File with Budget Lines does not exist: %1';
      Text001@1210190002 : TextConst 'ENU=File with Hours/Factor does not exist: %1';
      Text002@1210190003 : TextConst 'ENU=File with General Data does not exist: %1';
      GenLedgSetup@1210190033 : Record 98;
      ProjBudgetRec@1210190020 : Record 11012001;
      OptBudgetRec@1210190035 : Record 11012503;
      InStreamStrm@1210190005 : InStream;
      BudgetType@1100485007 : Code[3];
      gProjectCde@1100525000 : Code[20];
      IbisVersion@1100485004 : Text[10];
      Txt@1210190006 : Text[1024];
      RecordCode1Txt@1210190008 : Text[8];
      Text003@1210190009 : TextConst 'ENU=Data to be imported is not created with Trad-Version 5.00';
      Text004@1210190011 : TextConst 'ENU=Budget Type is not of traditional Trad Estimate';
      AmntTxt@1210190013 : Text[80];
      NettoTotDec@1210190014 : Decimal;
      BrutoTotDec@1100485003 : Decimal;
      HoursDec@1210190015 : Decimal;
      LaborDec@1210190016 : Decimal;
      MaterialDec@1210190017 : Decimal;
      PlantDec@1210190018 : Decimal;
      SubcDec@1210190019 : Decimal;
      Text005@1210190021 : TextConst 'ENU=Total Labor';
      Text006@1210190022 : TextConst 'ENU=Total Labor %1 does not match imported amount for labor %2';
      Text007@1210190023 : TextConst 'ENU=Total Material';
      Text008@1210190024 : TextConst 'ENU=Total material %1 does not match imported amount for material %2';
      Text009@1210190025 : TextConst 'ENU=Total Subcontracting';
      Text010@1210190026 : TextConst 'ENU=Total Subcontracting %1 does not match imported amount for subcontracting %2';
      Text011@1210190027 : TextConst 'ENU=Total Hours';
      Text012@1210190028 : TextConst 'ENU=Total Hours %1 does not match imported amount for hours %2';
      Text013@1210190029 : TextConst 'ENU=Total Budget';
      Text014@1210190030 : TextConst 'ENU=Total Amount %1 does not match imported amount  %2';
      Text015@1210190031 : TextConst 'ENU=Total Plant';
      Text016@1210190032 : TextConst 'ENU=Total Plant %1 does not match imported amount for plant %2';
      Text017@1210190034 : TextConst 'ENU=Currceny Budget differs from Local Currency';
      Text018@1100485001 : TextConst 'ENU=Data to be imported is not created with Ibis Infra';
      Text019@1100485000 : TextConst 'ENU=Budget Type is not of traditional Trad Estimate';
      Text020@1100485002 : TextConst 'ENU=The Estimate is not calculated';
      Text021@1100485005 : TextConst 'ENU=Version unknown, file can not be read.';
      Text022@1100485006 : TextConst 'ENU=Data to be imported is not created with Trad-Version 6.20';
      SettlementQuantityCde@1100528201 : Code[20];
      FromSettlementQuantity@1100528200 : Boolean;
      SettlementQuantityBudget@1100528202 : Record 11072244;

    PROCEDURE ImportTradTxtBudget@1210190001(lProjectCde@1210190004 : Code[20];lAdjustmentCde@1210190003 : Code[10];lExtensionCde@1210190002 : Code[10];lHouseModelCde@1210190009 : Code[20];lMainGroupCde@1210190008 : Code[20];lGroupCde@1210190007 : Code[20];lSubGroupCde@1210190006 : Code[20];lOptionCde@1210190001 : Code[20];lFolder@1100485002 : Text[250];lFileName@1210190000 : Text[100];lPlotCde@1100485000 : Code[20];lElementOptout@1100485003 : Boolean);
    VAR
      TempBlob@1100525001 : Record 99008535;
      FileMgt@1100525000 : Codeunit 419;
      ImportTradTxt500@1100525003 : XMLport 11012393;
      ImportTradTxt620@1100525004 : XMLport 11012396;
      IStream@1100525002 : InStream;
    BEGIN
      gProjectCde := lProjectCde;
      TestFilesExist(lFolder,lFileName);
      ImportGeneralData(lFolder,lFileName,0);
      IbisVersion := INSSTR(IbisVersion, '.', 2);
      CASE IbisVersion OF
        '5.00':
          BEGIN
            IF FromSettlementQuantity THEN
              ImportTradTxt500.SetDefaultsSettlQuantity(SettlementQuantityCde);
            ImportTradTxt500.DetermineSelection(
              lProjectCde,lAdjustmentCde,lExtensionCde,lHouseModelCde,lMainGroupCde,lGroupCde,
              lSubGroupCde,lOptionCde,lFolder,lFileName,lPlotCde,IbisVersion,lElementOptout);
            FileMgt.BLOBImport4PS(TempBlob, lFolder + 'b' + lFileName + '.txt', FALSE);
            TempBlob.Blob.CREATEINSTREAM(IStream);
            ImportTradTxt500.SETSOURCE(IStream);
            ImportTradTxt500.IMPORT;
          END;
        '6.20'..'6.99':          //* So now the whole version-6 series from 6.20
          BEGIN
            IF FromSettlementQuantity THEN
              ImportTradTxt620.SetDefaultsSettlQuantity(SettlementQuantityCde);
            ImportTradTxt620.DetermineSelection(
              lProjectCde,lAdjustmentCde,lExtensionCde,lHouseModelCde,lMainGroupCde,lGroupCde,
              lSubGroupCde,lOptionCde,lFolder,lFileName,lPlotCde,IbisVersion,BudgetType,lElementOptout);
            FileMgt.BLOBImport4PS(TempBlob, lFolder + 'b' + lFileName + '.txt', FALSE);
            TempBlob.Blob.CREATEINSTREAM(IStream);
            ImportTradTxt620.SETSOURCE(IStream);
            ImportTradTxt620.IMPORT;
          END;
      END;

      IF (lOptionCde = '') AND (SettlementQuantityCde = '') THEN BEGIN
        ProjBudgetRec.SETRANGE("Project No.",lProjectCde);
        ProjBudgetRec.SETRANGE("Adjustment Filter",lAdjustmentCde);
        ProjBudgetRec.SETRANGE("Extension Filter",lExtensionCde);
        ProjBudgetRec.SETRANGE("Option Filter",lOptionCde);
        IF ProjBudgetRec.FINDFIRST THEN BEGIN
          ProjBudgetRec.CALCFIELDS("Total Labor","Total material","Total Subcontracting","Total Plant",
            "Total Hours","Total Amount");
          IF ROUND(ABS(ProjBudgetRec."Total Labor" - LaborDec), 1) > 0 THEN
            StoreMessage(Text005,STRSUBSTNO(Text006,LaborDec,ProjBudgetRec."Total Labor"));
          IF ROUND(ABS(ProjBudgetRec."Total material" - MaterialDec), 1) > 0 THEN
            StoreMessage(Text007,STRSUBSTNO(Text008,MaterialDec,ProjBudgetRec."Total material"));
          IF ROUND(ABS(ProjBudgetRec."Total Subcontracting" - SubcDec), 1) > 0 THEN
            StoreMessage(Text009,STRSUBSTNO(Text010,SubcDec,ProjBudgetRec."Total Subcontracting"));
          IF ROUND(ABS(ProjBudgetRec."Total Plant" - PlantDec), 1) > 0 THEN
            StoreMessage(Text015,STRSUBSTNO(Text016,PlantDec,ProjBudgetRec."Total Plant"));
          IF ROUND(ABS(ProjBudgetRec."Total Hours" - HoursDec), 1) > 0 THEN
            StoreMessage(Text011,STRSUBSTNO(Text012,HoursDec,ProjBudgetRec."Total Hours"));
          IF ROUND(ABS(ProjBudgetRec."Total Amount" - NettoTotDec), 1) > 0 THEN
            StoreMessage(Text013,STRSUBSTNO(Text014,NettoTotDec,ProjBudgetRec."Total Amount"));
        END;
      END;//  ELSE BEGIN
      IF (lOptionCde <> '') AND (SettlementQuantityCde = '') THEN BEGIN
        OptBudgetRec.SETRANGE("Project No.",lProjectCde);
        IF lPlotCde <> '' THEN
          OptBudgetRec.SETRANGE("Plot No.",lPlotCde)
        ELSE
          OptBudgetRec.SETFILTER("Plot No.",'%1','');
        OptBudgetRec.SETRANGE("House Model",lHouseModelCde);
        OptBudgetRec.SETRANGE("Main Group",lMainGroupCde);
        OptBudgetRec.SETRANGE(Group,lGroupCde);
        OptBudgetRec.SETRANGE("Sub Group",lSubGroupCde);
        OptBudgetRec.SETRANGE(Option,lOptionCde);
        IF OptBudgetRec.FINDSET(FALSE, FALSE) THEN BEGIN
          OptBudgetRec.CALCFIELDS("Total Labor","Total Material","Total Subcontracting","Total Plant",
            "Total Hours","Total Amount");
                  IF OptBudgetRec."Total Labor" <> LaborDec THEN
          IF ROUND(ABS(OptBudgetRec."Total Labor" - LaborDec), 1) > 0 THEN
            StoreMessage(Text005,STRSUBSTNO(Text006,LaborDec,OptBudgetRec."Total Labor"));
          IF ROUND(ABS(OptBudgetRec."Total Material" - MaterialDec), 1) > 0 THEN
            StoreMessage(Text007,STRSUBSTNO(Text008,MaterialDec,OptBudgetRec."Total Material"));
          IF ROUND(ABS(OptBudgetRec."Total Subcontracting" - SubcDec), 1) > 0 THEN
            StoreMessage(Text009,STRSUBSTNO(Text010,SubcDec,OptBudgetRec."Total Subcontracting"));
          IF ROUND(ABS(OptBudgetRec."Total Plant" - PlantDec), 1) > 0 THEN
            StoreMessage(Text015,STRSUBSTNO(Text016,PlantDec,OptBudgetRec."Total Plant"));
          IF ROUND(ABS(OptBudgetRec."Total Hours" - HoursDec), 1) > 0 THEN
            StoreMessage(Text011,STRSUBSTNO(Text012,HoursDec,OptBudgetRec."Total Hours"));
          IF ROUND(ABS(OptBudgetRec."Total Amount" - NettoTotDec), 1) > 0 THEN
            StoreMessage(Text013,STRSUBSTNO(Text014,NettoTotDec,OptBudgetRec."Total Amount"));
        END;
      END;

      IF (lOptionCde = '') AND (SettlementQuantityCde <> '') THEN BEGIN
        SettlementQuantityBudget.SETRANGE("Project No.",lProjectCde);
        SettlementQuantityBudget.SETRANGE("Settlement Quantity Code",SettlementQuantityCde);
        IF SettlementQuantityBudget.FINDSET(FALSE, FALSE) THEN BEGIN
          SettlementQuantityBudget.CALCFIELDS("Total Labor","Total Material","Total Subcontracting","Total Plant",
            "Total Hours","Total Amount");
          IF ROUND(ABS(SettlementQuantityBudget."Total Labor" - LaborDec), 1) > 0 THEN
              StoreMessage(Text005,STRSUBSTNO(Text006,LaborDec,SettlementQuantityBudget."Total Labor"));
          IF ROUND(ABS(SettlementQuantityBudget."Total Material" - MaterialDec), 1) > 0 THEN
            StoreMessage(Text007,STRSUBSTNO(Text008,MaterialDec,SettlementQuantityBudget."Total Material"));
          IF ROUND(ABS(SettlementQuantityBudget."Total Subcontracting" - SubcDec), 1) > 0 THEN
            StoreMessage(Text009,STRSUBSTNO(Text010,SubcDec,SettlementQuantityBudget."Total Subcontracting"));
          IF ROUND(ABS(SettlementQuantityBudget."Total Plant" - PlantDec), 1) > 0 THEN
            StoreMessage(Text015,STRSUBSTNO(Text016,PlantDec,SettlementQuantityBudget."Total Plant"));
          IF ROUND(ABS(SettlementQuantityBudget."Total Hours" - HoursDec), 1) > 0 THEN
            StoreMessage(Text011,STRSUBSTNO(Text012,HoursDec,SettlementQuantityBudget."Total Hours"));
          IF ROUND(ABS(SettlementQuantityBudget."Total Amount" - NettoTotDec), 1) > 0 THEN
            StoreMessage(Text013,STRSUBSTNO(Text014,NettoTotDec,SettlementQuantityBudget."Total Amount"));
        END;
      END;
    END;

    PROCEDURE ImportIbisInfraBudget@1100485000(lProjectCde@1210190004 : Code[20];lAdjustmentCde@1210190003 : Code[10];lExtensionCde@1210190002 : Code[10];lOnlyBudgetLines@1100485000 : Boolean;lFolder@1100485001 : Text[250];lFileName@1210190000 : Text[100]);
    VAR
      TempBlob@1100525500 : Record 99008535;
      FileMgt@1100525501 : Codeunit 419;
      ImportIBISInfraBudget@1100525502 : XMLport 11012275;
      IStream@1100525503 : InStream;
    BEGIN
      gProjectCde := lProjectCde;
      TestFilesExist(lFolder,lFileName);
      ImportGeneralData(lFolder,lFileName,1);
      ImportIBISInfraBudget.SetSelections(
        lProjectCde,lAdjustmentCde,lExtensionCde,lOnlyBudgetLines,lFolder,lFileName);
      FileMgt.BLOBImport4PS(TempBlob, lFolder + 'b' + lFileName + '.txt', FALSE);
      TempBlob.Blob.CREATEINSTREAM(IStream);
      ImportIBISInfraBudget.SETSOURCE(IStream);
      ImportIBISInfraBudget.IMPORT;

      ProjBudgetRec.SETRANGE("Project No.",lProjectCde);
      ProjBudgetRec.SETRANGE("Adjustment Filter",lAdjustmentCde);
      ProjBudgetRec.SETRANGE("Extension Filter",lExtensionCde);
      ProjBudgetRec.SETRANGE("Option Filter",'');
      IF ProjBudgetRec.FINDSET(FALSE,FALSE) THEN BEGIN
        //Only Check total amount. Amounts per cost type can be exchanged during import.
        ProjBudgetRec.CALCFIELDS("Total Amount");
        IF ROUND(ABS(ProjBudgetRec."Total Amount" - BrutoTotDec), 1) > 0 THEN
          StoreMessage(Text013,STRSUBSTNO(Text014,BrutoTotDec,ProjBudgetRec."Total Amount"));
      END;
    END;

    PROCEDURE TestFilesExist@1210190002(lFolder@1100485003 : Text[250];lFileName@1210190000 : Text[100]);
    VAR
      FileMgt@1100525000 : Codeunit 419;
      lvFileName1@1100485000 : Text[250];
      lvFileName2@1100485001 : Text[250];
      lvFileName3@1100485002 : Text[250];
    BEGIN
      lvFileName1 := lFolder + 'b' + lFileName + '.txt';
      lvFileName2 := lFolder + 'f' + lFileName + '.txt';
      lvFileName3 := lFolder + 'g' + lFileName + '.txt';
      IF NOT FileMgt.ClientFileExists(lvFileName1) THEN BEGIN
        lvFileName1 := CONVERTSTR(lvFileName1, '\', '/');
        ERROR(STRSUBSTNO(Text000, lvFileName1));
      END;
      IF NOT FileMgt.ClientFileExists(lvFileName2) THEN BEGIN
        lvFileName2 := CONVERTSTR(lvFileName2, '\', '/');
        ERROR(STRSUBSTNO(Text001, lvFileName2));
      END;
      IF NOT FileMgt.ClientFileExists(lvFileName3) THEN BEGIN
        lvFileName3 := CONVERTSTR(lvFileName3, '\', '/');
        ERROR(STRSUBSTNO(Text002, lvFileName3));
      END;
    END;

    PROCEDURE ImportGeneralData@1210190000(lFolder@1100485001 : Text[250];lFileName@1210190000 : Text[100];lFileType@1100485000 : 'TradTxt,IbisInfra');
    VAR
      TempBlob@1100525001 : Record 99008535;
      FileMgt@1100525000 : Codeunit 419;
      lvEOS@1100485002 : Boolean;
    BEGIN
      GenLedgSetup.GET;
      GenLedgSetup.TESTFIELD("LCY Code");

      lvEOS := FALSE;
      IbisVersion := '';
      BudgetType := '';

      FileMgt.BLOBImport4PS(TempBlob, lFolder + 'g' + lFileName + '.txt', FALSE);
      TempBlob.Blob.CREATEINSTREAM(InStreamStrm);

      WHILE (NOT InStreamStrm.EOS()) AND (NOT lvEOS) DO BEGIN
        InStreamStrm.READTEXT(Txt);
        IF (lFileType = lFileType::TradTxt) AND (STRLEN(Txt) > 200) THEN BEGIN
          //* Trad5.00 heeft record-lengte van 126, Trad6.20 heeft lengte van 251 (>200 moet dan wel 6.20 zijn).
          //* Verschil in Trad6.20 is dat er maar 1 regel is die alle informatie bevat (dus niet meer per item een regel).
          lvEOS := TRUE;
          IbisVersion := COPYSTR(Txt,2,3);
          IF (COPYSTR(IbisVersion,1,1) <> '6') OR (COPYSTR(IbisVersion,2,2) < '20') THEN
            ERROR(Text022);  // Versie 6, update 20 (of hoger)
          //* Controle op doorgerekend is vervallen, zijn nu altijd doorgerekend
          IF NOT (COPYSTR(Txt,5,3) IN ['EUR', '', GenLedgSetup."LCY Code"]) THEN  //* Valuta
            ERROR(Text017);
          BudgetType := COPYSTR(Txt,8,3);
          IF NOT (BudgetType IN ['TRA', 'TRU']) THEN  //* Begrotingstype
            ERROR(Text004, BudgetType);
          EVALUATE(HoursDec,ConvertAmnt(COPYSTR(Txt,11,17)));     //* Totaal uren
          EVALUATE(LaborDec,ConvertAmnt(COPYSTR(Txt,48,17)));     //* Totaal arbeid
          EVALUATE(MaterialDec,ConvertAmnt(COPYSTR(Txt,65,17)));  //* Totaal materiaal
          EVALUATE(PlantDec,ConvertAmnt(COPYSTR(Txt,82,17)));     //* Totaal materieel
          EVALUATE(SubcDec,ConvertAmnt(COPYSTR(Txt,99,17)));      //* Totaal onderaanneming
          EVALUATE(NettoTotDec,ConvertAmnt(COPYSTR(Txt,133,17))); //* Netto totaal
        END ELSE BEGIN
          RecordCode1Txt := COPYSTR(Txt,2,8);
          RecordCode1Txt := DELCHR(RecordCode1Txt,'=',' ');
          CASE RecordCode1Txt OF
            'VERSIE':      BEGIN
                             IbisVersion := CopyTextString;
                             IF (lFileType = lFileType::TradTxt) AND (CopyTextString <> '500') THEN
                               ERROR(Text003);
                             IF (lFileType = lFileType::IbisInfra) AND (CopyTextString <> '300') THEN
                               ERROR(Text018);
                           END;
            'BEGRTYPE':    BEGIN
                             IF (lFileType = lFileType::TradTxt) AND (CopyTextString <> '1') THEN
                               ERROR(Text004, CopyTextString);
                             IF (lFileType = lFileType::IbisInfra) AND NOT (CopyTextString IN ['1','8']) THEN
                               ERROR(Text019);
                           END;
            'CALC':        IF CopyTextString <> 'J' THEN
                             ERROR(Text020);
            'NETTOTOT':    EVALUATE(NettoTotDec,ConvertAmnt(COPYSTR(Txt,46,80)));
            'BRUTOTOT':    EVALUATE(BrutoTotDec,ConvertAmnt(COPYSTR(Txt,46,80)));
            'TUREN':       EVALUATE(HoursDec,ConvertAmnt(COPYSTR(Txt,46,80)));
            'TARBE':       EVALUATE(LaborDec,ConvertAmnt(COPYSTR(Txt,46,80)));
            'TMATA':       EVALUATE(MaterialDec,ConvertAmnt(COPYSTR(Txt,46,80)));
            'TMATE':       EVALUATE(PlantDec,ConvertAmnt(COPYSTR(Txt,46,80)));
            'TONDE':       EVALUATE(SubcDec,ConvertAmnt(COPYSTR(Txt,46,80)));
            'CVALUTA':     IF NOT (CopyTextString IN ['EUR', '', GenLedgSetup."LCY Code"]) THEN
                             ERROR(Text017);
          END;
        END;
      END;

      IF IbisVersion = '' THEN
        ERROR(Text021);
    END;

    PROCEDURE ConvertAmnt@1210190013(IAmtTxt@1100485000 : Text[80]) @1210190000 : Text[80];
    BEGIN
      AmntTxt := IAmtTxt;
      IF AmntTxt <> '' THEN BEGIN
        AmntTxt := DELCHR(AmntTxt,'=',' ');
        AmntTxt := CONVERTSTR(AmntTxt,'.',',');
        EXIT(AmntTxt);
      END ELSE
        EXIT('0');
    END;

    PROCEDURE CopyTextString@1210190003() CopyTxt@1210190000 : Text[80];
    BEGIN
      CopyTxt := COPYSTR(Txt,46,80);
      CopyTxt := DELCHR(CopyTxt,'=',' ');
      EXIT(CopyTxt);
    END;

    PROCEDURE StoreMessage@5(lvDesc@11012000 : Text[50];lvMess@11012001 : Text[250]);
    VAR
      ErrorRec@1210190000 : Record 11012051;
      lvError@11012002 : Integer;
    BEGIN
      WITH ErrorRec DO BEGIN
        SETRANGE("User ID", USERID);
        SETRANGE("Source Type", "Source Type"::"Import Budget");
        IF FINDLAST THEN
          lvError := "Line No." + 1
        ELSE
          lvError := 1;

        INIT;
        "User ID" := USERID;
        "Source Type" := "Source Type"::"Import Budget";
        "Project No." := gProjectCde;
        "Line No." := lvError;
        "Error message" := lvMess;
        Description := lvDesc;
        INSERT;
      END;
    END;

    PROCEDURE SetDefaultsSettlQuantity@1100528203(ISettlementQuantityCde@11012000 : Code[20]);
    BEGIN
      FromSettlementQuantity := TRUE;
      SettlementQuantityCde := ISettlementQuantityCde;
    END;

    BEGIN
    END.
  }
}

