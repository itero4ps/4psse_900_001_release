OBJECT Codeunit 11012029 Determine Prognosis Date
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    TableNo=11072003;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      PrognosisRec@11012000 : Record 11012034;

    PROCEDURE DetermineDate@1(VAR ProjectRec@11012000 : Record 11072003);
    VAR
      EndDat@11012001 : Date;
    BEGIN
      PrognosisRec.RESET;
      IF ProjectRec."Single/Main/Sub Project" = ProjectRec."Single/Main/Sub Project"::"Main Project" THEN BEGIN
        PrognosisRec.SETRANGE("Main Project No.", ProjectRec."No.");
        PrognosisRec.SETCURRENTKEY("Main Project No.", "Prognosis Date");
      END ELSE BEGIN
        PrognosisRec.SETRANGE("Project No.",ProjectRec."No.");
        PrognosisRec.SETCURRENTKEY("Project No.", "Prognosis Date");
      END;

      IF ProjectRec.GETFILTER("Period Filter") <> '' THEN BEGIN
        EndDat := ProjectRec.GETRANGEMAX("Period Filter");
        PrognosisRec.SETRANGE("Prognosis Date",0D,EndDat);
      END;

      IF PrognosisRec.FINDLAST THEN
        ProjectRec.SETRANGE("Prognosis Filter",PrognosisRec."Prognosis Date")
      ELSE
        ProjectRec.SETRANGE("Prognosis Filter",0D);
    END;

    PROCEDURE DeterminePreviousDate@2(VAR ProjectRec@11012000 : Record 11072003);
    VAR
      StartDate@11012001 : Date;
      EndDatePrevPeriod@11012002 : Date;
    BEGIN
      StartDate := ProjectRec.GETRANGEMIN("Period Filter");
      IF StartDate > 00000101D THEN
        EndDatePrevPeriod := CALCDATE('<-1D>',StartDate);
      PrognosisRec.RESET;
      IF ProjectRec."Single/Main/Sub Project" = ProjectRec."Single/Main/Sub Project"::"Main Project" THEN BEGIN
        PrognosisRec.SETRANGE("Main Project No.", ProjectRec."No.");
        PrognosisRec.SETCURRENTKEY("Main Project No.", "Prognosis Date");
      END ELSE BEGIN
        PrognosisRec.SETRANGE("Project No.",ProjectRec."No.");
        PrognosisRec.SETCURRENTKEY("Project No.", "Prognosis Date");
      END;

      PrognosisRec.SETRANGE("Prognosis Date",0D,EndDatePrevPeriod);

      IF PrognosisRec.FINDLAST THEN
        ProjectRec.SETRANGE(PrevPrognosisFilter,PrognosisRec."Prognosis Date")
      ELSE
        ProjectRec.SETRANGE(PrevPrognosisFilter,0D);
    END;

    PROCEDURE DetermineBothDates@3(VAR ProjectRec@11012000 : Record 11072003);
    BEGIN
      DetermineDate(ProjectRec);
      DeterminePreviousDate(ProjectRec);
    END;

    BEGIN
    {
      4PS01 JD, 20 feb 2007, Project Cost Control forms and reports must give overview of the costs of a mainproject as well.
    }
    END.
  }
}

