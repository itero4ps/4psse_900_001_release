OBJECT Codeunit 11012032 CreateControlLinesElemCostObj
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    TableNo=11072003;
    OnRun=VAR
            MainProjFilter@1100485001 : Text[30];
            ProjFilter@1100485000 : Text[30];
          BEGIN
            IF NOT GUIALLOWED THEN
              HideWindow := TRUE;

            IF NOT HideWindow THEN
              Window.OPEN(
                Text000 +
                Text001 +
                Text002 +
                Text003 +
                Text004 +
                Text005 +
                Text006);

            PrognLine.SETCURRENTKEY("Main Project No.", "Project No.", Element, "Cost Object");
            IF "Single/Main/Sub Project" = Rec."Single/Main/Sub Project"::"Main Project" THEN BEGIN
              BudgetRec.SETCURRENTKEY("Main Project No.",Element,"Cost Object");
              ProjectLedgerRec.SETCURRENTKEY("Main Project No.",Element,"Global Dimension 2 Code");
              PurchOrderControlLine.SETCURRENTKEY("Main Project No.","Project No.",Element,"Shortcut Dimension 2 Code");
              ItemJnlLine.SETCURRENTKEY("Main Project No."); //this table only contains a few records, performance not a big issue
              ItemSurcharge.SETCURRENTKEY("Main Project No."); //** 4PS 14-06-2010
              HourLine.SETCURRENTKEY("Main Project No.", Element);
              HourSurcharge.SETCURRENTKEY("Main Project No.", Element);
              MainProjFilter := "No.";
              ProjFilter := '';
            END ELSE BEGIN
              BudgetRec.SETCURRENTKEY("Project No.",Element,"Cost Object");
              ProjectLedgerRec.SETCURRENTKEY("Job No.",Element,"Global Dimension 2 Code");
              PurchOrderControlLine.SETCURRENTKEY("Main Project No.","Project No.",Element,"Shortcut Dimension 2 Code");
              ItemJnlLine.SETCURRENTKEY("Main Project No.","Job No.");
              ItemSurcharge.SETCURRENTKEY("Main Project No.","Job No."); //** 4PS 14-06-2010
              HourLine.SETCURRENTKEY("Project No.");
              HourSurcharge.SETCURRENTKEY("Project No.");
              MainProjFilter := "Main Project";
              ProjFilter := "No.";
            END;

            LineCounter := 0;
            BudgetRec.SETFILTER("Project No.",ProjFilter);
            BudgetRec.SETFILTER("Main Project No.",MainProjFilter);
            IF NOT HideWindow THEN
              NoOfLines := BudgetRec.COUNT;
            IF BudgetRec.FINDSET(FALSE,FALSE) THEN
              REPEAT
                LineCounter += 1;
                IF NOT HideWindow THEN
                  Window.UPDATE(1,ROUND(LineCounter/NoOfLines*10000,1));
                CreateCostControlRecord(
                  "No.",BudgetRec.Element,BudgetRec."Cost Object",BudgetRec."Cost Type");
              UNTIL BudgetRec.NEXT = 0;

            LineCounter := 0;
            ProjectLedgerRec.SETFILTER("Job No.",ProjFilter);
            ProjectLedgerRec.SETFILTER("Main Project No.",MainProjFilter);
            ProjectLedgerRec.SETFILTER("Global Dimension 2 Code",'<>%1','');
            ProjectLedgerRec.SETFILTER("Cost Type",'%1..%2',ProjectLedgerRec."Cost Type"::Labor,ProjectLedgerRec."Cost Type"::Sundry);
            IF NOT HideWindow THEN
              NoOfLines := ProjectLedgerRec.COUNT;
            IF ProjectLedgerRec.FINDSET(FALSE,FALSE) THEN
              REPEAT
                LineCounter += 1;
                IF NOT HideWindow THEN
                  Window.UPDATE(2,ROUND(LineCounter/NoOfLines*10000,1));
                CreateCostControlRecord(
                  "No.",ProjectLedgerRec.Element,ProjectLedgerRec."Global Dimension 2 Code",ProjectLedgerRec."Cost Type");
              UNTIL ProjectLedgerRec.NEXT = 0;

            LineCounter := 0;
            PurchOrderControlLine.SETFILTER("Project No.",ProjFilter);
            PurchOrderControlLine.SETFILTER("Main Project No.",MainProjFilter);
            PurchOrderControlLine.SETFILTER("Shortcut Dimension 2 Code",'<>%1','');
            PurchOrderControlLine.SETFILTER("Cost Type",'%1..%2',
              PurchOrderControlLine."Cost Type"::Labor,PurchOrderControlLine."Cost Type"::Sundry);
            IF NOT HideWindow THEN
              NoOfLines := PurchOrderControlLine.COUNT;
            IF PurchOrderControlLine.FINDSET(FALSE,FALSE) THEN
              REPEAT
                LineCounter += 1;
                IF NOT HideWindow THEN
                  Window.UPDATE(3,ROUND(LineCounter/NoOfLines*10000,1));
                CreateCostControlRecord(
                  "No.",PurchOrderControlLine.Element,PurchOrderControlLine."Shortcut Dimension 2 Code",
                  PurchOrderControlLine."Cost Type"-1);
              UNTIL PurchOrderControlLine.NEXT = 0;

            LineCounter := 0;
            ItemJnlLine.SETFILTER("Job No.",ProjFilter);
            ItemJnlLine.SETFILTER("Main Project No.",MainProjFilter);
            ItemJnlLine.SETFILTER("Shortcut Dimension 2 Code",'<>%1','');
            ItemJnlLine.SETFILTER("Cost Type",'%1..%2',ItemJnlLine."Cost Type"::Labor,ItemJnlLine."Cost Type"::Sundry);
            IF NOT HideWindow THEN
              NoOfLines := ItemJnlLine.COUNT;
            IF ItemJnlLine.FINDSET(FALSE,FALSE) THEN
              REPEAT
                LineCounter += 1;
                IF NOT HideWindow THEN
                  Window.UPDATE(4,ROUND(LineCounter/NoOfLines*10000,1));
                CreateCostControlRecord(
                  "No.",ItemJnlLine.Element,ItemJnlLine."Shortcut Dimension 2 Code",ItemJnlLine."Cost Type"-1);
              UNTIL ItemJnlLine.NEXT = 0;

            //** 4PS 14-06-2010 sn
            LineCounter := 0;
            ItemSurcharge.SETFILTER("Job No.",ProjFilter);
            ItemSurcharge.SETFILTER("Main Project No.",MainProjFilter);
            ItemSurcharge.SETFILTER("Shortcut Dimension 2 Code",'<>%1','');
            ItemSurcharge.SETFILTER("Cost Type",'%1..%2',ItemSurcharge."Cost Type"::Labor,ItemSurcharge."Cost Type"::Sundry);
            IF NOT HideWindow THEN
              NoOfLines := ItemSurcharge.COUNT;
            IF ItemSurcharge.FINDSET(FALSE,FALSE) THEN
              REPEAT
                LineCounter += 1;
                IF NOT HideWindow THEN
                  Window.UPDATE(4,ROUND(LineCounter/NoOfLines*10000,1));
                CreateCostControlRecord(
                  "No.",ItemSurcharge.Element,ItemSurcharge."Shortcut Dimension 2 Code",ItemSurcharge."Cost Type"-1);
              UNTIL ItemSurcharge.NEXT = 0;
            //** 4PS 14-06-2010 en

            LineCounter := 0;
            HourLine.SETFILTER("Project No.", ProjFilter);
            HourLine.SETFILTER("Main Project No.", MainProjFilter);
            HourLine.SETFILTER("Cost Object",'<>%1','');
            HourLine.SETFILTER("Cost Type",'%1..%2',HourLine."Cost Type"::Labor,ItemJnlLine."Cost Type"::Sundry);
            IF NOT HideWindow THEN
              NoOfLines := HourLine.COUNT;
            IF HourLine.FINDSET(FALSE,FALSE) THEN
              REPEAT
                LineCounter += 1;
                IF NOT HideWindow THEN
                  Window.UPDATE(5,ROUND(LineCounter/NoOfLines*10000,1));
                CreateCostControlRecord(
                  "No.",HourLine.Element,HourLine."Cost Object",HourLine."Cost Type");
              UNTIL HourLine.NEXT = 0;

            LineCounter := 0;
            HourSurcharge.SETFILTER("Project No.", ProjFilter);
            HourSurcharge.SETFILTER("Main Project No.", MainProjFilter);
            HourSurcharge.SETFILTER("Cost Object",'<>%1','');
            HourSurcharge.SETFILTER("Cost Type",'%1..%2',HourSurcharge."Cost Type"::Labor,ItemJnlLine."Cost Type"::Sundry);
            IF NOT HideWindow THEN
              NoOfLines := HourSurcharge.COUNT;
            IF HourSurcharge.FINDSET(FALSE,FALSE) THEN
              REPEAT
                LineCounter += 1;
                IF NOT HideWindow THEN
                  Window.UPDATE(5,ROUND(LineCounter/NoOfLines*10000,1));
                CreateCostControlRecord(
                  "No.",HourSurcharge.Element,HourSurcharge."Cost Object",HourSurcharge."Cost Type");
              UNTIL HourSurcharge.NEXT = 0;

            LineCounter := 0;
            PrognLine.SETFILTER("Project No.", ProjFilter);
            PrognLine.SETFILTER("Main Project No.", MainProjFilter);
            PrognLine.SETFILTER("Cost Object",'<>%1','');
            PrognLine.SETFILTER("Cost Type",'%1..%2',PrognLine."Cost Type"::Labor,ItemJnlLine."Cost Type"::Sundry);
            IF NOT HideWindow THEN
              NoOfLines := PrognLine.COUNT;
            IF PrognLine.FINDSET(FALSE,FALSE) THEN
              REPEAT
                LineCounter += 1;
                IF NOT HideWindow THEN
                  Window.UPDATE(6,ROUND(LineCounter/NoOfLines*10000,1));
                CreateCostControlRecord(
                  "No.",PrognLine.Element,PrognLine."Cost Object",PrognLine."Cost Type");
              UNTIL PrognLine.NEXT = 0;


            //Delete Cost Control if source is nog longer present
            LineCounter := 0;
            CostControlRec.RESET;
            CostControlRec.SETRANGE("Project No.","No.");
            IF NOT HideWindow THEN
              NoOfLines := CostControlRec.COUNT;
            IF CostControlRec.FINDSET(TRUE,FALSE) THEN
              REPEAT
                LineCounter += 1;
                IF NOT HideWindow THEN
                  Window.UPDATE(7,ROUND(LineCounter/NoOfLines*10000,1));
                CostControlRec.CALCFIELDS("Single/Main/Sub Project");
                IF CostControlRec."Single/Main/Sub Project" <> CostControlRec."Single/Main/Sub Project"::"Main Project" THEN
                  CostControlRec.SETFILTER("Project Filter", "No.")
                ELSE
                  CostControlRec.SETFILTER("Project Filter", '');
                CostControlRec.CALCFIELDS(
                  "Present in Budget","Present in Project Ledger",
                  "Present in Purchase Lines","Present in Item Jnl Lines",
                  "Present in Hour Lines", "Present in Hour Surcharges",
                  "Present in Prognosis Lines");
                CostControlRec.CALCFIELDS("Present in Item Surcharges");  //** 4PS 14-06-2010
                IF NOT CostControlRec."Present in Budget"  AND
                   NOT CostControlRec."Present in Project Ledger" AND
                   NOT CostControlRec."Present in Purchase Lines" AND
                   NOT CostControlRec."Present in Item Jnl Lines" AND
                   NOT CostControlRec."Present in Item Surcharges" AND  //** 4PS 14-06-2010
                   NOT CostControlRec."Present in Hour Lines" AND
                   NOT CostControlRec."Present in Hour Surcharges" AND
                   NOT CostControlRec."Present in Prognosis Lines"
                THEN
                  CostControlRec.DELETE;
              UNTIL CostControlRec.NEXT = 0;

            CreateCostControlProjElem(Rec);

            IF NOT HideWindow THEN
              Window.CLOSE;
          END;

  }
  CODE
  {
    VAR
      Text000@11012010 : TextConst 'ENU=Budget Lines           @1@@@@@@@@@@@@@\';
      Text001@11012011 : TextConst 'ENU=Project Ledger Entries @2@@@@@@@@@@@@@\';
      Text002@11012012 : TextConst 'ENU=Purchase Lines         @3@@@@@@@@@@@@@\';
      Text003@1210190004 : TextConst 'ENU=Item Journal Lines     @4@@@@@@@@@@@@@\';
      Text004@1210190005 : TextConst 'ENU=Hour Lines             @5@@@@@@@@@@@@@\';
      Text005@1100525001 : TextConst 'ENU=Prognosis Lines        @6@@@@@@@@@@@@@\';
      Text006@11012013 : TextConst 'ENU=Final Check            @7@@@@@@@@@@@@@';
      DimValRec@11012001 : Record 349;
      BudgetRec@11012002 : Record 11012001;
      ProjectLedgerRec@11012003 : Record 11072005;
      ProjElemRec@11012004 : Record 11012010;
      CostControlRec@11012005 : Record 11012050;
      CostControlProjElemRec@1210190001 : Record 11012048;
      PurchOrderControlLine@11012006 : Record 11020221;
      ItemJnlLine@1210190000 : Record 83;
      ItemSurcharge@1100525002 : Record 11020585;
      HourLine@1210190003 : Record 11012039;
      HourSurcharge@1100485000 : Record 11020308;
      PrognLine@1100525000 : Record 11012035;
      Window@11012007 : Dialog;
      NoOfLines@11012008 : Integer;
      LineCounter@11012009 : Integer;
      DimMgt@11012014 : Codeunit 408;
      HideWindow@1210190002 : Boolean;

    PROCEDURE CreateCostControlRecord@2(JobNo@11012000 : Code[20];ElemCode@11012001 : Code[20];CostObjCode@11012002 : Code[20];CostType@11012003 : 'Labor,Material,Subcontracting,Plant,Sundry,Revenue');
    BEGIN
      WITH CostControlRec DO BEGIN
        IF ("Project No." = JobNo) AND
           (Element = ElemCode) AND
           ("Cost Object" = CostObjCode) AND
           ("Cost Type" = CostType)
        THEN
          EXIT;

        DimMgt.GetDimValueRec(2, CostObjCode, DimValRec, FALSE, '');
        IF DimValRec.Code <> '' THEN
          CostType := DimValRec."Cost Type";
        SETCURRENTKEY("Project No.",Element,"Cost Type","Cost Object");
        SETRANGE("Project No.",JobNo);
        SETRANGE(Element, ElemCode);
        SETRANGE("Cost Object", CostObjCode);
        SETRANGE("Cost Type", CostType);
        IF NOT FINDFIRST THEN BEGIN
          INIT;
          "Project No." := JobNo;
          Element := ElemCode;
          "Cost Object" :=  CostObjCode;
          "Unit Cost Object" := DimValRec."Unit of Measure";
          "Cost Type" := CostType;
          "Cost Component" := DimValRec."Cost Component";
          INSERT(TRUE);
        END ELSE
          IF "Cost Component" <> DimValRec."Cost Component" THEN BEGIN
            "Cost Component" := DimValRec."Cost Component";
            MODIFY;
          END;
      END;
    END;

    PROCEDURE CreateCostControlProjElem@1210190000(ProjRec@1210190000 : Record 11072003);
    VAR
      lvSubProjRec@1100485000 : Record 11072003;
      TempControlProjElem@1210190001 : TEMPORARY Record 11012048;
    BEGIN
      WITH CostControlProjElemRec DO BEGIN
        SETRANGE("Project No.", ProjRec."No.");
        IF FINDSET THEN
          REPEAT
            TempControlProjElem := CostControlProjElemRec;
            TempControlProjElem.INSERT;
          UNTIL NEXT = 0;

        IF ProjRec."Single/Main/Sub Project" = ProjRec."Single/Main/Sub Project"::"Main Project" THEN
          lvSubProjRec.SETRANGE("Main Project", ProjRec."No.")
        ELSE
          lvSubProjRec.SETRANGE("No.", ProjRec."No.");
        IF lvSubProjRec.FINDSET THEN BEGIN
          REPEAT
            ProjElemRec.SETRANGE("Project No.", lvSubProjRec."No.");
            IF ProjElemRec.FINDSET(FALSE,FALSE) THEN
              REPEAT
                IF NOT GET(ProjRec."No.", ProjElemRec.Element) THEN BEGIN
                  INIT;
                  "Project No." := ProjRec."No.";
                  Element := ProjElemRec.Element;
                  Description := ProjElemRec.Description;
                  "Description 2" := ProjElemRec."Description 2";
                  INSERT(TRUE);
                END ELSE BEGIN
                  IF (Description <> ProjElemRec.Description) OR
                     ("Main Project No." <> ProjRec."Main Project") THEN BEGIN
                    Description := ProjElemRec.Description;
                    "Main Project No." := ProjRec."Main Project";
                    MODIFY;
                  END;
                  IF ("Description 2" <> ProjElemRec."Description 2") THEN BEGIN
                    "Description 2" := ProjElemRec."Description 2";
                    MODIFY;
                  END;
                END;
                TempControlProjElem := CostControlProjElemRec;
                IF TempControlProjElem.DELETE THEN;
              UNTIL ProjElemRec.NEXT = 0;
          UNTIL lvSubProjRec.NEXT = 0;
        END;

        IF TempControlProjElem.FINDSET THEN
          REPEAT
            CostControlProjElemRec := TempControlProjElem;
            CostControlProjElemRec.DELETE;
          UNTIL TempControlProjElem.NEXT = 0;

      END;
    END;

    PROCEDURE SetHideWindow@1210190002();
    BEGIN
      HideWindow := TRUE;
    END;

    BEGIN
    {
      4PS01 JD, 19 feb 2007, Project Cost Control forms and reports must give overview of the costs of a mainproject as well.
      4PS02 JD, call 10019, 8 okt 2007, Restriction that element structure of subproject must be same as on mainproject is removed.
    }
    END.
  }
}

