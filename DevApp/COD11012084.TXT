OBJECT Codeunit 11012084 WhiteVision Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TiffViewer@1100528308 : DotNet "'_4PS.Tiff.Viewer.Nav, Version=1.23.0.0, Culture=neutral, PublicKeyToken=284c6828aa038142'._4PS.Tiff.Viewer.Nav.NavToWhiteVisionIntegration";
      FileMgt@1100525000 : Codeunit 419;
      ValueReturned@1100528300 : Boolean;
      Text000@1100528309 : TextConst 'ENU=WhiteVision viewer window is still present.';
      Text001@1100528310 : TextConst 'ENU=WhiteVision viewer can not be started.\\(File %1 can not be loaded)';
      SupplyingCompany@1100528400 : Text[30];

    PROCEDURE Start@1100528303();
    BEGIN
      IF (NOT ISNULL(TiffViewer)) THEN
        Stop;

      IF (NOT ISNULL(TiffViewer)) THEN
        ERROR(Text000);

      TiffViewer := TiffViewer.NavToWhiteVisionIntegration;

      TiffViewer.Start;
    END;

    PROCEDURE Stop@1100528304();
    BEGIN
      IF (NOT ISNULL(TiffViewer)) THEN BEGIN
        TiffViewer.Stop;
        CLEAR(TiffViewer);
      END;
    END;

    PROCEDURE ActivateField@1100528302(FieldName@1100528301 : Text[30];FieldType@1100528300 : 'Text,Decimal,Date,Money,RegEx') : Text[250];
    BEGIN
      ValueReturned := FALSE;
      EXIT(TiffViewer.ActivateField(FieldName, FieldType));
    END;

    PROCEDURE LoadDocuments@1100528300(DocNo@1100528300 : Code[20];ViewOnly@1100528301 : Boolean);
    VAR
      PurchasesPayablesSetup@1100528303 : Record 312;
      FileName@1100528304 : Text[250];
      MetaFileName@1100528302 : Text[250];
    BEGIN
      IF SupplyingCompany <> '' THEN
        PurchasesPayablesSetup.CHANGECOMPANY(SupplyingCompany);

      PurchasesPayablesSetup.GET;

      FileName := STRSUBSTNO('%1%2.%3',
        PurchasesPayablesSetup."File Location Scanned Invoices",
        DocNo,
        PurchasesPayablesSetup."File Extension Scanned Inv");

      IF (NOT FileMgt.ClientFileExists(FileName)) THEN BEGIN
        Stop;
        ERROR(Text001, FileName);
      END;

      IF (NOT ViewOnly) THEN BEGIN
        MetaFileName := COPYSTR(FileName, 1, STRLEN(FileName)-3) + 'xml';
        IF (NOT FileMgt.ClientFileExists(MetaFileName)) THEN BEGIN
          Stop;
          ERROR(Text001, MetaFileName);
        END;
      END;

      Start;
      TiffViewer.LoadDocument(FileName, FALSE);

      IF (NOT ViewOnly) THEN
        TiffViewer.LoadMetaDataFromFile(MetaFileName);
    END;

    PROCEDURE ShowDocument@1100525000();
    BEGIN
      TiffViewer.ShowDocument;
    END;

    PROCEDURE SetSupplyingCompany@1100528400(SupplyingCompany2@1100528400 : Text[30]);
    BEGIN
      SupplyingCompany := SupplyingCompany2;
    END;

    BEGIN
    END.
  }
}

