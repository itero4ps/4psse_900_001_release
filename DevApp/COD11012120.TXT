OBJECT Codeunit 11012120 Graphical Work Order Planning
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            StartGraphicalWorkOrderPlanning();
          END;

  }
  CODE
  {

    PROCEDURE StartGraphicalWorkOrderPlanning@1100525000();
    VAR
      CompanyPlanGroup@1100528601 : Record 11229300;
      OperatingSystemMgt@1100525000 : Codeunit 11012364;
      BaseClientURI@1100409000 : Text[250];
      CurrentCompanyPlanGroup@1100528602 : Code[10];
      GWPArguments@1100525001 : Text;
      Hostname@1100525002 : Text;
      NSTInstanceName@1100525003 : Text;
      Uri@1100525004 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
    BEGIN
      CurrentCompanyPlanGroup := CompanyPlanGroup.GetCurrentCompanyPlanGroupCode;
      CompanyPlanGroup.GET(CurrentCompanyPlanGroup);
      BaseClientURI := GETURL(CLIENTTYPE::Windows); //this results in something like dynamicsnav://serverName:portno/instanceName/

      IF CompanyPlanGroup."Auto. Determine Server Address" THEN
          Hostname := GetServerName
        ELSE
          Hostname := CompanyPlanGroup."Server Address";

      GWPArguments := STRSUBSTNO('Server:%1 WebserviceODataPort:%2 WebserviceSoapPort:%3 Instance:%4 CompanyPlanGroup:%5 BaseClientURI:%6 TenantID:%7',
          Hostname, CompanyPlanGroup."OData Service Port", CompanyPlanGroup."SOAP Service Port", CompanyPlanGroup."Instance Name",
          CompanyPlanGroup.Code, BaseClientURI, TENANTID);

      OperatingSystemMgt.ClientShell(GetGWPInstallDir, GWPArguments, FALSE);
    END;

    LOCAL PROCEDURE GetGWPInstallDir@1100525002() : Text;
    VAR
      baseKey@1100525003 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.Microsoft.Win32.RegistryKey" RUNONCLIENT;
      gwpKey@1100525002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.Microsoft.Win32.RegistryKey" RUNONCLIENT;
      registryHiveEnum@1100525001 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.Microsoft.Win32.RegistryHive" RUNONCLIENT;
      registryViewEnum@1100525000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.Microsoft.Win32.RegistryView" RUNONCLIENT;
      Text001@1100525004 : TextConst 'ENU=GWP is not installed on this client.';
    BEGIN
      baseKey := baseKey.OpenBaseKey(registryHiveEnum.LocalMachine, registryViewEnum.Registry32); //Registry32 stuff is needed to get the key from the 32Bits registry on 64bit machines
      gwpKey := baseKey.OpenSubKey('SOFTWARE\4PS\4PS GWP\', FALSE);
      IF ISNULL(gwpKey) THEN
        ERROR(Text001);
      EXIT(gwpKey.GetValue('InstallDir'));
    END;

    PROCEDURE GetServerName@1100525001() : Text;
    VAR
      Uri@1100525000 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
    BEGIN
      EXIT(FORMAT(Uri.Uri(GETURL(CLIENTTYPE::Windows)).Host));
    END;

    BEGIN
    END.
  }
}

