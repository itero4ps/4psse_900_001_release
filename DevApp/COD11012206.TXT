OBJECT Codeunit 11012206 Hours Consent Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            Consent(TRUE);
          END;

  }
  CODE
  {
    VAR
      Text000@1100485002 : TextConst 'ENU=You have %1 %2s waiting for consent.\View %2s?';
      Text001@1100528400 : TextConst 'ENU=%1 must be set up in %2 or %3.';
      Text009@1100485001 : TextConst 'ENU=No %1s present.';

    PROCEDURE Consent@1100485002(RunFromMenu@1100485007 : Boolean);
    VAR
      HumanResourcesSetup@1100528400 : Record 5218;
      SiteMngmntHoursSetup@1100528402 : Record 11012355;
      HoursConsentLine@1100485002 : Record 11020437;
      ConsentFilterString@1100525000 : Text[1000];
      ConsentHours@1100528401 : Boolean;
    BEGIN
      IF HumanResourcesSetup.GET THEN
        IF HumanResourcesSetup."Consent Hours" THEN
          ConsentHours := TRUE;

      IF NOT ConsentHours THEN
        IF SiteMngmntHoursSetup.GET THEN
          IF SiteMngmntHoursSetup."Consent Hours" THEN
            ConsentHours := TRUE;

      IF NOT RunFromMenu AND NOT ConsentHours THEN
        EXIT;

      IF RunFromMenu AND NOT ConsentHours THEN
        ERROR(
          Text001, HumanResourcesSetup.FIELDCAPTION("Consent Hours"),
          HumanResourcesSetup.TABLECAPTION, SiteMngmntHoursSetup.TABLECAPTION);

      BuildFilterString(ConsentFilterString); // 15-06-2010 ConsentFilterString is new

      HoursConsentLine.SETCURRENTKEY(Consenter, "Serial Number");
      HoursConsentLine.SETFILTER(Consenter, ConsentFilterString);
      HoursConsentLine.SETRANGE(LineType, HoursConsentLine.LineType::Main);
      HoursConsentLine.SETRANGE(Consent, FALSE);
      HoursConsentLine.SETRANGE(Denied, FALSE);
      IF HoursConsentLine.FINDSET THEN BEGIN
        IF NOT RunFromMenu THEN BEGIN
          IF CONFIRM(
            STRSUBSTNO(Text000, HoursConsentLine.COUNT, HoursConsentLine.TABLECAPTION),
            TRUE)
          THEN
            PAGE.RUN(PAGE::"Hours Consent Lines", HoursConsentLine);
        END ELSE
          PAGE.RUN(PAGE::"Hours Consent Lines", HoursConsentLine)
      END ELSE
        IF RunFromMenu THEN
          MESSAGE(Text009, HoursConsentLine.TABLECAPTION);
    END;

    PROCEDURE BuildFilterString@1100485012(VAR ConsentFilterString@1100485000 : Text[1000]);
    VAR
      HoursConsenter@1100528400 : Record 11020438;
    BEGIN
      ConsentFilterString := USERID;
      HoursConsenter.SETRANGE("User ID Substitute", USERID);
      HoursConsenter.SETRANGE("Substitute Active", TRUE);
      IF HoursConsenter.FINDSET THEN
        REPEAT
          ConsentFilterString := ConsentFilterString + '|' + HoursConsenter."User ID";
        UNTIL HoursConsenter.NEXT=0;
    END;

    LOCAL PROCEDURE GetTempHoursConsentDataSet@1100409000(VAR OTmpHoursConsentLine@1100409000 : Record 11020437);
    VAR
      TmpHoursConsentLine@1100409001 : TEMPORARY Record 11020437;
      PurchasesPayablesSetup@1100409002 : Record 312;
      HoursConsentLine@1100409004 : Record 11020437;
      ConsentFilterString@1100409003 : Text[1000];
    BEGIN
      TmpHoursConsentLine.COPY(OTmpHoursConsentLine, TRUE);
      OTmpHoursConsentLine.DELETEALL;

      IF NOT PurchasesPayablesSetup.GET THEN
        EXIT;
      IF NOT (PurchasesPayablesSetup."Use Consent Advice Orders" OR
              PurchasesPayablesSetup."Use Consent Purchase Orders" OR
              PurchasesPayablesSetup."Use Consent Purchase Invoices")
      THEN
        EXIT;

      BuildFilterString(ConsentFilterString);
      HoursConsentLine.SETCURRENTKEY(Consenter, "Serial Number");
      HoursConsentLine.SETFILTER(Consenter, ConsentFilterString);
      HoursConsentLine.SETRANGE(LineType, HoursConsentLine.LineType::Main);
      HoursConsentLine.SETRANGE(Consent, FALSE);
      HoursConsentLine.SETRANGE(Denied, FALSE);
      IF HoursConsentLine.FINDSET THEN BEGIN
        REPEAT
          OTmpHoursConsentLine := HoursConsentLine;
          OTmpHoursConsentLine.INSERT;
        UNTIL HoursConsentLine.NEXT = 0;
      END;
    END;

    PROCEDURE GetNoOfHoursConsentLines@1100528600() : Integer;
    VAR
      TmpHoursConsentLine@1100528600 : TEMPORARY Record 11020437;
    BEGIN
      GetTempHoursConsentDataSet(TmpHoursConsentLine);
      EXIT(TmpHoursConsentLine.COUNT);
    END;

    PROCEDURE ShowHoursConsentLines@1100528601();
    VAR
      TmpHoursConsentLine@1100528600 : TEMPORARY Record 11020437;
    BEGIN
      GetTempHoursConsentDataSet(TmpHoursConsentLine);
      PAGE.RUN(PAGE::"Hours Consent Lines", TmpHoursConsentLine);
    END;

    BEGIN
    END.
  }
}

