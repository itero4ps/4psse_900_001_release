OBJECT Codeunit 11012269 Standard Role Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Window@1100525004 : Dialog;
      Progress@1100525003 : Integer;
      NoOfRecords@1100525002 : Integer;
      Counter@1100525001 : Integer;
      LastUpdateTime@1100525000 : Time;
      Text000@1100525005 : TextConst 'ENU=Proces %1 @1@@@@@@@@@@@@@';

    PROCEDURE UpdateStandardRoles@1210190036();
    VAR
      StandardRoleCheckLine@1210190000 : Record 11020657;
    BEGIN
      IF StandardRoleCheckLine.FINDSET(TRUE,FALSE) THEN BEGIN
        LastUpdateTime := TIME;
        Window.OPEN(STRSUBSTNO(Text000,StandardRoleCheckLine.TABLECAPTION));
        NoOfRecords := StandardRoleCheckLine.COUNT;
        Counter := 0;
        REPEAT
          CASE StandardRoleCheckLine.Action OF
            StandardRoleCheckLine.Action::Update:
              BEGIN
                CASE StandardRoleCheckLine."Source TableID" OF
                  2000000004: UpdatePermissionSet(StandardRoleCheckLine);
                  2000000005: UpdatePermission(StandardRoleCheckLine);
                  11020570: UpdateRoleGroup(StandardRoleCheckLine);
                  11020571: UpdateRoleGroupRole(StandardRoleCheckLine);
                END;
              END;
            StandardRoleCheckLine.Action::Delete:
              BEGIN
                CASE StandardRoleCheckLine."Source TableID" OF
                  2000000004: DeletePermissionSet(StandardRoleCheckLine);
                  2000000005: DeletePermission(StandardRoleCheckLine);
                  11020570: DeleteRoleGroup(StandardRoleCheckLine);
                  11020571: DeleteRoleGroupRole(StandardRoleCheckLine);
                END;
              END;
          END;
          Counter := Counter + 1;
          UpdateProgressWindow(Counter);
        UNTIL StandardRoleCheckLine.NEXT = 0;
        Window.CLOSE;
      END;

      CLEAR(StandardRoleCheckLine);
      StandardRoleCheckLine.DELETEALL;
    END;

    PROCEDURE UpdatePermissionSet@1210190013("Standard Role Check Line"@1210190000 : Record 11020657);
    VAR
      PermissionSet@1210190001 : Record 2000000004;
    BEGIN
      IF NOT PermissionSet.GET("Standard Role Check Line".Role) THEN BEGIN
        PermissionSet."Role ID" := "Standard Role Check Line".Role;
        PermissionSet.INSERT(TRUE);
      END;
      PermissionSet.Name := "Standard Role Check Line"."Role Name";
      PermissionSet."Standard 4PS" := TRUE;
      PermissionSet.MODIFY;                                        // No Modify(TRUE)..
    END;

    PROCEDURE UpdatePermission@1210190010("Standard Role Check Line"@1210190000 : Record 11020657);
    VAR
      Permission@1210190001 : Record 2000000005;
    BEGIN
      IF NOT Permission.GET(
       "Standard Role Check Line".Role,
       "Standard Role Check Line"."Object Type",
       "Standard Role Check Line"."Object ID") THEN BEGIN
        Permission."Role ID" := "Standard Role Check Line".Role;
        Permission."Object Type" := "Standard Role Check Line"."Object Type";
        Permission."Object ID" := "Standard Role Check Line"."Object ID";
        Permission.INSERT;                                    // No INSERT(TRUE)..
      END;
      Permission."Read Permission" := "Standard Role Check Line"."Read Permission";
      Permission."Insert Permission" := "Standard Role Check Line"."Insert Permission";
      Permission."Modify Permission" := "Standard Role Check Line"."Modify Permission";
      Permission."Delete Permission" := "Standard Role Check Line"."Delete Permission";
      Permission."Execute Permission" := "Standard Role Check Line"."Execute Permission";
      Permission.MODIFY;                                      // No Modify(TRUE)..
    END;

    PROCEDURE UpdateRoleGroup@1210190000("Standard Role Check Line"@1210190000 : Record 11020657);
    VAR
      RoleGroup4PS@1210190001 : Record 11020570;
    BEGIN
      IF NOT RoleGroup4PS.GET("Standard Role Check Line"."Role Group") THEN BEGIN
        RoleGroup4PS.Code := "Standard Role Check Line"."Role Group";
        RoleGroup4PS.INSERT;                                  // No INSERT(TRUE)..
      END;
      RoleGroup4PS.Description := "Standard Role Check Line"."Role Group Name";
      RoleGroup4PS."Standard 4PS" := TRUE;
      RoleGroup4PS.Module := COPYSTR("Standard Role Check Line"."Role Group",3,3);
      RoleGroup4PS.MODIFY;                                    // No Modify(TRUE)..
    END;

    PROCEDURE UpdateRoleGroupRole@1210190030("Standard Role Check Line"@1210190000 : Record 11020657);
    VAR
      RoleGroupRole@1210190001 : Record 11020571;
    BEGIN
      IF NOT RoleGroupRole.GET("Standard Role Check Line"."Role Group","Standard Role Check Line".Role) THEN BEGIN
        RoleGroupRole."Role Group Code" := "Standard Role Check Line"."Role Group";
        RoleGroupRole."Role ID" := "Standard Role Check Line".Role;
        RoleGroupRole.INSERT;                                 // No INSERT(TRUE)..
      END;
      RoleGroupRole."Standard 4PS" := TRUE;
      RoleGroupRole."Updated on" := TODAY;
      RoleGroupRole.MODIFY;                                   // No Modify(TRUE)..
    END;

    PROCEDURE DeletePermissionSet@1210190001("Standard Role Check Line"@1210190001 : Record 11020657);
    VAR
      PermissionSet@1210190000 : Record 2000000004;
    BEGIN
      IF PermissionSet.GET("Standard Role Check Line".Role) THEN
        IF PermissionSet."Standard 4PS" THEN
          PermissionSet.DELETE;                                    // NO Delete(TRUE)..
    END;

    PROCEDURE DeletePermission@1210190018("Standard Role Check Line"@1210190000 : Record 11020657);
    VAR
      Permission@1210190001 : Record 2000000005;
    BEGIN
      IF Permission.GET(
         "Standard Role Check Line".Role,
         "Standard Role Check Line"."Object Type",
         "Standard Role Check Line"."Object ID") THEN
        Permission.DELETE;                                    // NO Delete(TRUE)..
    END;

    PROCEDURE DeleteRoleGroup@1210190019("Standard Role Check Line"@1210190000 : Record 11020657);
    VAR
      RoleGroup4PS@1210190001 : Record 11020570;
    BEGIN
      IF RoleGroup4PS.GET("Standard Role Check Line"."Role Group") THEN
        IF RoleGroup4PS."Standard 4PS" THEN
          RoleGroup4PS.DELETE;                                // NO Delete(TRUE)..
    END;

    PROCEDURE DeleteRoleGroupRole@1210190032("Standard Role Check Line"@1210190000 : Record 11020657);
    VAR
      RoleGroupRole@1210190001 : Record 11020571;
    BEGIN
      IF RoleGroupRole.GET("Standard Role Check Line"."Role Group","Standard Role Check Line".Role) THEN
        IF RoleGroupRole."Standard 4PS" THEN
          RoleGroupRole.DELETE;                               // NO Delete(TRUE)..
    END;

    PROCEDURE DeleteStandardRoleCheckLines@1100525000();
    VAR
      StandardRoleCheckLine@1100525000 : Record 11020657;
    BEGIN
      StandardRoleCheckLine.DELETEALL;
    END;

    PROCEDURE UpdateProgressWindow@9(NewProgress@11012000 : Integer);
    BEGIN
      IF NoOfRecords > 0 THEN BEGIN
        NewProgress := ROUND(NewProgress / NoOfRecords * 100,1);
        IF (NewProgress <> Progress) OR (TIME > LastUpdateTime + 5000) THEN BEGIN
          Window.UPDATE(1,100 * NewProgress);
          LastUpdateTime := TIME;
        END;
        Progress := NewProgress;
      END;
    END;

    BEGIN
    END.
  }
}

