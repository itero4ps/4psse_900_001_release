OBJECT Codeunit 11012273 String Functions
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            ResultText@1100525000 : Text;
          BEGIN
            ResultText := ReplaceStringPart(ResultText, '123', 10, 5);
            ResultText := ReplaceStringPart(ResultText, 'abcd', 1, 5);
            ResultText := ReplaceStringPart(ResultText, 'xx', 7, 3);
          END;

  }
  CODE
  {
    VAR
      Text000@1100528400 : TextConst 'ENU=Argument %1 of function ReplaceStrings has not been initialized.';
      Text001@1100528401 : TextConst 'ENU=ReplaceStrings expects 2 ArrayLists in the conversion to be of equal length: Converting from %1 to %2.';
      Text002@1100525000 : TextConst 'ENU=Password must be al least 4 Characters.';
      Text003@1100525001 : TextConst 'ENU=Incorrect Input Parameters.';

    PROCEDURE ReplaceString@1100528700(String@1100528700 : Text;FromString@1100528701 : Text;ToString@1100528702 : Text) Result : Text;
    VAR
      P@1100528703 : Integer;
    BEGIN
      Result := ReplaceString2(String, FromString, ToString, TRUE);
    END;

    LOCAL PROCEDURE SetP@1100409002(String@1100409003 : Text;FromString@1100409002 : Text;CaseSensitive@1100409000 : Boolean) P : Integer;
    BEGIN
      IF (NOT CaseSensitive) THEN
        P := STRPOS(LOWERCASE(String), LOWERCASE(FromString))
      ELSE
        P := STRPOS(String, FromString);
    END;

    PROCEDURE ReplaceString2@1100409001(String@1100528700 : Text;FromString@1100528701 : Text;ToString@1100528702 : Text;CaseSensitive@1100409000 : Boolean) Result : Text;
    VAR
      P@1100528703 : Integer;
    BEGIN
      P := SetP(String, FromString, CaseSensitive);

      WHILE P > 0 DO BEGIN
        Result += COPYSTR(String, 1, P - 1) + ToString;
        String := COPYSTR(String, P + STRLEN(FromString));
        P := SetP(String, FromString, CaseSensitive);
      END;
      IF String <> '' THEN
        Result += String;
    END;

    PROCEDURE ReplaceStrings@1100528401(String@1100528700 : Text;FromStrings@1100528401 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";ToStrings@1100528400 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList") Result : Text;
    VAR
      TempGUIDs@1100528402 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";
      NoOfReplacements@1100528404 : Integer;
      I@1100528403 : Integer;
    BEGIN
      IF ISNULL(FromStrings) THEN
        ERROR(Text000, 'FromStrings');
      IF ISNULL(ToStrings) THEN
        ERROR(Text000, 'ToStrings');
      IF FromStrings.Count <> ToStrings.Count THEN
        ERROR(Text001, FromStrings.Count, ToStrings.Count);
      IF String = '' THEN
        EXIT;

      TempGUIDs := TempGUIDs.ArrayList; //These guids prevent that a replacement is replaced again.
      NoOfReplacements := FromStrings.Count;
      Result := String;
      FOR I := 0 TO NoOfReplacements - 1  DO BEGIN
        TempGUIDs.Add(FORMAT(CREATEGUID));
        Result := ReplaceString(Result, FromStrings.Item(I), TempGUIDs.Item(I));
      END;
      FOR I := 0 TO NoOfReplacements - 1  DO
        Result := ReplaceString(Result, TempGUIDs.Item(I), ToStrings.Item(I));
    END;

    PROCEDURE ReplaceStringPart@1100525002(String@1100525000 : Text;ReplaceString@1100525003 : Text;Position@1100525001 : Integer;Length@1100525002 : Integer) : Text;
    VAR
      I@1100525004 : Integer;
    BEGIN
      IF (Position < 1) OR (Length < 1) THEN
        ERROR(Text003);
      IF (STRLEN(String) < Position + Length - 1) THEN
        String := PADSTR(String, Position + Length - 1);
      IF (STRLEN(ReplaceString) < Length) THEN
        ReplaceString := PADSTR(ReplaceString, Length);

      I := 1;
      REPEAT
        String[Position + I-1] := ReplaceString[I];
        I += 1;
      UNTIL (I > Length);
      EXIT(String);
    END;

    PROCEDURE SplitString@1210190002(StringToSplit@1210190000 : Text[1024];SplitText@1210190001 : Text[100];VAR SplittedArr@1210190002 : ARRAY [10] OF Text[250]);
    VAR
      SubStrings@1100528400 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";
      Counter@1210190005 : Integer;
    BEGIN
      SplitString2(StringToSplit, SplitText, SubStrings);
      IF ISNULL(SubStrings) THEN
        EXIT;

      FOR Counter := 1 TO 10 DO
        IF Counter <= SubStrings.Count THEN
          SplittedArr[Counter] := SubStrings.Item(Counter - 1);
    END;

    PROCEDURE SplitString2@1100528400(StringToSplit@1210190000 : Text;SplitText@1210190001 : Text;VAR SubStrings@1210190002 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList");
    VAR
      StrPosition@1210190003 : Integer;
    BEGIN
      SubStrings := SubStrings.ArrayList;
      WHILE (StringToSplit <> '') DO BEGIN
        StrPosition := STRPOS(StringToSplit, SplitText);
        IF (StrPosition > 0) THEN BEGIN
          SubStrings.Add(COPYSTR(StringToSplit, 1, StrPosition - 1));
          StringToSplit := COPYSTR(StringToSplit, StrPosition + STRLEN(SplitText));
        END ELSE BEGIN
          SubStrings.Add(COPYSTR(StringToSplit, 1));
          StringToSplit := '';
        END;
      END;
    END;

    PROCEDURE RTrim@1100528701(String@1100528700 : Text[1024]) Result : Text[1024];
    BEGIN
      Result := String;
      IF Result <> '' THEN
        WHILE COPYSTR(Result, STRLEN(Result), 1) = ' ' DO
          Result := COPYSTR(Result, 1, STRLEN(Result) - 1);
    END;

    PROCEDURE LTrim@1100528703(String@1100528700 : Text[1024]) Result : Text[1024];
    BEGIN
      Result := String;
      IF Result <> '' THEN
        WHILE COPYSTR(Result, 1, 1) = ' ' DO
          Result := COPYSTR(Result, 2);
    END;

    PROCEDURE RSTRPOS@1100525003(BaseString@1100525000 : Text[1024];SearchPattern@1100525001 : Text[100]) : Integer;
    VAR
      StringPos@1100525002 : Integer;
    BEGIN
      StringPos := STRLEN(BaseString) - STRLEN(SearchPattern);
      WHILE (StringPos > 0) DO BEGIN
        IF (STRPOS(COPYSTR(BaseString, StringPos), SearchPattern) <> 0) THEN
          EXIT(StringPos);
        StringPos -= 1;
      END;
      EXIT(-1);
    END;

    PROCEDURE CarriageReturn@1210190000() : Char;
    BEGIN
      EXIT(13);
    END;

    PROCEDURE LineFeed@1210190003() : Char;
    BEGIN
      EXIT(10);
    END;

    PROCEDURE Tab@1210190001() : Char;
    BEGIN
      EXIT(9);
    END;

    PROCEDURE Space@1210190004() : Char;
    BEGIN
      EXIT(32);
    END;

    PROCEDURE ReplaceDiacritics@1100525000(TextIn@1100525000 : Text) TextOut : Text;
    BEGIN
      TextOut := CONVERTSTR(
        TextIn,
        '€‚ƒ„…†‡ˆ‰Š‹ŒŽ‘’“”•–—˜™š› ¡¢£¤µ¶·½¾ÆÇÐÑÒÓÔiÖ×ØÓÔÞàáâãäåæéêëìí',
        'CueaaaaceeeiiiAAEaAooouuyOUoOaiounAAAcyaAoDEEEiIIIEEIOBOOoOuUUUyY');
    END;

    PROCEDURE IsFilterString@1100409000(FilterString@1100409000 : Text[500]) : Boolean;
    BEGIN
      //Contains FilterString a filter expression or just 1 value
      CASE TRUE OF
        STRPOS(FilterString, '>=') > 0,
        STRPOS(FilterString, '<=') > 0,
        STRPOS(FilterString, '<>') > 0,
        STRPOS(FilterString, '&') > 0,
        STRPOS(FilterString, '|') > 0,
        STRPOS(FilterString, '>') > 0,
        STRPOS(FilterString, '<') > 0,
        STRPOS(FilterString, '..') > 0,
        STRPOS(FilterString, '*') > 0,
        STRPOS(FilterString, '@') > 0:
          EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    PROCEDURE DeleteInvalidXMLCharacters@1100528402(TextIn@1100528400 : Text) TextOut : Text;
    VAR
      InvalidXMLCharacter@1100528402 : Record 11071899;
      InvalidChars@1100528403 : Text;
      I@1100528401 : Integer;
    BEGIN
      FOR I := 1 TO STRLEN(TextIn) DO
        IF NOT InvalidXMLCharacter.CharIsValid(TextIn[I]) THEN
          InvalidChars := STRSUBSTNO('%1%2', InvalidChars, TextIn[I]);

      TextOut := DELCHR(TextIn, '=', InvalidChars);
    END;

    PROCEDURE ReplaceCRLFWithPlaceholder@1100528403(VAR Text@1100528400 : Text);
    BEGIN
      Text := ReplaceString(
        Text, STRSUBSTNO('%1%2', CarriageReturn, LineFeed), PlaceHolderForCRLF);
    END;

    PROCEDURE ReplacePlaceholderWithCRLF@1100528404(VAR Text@1100528400 : Text);
    BEGIN
      Text := ReplaceString(
        Text, PlaceHolderForCRLF, STRSUBSTNO('%1%2', CarriageReturn, LineFeed));
    END;

    LOCAL PROCEDURE PlaceHolderForCRLF@1100528405() : Text[30];
    BEGIN
      EXIT('^^');
    END;

    PROCEDURE BitStringToInteger@1100528300(BitString@1100528300 : Text) BitValue : Integer;
    VAR
      Length@1100528301 : Integer;
      Counter@1100528302 : Integer;
    BEGIN
      BitValue := 0;
      Length := STRLEN(BitString);
      IF (Length = 0) THEN
        EXIT;

      FOR Counter := 1 TO Length DO BEGIN
        IF (BitString[Counter] = '1') THEN
          BitValue += (POWER(2, Length - Counter) * 1);
      END;
    END;

    PROCEDURE GeneratePasswordPlainText@1100528302(VAR PasswordPlain@1100525002 : Text;PasswordLength@1100525003 : Integer);
    VAR
      CharsToUse@1100528300 : Text;
      CharsToUseSpecial@1100525001 : Text;
      CharsToUseNumbers@1100525000 : Text;
      PasswordTmp@1100525004 : Text;
      Counter@1100528301 : Integer;
      CounterTmp@1100525006 : Integer;
      RandomNo@1100525005 : Integer;
    BEGIN
      IF (PasswordLength < 4) THEN
        ERROR(Text002);
      RANDOMIZE();
      CharsToUse := '1234567890ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz-_!=';
      CharsToUseNumbers := '1234567890';
      CharsToUseSpecial := '-_!=';
      FOR Counter := 1 TO PasswordLength - 2 DO
        PasswordTmp[Counter] := CharsToUse[RANDOM(STRLEN(CharsToUse))];
      PasswordTmp[PasswordLength - 1] := CharsToUseNumbers[RANDOM(STRLEN(CharsToUseNumbers))];
      PasswordTmp[PasswordLength] := CharsToUseSpecial[RANDOM(STRLEN(CharsToUseSpecial))];

      FOR Counter := 1 TO PasswordLength DO BEGIN
        RandomNo := RANDOM(STRLEN(PasswordTmp));
        PasswordPlain[Counter] := PasswordTmp[RandomNo];
        PasswordTmp := DELSTR(PasswordTmp, RandomNo, 1);
      END;
    END;

    PROCEDURE GeneratePasswordMD5@1100525001(VAR PasswordPlain@1100525002 : Text;VAR PasswordHash@1100525001 : Text;VAR MD5Hash@1100525000 : Boolean;PasswordLength@1100525003 : Integer);
    VAR
      DOEncryptionMgt@1100525004 : Codeunit 824;
    BEGIN
      GeneratePasswordPlainText(PasswordPlain, PasswordLength);
      PasswordHash := DOEncryptionMgt.CalculateStringMD5(PasswordPlain);
      MD5Hash := TRUE;
    END;

    BEGIN
    END.
  }
}

