OBJECT Codeunit 11012480 INSBOU Interface
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      LocalName@1100528301 : TextConst 'ENU=".//*[local-name()=""%1""]"';
      ORDRSP@1100528304 : TextConst 'ENU=ORDRSP';
      DESADV@1100528305 : TextConst 'ENU=DESADV';
      INVOIC@1100528306 : TextConst 'ENU=INVOIC';
      PRICAT@1100528307 : TextConst 'ENU=PRICAT';
      Text000@1100525000 : TextConst 'ENU=%1 not found for %2 %3.';
      Text001@1100525001 : TextConst 'ENU=Error occurred in communication with INSBOU Backend.\\ Method: %1\ Error Code: %2\ Error Message:\ %3';
      ConsumedWebService@1100525016 : Record 11229797;
      ConsumedWebServiceLine@1100525015 : Record 11229798;
      MarketingSetup@1100525004 : Record 5079;
      PurchasesPayablesSetup@1100525010 : Record 312;
      PurchaseHeader@1100525021 : Record 38;
      Vendor@1100525022 : Record 23;
      VendorINSBOULogin@1100525020 : Record 11012132;
      CompanyInformation@1100525023 : Record 79;
      XMLDOMManagement4PS@1100525013 : Codeunit 11020220;
      XMLFormat4PS@1100525012 : Codeunit 11020221;
      VendorNo@1100525024 : Code[20];
      OrderResponseNumber@1100525025 : Code[17];
      InvoiceNumber@1100525026 : Code[17];
      DespatchAdviceNumber@1100528308 : Code[17];
      PriceCataloqueNumber@1100528309 : Code[17];
      RequestXML@1100525009 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      RequestNamespaceManager@1100525008 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNamespaceManager";
      ResponseXML@1100525007 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      ResponseNamespaceManager@1100525006 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNamespaceManager";
      RootNode@1100525005 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      FoundNode@1100525003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      Text002@1100525002 : TextConst 'ENU=%1 %2 sent to %3 %4.';
      Text003@1100525017 : TextConst 'ENU=%1 ''%2'' does not exist.';
      Text004@1100525011 : TextConst 'ENU=Field %1 is empty.Do you want to continue?';
      Text005@1100525014 : TextConst 'ENU=Process aborted.';
      Text006@1100525018 : TextConst 'ENU=Field %1 %2 does not have a valid value conform the INSBOU standard (current value: ''%3'').';
      Text007@1100525019 : TextConst 'ENU=Field %1 %2 is too long (current length: %3 and should be %4).';
      NodeList@1100528300 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
      Text008@1100528302 : TextConst 'ENU=%1 %2 is not supported for sending a INSBOU %3.';
      Text009@1100528303 : TextConst 'ENU=INSBOU Message %1 %2 %3 is not supported.';

    PROCEDURE GetAvailableMessages@1100528300(VendorNo@1100528300 : Code[20]);
    VAR
      INSBOUSetup@1100525000 : Record 11012348;
      ConsumedWebServiceMgt@1100528301 : Codeunit 11229310;
    BEGIN
      INSBOUSetup.GET;
      GetVendorINSBOULogin(VendorINSBOULogin, VendorNo, 0, '');
      CheckVendorINSBOU(VendorINSBOULogin);
      SetGlobalVars(VendorINSBOULogin."Consumed Web Service Code", 10);

      ConsumedWebServiceMgt.CreateRequest(ConsumedWebService.Code, ConsumedWebServiceLine."Seq. No.", RequestXML, RequestNamespaceManager);
      XMLDOMManagement4PS.FindNode(RequestXML, STRSUBSTNO(LocalName, ConsumedWebServiceLine."Request Root Tag"), RootNode);
      XMLDOMManagement4PS.AddElement(RootNode, STRSUBSTNO('%1:MsgType', ConsumedWebServiceLine."Prefix Method Namespace"),
        'Set MsgType here', ConsumedWebServiceLine."Method Namespace", FoundNode);

      IF (INSBOUSetup."ORDRSP Messages") THEN SendGetAvailableMessages(VendorNo, ORDRSP);
      IF (INSBOUSetup."INVOIC Messages") THEN SendGetAvailableMessages(VendorNo, INVOIC);
      IF (INSBOUSetup."DESADV Messages") THEN SendGetAvailableMessages(VendorNo, DESADV);
      IF (INSBOUSetup."PRICAT Messages") THEN SendGetAvailableMessages(VendorNo, PRICAT);
    END;

    PROCEDURE GetMessage@1100528301(VAR INSBOUAvailableMessage@1100528300 : Record 11020432);
    VAR
      ConsumedWebServiceMgt@1100528301 : Codeunit 11229310;
    BEGIN
      INSBOUAvailableMessage.SETRANGE(Status, INSBOUAvailableMessage.Status::Received);
      IF (NOT INSBOUAvailableMessage.FINDSET(TRUE)) THEN
        EXIT;

      REPEAT
        CASE INSBOUAvailableMessage.Type OF
          ORDRSP: GetVendorINSBOULogin(VendorINSBOULogin, INSBOUAvailableMessage."Vendor No.", 2, '');
          DESADV: GetVendorINSBOULogin(VendorINSBOULogin, INSBOUAvailableMessage."Vendor No.", 3, '');
          PRICAT: GetVendorINSBOULogin(VendorINSBOULogin, INSBOUAvailableMessage."Vendor No.", 7, '');
          INVOIC: GetVendorINSBOULogin(VendorINSBOULogin, INSBOUAvailableMessage."Vendor No.", 13, '');
          ELSE
            ERROR(Text009, INSBOUAvailableMessage.Type, INSBOUAvailableMessage.Format, INSBOUAvailableMessage.Version);
        END;
        CheckVendorINSBOU(VendorINSBOULogin);
        SetGlobalVars(VendorINSBOULogin."Consumed Web Service Code", 20);

        ConsumedWebServiceMgt.CreateRequest(ConsumedWebService.Code, ConsumedWebServiceLine."Seq. No.", RequestXML, RequestNamespaceManager);
        XMLDOMManagement4PS.FindNodeNs(RequestXML, STRSUBSTNO('.//%1:%2', ConsumedWebServiceLine."Prefix Method Namespace",
          ConsumedWebServiceLine."Request Root Tag"), RootNode, RequestNamespaceManager);

        XMLDOMManagement4PS.AddElement(RootNode, STRSUBSTNO('%1:MsgId', ConsumedWebServiceLine."Prefix Method Namespace"),
          INSBOUAvailableMessage.ID, ConsumedWebServiceLine."Method Namespace", FoundNode);
        XMLDOMManagement4PS.AddElement(RootNode, STRSUBSTNO('%1:MsgFormat', ConsumedWebServiceLine."Prefix Method Namespace"),
          INSBOUAvailableMessage.Format, ConsumedWebServiceLine."Method Namespace", FoundNode);
        XMLDOMManagement4PS.AddElement(RootNode, STRSUBSTNO('%1:MsgVersion', ConsumedWebServiceLine."Prefix Method Namespace"),
          INSBOUAvailableMessage.Version, ConsumedWebServiceLine."Method Namespace", FoundNode);

        ConsumedWebServiceMgt.SendCWS(ConsumedWebService.Code, ConsumedWebServiceLine."Seq. No.", RequestXML, ResponseXML,
          ResponseNamespaceManager, SetMessageId);
        CheckResponseSoapFault;
        ProcessGetMessage(FALSE, '');

        INSBOUAvailableMessage.Status := INSBOUAvailableMessage.Status::Processed;
        INSBOUAvailableMessage.MODIFY(TRUE);
        COMMIT;
      UNTIL (INSBOUAvailableMessage.NEXT = 0);
    END;

    PROCEDURE DeleteMessage@1100528303(VAR INSBOUAvailableMessage@1100528300 : Record 11020432);
    VAR
      TempINSBOUAvailableMessage@1100528308 : TEMPORARY Record 11020432;
      ConsumedWebServiceMgt@1100528307 : Codeunit 11229310;
      ToggleCode@1100528301 : Code[20];
    BEGIN
      INSBOUAvailableMessage.SETRANGE(Status, INSBOUAvailableMessage.Status::Processed);
      IF (NOT INSBOUAvailableMessage.FINDSET(TRUE, TRUE)) THEN
        EXIT;

      ToggleCode := GetToggleCodeDeleteMessage(INSBOUAvailableMessage);
      REPEAT
        IF (ToggleCode <> GetToggleCodeDeleteMessage(INSBOUAvailableMessage)) THEN BEGIN
          SendDeleteMessage(TempINSBOUAvailableMessage);
          TempINSBOUAvailableMessage.DELETEALL;
          COMMIT;
        END;
        TempINSBOUAvailableMessage.COPY(INSBOUAvailableMessage);
        TempINSBOUAvailableMessage.INSERT;
        ToggleCode := GetToggleCodeDeleteMessage(INSBOUAvailableMessage);
        INSBOUAvailableMessage.DELETE(TRUE);
      UNTIL (INSBOUAvailableMessage.NEXT = 0);
      SendDeleteMessage(TempINSBOUAvailableMessage);
      COMMIT;
    END;

    PROCEDURE SendPuchaseOder@1100525000(OrderType@1100525009 : Option;OrderNo@1100525000 : Code[20]);
    VAR
      PurchaseHeaderExtension@1100525002 : Record 11020398;
      TempBlob@1100525004 : Record 99008535;
      INSBOUAvailableMessage@1100528304 : Record 11020432;
      FileManagement@1100525008 : Codeunit 419;
      ConsumedWebServiceMgt@1100525010 : Codeunit 11229310;
      INSBOUOrderWS@1100528300 : XMLport 11072003;
      CurrDateTimeTxt@1100525001 : Text;
      ClientTempFileName@1100528301 : Text;
      OutputFileName@1100525006 : Text[1024];
      Pos@1100528302 : Integer;
      Ostream@1100525005 : OutStream;
      PurchaseOrderXML@1100525011 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      PurchaseOrderRootNode@1100525013 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      NameSpaceManager@1100528303 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNamespaceManager";
    BEGIN
      PurchaseHeader.GET(OrderType, OrderNo);
      Vendor.GET(PurchaseHeader."Buy-from Vendor No.");
      GetVendorINSBOULogin(VendorINSBOULogin, PurchaseHeader."Buy-from Vendor No.", 1,
        PurchaseHeader."Shortcut Dimension 1 Code");
      CheckVendorINSBOU(VendorINSBOULogin);
      SetGlobalVars(VendorINSBOULogin."Consumed Web Service Code", 40);

      //Do all Purchase Order checks
      CheckPurchaseHeader(PurchaseHeader);
      CheckPurchaseLines(OrderType, OrderNo);

      //Create PurchaseOrder XML
      TempBlob.Blob.CREATEOUTSTREAM(Ostream);
      INSBOUOrderWS.SetVars(OrderType, OrderNo);
      INSBOUOrderWS.SETDESTINATION(Ostream);
      INSBOUOrderWS.EXPORT;
      ClientTempFileName := FileManagement.ClientTempFileName('xml');
      FileManagement.BLOBExport4PS(TempBlob, ClientTempFileName, FALSE, TRUE);
      IF (NOT ISNULL(PurchaseOrderXML)) THEN
        CLEAR(PurchaseOrderXML);
      PurchaseOrderXML := PurchaseOrderXML.XmlDocument;
      XMLDOMManagement4PS.LoadXMLFromClientFile(PurchaseOrderXML, ClientTempFileName, TRUE);
      //Add Mandatory Namespaces
      PurchaseOrderXML.DocumentElement.SetAttribute('xmlns:xs', 'http://www.w3.org/2001/XMLSchema');
      PurchaseOrderXML.DocumentElement.SetAttribute('xmlns:xsi', 'http://www.w3.org/2001/XMLSchema-instance');
      //PurchaseOrderXML.DocumentElement.SetAttribute('xsi:noNamespaceSchemaLocation', 'Order_insbou003.xsd');
      Pos := STRPOS(PurchaseOrderXML.InnerXml, '<OrderType>');
      PurchaseOrderXML.LoadXml(COPYSTR(PurchaseOrderXML.OuterXml, 1, Pos-2) + ' xsi:noNamespaceSchemaLocation="Order_insbou003.xsd"' +
        COPYSTR(PurchaseOrderXML.OuterXml, Pos-1));

      //Clean PurchaseOrder XML
      XMLDOMManagement4PS.FindNode(PurchaseOrderXML, STRSUBSTNO(LocalName, 'Order'), PurchaseOrderRootNode);
      XMLDOMManagement4PS.RemoveEmptyTags(PurchaseOrderRootNode);

      //Create SOAP request
      ConsumedWebServiceMgt.CreateRequest(ConsumedWebService.Code, ConsumedWebServiceLine."Seq. No.", RequestXML, RequestNamespaceManager);
      CASE VendorINSBOULogin.Version OF
        '003':
          CreateOrders003;
        ELSE
          ERROR(Text008, VendorINSBOULogin.FIELDCAPTION(Version), VendorINSBOULogin.Version, PurchaseHeader.TABLECAPTION);
      END;
      //Add CDATA
      XMLDOMManagement4PS.FindNode(RequestXML, STRSUBSTNO(LocalName, 'MsgContent'), FoundNode);
      FoundNode.InnerXml := STRSUBSTNO('<![CDATA[%1]]>', PurchaseOrderXML.OuterXml);
      //ERROR('RequestXML: \%1', RequestXML.OuterXml); //remove kz

      //Send SOAP Request
      ConsumedWebServiceMgt.SendCWS(ConsumedWebService.Code, ConsumedWebServiceLine."Seq. No.", RequestXML, ResponseXML,
        ResponseNamespaceManager, SetMessageId);
      CheckResponseSoapFault;

      //Store PurchaseOrder XML as document to Purchase Order
      CurrDateTimeTxt := FORMAT(CURRENTDATETIME, 0, 9);
      CurrDateTimeTxt := CONVERTSTR(CurrDateTimeTxt, ':', '-');
      CurrDateTimeTxt := DELCHR(CurrDateTimeTxt, '<=>', 'Z');
      OutputFileName := STRSUBSTNO('%1INSBOU-%2-%3-%4-%5.xml', MarketingSetup."Default Source Dir.Ext.Doc", PurchaseHeader.TABLECAPTION,
        PurchaseHeader."Document Type", PurchaseHeader."No.", CurrDateTimeTxt);
      XMLDOMManagement4PS.SaveXMLToClientFile(PurchaseOrderXML, OutputFileName);
      AddDocumentToPurchaseHeader(PurchaseHeader, OutputFileName);

      PurchaseHeader.GET(OrderType, OrderNo);
      PurchaseHeaderExtension."GS1 Sent by" := USERID;
      PurchaseHeaderExtension."GS1 Sent on" := TODAY;
      PurchaseHeaderExtension.UpdatePurchHeadExtension(OrderType, OrderNo);

      MESSAGE(Text002, PurchaseHeader."Document Type", PurchaseHeader."No.", PurchaseHeader.FIELDCAPTION("Buy-from Vendor No."),
        PurchaseHeader."Buy-from Vendor No.");
      COMMIT;

      //Process Order Response (ORDRSP)
      ProcessGetMessage(TRUE, PurchaseHeader."Buy-from Vendor No.");
    END;

    PROCEDURE ProcessOrdrsp003@1100528306(MsgId@1100525000 : Text;MsgContent@1100528308 : Text);
    VAR
      FileManagement@1100528306 : Codeunit 419;
      RegExpManagement@1100525001 : Codeunit 11012262;
      INSBOUOrderResponseWS@1100528304 : XMLport 11072004;
      IStream@1100528303 : InStream;
      XMLDoc@1100528302 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      XMLNode@1100528301 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XMLFile@1100528300 : File;
      ServerFileName@1100528309 : Text;
    BEGIN
      XMLDoc := XMLDoc.XmlDocument;
      ServerFileName := FileManagement.ServerTempFileName('xml');

      MsgContent := RegExpManagement.Replace(MsgContent, 'xsi:noNamespaceSchemaLocation=".*_insbou003.xsd"', '');
      XMLDoc.LoadXml(MsgContent);
      XMLDOMManagement4PS.RemoveNamespaces(XMLDoc, XMLDoc);
      XMLDoc.Save(ServerFileName);

      XMLFile.OPEN(ServerFileName);
      XMLFile.CREATEINSTREAM(IStream);
      INSBOUOrderResponseWS.SetMessageId(MsgId);
      INSBOUOrderResponseWS.SETSOURCE(IStream);
      INSBOUOrderResponseWS.IMPORT;
      XMLFile.CLOSE;

      VendorNo := INSBOUOrderResponseWS.GetOrderResponseVendorNo;
      OrderResponseNumber := INSBOUOrderResponseWS.GetOrderResponseOrderResponseNo;
    END;

    PROCEDURE ProcessInvoic003@1100528305(MsgId@1100525000 : Text;MsgContent@1100528308 : Text);
    VAR
      FileManagement@1100528307 : Codeunit 419;
      RegExpManagement@1100528306 : Codeunit 11012262;
      INSBOUInvoiceWS@1100528304 : XMLport 11072002;
      IStream@1100528303 : InStream;
      XMLDoc@1100528302 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      XMLNode@1100528301 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      AttachmentNode@1100528311 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      AttachedDataNode@1100528310 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      ServerFileName@1100528305 : Text;
      XMLFile@1100528300 : File;
      AttachedFileName@1100528312 : Text;
    BEGIN
      XMLDoc := XMLDoc.XmlDocument;
      ServerFileName := FileManagement.ServerTempFileName('xml');

      MsgContent := RegExpManagement.Replace(MsgContent, 'xsi:noNamespaceSchemaLocation=".*_insbou003.xsd"', '');
      XMLDoc.LoadXml(MsgContent);
      XMLDOMManagement4PS.RemoveNamespaces(XMLDoc, XMLDoc);
      XMLDOMManagement4PS.FindNode(XMLDoc, STRSUBSTNO(LocalName, 'Invoice'), XMLNode);
      IF (XMLDOMManagement4PS.FindNode(XMLDoc, STRSUBSTNO(LocalName, 'Attachment'), AttachmentNode)) THEN BEGIN
        IF (XMLDOMManagement4PS.FindNode(AttachmentNode, STRSUBSTNO(LocalName, 'FileName'), FoundNode)) THEN
          AttachedFileName := FoundNode.InnerText;
        XMLDOMManagement4PS.FindNode(AttachmentNode, STRSUBSTNO(LocalName, 'AttachedData'), AttachedDataNode);
        XMLNode.RemoveChild(AttachmentNode);
      END;
      XMLDoc.Save(ServerFileName);

      XMLFile.OPEN(ServerFileName);
      XMLFile.CREATEINSTREAM(IStream);
      INSBOUInvoiceWS.SetMessageId(MsgId);
      IF (NOT ISNULL(AttachmentNode)) THEN
        INSBOUInvoiceWS.SetAttachmentContent(AttachedDataNode.InnerText, AttachedFileName);
      INSBOUInvoiceWS.SETSOURCE(IStream);
      INSBOUInvoiceWS.IMPORT;
      XMLFile.CLOSE;

      VendorNo := INSBOUInvoiceWS.GetInvoiceVendorNo;
      InvoiceNumber := INSBOUInvoiceWS.GetInvoiceInvoiceNo;
    END;

    PROCEDURE ProcessDesadv003@1100528311(MsgId@1100525000 : Text;MsgContent@1100528308 : Text);
    VAR
      FileManagement@1100528307 : Codeunit 419;
      RegExpManagement@1100528306 : Codeunit 11012262;
      INSBOUDespatchAdviceWS@1100528304 : XMLport 11072006;
      IStream@1100528303 : InStream;
      XMLDoc@1100528302 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      XMLNode@1100528301 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XMLFile@1100528300 : File;
      ServerFileName@1100528309 : Text;
    BEGIN
      XMLDoc := XMLDoc.XmlDocument;
      ServerFileName := FileManagement.ServerTempFileName('xml');

      MsgContent := RegExpManagement.Replace(MsgContent, 'xsi:noNamespaceSchemaLocation=".*_insbou003.xsd"', '');
      XMLDoc.LoadXml(MsgContent);
      XMLDOMManagement4PS.RemoveNamespaces(XMLDoc, XMLDoc);
      XMLDoc.Save(ServerFileName);

      XMLFile.OPEN(ServerFileName);
      XMLFile.CREATEINSTREAM(IStream);
      INSBOUDespatchAdviceWS.SetMessageId(MsgId);
      INSBOUDespatchAdviceWS.SETSOURCE(IStream);
      INSBOUDespatchAdviceWS.IMPORT;
      XMLFile.CLOSE;

      VendorNo := INSBOUDespatchAdviceWS.GetDespatchAdviceVendorNo;
      DespatchAdviceNumber := INSBOUDespatchAdviceWS.GetDespatchAdviceMessageNo;
    END;

    PROCEDURE ProcessPricat003@1100528316(MsgId@1100525000 : Text;MsgContent@1100528308 : Text);
    VAR
      FileManagement@1100528307 : Codeunit 419;
      RegExpManagement@1100528306 : Codeunit 11012262;
      INSBOUPriceCatalogueWS@1100528304 : XMLport 11072007;
      IStream@1100528303 : InStream;
      XMLDoc@1100528302 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      XMLNode@1100528301 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XMLFile@1100528300 : File;
      ServerFileName@1100528309 : Text;
    BEGIN
      XMLDoc := XMLDoc.XmlDocument;
      ServerFileName := FileManagement.ServerTempFileName('xml');

      MsgContent := RegExpManagement.Replace(MsgContent, 'xsi:noNamespaceSchemaLocation=".*_insbou003.xsd"', '');
      XMLDoc.LoadXml(MsgContent);
      XMLDOMManagement4PS.RemoveNamespaces(XMLDoc, XMLDoc);
      XMLDoc.Save(ServerFileName);

      XMLFile.OPEN(ServerFileName);
      XMLFile.CREATEINSTREAM(IStream);
      INSBOUPriceCatalogueWS.SetMessageId(MsgId);
      INSBOUPriceCatalogueWS.SETSOURCE(IStream);
      INSBOUPriceCatalogueWS.IMPORT;
      XMLFile.CLOSE;

      VendorNo := INSBOUPriceCatalogueWS.GetPriceCataloqueVendorNo;
      PriceCataloqueNumber := INSBOUPriceCatalogueWS.GetPriceCataloquePriceCataloqueNo;
    END;

    PROCEDURE GetVendorNo@1100525008() : Code[20];
    BEGIN
      EXIT(VendorNo);
    END;

    PROCEDURE GetOrderResponseNumber@1100525009() : Code[17];
    BEGIN
      EXIT(OrderResponseNumber);
    END;

    PROCEDURE GetInvoiceNumber@1100525010() : Code[17];
    BEGIN
      EXIT(InvoiceNumber);
    END;

    PROCEDURE GetDespatchAdviceNumber@1100528315() : Code[17];
    BEGIN
      EXIT(DespatchAdviceNumber);
    END;

    PROCEDURE GetPriceCataloqueNumber@1100528314() : Code[17];
    BEGIN
      EXIT(PriceCataloqueNumber);
    END;

    LOCAL PROCEDURE "-- Internal --"@1100528312();
    BEGIN
    END;

    LOCAL PROCEDURE SendGetAvailableMessages@1100528302(VendorNo@1100528302 : Code[20];MsgType@1100528300 : Code[20]);
    VAR
      ConsumedWebServiceMgt@1100528301 : Codeunit 11229310;
    BEGIN
      XMLDOMManagement4PS.FindNode(RequestXML, STRSUBSTNO(LocalName, 'MsgType'), FoundNode);
      FoundNode.InnerText := MsgType;

      ConsumedWebServiceMgt.SendCWS(ConsumedWebService.Code, ConsumedWebServiceLine."Seq. No.", RequestXML, ResponseXML,
        ResponseNamespaceManager, SetMessageId);
      CheckResponseSoapFault;

      ProcessGetAvailableMessages(VendorNo);
    END;

    LOCAL PROCEDURE ProcessGetAvailableMessages@1100528304(VendorNo@1100528307 : Code[20]);
    VAR
      ConsumedWebServiceMgt@1100528306 : Codeunit 11229310;
      FileManagement@1100528305 : Codeunit 419;
      INSBOUAvailableMessageWS@1100528304 : XMLport 11072005;
      ServerFileName@1100525000 : Text;
      XMLFile@1100528302 : File;
      IStream@1100528301 : InStream;
      XMLDoc@1100528300 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
    BEGIN
      IF (NOT XMLDOMManagement4PS.FindNode(ResponseXML, STRSUBSTNO(LocalName, ConsumedWebServiceLine."Response Root Tag"), FoundNode)) THEN
        EXIT;

      ServerFileName := FileManagement.ServerTempFileName('xml');
      XMLDoc := XMLDoc.XmlDocument;
      XMLDoc.LoadXml(FoundNode.OuterXml);
      XMLDOMManagement4PS.RemoveNamespaces(XMLDoc, XMLDoc);
      XMLDoc.Save(ServerFileName);

      XMLFile.OPEN(ServerFileName);
      XMLFile.CREATEINSTREAM(IStream);
      INSBOUAvailableMessageWS.SetVendorNo(VendorNo);
      INSBOUAvailableMessageWS.SETSOURCE(IStream);
      INSBOUAvailableMessageWS.IMPORT;
      XMLFile.CLOSE;
    END;

    LOCAL PROCEDURE ProcessGetMessage@1100528313(AddAvailableMessage@1100528302 : Boolean;VendorNo@1100528305 : Code[20]);
    VAR
      INSBOUAvailableMessage@1100528304 : Record 11020432;
      MsgId@1100525000 : Text;
      MsgType@1100528300 : Code[20];
      MsgFormat@1100528307 : Code[20];
      MsgVersion@1100528301 : Code[20];
      MsgContent@1100528303 : Text;
      RootNode@1100528306 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      IF (NOT XMLDOMManagement4PS.FindNode(ResponseXML, STRSUBSTNO(LocalName, ConsumedWebServiceLine."Response Root Tag"), RootNode)) THEN
        EXIT;

      MsgId := XMLDOMManagement4PS.FindNodeText(RootNode, STRSUBSTNO(LocalName, 'MsgId'));
      MsgType := XMLDOMManagement4PS.FindNodeText(RootNode, STRSUBSTNO(LocalName, 'MsgType'));
      MsgFormat := XMLDOMManagement4PS.FindNodeText(RootNode, STRSUBSTNO(LocalName, 'MsgFormat'));
      MsgVersion := XMLDOMManagement4PS.FindNodeText(RootNode, STRSUBSTNO(LocalName, 'MsgVersion'));
      IF (AddAvailableMessage) AND (MsgId <> '') THEN
        CreateAvailableMessageRec(VendorNo, MsgId, MsgType, MsgVersion,
          XMLDOMManagement4PS.FindNodeText(RootNode, STRSUBSTNO(LocalName, 'MsgDateTime')),
          XMLDOMManagement4PS.FindNodeText(RootNode, STRSUBSTNO(LocalName, 'MsgFormat')));

      //Get CDATA content (MsgContent is without the '<!CDATA[[' and ']]>' enclosure tags)
      MsgContent := XMLDOMManagement4PS.FindNodeText(RootNode, STRSUBSTNO(LocalName, 'MsgContent'));

      CASE TRUE OF
        (MsgType = ORDRSP) AND (MsgFormat = 'INSBOU') AND (MsgVersion = '003'):
          ProcessOrdrsp003(MsgId, MsgContent);
        (MsgType = INVOIC) AND (MsgFormat = 'INSBOU') AND (MsgVersion = '003'):
          ProcessInvoic003(MsgId, MsgContent);
        (MsgType = DESADV) AND (MsgFormat = 'INSBOU') AND (MsgVersion = '003'):
          ProcessDesadv003(MsgId, MsgContent);
        (MsgType = PRICAT) AND (MsgFormat = 'INSBOU') AND (MsgVersion = '003'):
          ProcessPricat003(MsgId, MsgContent);
        ELSE
          ERROR(Text009, MsgType, MsgFormat, MsgVersion);
      END;

      IF (AddAvailableMessage) AND (MsgId <> '') THEN BEGIN
        INSBOUAvailableMessage.GET(VendorNo, MsgId);
        INSBOUAvailableMessage.Status := INSBOUAvailableMessage.Status::Processed;
        INSBOUAvailableMessage.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE SendDeleteMessage@1100528310(VAR TempINSBOUAvailableMessage@1100528301 : TEMPORARY Record 11020432);
    VAR
      INSBOUAvailableMessage@1100528302 : Record 11020432;
      ConsumedWebServiceMgt@1100528300 : Codeunit 11229310;
    BEGIN
      IF (TempINSBOUAvailableMessage.COUNT = 0) THEN
        EXIT;
      TempINSBOUAvailableMessage.FINDSET;
      GetVendorINSBOULogin(VendorINSBOULogin, TempINSBOUAvailableMessage."Vendor No.", 0, '');
      CheckVendorINSBOU(VendorINSBOULogin);
      SetGlobalVars(VendorINSBOULogin."Consumed Web Service Code", 30);

      ConsumedWebServiceMgt.CreateRequest(ConsumedWebService.Code, ConsumedWebServiceLine."Seq. No.", RequestXML, RequestNamespaceManager);
      XMLDOMManagement4PS.FindNodeNs(RequestXML, STRSUBSTNO('.//%1:%2', ConsumedWebServiceLine."Prefix Method Namespace",
        ConsumedWebServiceLine."Request Root Tag"), RootNode, RequestNamespaceManager);

      REPEAT
        XMLDOMManagement4PS.AddElement(RootNode, STRSUBSTNO('%1:MsgId', ConsumedWebServiceLine."Prefix Method Namespace"),
          TempINSBOUAvailableMessage.ID, ConsumedWebServiceLine."Method Namespace", FoundNode);
      UNTIL (TempINSBOUAvailableMessage.NEXT = 0);

      ConsumedWebServiceMgt.SendCWS(ConsumedWebService.Code, ConsumedWebServiceLine."Seq. No.", RequestXML, ResponseXML,
        ResponseNamespaceManager, SetMessageId);
      CheckResponseSoapFault;

      TempINSBOUAvailableMessage.FINDSET;
      REPEAT
        INSBOUAvailableMessage.GET(TempINSBOUAvailableMessage."Vendor No.", TempINSBOUAvailableMessage.ID);
        INSBOUAvailableMessage.DELETE(TRUE);
      UNTIL (TempINSBOUAvailableMessage.NEXT = 0);
    END;

    LOCAL PROCEDURE CreateOrders003@1100528309();
    VAR
      MessageNode@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      XMLDOMManagement4PS.FindNode(RequestXML, STRSUBSTNO(LocalName, ConsumedWebServiceLine."Request Root Tag"),
        RootNode);

      XMLDOMManagement4PS.AddElement(RootNode, STRSUBSTNO('%1:MessageServiceMessage', ConsumedWebServiceLine."Prefix Method Namespace"),
        '', ConsumedWebServiceLine."Method Namespace", FoundNode);
      XMLDOMManagement4PS.FindNode(RequestXML, STRSUBSTNO(LocalName, 'MessageServiceMessage'), MessageNode);

      XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgId', ConsumedWebServiceLine."Prefix Method Namespace"),
        DELCHR(CREATEGUID, '<=>', '{}'), ConsumedWebServiceLine."Method Namespace", FoundNode);
      XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgType', ConsumedWebServiceLine."Prefix Method Namespace"),
        'ORDERS', ConsumedWebServiceLine."Method Namespace", FoundNode);
      XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgFormat', ConsumedWebServiceLine."Prefix Method Namespace"),
        'INSBOU', ConsumedWebServiceLine."Method Namespace", FoundNode);
      XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgVersion', ConsumedWebServiceLine."Prefix Method Namespace"),
        '003', ConsumedWebServiceLine."Method Namespace", FoundNode);
      XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgDateTime', ConsumedWebServiceLine."Prefix Method Namespace"),
        FORMAT(CURRENTDATETIME, 0, 9), ConsumedWebServiceLine."Method Namespace", FoundNode);
      XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgContent', ConsumedWebServiceLine."Prefix Method Namespace"),
        '', ConsumedWebServiceLine."Method Namespace", FoundNode);
    END;

    LOCAL PROCEDURE GetVendorINSBOULogin@1100525001(VAR VendorGS1Login@1100525000 : Record 11012132;VendorNo@1100525001 : Code[20];MsgType@1100528301 : Option;DepartmentCode@1100528302 : Code[20]);
    BEGIN
      IF (VendorINSBOULogin.GET(VendorNo, MsgType, DepartmentCode)) THEN
        EXIT;
      IF (VendorINSBOULogin.GET(VendorNo, MsgType, '')) THEN
        EXIT;
      IF (VendorINSBOULogin.GET(VendorNo, 0, '')) THEN
        EXIT;

      ERROR(Text000, VendorINSBOULogin.TABLECAPTION, PurchaseHeader.FIELDCAPTION("Buy-from Vendor No."),
        PurchaseHeader."Buy-from Vendor No.");
    END;

    LOCAL PROCEDURE SetGlobalVars@1100525005(ConsumedWebServiceCode@1100528300 : Code[20];SeqNo@1100528301 : Integer);
    BEGIN
      IF (NOT ISNULL(RequestXML)) THEN
        CLEAR(RequestXML);
      IF (NOT ISNULL(RequestNamespaceManager)) THEN
        CLEAR(RequestNamespaceManager);
      IF (NOT ISNULL(ResponseXML)) THEN
        CLEAR(ResponseXML);
      IF (NOT ISNULL(ResponseNamespaceManager)) THEN
        CLEAR(ResponseNamespaceManager);

      ConsumedWebService.GET(ConsumedWebServiceCode);
      ConsumedWebServiceLine.GET(ConsumedWebServiceCode, SeqNo);
      CompanyInformation.GET;
      MarketingSetup.GET;
      MarketingSetup.TESTFIELD("Default Source Dir.Ext.Doc");
      MarketingSetup.TESTFIELD("Default Ext. Doc. Directory");
      MarketingSetup.TESTFIELD("Document Group");
      PurchasesPayablesSetup.GET;
      PurchasesPayablesSetup.TESTFIELD("INSBOU Document Type");
    END;

    LOCAL PROCEDURE CheckPurchaseHeader@1100525003(PurchaseHeader@1100525000 : Record 38);
    VAR
      PurchaseHeaderExtension@1100528300 : Record 11020398;
      PurchasePlanPhase@1100525002 : Record 11020417;
      PurchasePlanManagement@1100525001 : Codeunit 11012242;
    BEGIN
      IF (NOT PurchaseHeaderExtension.GET(PurchaseHeader."Document Type", PurchaseHeader."No.")) THEN
        PurchaseHeaderExtension.INIT;
      PurchaseHeaderExtension.TESTFIELD("GS1 Sent on", 0D);
      PurchasePlanManagement.TestPhaseCanBeFinished(PurchaseHeader, 9, 1, PurchasePlanPhase);
      PurchaseHeader.TESTFIELD("Buy-from Vendor No.");
      PurchaseHeader.TESTFIELD("ICM File Sent", FALSE);

      IF (STRLEN(PurchaseHeader."No.") > 17) THEN
        ERROR(Text007, PurchaseHeader.TABLECAPTION, PurchaseHeader.FIELDCAPTION("No."),
          STRLEN(PurchaseHeader."No."), 17);
      IF (PurchaseHeader."Document Type" <> PurchaseHeader."Document Type"::Quote) THEN
        IF (PurchaseHeader."Order Date" = 0D) THEN
          ERROR(Text006, PurchaseHeader.TABLECAPTION, PurchaseHeader.FIELDCAPTION("Order Date"),
            PurchaseHeader."Order Date");
      IF (PurchaseHeader."Due Date" = 0D) THEN
        ERROR(Text006, PurchaseHeader.TABLECAPTION, PurchaseHeader.FIELDCAPTION("Due Date"),
          PurchaseHeader."Due Date");
      IF (PurchaseHeader."Expected Receipt Date" = 0D) THEN
        ERROR(Text006, PurchaseHeader.TABLECAPTION, PurchaseHeader.FIELDCAPTION("Expected Receipt Date"),
          PurchaseHeader."Expected Receipt Date");
      IF (STRLEN(PurchaseHeader."Job No.") > 17) THEN
        ERROR(Text007, PurchaseHeader.TABLECAPTION, PurchaseHeader.FIELDCAPTION("Job No."),
          STRLEN(PurchaseHeader."Job No."), 17);
      IF (PurchaseHeader."Ship-to Name" = '') THEN
        ERROR(Text006, PurchaseHeader.TABLECAPTION, PurchaseHeader.FIELDCAPTION("Ship-to Name"),
          PurchaseHeader."Ship-to Name");
      IF (PurchaseHeader."Ship-to Post Code" = '') THEN
        ERROR(Text006, PurchaseHeader.TABLECAPTION, PurchaseHeader.FIELDCAPTION("Ship-to Post Code"),
          PurchaseHeader."Ship-to Post Code");

      IF (Vendor."GLN Code" = '') THEN
        ERROR(Text001, Vendor.TABLECAPTION, Vendor.FIELDCAPTION("GLN Code"),
          Vendor."GLN Code");
      IF (STRLEN(Vendor."GLN Code") > 13) THEN
        ERROR(Text007, Vendor.TABLECAPTION, Vendor.FIELDCAPTION("GLN Code"),
          STRLEN(Vendor."GLN Code"), 13);
      IF (STRLEN(DELCHR(Vendor."GLN Code", '<=>', '0123456789')) <> 0) THEN
        ERROR(Text006, Vendor.TABLECAPTION, Vendor.FIELDCAPTION("GLN Code"),
          Vendor."GLN Code");

      WHILE (STRLEN(CompanyInformation."Global Location Number") < 13) DO
        CompanyInformation."Global Location Number" := '0' + CompanyInformation."Global Location Number";
      IF (STRLEN(CompanyInformation."Global Location Number") > 13) THEN
        ERROR(Text007, CompanyInformation.TABLECAPTION, CompanyInformation.FIELDCAPTION("Global Location Number"),
          STRLEN(CompanyInformation."Global Location Number"), 13);
      IF (STRLEN(DELCHR(CompanyInformation."Global Location Number", '<=>', '0123456789')) <> 0) THEN
        ERROR(Text006, CompanyInformation.TABLECAPTION, CompanyInformation.FIELDCAPTION("Global Location Number"),
          CompanyInformation."Global Location Number");
    END;

    LOCAL PROCEDURE CheckPurchaseLines@1100525004(OrderType@1100525003 : Option;OrderNo@1100525002 : Code[20]);
    VAR
      PurchaseHeader@1100528300 : Record 38;
      PurchaseLine@1100525001 : Record 39;
      TradeItem@1100525004 : Record 11012317;
      ConfirmEmptyTradeItem@1100525000 : Boolean;
    BEGIN
      PurchaseHeader.GET(OrderType, OrderNo);

      PurchaseLine.SETRANGE("Document Type", OrderType);
      PurchaseLine.SETRANGE("Document No.", OrderNo);
      PurchaseLine.SETFILTER("Shortcut Dimension 2 Code", '<>%1', '');
      PurchaseLine.SETRANGE("Removal Contribution", FALSE);
      PurchaseLine.SETRANGE("Vendor Charge", FALSE);
      PurchaseLine.SETRANGE("Exclude From Electronic Order", FALSE);
      PurchaseLine.FINDSET;
      REPEAT
        IF (PurchaseLine."Trade Item" = '') THEN BEGIN
          IF NOT ConfirmEmptyTradeItem THEN BEGIN
            IF NOT CONFIRM(Text004, FALSE, PurchaseLine.FIELDCAPTION("Trade Item")) THEN
              ERROR(Text005);
            ConfirmEmptyTradeItem := TRUE;
          END;
        END;

        IF (PurchaseLine."Vendor (Trade Item)" = '') THEN BEGIN
          IF NOT ConfirmEmptyTradeItem THEN BEGIN
            IF NOT CONFIRM(Text004, FALSE, PurchaseLine.FIELDCAPTION("Vendor (Trade Item)")) THEN
              ERROR(Text005);
            ConfirmEmptyTradeItem := TRUE;
          END;
        END;

        IF (NOT ConfirmEmptyTradeItem) THEN BEGIN
          IF (NOT TradeItem.GET(PurchaseLine."Vendor (Trade Item)", PurchaseLine."Trade Item")) THEN
              ERROR(Text003, TradeItem.TABLECAPTION, PurchaseLine."Trade Item");

          IF (TradeItem."Application ID" = '') THEN
            ERROR(Text006, TradeItem.TABLECAPTION, TradeItem.FIELDCAPTION("Application ID"),
              TradeItem."Application ID");

          CASE TradeItem."Application ID" OF
            'KGM','LTR','MTR','PCE': ;
            ELSE
              ERROR(Text006, TradeItem.TABLECAPTION, TradeItem.FIELDCAPTION("Application ID"),
                TradeItem."Application ID");
          END;
        END;

        IF (PurchaseLine."Document Type" <> PurchaseLine."Document Type":: Quote) AND
           (PurchaseLine."Order Date" = 0D)
        THEN
          ERROR(Text006, PurchaseLine.TABLECAPTION, PurchaseLine.FIELDCAPTION("Order Date"),
            PurchaseLine."Order Date");
        IF (PurchaseLine."Expected Receipt Date" = 0D) OR
           (PurchaseLine."Expected Receipt Date" <> PurchaseHeader."Expected Receipt Date") //kz (waits for Oosterberg to support this)
        THEN
          ERROR(Text006, PurchaseLine.TABLECAPTION, PurchaseLine.FIELDCAPTION("Expected Receipt Date"),
            PurchaseLine."Expected Receipt Date");

        IF (NOT ConfirmEmptyTradeItem) THEN BEGIN
          WHILE (STRLEN(TradeItem."GTIN Code (Item)") < 14) DO
            TradeItem."GTIN Code (Item)" := '0' + TradeItem."GTIN Code (Item)";
          IF (STRLEN(TradeItem."GTIN Code (Item)") > 14) THEN
            ERROR(Text007, TradeItem.TABLECAPTION, PurchaseLine."Trade Item",
              STRLEN(TradeItem."GTIN Code (Item)"), 14);
        END;
      UNTIL (PurchaseLine.NEXT = 0);
    END;

    LOCAL PROCEDURE CheckResponseSoapFault@1100528330();
    VAR
      FaultCode@1100528301 : Text;
      FaultString@1100528302 : Text;
    BEGIN
      IF (XMLDOMManagement4PS.FindNode(ResponseXML, './/*[local-name()="Fault"]', FoundNode)) THEN BEGIN
        IF (XMLDOMManagement4PS.FindNode(ResponseXML, './/*[local-name()="faultcode"]', FoundNode)) THEN
          FaultCode := FoundNode.InnerText;
        IF (XMLDOMManagement4PS.FindNode(ResponseXML, './/*[local-name()="faultstring"]', FoundNode)) THEN
          FaultString := FoundNode.InnerText;
        IF (XMLDOMManagement4PS.FindNode(ResponseXML, './/*[local-name()="description"]', FoundNode)) THEN
          FaultString := FoundNode.InnerText;
        ERROR(Text001, ConsumedWebServiceLine."Method Name", FaultCode, FaultString);
      END;
    END;

    LOCAL PROCEDURE CheckVendorINSBOU@1100525002(VendorGS1Login@1100525000 : Record 11012132);
    BEGIN
      VendorINSBOULogin.TESTFIELD(URL);
      VendorINSBOULogin.TESTFIELD("User Name");
      VendorINSBOULogin.TESTFIELD(Password);
    END;

    LOCAL PROCEDURE AddDocumentToPurchaseHeader@1100525006(PurchaseHeader@1100525005 : Record 38;FullFileName@1100525000 : Text);
    VAR
      DocumentProperties@1100525002 : Record 11012746;
      Vendor@1100525006 : Record 23;
      ExternalDocumentManagement@1100525001 : Codeunit 11012403;
      DocumentLinkManagement@1100525004 : Codeunit 11012401;
      RecRef@1100525007 : RecordRef;
    BEGIN
      IF (FullFileName = '') THEN
        EXIT;

      DocumentProperties.INIT;
      DocumentProperties."Document Group" := MarketingSetup."Document Group";
      DocumentProperties."Document Type" := PurchasesPayablesSetup."INSBOU Document Type";
      DocumentProperties.Description := FORMAT(PurchaseHeader."Purchase Order Type") + ' ' + PurchaseHeader."No.";
      DocumentProperties."External Document" := TRUE;
      DocumentProperties.INSERT(TRUE);

      ExternalDocumentManagement.RegisterFile(FullFileName, DocumentProperties."Document Type");
      DocumentProperties.VALIDATE(File, FullFileName);
      DocumentProperties.MODIFY(TRUE);

      RecRef.GETTABLE(PurchaseHeader);

      DocumentLinkManagement.CreateOneDocumentLink(DocumentProperties, RecRef.RECORDID);
    END;

    LOCAL PROCEDURE GetToggleCodeGetMessage@1100528307(INSBOUAvailableMessage@1100528300 : Record 11020432) : Code[100];
    BEGIN
      EXIT(STRSUBSTNO('%1-%2-%3', INSBOUAvailableMessage."Vendor No.", INSBOUAvailableMessage.Format, INSBOUAvailableMessage.Version));
    END;

    LOCAL PROCEDURE GetToggleCodeDeleteMessage@1100528308(INSBOUAvailableMessage@1100528300 : Record 11020432) : Code[100];
    BEGIN
      EXIT(STRSUBSTNO('%1', INSBOUAvailableMessage."Vendor No."));
    END;

    LOCAL PROCEDURE SetMessageId@1100525007() : Text;
    BEGIN
      EXIT(STRSUBSTNO('%1-%2', ConsumedWebService.Code, ConsumedWebServiceLine."Method Name"));
    END;

    LOCAL PROCEDURE CreateAvailableMessageRec@1100528317(VendorNo@1100528308 : Code[20];MsgId@1100528307 : Text;MsgType@1100528306 : Code[20];MsgVersion@1100528305 : Code[20];MsgDateTime@1100528310 : Text;MsgFormat@1100528311 : Code[20]);
    VAR
      FileManagement@1100528309 : Codeunit 419;
      INSBOUAvailableMessageWS@1100528304 : XMLport 11072005;
      ServerFileName@1100528303 : Text;
      XMLFile@1100528302 : File;
      IStream@1100528301 : InStream;
      XMLDoc@1100528300 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
    BEGIN
      ServerFileName := FileManagement.ServerTempFileName('xml');
      XMLDoc := XMLDoc.XmlDocument;
      XMLDoc.LoadXml(STRSUBSTNO(
      '<GetAvailableMessagesResponse>'+
      '  <MessageServiceAvailableMessage>'+
      '    <MsgId>%1</MsgId>'+
      '    <MsgDateTime>%2</MsgDateTime>' +
      '    <AvailableFormat>'+
      '      <MsgFormat>%3</MsgFormat>'+
      '      <MsgVersion>%4</MsgVersion>'+
      '    </AvailableFormat>'+
      '    <MsgType>%5</MsgType>'+
      '  </MessageServiceAvailableMessage>' +
      '</GetAvailableMessagesResponse>',
        MsgId, MsgDateTime, MsgFormat, MsgVersion, MsgType));
      XMLDoc.Save(ServerFileName);

      XMLFile.OPEN(ServerFileName);
      XMLFile.CREATEINSTREAM(IStream);
      INSBOUAvailableMessageWS.SetVendorNo(VendorNo);
      INSBOUAvailableMessageWS.SETSOURCE(IStream);
      INSBOUAvailableMessageWS.IMPORT;
      XMLFile.CLOSE;
    END;

    EVENT ResponseXML@1100525007::NodeInserting@93(sender@1100525001 : Variant;e@1100525000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100525007::NodeInserted@94(sender@1100525001 : Variant;e@1100525000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100525007::NodeRemoving@95(sender@1100525001 : Variant;e@1100525000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100525007::NodeRemoved@96(sender@1100525001 : Variant;e@1100525000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100525007::NodeChanging@97(sender@1100525001 : Variant;e@1100525000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100525007::NodeChanged@98(sender@1100525001 : Variant;e@1100525000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    BEGIN
    {
       Text: LocalName = .//*[local-name()="%1"]
       STRSUBSTNO(LocalName, ...)

      MsgTypes:
      MsgType   MsgType
      OptionNr
       1  ORDERS Order (Customer -> Vendor)
       2  ORDRSP Order Response (Vendor -> Customer)
       3  DESADV Despatch Advice (Vendor -> Customer)
      13  INVOIC Invoice (Vendor -> Customer)
       7  PRICAT Item Data (Vendor -> Customer)
          PRODAT Product Data (Vendor -> Customer)
          TRMMSG Terms Message (conditiebestand) (Vendor -> Customer)
          REQOTE Quote Request (Vendor -> Customer)
          QUOTES Quote (Vendor -> Customer)
          ORDCHG Order Change (Customer -> Vendor)
          ORSSTA Order Status Request (Customer -> Vendor)
          ORDREP Order Status Report (Vendor -> Customer)
          COLINV Collect Invoice (Vendor -> Customer)
          INVRPT Inventory Report (Customer -> Vendor)
    }
    END.
  }
}

