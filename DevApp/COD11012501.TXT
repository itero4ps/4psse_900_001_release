OBJECT Codeunit 11012501 Cross List Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1100485000 : TextConst 'ENU=No Options present of Project ''%1''.';
      Text001@1100485001 : TextConst 'ENU=of the option is ''%1'', modify option only possible on the plot';

    PROCEDURE CopyOptions@1210190004(IProjectNo@1100485000 : Code[20]);
    VAR
      lvCopyProjOptionFrm@1210190000 : Page 11012544;
    BEGIN
      lvCopyProjOptionFrm.SetDefaultsCrossList(IProjectNo);
      lvCopyProjOptionFrm.StdDialogRunModal;
    END;

    PROCEDURE CopyOptionsSelection@1210190002(IProjectNo@1100485000 : Code[20]);
    VAR
      lvOptionRec@1210190002 : Record 11012502;
      lvOptionListFrm@1210190001 : Page 11012508;
    BEGIN
      lvOptionRec.FILTERGROUP(9);
      lvOptionRec.SETRANGE("Project No.", IProjectNo);
      lvOptionRec.SETRANGE("Plot No.", '');
      lvOptionRec.FILTERGROUP(0);
      IF NOT lvOptionRec.FINDSET(FALSE, FALSE) THEN
        ERROR(Text000, IProjectNo);

      lvOptionListFrm.SETTABLEVIEW(lvOptionRec);
      lvOptionListFrm.SETRECORD(lvOptionRec);
      lvOptionListFrm.SetDefaultCrossList(IProjectNo);
      lvOptionListFrm.RUNMODAL;
    END;

    PROCEDURE ProcessOptionCrossList@1100525001(ProjectNo@1100485000 : Code[20]);
    VAR
      lvCrossListRec@1210190000 : Record 11012515;
    BEGIN
      //rtc
      lvCrossListRec.SETRANGE("Project No.", ProjectNo);
      REPORT.RUNMODAL(REPORT::"Process Option Cross List", TRUE, TRUE, lvCrossListRec);
    END;

    PROCEDURE CopyCrossListToPlotOptions@1210190000(ICrossListRec@1100485000 : Record 11012515;IOptionLevel@1100485001 : Boolean);
    VAR
      lvCrossListRec@1210190000 : Record 11012515;
    BEGIN
      lvCrossListRec.SETRANGE("Project No.", ICrossListRec."Project No.");
      IF IOptionLevel THEN BEGIN
        lvCrossListRec.SETRANGE("House Model", ICrossListRec."House Model");
        lvCrossListRec.SETRANGE("Main Group", ICrossListRec."Main Group");
        lvCrossListRec.SETRANGE(Group, ICrossListRec.Group);
        lvCrossListRec.SETRANGE("Sub Group", ICrossListRec."Sub Group");
        lvCrossListRec.SETRANGE(Option, ICrossListRec.Option);
      END;
      REPORT.RUNMODAL(REPORT::"Process Option Cross List", TRUE, TRUE, lvCrossListRec);
    END;

    PROCEDURE EditCrossListMatrix@1210190001(ICrossListRec@1100485000 : Record 11012515);
    VAR
      lvCrossListRec@1210190000 : Record 11012515;
      OptionCrossListMatrix@1100525000 : Page 11012498;
    BEGIN
      lvCrossListRec.FILTERGROUP(9);
      lvCrossListRec.SETRANGE("Project No.", ICrossListRec."Project No.");
      lvCrossListRec.FILTERGROUP(0);
      lvCrossListRec."Project No." := ICrossListRec."Project No.";
      lvCrossListRec."House Model" := ICrossListRec."House Model";
      lvCrossListRec."Main Group":= ICrossListRec."Main Group";
      lvCrossListRec.Group := ICrossListRec.Group;
      lvCrossListRec."Sub Group" := ICrossListRec."Sub Group";
      lvCrossListRec.Option := ICrossListRec.Option;
      //RTC.sn
      OptionCrossListMatrix.SETTABLEVIEW(lvCrossListRec);
      OptionCrossListMatrix.SETRECORD(lvCrossListRec);
      OptionCrossListMatrix.RUNMODAL;
      //RTC.en
    END;

    PROCEDURE EditCrossListMatrix2@1100525000(IProjNo@1100485000 : Code[20]);
    VAR
      Proj@1210190000 : Record 11072003;
    BEGIN
      Proj.FILTERGROUP(9);
      Proj.SETRANGE("No.", IProjNo);
      Proj.FILTERGROUP(0);
      Proj."No." := IProjNo;
      PAGE.RUNMODAL(PAGE::"Option Cross List Matrix 2", Proj);
    END;

    PROCEDURE ExportOptCrosslistToExcel@1100485000(IProjectNo@1100485002 : Code[20]);
    VAR
      lvOptCrosslistRec@1100485001 : Record 11012515;
      lvExportOptCrosslistToExcelRep@1100485000 : Report 11012529;
    BEGIN
      lvOptCrosslistRec.SETRANGE("Project No.", IProjectNo);
      lvExportOptCrosslistToExcelRep.SETTABLEVIEW(lvOptCrosslistRec);
      lvExportOptCrosslistToExcelRep.RUNMODAL;
    END;

    PROCEDURE CheckPlotOptionStatus@1100485002(ICrossListLineRec@1100485002 : Record 11012516;IErrorMode@1100485000 : Boolean) : Boolean;
    VAR
      lvPlotOptionRec@1100485001 : Record 11012502;
      JobsSetup@1100530000 : Record 315;
    BEGIN
      JobsSetup.GET; //mg, 07-01-11: M19282
      WITH ICrossListLineRec DO BEGIN
        IF lvPlotOptionRec.GET("Project No.", "Plot No.", "House Model", "Main Group", Group, "Sub Group", Option) THEN BEGIN
          IF (lvPlotOptionRec.Status > lvPlotOptionRec.Status::Order) OR
            ((lvPlotOptionRec.Status = lvPlotOptionRec.Status::Order) AND
             NOT JobsSetup."Allow Change Ordered Options") THEN BEGIN //mg.c, 07-01-11: M19282
            IF IErrorMode THEN
              lvPlotOptionRec.FIELDERROR(Status, STRSUBSTNO(Text001, lvPlotOptionRec.Status))
            ELSE
              EXIT(FALSE);
           END;
        END;
      END;

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

