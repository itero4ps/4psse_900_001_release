OBJECT Codeunit 11012591 Plant E-mail Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ExitOrder@1100409007 : Record 11012559;
      PlantOrder@1100409008 : Record 11012556;
      TransportOrder@1100409002 : Record 11020505;
      TmpReportSelections1@1100409009 : TEMPORARY Record 77;
      TmpReportSelections2@1100409004 : TEMPORARY Record 77;
      MailDocsOrder@1100409011 : Boolean;
      MailDocsOrderLine@1100409010 : Boolean;

    PROCEDURE InitEmailProcess@1100409001(TableNo@1100409000 : Integer;UseDefaultReportSelection@1100409003 : Boolean;MailDocsOrder1@1100409006 : Boolean;MailDocsOrderLine1@1100409005 : Boolean;ReportSel@1100409009 : Integer);
    VAR
      CompanyInfo@1100409007 : Record 79;
      DocPrint@1100409004 : Codeunit 229;
      UsageReportSel@1100409002 : Option;
    BEGIN
      MailDocsOrder := MailDocsOrder1;
      MailDocsOrderLine := MailDocsOrderLine1;

      CASE TableNo OF
        DATABASE::"Exit Order":
          UsageReportSel := TmpReportSelections1.Usage::"1558";
        DATABASE::"Plant Order":
          UsageReportSel := TmpReportSelections1.Usage::"1555";  // Dispatch Note
        DATABASE::"Plant Transport Order":
          UsageReportSel := TmpReportSelections1.Usage::"1551";
      END;

      IF ReportSel > 0 THEN BEGIN
        IF NOT ((TableNo = DATABASE::"Plant Order") AND (ReportSel = 2)) THEN BEGIN
          CASE UseDefaultReportSelection OF
            TRUE: GetDefaultReportSelection(UsageReportSel, TmpReportSelections1);
            FALSE: DocPrint.GetReportSelection(UsageReportSel, TmpReportSelections1, TRUE);
          END;
        END;
        IF (TableNo = DATABASE::"Plant Order") AND ((ReportSel >= 2)) THEN BEGIN
          UsageReportSel := TmpReportSelections2.Usage::"1556";  // Removal Dispatch Note
          CASE UseDefaultReportSelection OF
            TRUE: GetDefaultReportSelection(UsageReportSel, TmpReportSelections2);
            FALSE: DocPrint.GetReportSelection(UsageReportSel, TmpReportSelections2, TRUE);
          END;
        END;
      END;
    END;

    PROCEDURE EmailExitOrder@1100409000(ExitOrder1@1100409000 : Record 11012559);
    VAR
      OrderCont@1100409001 : Record 11012536;
    BEGIN
      ExitOrder.COPY(ExitOrder1);
      ExitOrder.SETRANGE("No.", ExitOrder."No.");
      EmailOrder(DATABASE::"Exit Order", OrderCont.Type::ExitOrder, ExitOrder."No.", ExitOrder.Location, '');
    END;

    PROCEDURE EmailPlantOrder@1100409020(PlantOrder1@1100409000 : Record 11012556);
    VAR
      OrderCont@1100409001 : Record 11012536;
      UserSetup@1100525002 : Record 91;
      WebLogin@1100525000 : Record 11125660;
      ExtraEmail@1100525001 : Text;
    BEGIN
      PlantOrder.COPY(PlantOrder1);
      PlantOrder.SETRANGE("No.", PlantOrder."No.");
      IF (PlantOrder."Requested by" <> '') THEN BEGIN
        ExtraEmail := UserSetup.GetUserEmail(PlantOrder."Requested by");
        IF (ExtraEmail = '') AND WebLogin.READPERMISSION THEN BEGIN
          IF WebLogin.GET(PlantOrder."Requested by") THEN
            ExtraEmail := WebLogin."E-Mail";
        END;
      END;
      CASE PlantOrder.Type OF
        PlantOrder.Type::Arrival:
          EmailOrder(DATABASE::"Plant Order", OrderCont.Type::"PO To", PlantOrder."No.", PlantOrder."To Location", ExtraEmail);
        PlantOrder.Type::Removal:
          EmailOrder(DATABASE::"Plant Order", OrderCont.Type::"PO From", PlantOrder."No.", PlantOrder."From Location", ExtraEmail);
        PlantOrder.Type::"Other Transfers":
          BEGIN
            EmailOrder(DATABASE::"Plant Order", OrderCont.Type::"PO To", PlantOrder."No.", PlantOrder."To Location", '');
            EmailOrder(DATABASE::"Plant Order", OrderCont.Type::"PO From", PlantOrder."No.", PlantOrder."From Location", '');
          END;
      END;
    END;

    PROCEDURE EmailPlantOrderOnStatusChange@1100409008(PlantOrder1@1100409000 : Record 11012556);
    VAR
      PlantLoc@1100409001 : Record 11012554;
      PlantLocation1@1100409002 : Code[20];
      PlantLocation2@1100409003 : Code[20];
    BEGIN
      PlantOrder.COPY(PlantOrder1);
      PlantOrder.SETRANGE("No.", PlantOrder."No.");
      CASE PlantOrder.Type OF
        PlantOrder.Type::Arrival:
          PlantLocation1 := PlantOrder."To Location";
        PlantOrder.Type::Removal:
          PlantLocation1 := PlantOrder."From Location";
        PlantOrder.Type::"Other Transfers":
          BEGIN
            PlantLocation1 := PlantOrder."To Location";
            PlantLocation2 := PlantOrder."From Location";
          END;
      END;
      IF PlantLocation1 <> '' THEN BEGIN
        IF PlantLoc.GET(PlantLocation1) THEN BEGIN
          IF PlantLoc."E-Mail Status PO Modified" <> '' THEN
            MailHandler((11012556*10) + 1, PlantOrder."No.", PlantLoc."E-Mail Status PO Modified");
        END;
      END;
      IF PlantLocation2 <> '' THEN BEGIN
        IF PlantLoc.GET(PlantLocation2) THEN BEGIN
          IF PlantLoc."E-Mail Status PO Modified" <> '' THEN
            MailHandler((11012556*10) + 1, PlantOrder."No.", PlantLoc."E-Mail Status PO Modified");
        END;
      END;
    END;

    PROCEDURE EmailTransportOrder@1100409011(TransportOrder1@1100409000 : Record 11020505);
    VAR
      OrderCont@1100409001 : Record 11012536;
    BEGIN
      TransportOrder.COPY(TransportOrder1);
      TransportOrder.SETRANGE("No.", TransportOrder."No.");
      EmailOrder(DATABASE::"Plant Transport Order", OrderCont.Type::"TO To", TransportOrder."No.", TransportOrder."To Location", '');
    END;

    LOCAL PROCEDURE EmailOrder@1100409004(TableNo@1100409006 : Integer;OrderType@1100409000 : Option;OrderNo@1100409002 : Code[20];PlantLocation@1100409001 : Code[20];ExtraEmail@1100525000 : Text);
    VAR
      OrderCont@1100409005 : Record 11012536;
      PlantLocationCont@1100409004 : Record 11012594;
      PlantLoc@1100409003 : Record 11012554;
      Counter@1100409007 : Integer;
    BEGIN
      OrderCont.SETRANGE(Type, OrderType);
      OrderCont.SETRANGE("Order No.", OrderNo);
      IF OrderCont.FINDSET THEN BEGIN
        REPEAT
          IF OrderCont.ReceiveEmail() THEN BEGIN
            MailHandler(TableNo, OrderNo, OrderCont."E-Mail");
            IF OrderCont."E-Mail" = ExtraEmail THEN
              ExtraEmail := '';
            Counter := Counter + 1;
          END;
        UNTIL OrderCont.NEXT = 0;
      END;
      IF (PlantLocation <> '') AND (Counter = 0) THEN BEGIN  // Only if not already email to a Order Contact
        IF PlantLoc.GET(PlantLocation) THEN BEGIN
          IF PlantLoc."E-Mail" <> '' THEN BEGIN
            MailHandler(TableNo, OrderNo, PlantLoc."E-Mail");
            IF PlantLoc."E-Mail" = ExtraEmail THEN
              ExtraEmail := '';
          END;
        END;
      END;
      IF ExtraEmail <> '' THEN
        MailHandler(TableNo, OrderNo, ExtraEmail);
    END;

    LOCAL PROCEDURE MailHandler@1100409005(TableNo@1100409000 : Integer;OrderNo@1100409005 : Code[20];Email@1100409001 : Text[100]);
    VAR
      EMailSetup@1100528401 : Record 11229911;
      TmpReportSelections@1100409006 : TEMPORARY Record 77;
      TempEmailAttachment@1100528402 : TEMPORARY Record 11229294;
      DocumentPrint@1100528400 : Codeunit 229;
      DocumentMailing@1100528403 : Codeunit 260;
    BEGIN
      IF NOT ((TableNo = DATABASE::"Plant Order") AND (PlantOrder.Type = PlantOrder.Type::Removal)) THEN BEGIN
        IF TmpReportSelections1.FINDFIRST THEN BEGIN
          REPEAT
            TmpReportSelections := TmpReportSelections1;
            TmpReportSelections.INSERT;
          UNTIL TmpReportSelections1.NEXT = 0;
        END;
      END ELSE BEGIN
        IF TmpReportSelections2.FINDFIRST THEN BEGIN
          REPEAT
            TmpReportSelections := TmpReportSelections2;
            TmpReportSelections.INSERT;
          UNTIL TmpReportSelections2.NEXT = 0;
        END;
      END;
      IF TmpReportSelections.FINDSET THEN BEGIN
        REPEAT
          CASE TableNo OF
            DATABASE::"Exit Order":
              DocumentPrint.SaveAnyRecordReportAsPdf(
                ExitOrder, EMailSetup.Usage::"Plant Exit Order", ExitOrder."Department Code",
                TmpReportSelections."Report ID", TRUE, TempEmailAttachment);
            DATABASE::"Plant Order":
              DocumentPrint.SaveAnyRecordReportAsPdf(
                PlantOrder, EMailSetup.Usage::"Plant Order", PlantOrder."Department Code",
                TmpReportSelections."Report ID", TRUE, TempEmailAttachment);
            DATABASE::"Plant Transport Order":
              DocumentPrint.SaveAnyRecordReportAsPdf(
                TransportOrder,EMailSetup.Usage::"Plant Transport Order", TransportOrder."Department Code",
                TmpReportSelections."Report ID", TRUE, TempEmailAttachment)
          END;
        UNTIL TmpReportSelections.NEXT = 0;
      END;

      CASE TableNo OF
        DATABASE::"Exit Order":
          AddDocAttachmentsExitOrder(OrderNo, TempEmailAttachment);
        DATABASE::"Plant Order":
          AddDocAttachmentsPlantOrder(OrderNo, TempEmailAttachment);
        DATABASE::"Plant Transport Order":
          AddDocAttachmentsTransportOrder(OrderNo, TempEmailAttachment);
      END;

      COMMIT;
      DocumentMailing.SetTempEmailAttachments(
        TempEmailAttachment);
      IF TempEmailAttachment.FINDFIRST THEN;
      CASE TableNo OF
        DATABASE::"Exit Order":
          DocumentMailing.EmailFileFromPlantExitOrder(
            ExitOrder, TempEmailAttachment."Attachment Server File Path", '', '', Email, FALSE);
        DATABASE::"Plant Order":
          DocumentMailing.EmailFileFromPlantOrder(
            PlantOrder, TempEmailAttachment."Attachment Server File Path", '', '', Email, FALSE);
        (11012556*10) + 1:  // PO - Status Change
          DocumentMailing.EmailFileFromPlantOrderStatusChange(
            PlantOrder, TempEmailAttachment."Attachment Server File Path", '', '', Email, FALSE);
        DATABASE::"Plant Transport Order":
          DocumentMailing.EmailFileFromPlantTransportOrder(
            TransportOrder, TempEmailAttachment."Attachment Server File Path", '', '', Email, FALSE);
      END;
    END;

    PROCEDURE GetDefaultReportSelection@1100525018(UsageReport@1100525000 : Integer;VAR TmpReportSelections@1100525001 : TEMPORARY Record 77);
    VAR
      DocumentPrint@1100528400 : Codeunit 229;
    BEGIN
      DocumentPrint.GetDefaultReportSelection(UsageReport, TmpReportSelections);
    END;

    PROCEDURE AddDocAttachmentsExitOrder@1100409002(OrderNo@1100409002 : Code[20];VAR TempEmailAttachment@1100528400 : TEMPORARY Record 11229294);
    VAR
      ExitOrderLine@1100409000 : Record 11012560;
      RecRef@1100409001 : RecordRef;
    BEGIN
      //DP00183
      IF MailDocsOrder THEN BEGIN
        CLEAR(RecRef);
        RecRef.GETTABLE(ExitOrder);
        AddDocAttachments(RecRef, TempEmailAttachment);
      END;
      IF MailDocsOrderLine THEN BEGIN
        ExitOrderLine.SETRANGE("Exit-order No.", OrderNo);
        IF ExitOrderLine.FINDSET THEN BEGIN
          REPEAT
            CLEAR(RecRef);
            RecRef.GETTABLE(ExitOrderLine);
            AddDocAttachments(RecRef, TempEmailAttachment);
          UNTIL ExitOrderLine.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE AddDocAttachmentsPlantOrder@1100409006(OrderNo@1100409002 : Code[20];VAR TempEmailAttachment@1100528400 : TEMPORARY Record 11229294);
    VAR
      PlantOrderLine@1100409001 : Record 11012557;
      RecRef@1100409000 : RecordRef;
    BEGIN
      //DP00183
      IF MailDocsOrder THEN BEGIN
        CLEAR(RecRef);
        RecRef.GETTABLE(PlantOrder);
        AddDocAttachments(RecRef, TempEmailAttachment);
      END;
      IF MailDocsOrderLine THEN BEGIN
        PlantOrderLine.SETRANGE("Plant Order No.", OrderNo);
        IF PlantOrderLine.FINDSET THEN BEGIN
          REPEAT
            CLEAR(RecRef);
            RecRef.GETTABLE(PlantOrderLine);
            AddDocAttachments(RecRef, TempEmailAttachment);
          UNTIL PlantOrderLine.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE AddDocAttachmentsTransportOrder@1100409007(OrderNo@1100409002 : Code[20];VAR TempEmailAttachment@1100528400 : TEMPORARY Record 11229294);
    VAR
      RecRef@1100409001 : RecordRef;
    BEGIN
      IF MailDocsOrder THEN BEGIN
        CLEAR(RecRef);
        RecRef.GETTABLE(TransportOrder);
        AddDocAttachments(RecRef, TempEmailAttachment);
      END;
    END;

    PROCEDURE AddDocAttachments@1100529000(RecRef@1100529000 : RecordRef;VAR TempEmailAttachment@1100528400 : TEMPORARY Record 11229294);
    VAR
      DocumentLink@1100529001 : Record 11012747;
      DocumentProperties@1100529002 : Record 11012746;
      DocumentLinkManagement@1100528401 : Codeunit 11012401;
      DocumentPrint@1100528402 : Codeunit 229;
    BEGIN
      //DP000183
      DocumentLinkManagement.GetDocuments(DocumentProperties, RecRef);
      DocumentProperties.MARKEDONLY(TRUE);
      DocumentPrint.AddRelatedDocuments(TempEmailAttachment,DocumentProperties, FALSE, FALSE);
    END;

    BEGIN
    {
      Code of Plant E-mail reports R11012611/612/618 (Email TO/EO/PO) is moved to this codeunit
    }
    END.
  }
}

