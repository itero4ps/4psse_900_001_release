OBJECT Codeunit 11012827 Maintenance Sales And Cost Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=Total Installment Amounts %1 is not equal to Contract Amount %2';

    PROCEDURE GetMaintSlsAndCostsContract@1100525000(VAR lvServContractRec@1100525000 : Record 11012812;VAR lvTotalCostDec@1100525002 : Decimal;VAR lvTotalHoursDec@1100525004 : Decimal;VAR lvTotalRevenueDec@1100525003 : Decimal;VAR lvTotalLaborDec@1100525011 : Decimal;VAR lvTotalMaterialDec@1100525012 : Decimal;VAR lvTotalSubcontractingDec@1100525013 : Decimal;VAR lvTotalPlantDec@1100525014 : Decimal;VAR lvTotalSundryDec@1100525015 : Decimal;VAR lvTotalInstallmDec@1100526202 : Decimal;VAR lvTotalContrAmntDec@1100526214 : Decimal);
    VAR
      ContractObjectMaintenance@1100525001 : Record 11071692;
      lvNoDaysInt@1100525005 : Integer;
      lvStartDatePeriod@1100525006 : Date;
      lvEndDatePeriod@1100525007 : Date;
      ContractObject@1100526200 : Record 11071702;
      lvNoOfDaysYearInt@1100526215 : Integer;
      lvMaintenanceManagement@1100528600 : Codeunit 11012829;
      lvTotalSurcharge@1100528602 : Decimal;
      lvTotalInstallmDecFC@1100528800 : Decimal;
    BEGIN
      lvStartDatePeriod := lvServContractRec.GETRANGEMIN("Period Filter");
      lvEndDatePeriod := lvServContractRec.GETRANGEMAX("Period Filter");

      IF lvStartDatePeriod = 0D THEN
        lvStartDatePeriod := lvServContractRec."Starting Date";

      lvNoOfDaysYearInt := NoOfDaysPerYear(lvStartDatePeriod);

      lvTotalCostDec := 0;
      lvTotalRevenueDec := 0;
      lvTotalHoursDec := 0;
      lvTotalLaborDec := 0;
      lvTotalMaterialDec := 0;
      lvTotalSubcontractingDec := 0;
      lvTotalPlantDec := 0;
      lvTotalSundryDec := 0;
      lvTotalSurcharge := 0;

      ContractObject.SETRANGE("Service Contract No.",lvServContractRec."No.");
      IF ContractObject.FINDSET(FALSE,FALSE) THEN BEGIN
        REPEAT
          IF CalculateMaintRevenue(
            lvServContractRec, ContractObject, lvTotalRevenueDec, lvNoDaysInt, lvNoOfDaysYearInt, lvStartDatePeriod,lvEndDatePeriod, '')
          THEN BEGIN
            WITH ContractObjectMaintenance DO BEGIN
              SETRANGE("Service Contract No.",lvServContractRec."No.");
              SETRANGE("Object No.",ContractObject."Service Object No.");
              IF FINDSET(FALSE,FALSE) THEN BEGIN
                REPEAT
                  lvMaintenanceManagement.CalculateMaintBudgetVisit(ContractObjectMaintenance,lvServContractRec,lvTotalLaborDec,
                                                                     lvTotalHoursDec,lvTotalMaterialDec,lvTotalSubcontractingDec,
                                                                     lvTotalPlantDec,lvTotalSundryDec,lvTotalCostDec,lvTotalSurcharge,
                                                                     lvStartDatePeriod,
                                                                     lvEndDatePeriod);
                UNTIL NEXT = 0;
              END;
            END;
          END;
        UNTIL ContractObject.NEXT = 0;
      END;
      GetInstallmentAmount(lvServContractRec,lvTotalInstallmDec,lvTotalInstallmDecFC,
        lvTotalContrAmntDec,lvStartDatePeriod,lvEndDatePeriod);

      lvTotalCostDec += lvTotalSurcharge;
    END;

    PROCEDURE CalculateContractRevenue@1100528602(IServiceContractNo@1100528600 : Code[20];IServiceCategory@1100528611 : Code[10];IPeriodStartingDate@1100528601 : Date;IPeriodEndingDate@1100528602 : Date) : Decimal;
    VAR
      ServiceContract@1100528610 : Record 11012812;
      ContractObject@1100528609 : Record 11071702;
      NoOfDaysYear@1100528608 : Integer;
      NoOfDays@1100528607 : Integer;
      TotalRevenue@1100528606 : Decimal;
      TotalInstallmentAmount@1100528605 : Decimal;
      TotalInstallmentAmountFC@1100528604 : Decimal;
      TotalContractAmount@1100528603 : Decimal;
    BEGIN
      ServiceContract.GET(IServiceContractNo);
      IF ServiceContract."Contract Form" = ServiceContract."Contract Form"::"Framework Contract" THEN
        EXIT;
      IF IPeriodStartingDate = 0D THEN
        IPeriodStartingDate := ServiceContract."Starting Date";

      NoOfDaysYear := NoOfDaysPerYear(IPeriodStartingDate);

      IF ServiceContract."Invoice Type" = ServiceContract."Invoice Type"::Dynamic THEN BEGIN
        ContractObject.SETRANGE("Service Contract No.", IServiceContractNo);
        IF ContractObject.FINDSET THEN BEGIN
          REPEAT
            ContractObject.TESTFIELD("Invoice From");
            CalculateMaintRevenue(ServiceContract, ContractObject, TotalRevenue, NoOfDays, NoOfDaysYear,
              IPeriodStartingDate, IPeriodEndingDate, IServiceCategory);
          UNTIL ContractObject.NEXT = 0;
        END;
        EXIT(TotalRevenue);
      END ELSE BEGIN
        GetInstallmentAmountByCategoryWithUpdate(
          ServiceContract, IServiceCategory, TotalInstallmentAmount, TotalInstallmentAmountFC, TotalContractAmount,
          IPeriodStartingDate, IPeriodEndingDate, TRUE);
        EXIT(TotalInstallmentAmount);
      END;
    END;

    PROCEDURE CalculateMaintRevenue@1100526204(IServiceContract@1100526205 : Record 11012812;IContractObject@1100526200 : Record 11071702;VAR OTotalRevenue@1100526202 : Decimal;VAR ONoDays@1100526204 : Integer;INoOfDaysYear@1100526203 : Integer;IStartDatePeriod@1100526207 : Date;IEndDatePeriod@1100526206 : Date;IServiceCategory@1100528603 : Code[10]) InDateFilter : Boolean;
    VAR
      MaintenanceInvoiceMgt@1100528600 : Codeunit 11012828;
      Revenue@1100526201 : Decimal;
      RecordFound@1100409000 : Boolean;
      InvoiceFrom@1100525000 : Date;
      UnitPrice@1100528601 : Decimal;
      TotalDiscount@1100528602 : Decimal;
      DivisionPercentage@1100528604 : Decimal;
    BEGIN
      IContractObject.CALCFIELDS("Standard Object No.", "Object Group", Quantity);
      IF IContractObject."Ending Date" = 0D THEN
        IContractObject."Ending Date" := IEndDatePeriod;
      IF IStartDatePeriod <= IContractObject."Ending Date" THEN BEGIN
        IF IContractObject."Invoiced Until" <> 0D THEN
          InvoiceFrom := IContractObject."Invoiced Until" +1
        ELSE
          InvoiceFrom := IContractObject."Invoice From";
        IF InvoiceFrom = 0D THEN BEGIN
          IServiceContract.TESTFIELD("Starting Date");
          InvoiceFrom := IServiceContract."Starting Date";
        END;
        ONoDays := DetermineNoOfDays(IStartDatePeriod, IEndDatePeriod, InvoiceFrom, IContractObject."Ending Date");
        Revenue :=
          MaintenanceInvoiceMgt.GetSCObjectPriceAgreementByDat(
            IContractObject."Service Contract No.", IContractObject."Service Object No.", IEndDatePeriod, RecordFound);
        IF NOT RecordFound THEN
          Revenue := GetRate(IServiceContract."Customer No.",
                                  IEndDatePeriod,
                                  IContractObject."Object Group",
                                  IContractObject."Customer Object Group",
                                  IContractObject."Service Package");
        Revenue *= IContractObject.Quantity;
        UnitPrice := Revenue / INoOfDaysYear;
        OTotalRevenue += UnitPrice * ONoDays;
        IF IServiceContract."Dynamic Invoice Type" = IServiceContract."Dynamic Invoice Type"::Days THEN
          TotalDiscount := GetDynamicRevenueTotalDiscount(IContractObject, IStartDatePeriod, IEndDatePeriod, UnitPrice)
        ELSE
          TotalDiscount := GetDynamicFixedRevenueTotalDiscount(IContractObject, IStartDatePeriod, IEndDatePeriod, UnitPrice);
        OTotalRevenue += TotalDiscount;
        DivisionPercentage := IServiceContract.GetCategoryDivisionPercentage(IServiceCategory, IStartDatePeriod);
        OTotalRevenue *= DivisionPercentage / 100;
        EXIT(TRUE);
      END ELSE
        EXIT(FALSE);
    END;

    PROCEDURE GetInstallmentAmount@1100526203(IServiceContract@1100526200 : Record 11012812;VAR OTotalInstallmentAmount@1100526202 : Decimal;VAR OTotalInstallmentAmountFC@1100528800 : Decimal;VAR OTotalContractAmount@1100526201 : Decimal;IStartDatePeriod@1100526207 : Date;IEndDatePeriod@1100526206 : Date);
    BEGIN
      GetInstallmentAmountWithUpdate(
        IServiceContract, OTotalInstallmentAmount, OTotalInstallmentAmountFC, OTotalContractAmount,
        IStartDatePeriod, IEndDatePeriod, FALSE);
    END;

    PROCEDURE GetInstallmentAmountWithUpdate@1100528600(IServiceContract@1100526200 : Record 11012812;VAR OTotalInstallmentAmount@1100526202 : Decimal;VAR OTotalInstallmentAmountFC@1100528800 : Decimal;VAR OTotalContractAmount@1100526201 : Decimal;IStartDatePeriod@1100526207 : Date;IEndDatePeriod@1100526206 : Date;IUpdateAmounts@1100528602 : Boolean);
    BEGIN
      GetInstallmentAmountByCategoryWithUpdate(
        IServiceContract, '', OTotalInstallmentAmount, OTotalInstallmentAmountFC, OTotalContractAmount, IStartDatePeriod, IEndDatePeriod, IUpdateAmounts);
    END;

    PROCEDURE GetInstallmentAmountByCategoryWithUpdate@1100528601(IServiceContract@1100526200 : Record 11012812;IServiceCategory@1100528603 : Code[10];VAR OTotalInstallmentAmount@1100526202 : Decimal;VAR OTotalInstallmentAmountFC@1100528800 : Decimal;VAR OTotalContractAmount@1100526201 : Decimal;IStartDatePeriod@1100526207 : Date;IEndDatePeriod@1100526206 : Date;IUpdateAmounts@1100528602 : Boolean);
    VAR
      ContractInstallment@1100526203 : Record 11071707;
      Currency@1100528607 : Record 4;
      Date@1100528608 : Record 2000000007;
      StartContractDate@1100526205 : Date;
      EndContractDate@1100526204 : Date;
      NoDaysInt@1100526208 : Integer;
      MaintenanceInvoiceMgt@1100526215 : Codeunit 11012828;
      NoOfDaysYearInt@1100526216 : Integer;
      InstallmStartingDate@1100528601 : Date;
      InstallmEndingDate@1100528600 : Date;
      InvoiceInterval@1100525000 : DateFormula;
      InstallmentAmount@1100528604 : Decimal;
      InstallmentDiscountAmount@1100528610 : Decimal;
      InstallmentAmountFC@1100528605 : Decimal;
      InstallmentDiscountAmountFC@1100528611 : Decimal;
      DivisionPercentage@1100528606 : Decimal;
      InstallmentNoOfDays@1100528609 : Integer;
    BEGIN
      //Calculate Installment Amount
      WITH ContractInstallment DO BEGIN
        OTotalInstallmentAmount := 0;
        OTotalInstallmentAmountFC := 0;
        OTotalContractAmount := 0;
        IF IServiceContract."Invoice Type" = IServiceContract."Invoice Type"::Installments THEN BEGIN

          NoOfDaysYearInt := NoOfDaysPerYear(IStartDatePeriod);

          IF (IServiceContract."Starting Date" <> 0D) AND (IServiceContract."Starting Date" > IStartDatePeriod) THEN
            StartContractDate := IServiceContract."Starting Date"
          ELSE
            StartContractDate := IStartDatePeriod;

          IF (IServiceContract."Ending Date" <> 0D) AND (IServiceContract."Ending Date" < IEndDatePeriod) THEN
            EndContractDate := IServiceContract."Ending Date"
          ELSE
            EndContractDate := IEndDatePeriod;

          SETRANGE("Service Contract No.", IServiceContract."No.");
          IF FINDSET(FALSE,FALSE) THEN BEGIN
            REPEAT
              IF IServiceContract."Installments based on Progress" THEN
                InvoiceInterval := IServiceContract."Control Period Interval"
              ELSE
                InvoiceInterval := "Invoice Interval";
              IF FORMAT(InvoiceInterval) <> '' THEN BEGIN
                InstallmStartingDate := StartContractDate;
                InstallmEndingDate := EndContractDate;

                IF "Invoice From" <> 0D THEN
                  InstallmStartingDate := "Invoice From";
                IF ("Ending Date" <> 0D) AND ("Ending Date" < InstallmEndingDate) THEN
                  InstallmEndingDate := "Ending Date";
                REPEAT
                  IF InstallmStartingDate <= InstallmEndingDate THEN BEGIN
                    ContractInstallment."Invoiced Until" := InstallmStartingDate;
                    IF IUpdateAmounts AND (IServiceContract."Installment Type" = IServiceContract."Installment Type"::Object) THEN
                      MaintenanceInvoiceMgt.DetermineInstallmentAmount(IServiceContract."No.", InstallmStartingDate, TRUE);

                    Date.RESET;
                    Date.SETRANGE("Period Type", Date."Period Type"::Date);
                    Date.SETRANGE("Period Start", InstallmStartingDate, CALCDATE(InvoiceInterval, InstallmStartingDate) -1);
                    InstallmentNoOfDays := Date.COUNT;
                    Date.FILTERGROUP(2);
                    Date.SETRANGE("Period Start", IStartDatePeriod, IEndDatePeriod);

                    DivisionPercentage := IServiceContract.GetCategoryDivisionPercentage(IServiceCategory, InstallmStartingDate);

                    InstallmentAmount := MaintenanceInvoiceMgt.GetInstallmAmntLCYByDate(ContractInstallment) * DivisionPercentage / 100;
                    InstallmentDiscountAmount :=
                      -GetInstallmentRevenueTotalDiscount(ContractInstallment, InstallmStartingDate, CALCDATE(InvoiceInterval, InstallmStartingDate) -1, InstallmentAmount, TRUE)
                        * DivisionPercentage / 100;
                    InstallmentAmount := (InstallmentAmount - InstallmentDiscountAmount) * Date.COUNT / InstallmentNoOfDays;

                    InstallmentAmountFC := MaintenanceInvoiceMgt.GetInstallmAmntByDate(ContractInstallment) * DivisionPercentage / 100;
                    InstallmentDiscountAmountFC :=
                      -GetInstallmentRevenueTotalDiscount(ContractInstallment, InstallmStartingDate, CALCDATE(InvoiceInterval, InstallmStartingDate) -1, InstallmentAmountFC, FALSE)
                        * DivisionPercentage / 100;
                    InstallmentAmountFC := (InstallmentAmountFC - InstallmentDiscountAmountFC) * Date.COUNT / InstallmentNoOfDays;

                    OTotalInstallmentAmount += InstallmentAmount;
                    OTotalInstallmentAmountFC += InstallmentAmountFC;
                    InstallmStartingDate := CALCDATE(InvoiceInterval, InstallmStartingDate);
                  END;
                UNTIL InstallmStartingDate > InstallmEndingDate;
              END;
            UNTIL NEXT = 0;
          END;
          NoDaysInt := DetermineNoOfDays(IStartDatePeriod, IEndDatePeriod, IStartDatePeriod, IEndDatePeriod);
          OTotalContractAmount :=
            MaintenanceInvoiceMgt.GetContractAmntLCYByDate(IServiceContract, IEndDatePeriod) / NoOfDaysYearInt * NoDaysInt;
        END;
      END;
      Currency.InitRoundingPrecision;
      OTotalInstallmentAmount := ROUND(OTotalInstallmentAmount, Currency."Amount Rounding Precision");
      OTotalContractAmount := ROUND(OTotalContractAmount, Currency."Amount Rounding Precision");
      IF NOT Currency.GET(IServiceContract."Currency Code") THEN
        Currency.InitRoundingPrecision;
      OTotalInstallmentAmountFC := ROUND(OTotalInstallmentAmountFC, Currency."Amount Rounding Precision");
    END;

    PROCEDURE DetermineNoOfDays@1100526200(lvStartDatePeriod@1100526200 : Date;lvEndDatePeriod@1100526201 : Date;lvStartingDate@1100526203 : Date;lvEndingDate@1100526202 : Date) lvNoDaysInt : Integer;
    BEGIN
      IF lvEndingDate = 0D THEN
        lvEndingDate := lvEndDatePeriod;

      lvNoDaysInt := lvEndingDate - lvStartingDate +1;

      IF lvStartingDate < lvStartDatePeriod THEN
        lvNoDaysInt := lvNoDaysInt - (lvStartDatePeriod - lvStartingDate);

      IF lvEndingDate > lvEndDatePeriod THEN
        lvNoDaysInt := lvNoDaysInt - (lvEndingDate - lvEndDatePeriod);

      IF lvNoDaysInt < 0 THEN
        EXIT(0)
      ELSE
        EXIT(lvNoDaysInt);
    END;

    PROCEDURE GetMaintSlsRateContrObjLCY@1100525002(lvObjectByContractRec@1100525000 : Record 11071702) MaintSalesRate : Decimal;
    VAR
      lvContractRec@1100525001 : Record 11012812;
      lvIndexDate@1100528600 : Date;
    BEGIN
      // dp00116: GetMaintSlsRateContrObj renamed to GetMaintSlsRateContrObjLCY
      IF lvContractRec.GET(lvObjectByContractRec."Service Contract No.") THEN BEGIN
        lvObjectByContractRec.CALCFIELDS("Standard Object No.");
        lvObjectByContractRec.CALCFIELDS("Object Group", "Invoice Interval");

        IF (lvContractRec."Invoiced Until" <> 0D) AND (FORMAT(lvContractRec."Invoice Interval") <> '')THEN
          lvIndexDate := CALCDATE(lvContractRec."Invoice Interval", lvContractRec."Invoiced Until")
        ELSE
          lvIndexDate := lvObjectByContractRec."Invoice From";

        EXIT(GetRate(lvContractRec."Customer No.",
                     lvIndexDate,
                     lvObjectByContractRec."Object Group",
                     lvObjectByContractRec."Customer Object Group",
                     lvObjectByContractRec."Service Package"));
      END ELSE
        EXIT(0);
    END;

    PROCEDURE GetMaintSlsRateContrObj@1100528800(lvObjectByContractRec@1100525000 : Record 11071702) MaintSalesRate : Decimal;
    VAR
      lvContractRec@1100525001 : Record 11012812;
      lvIndexDate@1100528600 : Date;
      ServiceRateLCY@1100528801 : Decimal;
    BEGIN
      // dp00116.n
      IF lvContractRec.GET(lvObjectByContractRec."Service Contract No.") THEN BEGIN
        lvObjectByContractRec.CALCFIELDS("Standard Object No.");
        lvObjectByContractRec.CALCFIELDS("Object Group", "Invoice Interval");

        IF (lvContractRec."Invoiced Until" <> 0D) AND (FORMAT(lvContractRec."Invoice Interval") <> '')THEN
          lvIndexDate := CALCDATE(lvContractRec."Invoice Interval", lvContractRec."Invoiced Until")
        ELSE
          lvIndexDate := lvObjectByContractRec."Invoice From";

        ServiceRateLCY := GetRate(lvContractRec."Customer No.",
                     lvIndexDate,
                     lvObjectByContractRec."Object Group",
                     lvObjectByContractRec."Customer Object Group",
                     lvObjectByContractRec."Service Package");

        EXIT(CalcServiceRateFromLCY(ServiceRateLCY,lvContractRec."Currency Code"));
      END ELSE
        EXIT(0);
    END;

    PROCEDURE GetRate@1100525003(ICustomerCde@1100525000 : Code[20];IIndexDate@1100525001 : Date;IObjectGroupCde@1100525002 : Code[10];ICustomerObjectGroupCde@1100526200 : Code[10];IServicePackageCde@1100525003 : Code[20]) RateDec : Decimal;
    VAR
      MaintenanceSalesRate@1100525004 : Record 11020341;
    BEGIN
      IF IIndexDate = 0D THEN
        IIndexDate := TODAY();
      WITH MaintenanceSalesRate DO BEGIN
        SETRANGE("Service Package", IServicePackageCde);
        IF ICustomerObjectGroupCde = '' THEN BEGIN
          SETRANGE("Object Group", IObjectGroupCde);
          SETRANGE("Object Group Type", "Object Group Type"::Standard);
        END ELSE BEGIN
          SETRANGE("Object Group", ICustomerObjectGroupCde);
          SETRANGE("Object Group Type", "Object Group Type"::Customer);
        END;
        SETRANGE("Customer No.", ICustomerCde);
        SETRANGE("Reference Date", 0D, IIndexDate);
        IF FINDLAST THEN
          RateDec := "Sales Rate";

        IF RateDec = 0 THEN
          IF ICustomerObjectGroupCde <> '' THEN BEGIN
            SETRANGE("Object Group", IObjectGroupCde);
            SETRANGE("Object Group Type", "Object Group Type"::Standard);
            IF FINDLAST THEN
              RateDec := "Sales Rate";
          END;

        IF RateDec = 0 THEN BEGIN
          SETRANGE("Customer No.", '');
          IF FINDLAST THEN
            RateDec := "Sales Rate";
        END;
      END;
      RateDec *= 1 - GetDiscountPerc(ICustomerCde, IIndexDate) / 100;
    END;

    PROCEDURE GetDiscountPerc@1100526201(ICustomerCde@1100526205 : Code[20];IIndexDate@1100526204 : Date) DiscountDec : Decimal;
    VAR
      MaintenanceSalesDiscount@1100526200 : Record 11071709;
      Customer@1100526206 : Record 18;
    BEGIN
      WITH MaintenanceSalesDiscount DO BEGIN
        SETCURRENTKEY("Customer No.");
        SETRANGE("Index Date", 0D, IIndexDate);
        SETRANGE("Customer No.", ICustomerCde);
        IF ISEMPTY THEN BEGIN
          IF NOT Customer.GET(ICustomerCde) THEN
            Customer.INIT;
          SETCURRENTKEY("Customer Disc. Group");
          SETRANGE("Customer No.", '');
          SETRANGE("Customer Disc. Group", Customer."Customer Disc. Group");
        END;
        IF ISEMPTY THEN BEGIN
          SETRANGE("Customer No.", '');
          SETRANGE("Customer Disc. Group", '');
        END;
        IF FINDLAST THEN
          DiscountDec := "Discount %";
      END;
    END;

    PROCEDURE NoOfDaysPerYear@1100526202(IStartDate@1100525000 : Date) : Integer;
    VAR
      EndDate@1100525004 : Date;
    BEGIN
      EndDate := CALCDATE('<+1Y>', IStartDate);
      EXIT(EndDate - IStartDate);
    END;

    PROCEDURE CalcServiceRateFromLCY@1100528802(ContractRateLCY@1100528803 : Decimal;CurrencyCode@1100528804 : Code[10]) ContractRate : Decimal;
    VAR
      CurrencyDate@1100528802 : Date;
      CurrencyExchangeRate@1100528801 : Record 330;
      Currency2@1100528800 : Record 4;
    BEGIN
      // dp00116.n
      Currency2.InitRoundingPrecision;
      IF CurrencyCode <> '' THEN BEGIN
        CurrencyDate := TODAY;
        ContractRate :=
        ROUND(
          CurrencyExchangeRate.ExchangeAmtLCYToFCY(
            0, '', CurrencyDate, CurrencyCode, ContractRateLCY,
            CurrencyExchangeRate.ExchangeRate(0, '', CurrencyDate, CurrencyCode,TRUE),TRUE),
            Currency2."Amount Rounding Precision");
      END ELSE BEGIN
        ContractRate := ROUND(ContractRateLCY,Currency2."Amount Rounding Precision");
      END;
    END;

    PROCEDURE CheckInstallAmountsWithContractAmount@1100528605(IServiceContractNo@1100528601 : Code[20];IType@1100528600 : 'Service Contract,Installment,Price Agreement');
    VAR
      ServiceContract@1100525001 : Record 11012812;
      GetMaintenanceSalesAndCost@1100525000 : Codeunit 11012827;
      TotalInstallmentAmount@1100525002 : Decimal;
      TotalInstallmentAmountFCY@1100525003 : Decimal;
      ContractAmount@1100525004 : Decimal;
    BEGIN
      IF NOT (IType IN [IType::Installment, IType::"Service Contract"]) THEN
        EXIT;
      IF NOT ServiceContract.GET(IServiceContractNo) THEN
        EXIT;
      TotalInstallmentAmount := GetMaintenanceSalesAndCost.GetInstallmentAmountPerYear(ServiceContract, CALCDATE('<CY>'), FALSE, ContractAmount);
      IF TotalInstallmentAmount <> ContractAmount THEN
        MESSAGE(Text000, TotalInstallmentAmount, ContractAmount);
    END;

    LOCAL PROCEDURE GetInstallmentRevenueTotalDiscount@1100528633(IContractInstallment@1100528600 : Record 11071707;IInvoiceFrom@1100528601 : Date;IInvoiceUntil@1100528602 : Date;IUnitPrice@1100528609 : Decimal;ILCY@1100528613 : Boolean) UnitPrice : Decimal;
    VAR
      Date@1100528603 : Record 2000000007;
      ServiceSalesDiscSurch@1100528606 : Record 11072237;
      MaintenanceInvoiceMgt@1100528611 : Codeunit 11012828;
      LastDiscountCalcStartDate@1100528604 : Date;
      LastDiscountCalcEndDate@1100528605 : Date;
      DiscountPeriodEnd@1100528607 : Date;
      AmountPerMonth@1100528610 : Decimal;
    BEGIN
      Date.SETRANGE("Period Type", Date."Period Type"::Month);
      Date.SETRANGE("Period Start", IInvoiceFrom, IInvoiceUntil);
      IF NOT Date.FINDSET THEN
        EXIT;
      LastDiscountCalcStartDate := IInvoiceFrom;
      REPEAT
        IF IContractInstallment.GetServiceSalesDiscount(Date."Period Start", ServiceSalesDiscSurch, DiscountPeriodEnd) THEN BEGIN
          IF ServiceSalesDiscSurch.Percentage <> 0 THEN BEGIN
            AmountPerMonth := IUnitPrice / MaintenanceInvoiceMgt.ConvertPeriodToNoOfMonths(IContractInstallment."Invoice Interval");
            UnitPrice += -AmountPerMonth * ServiceSalesDiscSurch.Percentage / 100
          END ELSE
            IF ILCY THEN
              UnitPrice += -ServiceSalesDiscSurch."Amount (LCY)" / MaintenanceInvoiceMgt.ConvertPeriodToNoOfMonths(ServiceSalesDiscSurch.Period)
            ELSE
              UnitPrice += -ServiceSalesDiscSurch.Amount / MaintenanceInvoiceMgt.ConvertPeriodToNoOfMonths(ServiceSalesDiscSurch.Period);
        END;
      UNTIL Date.NEXT = 0;
    END;

    LOCAL PROCEDURE GetDynamicRevenueTotalDiscount@1100528606(IContractObject@1100528603 : Record 11071702;IPeriodStart@1100528602 : Date;IPeriodEnd@1100528600 : Date;IUnitPrice@1100528604 : Decimal) TotalDiscount : Decimal;
    VAR
      Date@1100528601 : Record 2000000007;
      ServiceSalesDiscSurch@1100528606 : Record 11072237;
      LastServiceSalesDiscSurch@1100528608 : Record 11072237;
      LastDiscountCalcStartDate@1100528605 : Date;
      LastDiscountCalcEndDate@1100528611 : Date;
      DiscountPeriodEnd@1100528607 : Date;
      UnitPrice@1100528610 : Decimal;
    BEGIN
      LastDiscountCalcStartDate := IPeriodStart;
      IF LastDiscountCalcStartDate < IContractObject."Invoice From" THEN
        LastDiscountCalcStartDate := IContractObject."Invoice From";
      LastDiscountCalcEndDate := IPeriodEnd;
      Date.SETRANGE("Period Type", Date."Period Type"::Month);
      Date.SETRANGE("Period Start", LastDiscountCalcStartDate, IPeriodEnd);
      IF NOT Date.FINDSET THEN
        EXIT;
      REPEAT
        IF IContractObject.GetServiceSalesDiscount(Date."Period Start", ServiceSalesDiscSurch, DiscountPeriodEnd) THEN BEGIN
          IF FORMAT(LastServiceSalesDiscSurch) <> FORMAT(ServiceSalesDiscSurch) THEN BEGIN
            IF LastServiceSalesDiscSurch.Code <> '' THEN BEGIN
              UnitPrice := -IUnitPrice * LastServiceSalesDiscSurch.Percentage / 100;
              TotalDiscount += UnitPrice * (LastDiscountCalcEndDate - LastDiscountCalcStartDate +1);
              LastDiscountCalcStartDate := LastDiscountCalcEndDate +1;
              IF LastDiscountCalcStartDate < ServiceSalesDiscSurch."Starting Date" THEN
                LastDiscountCalcStartDate := ServiceSalesDiscSurch."Starting Date";
              UnitPrice := 0;
            END;
            LastServiceSalesDiscSurch := ServiceSalesDiscSurch;
          END;

          LastDiscountCalcEndDate := NORMALDATE(Date."Period End");
          IF LastDiscountCalcEndDate > IContractObject."Ending Date" THEN
            LastDiscountCalcEndDate := IContractObject."Ending Date";
          IF LastDiscountCalcEndDate > IPeriodEnd THEN
            LastDiscountCalcEndDate := IPeriodEnd;
          IF LastDiscountCalcEndDate > DiscountPeriodEnd THEN
            LastDiscountCalcEndDate := DiscountPeriodEnd;
        END;
      UNTIL Date.NEXT = 0;
      UnitPrice := -IUnitPrice * LastServiceSalesDiscSurch.Percentage / 100;
      TotalDiscount += UnitPrice * (LastDiscountCalcEndDate - LastDiscountCalcStartDate +1);
    END;

    LOCAL PROCEDURE GetDynamicFixedRevenueTotalDiscount@1100528603(IContractObject@1100528603 : Record 11071702;IPeriodStart@1100528602 : Date;IPeriodEnd@1100528601 : Date;IUnitPrice@1100528600 : Decimal) TotalDiscount : Decimal;
    VAR
      Date@1100528610 : Record 2000000007;
      ServiceSalesDiscSurch@1100528609 : Record 11072237;
      LastServiceSalesDiscSurch@1100528608 : Record 11072237;
      MaintenanceInvoiceMgt@1100528611 : Codeunit 11012828;
      LastDiscountCalcStartDate@1100528607 : Date;
      LastDiscountCalcEndDate@1100528606 : Date;
      DiscountPeriodEnd@1100528605 : Date;
      AmountPerMonth@1100528604 : Decimal;
    BEGIN
      LastDiscountCalcStartDate := IPeriodStart;
      IF LastDiscountCalcStartDate < IContractObject."Invoice From" THEN
        LastDiscountCalcStartDate := IContractObject."Invoice From";
      Date.SETRANGE("Period Type", Date."Period Type"::Month);
      Date.SETRANGE("Period Start", LastDiscountCalcStartDate, IPeriodEnd);
      IF NOT Date.FINDSET THEN
        EXIT;
      REPEAT
        IF IContractObject.GetServiceSalesDiscount(Date."Period Start", ServiceSalesDiscSurch, DiscountPeriodEnd) THEN BEGIN
          IF ServiceSalesDiscSurch.Percentage <> 0 THEN BEGIN
            AmountPerMonth := IContractObject.GetObjectInvAmountPerYear(Date."Period End") / 12;
            TotalDiscount += -AmountPerMonth * ServiceSalesDiscSurch.Percentage / 100
          END ELSE
            TotalDiscount += -ServiceSalesDiscSurch.Amount / MaintenanceInvoiceMgt.ConvertPeriodToNoOfMonths(ServiceSalesDiscSurch.Period);
        END;
      UNTIL Date.NEXT = 0;
    END;

    PROCEDURE GetInstallmentAmountPerYear@1100528604(IServiceContract@1100526200 : Record 11012812;ISeekDate@1100526207 : Date;IUpdateAmounts@1100528604 : Boolean;VAR OTotalContractAmount@1100528610 : Decimal) : Decimal;
    VAR
      ContractInstallment@1100528600 : Record 11071707;
      MaintenanceManagement@1100528603 : Codeunit 11012829;
      MaintenanceInvoiceMgt@1100528605 : Codeunit 11012828;
      InvoiceInterval@1100528602 : DateFormula;
      Factor@1100528601 : Decimal;
      DivisionPercentage@1100528606 : Decimal;
      InstallmentAmount@1100528607 : Decimal;
      InstallmentDiscountAmount@1100528608 : Decimal;
      TotalInstallmentAmount@1100528609 : Decimal;
    BEGIN
      IF IServiceContract."Invoice Type" <> IServiceContract."Invoice Type"::Installments THEN
        EXIT;

      ContractInstallment.SETRANGE("Service Contract No.", IServiceContract."No.");
      ContractInstallment.SETFILTER("Ending Date", '%1|>=%2', 0D, ISeekDate);
      IF ContractInstallment.FINDSET THEN BEGIN
        REPEAT
          IF IServiceContract."Installments based on Progress" THEN
            InvoiceInterval := IServiceContract."Control Period Interval"
          ELSE
            InvoiceInterval := ContractInstallment."Invoice Interval";
          Factor := MaintenanceManagement.ConvPeriodToTimesPerYear(InvoiceInterval);

          IF IUpdateAmounts AND (IServiceContract."Installment Type" = IServiceContract."Installment Type"::Object) THEN
            MaintenanceInvoiceMgt.DetermineInstallmentAmount(IServiceContract."No.", ISeekDate, TRUE);

          DivisionPercentage := IServiceContract.GetCategoryDivisionPercentage('', ISeekDate);

          InstallmentAmount := MaintenanceInvoiceMgt.GetInstallmAmntLCYByDate(ContractInstallment) * DivisionPercentage / 100;
          InstallmentDiscountAmount :=
            -GetInstallmentRevenueTotalDiscount(ContractInstallment, ISeekDate, CALCDATE(InvoiceInterval, ISeekDate) -1, InstallmentAmount, TRUE)
               * DivisionPercentage / 100;
          InstallmentAmount -= InstallmentDiscountAmount;
          InstallmentAmount *= Factor;

          TotalInstallmentAmount += InstallmentAmount;
        UNTIL ContractInstallment.NEXT = 0;
      END;
      OTotalContractAmount :=
        MaintenanceInvoiceMgt.GetContractAmntLCYByDate(IServiceContract, ISeekDate);

      EXIT(TotalInstallmentAmount);
    END;

    BEGIN
    END.
  }
}

