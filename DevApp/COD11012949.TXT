OBJECT Codeunit 11012949 Document Portal Serv Location
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      SharePointSetup4PS@1100528302 : Record 11012730;
      DocumentPortalManagement@1100528300 : Codeunit 11012203;
      ErrorString@1100528311 : Text;
      InternTxt@1100528310 : Text[10];
      ExternTxt@1100528309 : Text[10];
      NavCompanyId@1100528308 : Text[50];
      WebOfficeAppServerGuid@1100528307 : Text[50];
      CompanyLogoFeatureGuid@1100528306 : Text[50];
      DesignFeatureGuid@1100528305 : Text[50];
      FullDocumentPortalURL@1100528303 : Text;
      Debug@1100528329 : Boolean;
      DebugText@1100528328 : Text;
      GlobalVarsAreSet@1100528301 : Boolean;
      Text000@1100528316 : TextConst 'ENU=Location Portal...\\#1########\#2########\#3########\#4########\#5########\#6########';
      Text001@1100528315 : TextConst 'ENU=Check: %1';
      Text002@1100528314 : TextConst 'ENU=Change: %1';
      Text003@1100528313 : TextConst 'ENU=Connect to:';
      Text004@1100528312 : TextConst 'ENU=Error while communicating with the Document Portal.\\Error Message:\%1';
      LoggedIn@1100528320 : Boolean;
      WindowIsOpen@1100528319 : Boolean;
      Window@1100528318 : Dialog;
      ProjectSite@1100528317 : DotNet "'FPS.Microsoft.SharePoint.Client, Version=1.40.0.0, Culture=neutral, PublicKeyToken=284c6828aa038142'.FPS.Microsoft.SharePoint.Client.ProjectSite" RUNONCLIENT;
      Text005@1100528321 : TextConst 'ENU=Document checked out by you or someone else.';
      Text006@1100528322 : TextConst 'ENU=Connection is running correctly.';
      Text015@1100528323 : TextConst 'ENU=There can only be 1 %1 Records with %2 %3.';
      Text017@1100528327 : TextConst 'ENU=You have no permission to execute this action.\\%1';
      Text017a@1100528326 : TextConst 'ENU=(User %1 not present)';
      Text017b@1100528325 : TextConst 'ENU=(Employee %1 is not linked to this Project)';
      Text017c@1100528324 : TextConst 'ENU=(Not all Owner permissions defined for Emplyee %1)';

    LOCAL PROCEDURE SetGlobalVars@1100528300(ServiceLocation@1100528300 : Record 11012801);
    BEGIN
      IF (GlobalVarsAreSet) THEN
        EXIT;
      SharePointSetup4PS.GET;
      SharePointSetup4PS.TESTFIELD("Use Location Portal", TRUE);

      InternTxt := DocumentPortalManagement.GetInternTxt;
      ExternTxt := DocumentPortalManagement.GetExternTxt;
      NavCompanyId := DocumentPortalManagement.GetNavCompanyId;
      WebOfficeAppServerGuid := DocumentPortalManagement.GetWebOfficeAppServerGuid;
      CompanyLogoFeatureGuid := DocumentPortalManagement.GetCompanyLogoFeatureGuid;
      DesignFeatureGuid := DocumentPortalManagement.GetDesignFeatureGuid;

      IF (ServiceLocation.IsServLocInLocationPortal) THEN
        FullDocumentPortalURL := ServiceLocation."Location Portal URL"
      ELSE
        FullDocumentPortalURL := STRSUBSTNO('%1/%2/%3_%4', SharePointSetup4PS."Portal URL", SharePointSetup4PS."Portal URL Suffix",
          COMPANYNAME, ServiceLocation."No.");

      IF (ISNULL(ProjectSite)) THEN
        ProjectSite := ProjectSite.ProjectSite;
    END;

    LOCAL PROCEDURE MakeConnection@1100528301(ServiceLocation@1100528300 : Record 11012801;DoLogin@1100528301 : Boolean);
    VAR
      DocumentPortalManagement@1100528302 : Codeunit 11012203;
    BEGIN
      SetGlobalVars(ServiceLocation);
      UpdateWindow(STRSUBSTNO(Text001), FullDocumentPortalURL, '', '', '', '');
      DocumentPortalManagement.CheckDocumentPortalSetup;
      UpdateWindow(STRSUBSTNO(Text003), FullDocumentPortalURL, '', '', '', '');

      IF (DoLogin) AND (NOT LoggedIn) THEN BEGIN
        IF (NOT ProjectSite.LoginProjectSiteCurrentUser(FullDocumentPortalURL, ErrorString)) THEN
          ThrowError(TRUE, 'LoginProjectSite');
        LoggedIn := TRUE;
      END;
    END;

    LOCAL PROCEDURE MakeConnectionAsAdmin@1100528329(ServiceLocation@1100528300 : Record 11012801;DoLogin@1100528302 : Boolean;ShowError@1100528303 : Boolean) : Boolean;
    VAR
      DocumentPortalManagement@1100528301 : Codeunit 11012203;
    BEGIN
      SetGlobalVars(ServiceLocation);
      UpdateWindow(STRSUBSTNO(Text001), FullDocumentPortalURL, '', '', '', '');
      DocumentPortalManagement.CheckDocumentPortalSetup;
      UpdateWindow(STRSUBSTNO(Text003), FullDocumentPortalURL, '', '', '', '');
      IF (ISNULL(ProjectSite)) THEN
        ProjectSite := ProjectSite.ProjectSite;

      IF (DoLogin) AND (NOT LoggedIn) THEN BEGIN
        IF (NOT ProjectSite.LoginProjectSite(FullDocumentPortalURL, SharePointSetup4PS."Owner ID",
          SharePointSetup4PS."Owner Password", SharePointSetup4PS."Owner Domain", ErrorString))
        THEN BEGIN
          ThrowError(ShowError, STRSUBSTNO('LoginProjectSite (Project: %1)', ServiceLocation."No."));
          EXIT(FALSE);
        END;
        LoggedIn := TRUE;
      END;
      EXIT(TRUE);
    END;

    PROCEDURE CheckWebserviceConnection@1100528331(ServiceLocation@1100528300 : Record 11012801);
    BEGIN
      SetGlobalVars(ServiceLocation);
      ServiceLocation.TESTFIELD("Location Portal Site Generated", TRUE);
      ServiceLocation.TESTFIELD("Location Portal URL");

      MakeConnection(ServiceLocation, TRUE);
      IF (NOT ProjectSite.CheckWebServiceConnection(ErrorString)) THEN
        ThrowError(TRUE, 'CheckWebServiceConnection');
      MESSAGE(Text006);
    END;

    PROCEDURE CheckFBAConnection@1100528306();
    VAR
      DocumentPortalManagement@1100528300 : Codeunit 11012203;
      CallStack@1100528301 : Text;
    BEGIN
      DocumentPortalManagement.SetGlobalVariables(1, Debug); //Project
      IF (ISNULL(ProjectSite)) THEN
        ProjectSite := ProjectSite.ProjectSite;
      IF (NOT DocumentPortalManagement.CheckFBAConnection(ProjectSite, CallStack, ErrorString)) THEN
        ThrowError(TRUE, CallStack);
    END;

    LOCAL PROCEDURE CheckLocation@1100525026(ServiceLocation@1100525000 : Record 11012801);
    BEGIN
      UpdateWindow(STRSUBSTNO(Text001, ServiceLocation.TABLECAPTION), ServiceLocation."No.", '', '', '', '');
      ServiceLocation.TESTFIELD("Make Location Portal  Site", TRUE);
      ServiceLocation.TESTFIELD("Location Portal Site Generated", FALSE);
      ServiceLocation.TESTFIELD("Location Portal URL", '');
      CheckLocationCommon(ServiceLocation);
    END;

    LOCAL PROCEDURE CheckLocationCommon@1100525042(ServiceLocation@1100525000 : Record 11012801);
    VAR
      UserSetup@1100525001 : Record 91;
      EmployeeNo@1100525002 : Code[20];
    BEGIN
      IF (ServiceLocation."Location Manager" <> '') THEN BEGIN
        UserSetup.SETRANGE("Employee No.", ServiceLocation."Location Manager");
        UserSetup.FINDFIRST;
        IF (UserSetup.COUNT <> 1) THEN
          ERROR(Text015, UserSetup.TABLECAPTION, UserSetup.FIELDCAPTION("Employee No."), UserSetup."Employee No.");
      END;

      //Check current USERID
      UserSetup.RESET;
      UserSetup.SETRANGE("User ID", USERID);
      UserSetup.FINDFIRST;
      UserSetup.TESTFIELD("Employee No.");
      EmployeeNo := UserSetup."Employee No.";

      UserSetup.RESET;
      UserSetup.SETRANGE("Employee No.", EmployeeNo);
      IF (UserSetup.COUNT <> 1) THEN
        ERROR(Text015, UserSetup.TABLECAPTION, UserSetup.FIELDCAPTION("Employee No."), EmployeeNo);

      {
      ProjectResponsiblePerson.SETRANGE("Project No.", Job."No.");
      ProjectResponsiblePerson.SETRANGE("Employee No.", EmployeeNo);
      ProjectResponsiblePerson.FINDFIRST;

      RespPersPermPPT.SETRANGE(Type, RespPersPermPPT.Type::Access);
      RespPersPermPPT.SETRANGE("Project No.", Job."No.");
      RespPersPermPPT.SETRANGE("Line No.", ProjectResponsiblePerson."Line No.");
      RespPersPermPPT.FINDFIRST;
      REPEAT
        RespPersPermPPT.TESTFIELD("Permission Level", RespPersPermPPT."Permission Level"::Owner);
      UNTIL (RespPersPermPPT.NEXT = 0);
      }
    END;

    LOCAL PROCEDURE CheckActionPermission@1100525009(Job@1100525000 : Record 11072003;Action@1100525001 : 'CreateSite,IsAdmin,DeActivateSite,ActivateSite,UpdateStaticProjectContent,CreateQuoteSite,UpdateStaticQuoteContent,UpdateQuoteRequests');
    VAR
      UserSetup@1100525002 : Record 91;
      ProjectResponsiblePerson@1100525003 : Record 11012040;
      RespPersPermPPT@1100525004 : Record 11229812;
    BEGIN
      IF (NOT UserSetup.GET(USERID)) THEN
        ERROR(Text017, STRSUBSTNO(Text017a, USERID));
      UserSetup.TESTFIELD("Employee No.");

      IF (Action <> Action::IsAdmin) THEN BEGIN
        ProjectResponsiblePerson.SETRANGE("Project No.", Job."No.");
        ProjectResponsiblePerson.SETRANGE("Employee No.", UserSetup."Employee No.");
        IF (NOT ProjectResponsiblePerson.FINDFIRST) THEN
          ERROR(Text017, STRSUBSTNO(Text017b, UserSetup."Employee No."));

        RespPersPermPPT.SETRANGE("Subsite Type", RespPersPermPPT."Subsite Type"::Project);
        RespPersPermPPT.SETRANGE(Type, RespPersPermPPT.Type::Access);
        RespPersPermPPT.SETRANGE("Project No.", Job."No.");
        RespPersPermPPT.SETRANGE("Line No.", ProjectResponsiblePerson."Line No.");
      END;

      CASE Action OF
        Action::CreateSite,
        Action::DeActivateSite,
        Action::ActivateSite: BEGIN
          RespPersPermPPT.SETRANGE("Project Portal Subsite", '');
          RespPersPermPPT.SETRANGE("Document Type", '');
          RespPersPermPPT.SETRANGE("Project Element", '');
          RespPersPermPPT.SETFILTER("Permission Level", '<>%1', RespPersPermPPT."Permission Level"::Owner);
          IF (RespPersPermPPT.FINDFIRST) THEN
            ERROR(Text017, STRSUBSTNO(Text017c, ProjectResponsiblePerson."Employee No."));
        END;
        Action::UpdateStaticProjectContent: BEGIN
          RespPersPermPPT.SETFILTER("Document Type", '<>%1', '');
          RespPersPermPPT.SETRANGE("Project Element", '');
          RespPersPermPPT.SETFILTER("Permission Level", '<>%1', RespPersPermPPT."Permission Level"::Owner);
          RespPersPermPPT.SETRANGE("Quote Request Subsite", FALSE);
          IF (RespPersPermPPT.FINDFIRST) THEN
            ERROR(Text017, STRSUBSTNO(Text017c, ProjectResponsiblePerson."Employee No."));
        END;
        Action::CreateQuoteSite,
        Action::UpdateStaticQuoteContent: BEGIN
          RespPersPermPPT.SETRANGE("Project Element", '');
          RespPersPermPPT.SETFILTER("Permission Level", '<>%1', RespPersPermPPT."Permission Level"::Owner);
          RespPersPermPPT.SETRANGE("Quote Request Subsite", TRUE);
          IF (RespPersPermPPT.FINDFIRST) THEN
            ERROR(Text017, STRSUBSTNO(Text017c, ProjectResponsiblePerson."Employee No."));
        END;
        Action::UpdateQuoteRequests: BEGIN
          RespPersPermPPT.SETRANGE("Project Element", '');
          RespPersPermPPT.SETFILTER("Permission Level", '<%1', RespPersPermPPT."Permission Level"::Member);
          RespPersPermPPT.SETRANGE("Quote Request Subsite", TRUE);
          IF (RespPersPermPPT.FINDFIRST) THEN
            ERROR(Text017, STRSUBSTNO(Text017c, ProjectResponsiblePerson."Employee No."));
        END;
        Action::IsAdmin: BEGIN
          IF (NOT (LOWERCASE(USERID) IN ['4ps\kzwerver'])) THEN
            ERROR(Text017 + ' (User)');
        END;
        ELSE
          ERROR(Text017 + ' (User)');
      END;
    END;

    LOCAL PROCEDURE CheckAllRequirements@1100528316(ServiceLocation@1100528300 : Record 11012801);
    VAR
      SharePointIntegration@1100528301 : Codeunit 11020210;
      DocumentPortalManagement@1100528302 : Codeunit 11012203;
    BEGIN
      IF (NOT SharePointIntegration.IsProjectPortalActive) THEN
        EXIT;

      SetGlobalVars(ServiceLocation);
      DocumentPortalManagement.CheckDocumentPortalSetup;
      IF (SharePointSetup4PS."FBA Membership Provider"+SharePointSetup4PS."FBA Server"+SharePointSetup4PS."FBA Database" <> '') THEN
        DocumentPortalManagement.CheckFBASetup;
      DocumentPortalManagement.CheckMailSetup;
      DocumentPortalManagement.SetGlobalVariables(2, Debug); //Location
      DocumentPortalManagement.CheckSearchCenter;
      DocumentPortalManagement.CheckViewAndMetaDataList;
      CheckLocation(ServiceLocation);
      {
      CheckResponsiblePersons(ServiceLocation);
      CheckAuthorities(ServiceLocation);
      CheckDocuments(ServiceLocation);
      }
    END;

    LOCAL PROCEDURE ThrowError@1100528307(ShowError@1100409000 : Boolean;CallStack@1100528300 : Text);
    BEGIN
      IF (NOT ShowError) THEN
        EXIT;
      CASE ErrorString OF
        '424242', //Site present
        '424243': //Site not present
          EXIT;
        '424244': //Document checked out by someone else
          ErrorString := STRSUBSTNO(Text005)
      END;
      ERROR(Text004, ErrorString, CallStack);
    END;

    LOCAL PROCEDURE UpdateWindow@1100528303(ActionMessage@1100525000 : Text;Message1@1100525001 : Text;Message2@1100525002 : Text;Message3@1100525003 : Text;Message4@1100525004 : Text;Message5@1100525005 : Text);
    BEGIN
      IF (GUIALLOWED) THEN BEGIN
        IF (NOT WindowIsOpen) THEN
          Window.OPEN(Text000);
        Window.UPDATE(1, ActionMessage);
        Window.UPDATE(2, Message1);
        Window.UPDATE(3, Message2);
        Window.UPDATE(4, Message3);
        Window.UPDATE(5, Message4);
        Window.UPDATE(6, Message5);
      END;
      WindowIsOpen := TRUE;
    END;

    BEGIN
    END.
  }
}

