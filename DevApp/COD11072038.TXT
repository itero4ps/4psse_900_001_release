OBJECT Codeunit 11072038 FA Reclass. Jnl.-Transfer
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW17.00,4PS9.00 (5644);
  }
  PROPERTIES
  {
    TableNo=11072084;
    OnRun=BEGIN
            FAReclassJnlLine.COPY(Rec);
            Code;
            COPY(FAReclassJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to reclassify the journal lines?';
      Text001@1001 : TextConst 'ENU=There is nothing to reclassify.';
      Text002@1002 : TextConst 'ENU=The journal lines were successfully reclassified.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully reclassified. You are now in the %1 journal.';
      FAReclassJnlTempl@1004 : Record 11072082;
      FAReclassJnlLine@1005 : Record 11072084;
      FAReclassTransferBatch@1006 : Codeunit 11072037;
      JnlBatchName2@1007 : Code[10];
      gBlnBackground@1210190000 : Boolean;
      TmpFAJnlLine@1100409001 : TEMPORARY Record 5621;
      DoFillTmpFAJnlLine@1100409000 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH FAReclassJnlLine DO BEGIN
        FAReclassJnlTempl.GET("Journal Template Name");

        IF (NOT gBlnBackground) THEN  //**4PS.n FA
          IF NOT CONFIRM(Text000,FALSE) THEN
            EXIT;

        JnlBatchName2 := "Journal Batch Name";

        FAReclassTransferBatch.DoNotCommit; //**4PS.n
        IF DoFillTmpFAJnlLine THEN FAReclassTransferBatch.SetFillTmpFAJnlLine();  //**4PS.n C000883
        FAReclassTransferBatch.RUN(FAReclassJnlLine);
        IF DoFillTmpFAJnlLine THEN FAReclassTransferBatch.GetTmpFAJnlLine(TmpFAJnlLine);  //**4PS.n C000883

        IF (NOT gBlnBackground) THEN BEGIN  //**4PS.n FA
          IF "Line No." = 0 THEN
            MESSAGE(Text001)
          ELSE
            IF JnlBatchName2 = "Journal Batch Name" THEN
              MESSAGE(Text002)
            ELSE
              MESSAGE(
                Text003,
                "Journal Batch Name");
        END;  //**4PS.n FA

        IF NOT FIND('=><') OR (JnlBatchName2 <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP := 2;
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP := 0;
          "Line No." := 1;
        END;
      END;
    END;

    PROCEDURE Background@1210190001(iBlnBackground@1210190000 : Boolean);
    BEGIN
      //**4PS.n FA
      // Function: Background
      // If true, no messages to- nor confirmations from- the user will be generated

      gBlnBackground := iBlnBackground;
    END;

    PROCEDURE SetFillTmpFAJnlLine@1100409000();
    BEGIN
      //**4PS C000883
      DoFillTmpFAJnlLine := TRUE;
      TmpFAJnlLine.RESET;
      TmpFAJnlLine.DELETEALL;
    END;

    PROCEDURE GetTmpFAJnlLine@1100409001(VAR FAJnlLineTmp@1100409000 : TEMPORARY Record 5621);
    BEGIN
      //**4PS C000883
      WITH TmpFAJnlLine DO BEGIN
        IF FINDFIRST THEN BEGIN
          REPEAT
            IF NOT FAJnlLineTmp.GET("Journal Template Name", "Journal Batch Name", "Line No.") THEN BEGIN
              FAJnlLineTmp := TmpFAJnlLine;
              FAJnlLineTmp.INSERT;
            END;
          UNTIL NEXT = 0;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

