OBJECT Codeunit 11072080 INSBOU Management WS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      OK@1100528300 : TextConst 'ENU=ok';
      Text000@1100528301 : TextConst 'ENU=Web Service is working!';
      Text001@1100528302 : TextConst 'ENU=INSBOU Message %1 %2 %3 is not supported.';

    PROCEDURE TestWebService@1100528300() : Text;
    BEGIN
      EXIT(Text000);
    END;

    PROCEDURE OrderResponseMessage@1100528301(MsgFormat@1100528302 : Code[20];MsgVersion@1100528300 : Code[20];MsgContent@1100528301 : Text) : Text;
    VAR
      INSBOUPurchaseOrderResponse@1100525002 : Record 11072658;
      INSBOUInterface@1100525004 : Codeunit 11012480;
      INSBOUOrderResponse@1100525000 : Codeunit 11072078;
      MsgType@1100528303 : Code[20];
      VendorNo@1100525003 : Code[20];
      OrderResponseNumber@1100525001 : Code[17];
    BEGIN
      MsgType := 'ORDRSP';
      CASE TRUE OF
        (MsgFormat = 'INSBOU') AND (MsgVersion = '003'): BEGIN
          INSBOUInterface.ProcessOrdrsp003(STRSUBSTNO('SOAP: %1', FORMAT(CURRENTDATETIME, 0, 9)), COPYSTR(MsgContent, 1));
          VendorNo := INSBOUInterface.GetVendorNo;
          OrderResponseNumber := INSBOUInterface.GetOrderResponseNumber;
        END;
        ELSE
          ERROR(Text001, MsgType, MsgFormat, MsgVersion);
      END;

      IF (VendorNo <> '') AND (OrderResponseNumber <> '') THEN BEGIN
        IF (INSBOUPurchaseOrderResponse.GET(VendorNo, OrderResponseNumber)) THEN BEGIN
          IF (INSBOUPurchaseOrderResponse.StatusCode = '29') THEN BEGIN
            CLEARLASTERROR;
            COMMIT;
            INSBOUPurchaseOrderResponse.SETRECFILTER;
            IF (NOT INSBOUOrderResponse.RUN(INSBOUPurchaseOrderResponse)) THEN BEGIN
              INSBOUPurchaseOrderResponse.GET(VendorNo, OrderResponseNumber);
              INSBOUPurchaseOrderResponse."Error Message" := COPYSTR(GETLASTERRORTEXT, 1, MAXSTRLEN(INSBOUPurchaseOrderResponse."Error Message"));
              INSBOUPurchaseOrderResponse.MODIFY(TRUE);
            END;
          END;
        END;
      END;
      EXIT(OK);
    END;

    PROCEDURE InvoiceMessage@1100528302(MsgFormat@1100528302 : Code[20];MsgVersion@1100528301 : Code[20];MsgContent@1100528300 : Text) : Text;
    VAR
      INSBOUInvoice@1100525000 : Record 11072656;
      INSBOUInterface@1100525004 : Codeunit 11012480;
      INSBOUInvoiceCU@1100525003 : Codeunit 11072077;
      MsgType@1100528303 : Code[20];
      VendorNo@1100525002 : Code[20];
      InvoiceNumber@1100525001 : Code[17];
    BEGIN
      MsgType := 'INVOIC';
      CASE TRUE OF
        (MsgFormat = 'INSBOU') AND (MsgVersion = '003'): BEGIN
          INSBOUInterface.ProcessInvoic003(STRSUBSTNO('SOAP: %1', FORMAT(CURRENTDATETIME, 0, 9)), COPYSTR(MsgContent, 1));
          VendorNo := INSBOUInterface.GetVendorNo;
          InvoiceNumber := INSBOUInterface.GetInvoiceNumber;
        END;
        ELSE
          ERROR(Text001, MsgType, MsgFormat, MsgVersion);
      END;

      IF (VendorNo <> '') AND (InvoiceNumber <> '') THEN BEGIN
        IF (INSBOUInvoice.GET(VendorNo, InvoiceNumber)) THEN BEGIN
          CLEARLASTERROR;
          COMMIT;
          INSBOUInvoice.SETRECFILTER;
          IF (NOT INSBOUInvoiceCU.RUN(INSBOUInvoice)) THEN BEGIN
            INSBOUInvoice.GET(VendorNo, InvoiceNumber);
            INSBOUInvoice."Error Message" := COPYSTR(GETLASTERRORTEXT, 1, MAXSTRLEN(INSBOUInvoice."Error Message"));
            INSBOUInvoice.MODIFY(TRUE);
          END;
        END;
      END;
      EXIT(OK);
    END;

    PROCEDURE InvoiceDocumentMessage@1100528303(SupplierGLN@1100528305 : Code[13];InvoiceNo@1100528300 : Code[20];AttachedData@1100528307 : Text;FileName@1100528304 : Text) : Text;
    VAR
      VendorINSBOULogin@1100528306 : Record 11012132;
      INSBOUInvoice@1100525003 : Record 11072656;
      InvoiceSiB@1100525002 : Record 11072653;
      INSBOUInvoiceCU@1100525001 : Codeunit 11072077;
      INSBOUInvoiceWS@1100525000 : XMLport 11072002;
    BEGIN
      VendorINSBOULogin.SETRANGE("Vendor GLN", SupplierGLN);
      VendorINSBOULogin.FINDFIRST;

      INSBOUInvoiceWS.SetAttachmentContent(AttachedData, FileName);
      INSBOUInvoiceWS.LinkInvoiceDocument(VendorINSBOULogin."Vendor No.", InvoiceNo);

      IF (INSBOUInvoice.GET(VendorINSBOULogin."Vendor No.", InvoiceNo)) THEN BEGIN
        IF (INSBOUInvoice."Process Status" = INSBOUInvoice."Process Status"::Processed) THEN BEGIN
          InvoiceSiB.GET(InvoiceNo);
          INSBOUInvoiceCU.LinkInvoiceDocument(INSBOUInvoice, InvoiceSiB);
        END;
      END;
      EXIT(OK);
    END;

    PROCEDURE DespatchAdviceMessage@1100528304(MsgFormat@1100528302 : Code[20];MsgVersion@1100528300 : Code[20];MsgContent@1100528301 : Text) : Text;
    VAR
      INSBOUInterface@1100525000 : Codeunit 11012480;
      MsgType@1100528303 : Code[20];
      VendorNo@1100528304 : Code[20];
      DespatchAdviceNumber@1100528305 : Code[17];
    BEGIN
      MsgType := 'DESADV';
      CASE TRUE OF
        (MsgFormat = 'INSBOU') AND (MsgVersion = '003'): BEGIN
          INSBOUInterface.ProcessDesadv003(STRSUBSTNO('SOAP: %1', FORMAT(CURRENTDATETIME, 0, 9)), COPYSTR(MsgContent, 1));
          VendorNo := INSBOUInterface.GetVendorNo;
          DespatchAdviceNumber := INSBOUInterface.GetDespatchAdviceNumber;
        END;
        ELSE
          ERROR(Text001, MsgType, MsgFormat, MsgVersion);
      END;
      EXIT(OK);
    END;

    PROCEDURE PriceCataloqueMessage@1100528305(MsgFormat@1100528302 : Code[20];MsgVersion@1100528300 : Code[20];MsgContent@1100528301 : Text) : Text;
    VAR
      INSBOUPriceCatalogue@1100528305 : Record 11012344;
      INSBOUInterface@1100525000 : Codeunit 11012480;
      INSBOUPriceCatalogueCU@1100528306 : Codeunit 11072081;
      MsgType@1100528303 : Code[20];
      VendorNo@1100528304 : Code[20];
      PriceCatalogueNumber@1100528307 : Code[17];
    BEGIN
      MsgType := 'PRICAT';
      CASE TRUE OF
        (MsgFormat = 'INSBOU') AND (MsgVersion = '003'): BEGIN
          INSBOUInterface.ProcessPricat003(STRSUBSTNO('SOAP: %1', FORMAT(CURRENTDATETIME, 0, 9)), COPYSTR(MsgContent, 1));
          VendorNo := INSBOUInterface.GetVendorNo;
          PriceCatalogueNumber := INSBOUInterface.GetPriceCataloqueNumber;
        END;
        ELSE
          ERROR(Text001, MsgType, MsgFormat, MsgVersion);
      END;

      IF (VendorNo <> '') AND (PriceCatalogueNumber <> '') THEN BEGIN
        IF (INSBOUPriceCatalogue.GET(VendorNo, PriceCatalogueNumber)) THEN BEGIN
          CLEARLASTERROR;
          COMMIT;
          INSBOUPriceCatalogue.SETRECFILTER;
          IF (NOT INSBOUPriceCatalogueCU.RUN(INSBOUPriceCatalogue)) THEN BEGIN
            INSBOUPriceCatalogue.GET(VendorNo, PriceCatalogueNumber);
            INSBOUPriceCatalogue."Error Message" := COPYSTR(GETLASTERRORTEXT, 1, MAXSTRLEN(INSBOUPriceCatalogue."Error Message"));
            INSBOUPriceCatalogue.MODIFY(TRUE);
          END;
        END;
      END;
      EXIT(OK);
    END;

    BEGIN
    END.
  }
}

