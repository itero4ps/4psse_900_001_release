OBJECT Codeunit 11125348 Approvals Bridge 4PS Construct
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    Permissions=TableData 454=rimd;
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      NoWorkflowEnabledErr@1100528200 : TextConst 'ENU=This record is not supported by related approval workflow.';
      WorkflowManagement@1100528201 : Codeunit 1501;
      WorkflowEventHandling4PS@1100528203 : Codeunit 11125350;
      Text003@1100528202 : TextConst 'ENU=The table %1 is not supported for approval in Document Capture.';
      WorkflowStepInstance@1100528204 : Record 1504;
      WorkflowStepArgument@1100528205 : Record 1523;
      Text005@1100528206 : TextConst 'ENU=There is no approval request to delegate.';
      PendingApprovalMsg@1100528208 : TextConst 'ENU=An approval request has been sent for Document %1';
      DocStatusChangedMsg@1100528207 : TextConst 'ENU="%1 has been automatically approved. "';

    [Integration]
    PROCEDURE OnSendPostedPurchaseInvoiceForApproval@1100528200(VAR PurchInvHeader@1100528200 : Record 122);
    BEGIN
    END;

    [Integration]
    PROCEDURE OnSendPostedPurchaseCreditMemoForApproval@3(VAR PurchCrMemoHdr@1100528200 : Record 124);
    BEGIN
    END;

    [Integration]
    PROCEDURE OnCancelPostedPurchaseInvoiceApprovalRequest@4(VAR PurchInvHeader@1000 : Record 122);
    BEGIN
    END;

    [Integration]
    PROCEDURE OnCancelPostedPurchaseCreditMemoApprovalRequest@1100528201(VAR PurchCrMemoHdr@1000 : Record 124);
    BEGIN
    END;

    [Integration]
    PROCEDURE OnPostedPurchaseInvoiceForceApproveApprovalRequest@19(VAR PurchInvHeader@1000 : Record 122);
    BEGIN
    END;

    [Integration]
    PROCEDURE OnPostedPurchaseCreditMemoForceApproveApprovalRequest@1100528202(VAR PurchCrMemoHdr@1000 : Record 124);
    BEGIN
    END;

    PROCEDURE SendPurchDocApprovalRequest@8(Variant@1000 : Variant);
    VAR
      RecRef@1100528200 : RecordRef;
    BEGIN
      IF IsPostedPurchaseApprovalsWorkflowEnabled(Variant) THEN BEGIN
        RecRef.GETTABLE(Variant);
        CASE RecRef.NUMBER OF
          DATABASE::"Purch. Inv. Header":OnSendPostedPurchaseInvoiceForApproval(Variant);
          DATABASE::"Purch. Cr. Memo Hdr.":OnSendPostedPurchaseCreditMemoForApproval(Variant);
        END;
      END;
    END;

    PROCEDURE CancelApprovalRequest@10(Variant@1100528200 : Variant);
    VAR
      RecRef@1100528203 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Variant);
      CASE RecRef.NUMBER OF
        DATABASE::"Purch. Inv. Header":OnCancelPostedPurchaseInvoiceApprovalRequest(Variant);
        DATABASE::"Purch. Cr. Memo Hdr.":OnCancelPostedPurchaseCreditMemoApprovalRequest(Variant);
      END;
    END;

    PROCEDURE ForceApproval@1100528205(Variant@1100528200 : Variant);
    VAR
      RecRef@1100528201 : RecordRef;
    BEGIN
      IF CheckPostedPurchaseForceApproveApprovalsWorkflowEnabled(Variant) THEN BEGIN
        RecRef.GETTABLE(Variant);
        CASE RecRef.NUMBER OF
          DATABASE::"Purch. Inv. Header":OnPostedPurchaseInvoiceForceApproveApprovalRequest(Variant);
          DATABASE::"Purch. Cr. Memo Hdr.":OnPostedPurchaseCreditMemoForceApproveApprovalRequest(Variant);
        END;
      END;
    END;

    PROCEDURE CheckPostedPurchaseApprovalsWorkflowEnabled@77(VAR Variant@1100528204 : Variant) : Boolean;
    BEGIN
      IF NOT IsPostedPurchaseApprovalsWorkflowEnabled(Variant) THEN
        ERROR(NoWorkflowEnabledErr);
      EXIT(TRUE);
    END;

    PROCEDURE IsPostedPurchaseApprovalsWorkflowEnabled@59(VAR Variant@1100528200 : Variant) : Boolean;
    VAR
      RecRef@1100528201 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Variant);
      CASE RecRef.NUMBER OF
        DATABASE::"Purch. Inv. Header":
          EXIT(WorkflowManagement.CanExecuteWorkflow(Variant,WorkflowEventHandling4PS.RunWorkflowOnSendPostedPurchaseInvoiceForApprovalCode));
        DATABASE::"Purch. Cr. Memo Hdr.":
          EXIT(WorkflowManagement.CanExecuteWorkflow(Variant,WorkflowEventHandling4PS.RunWorkflowOnSendPostedPurchaseCreditMemoForApprovalCode));
      END;
      EXIT(FALSE);
    END;

    PROCEDURE CheckPostedPurchaseForceApproveApprovalsWorkflowEnabled@1100528204(VAR Variant@1100528204 : Variant) : Boolean;
    BEGIN
      IF NOT IsPostedPurchaseForceApproveApprovalsWorkflowEnabled(Variant) THEN
        ERROR(NoWorkflowEnabledErr);
      EXIT(TRUE);
    END;

    PROCEDURE IsPostedPurchaseForceApproveApprovalsWorkflowEnabled@1100528203(VAR Variant@1100528200 : Variant) : Boolean;
    VAR
      RecRef@1100528201 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Variant);
      CASE RecRef.NUMBER OF
        DATABASE::"Purch. Inv. Header":
          EXIT(WorkflowManagement.CanExecuteWorkflow(Variant,WorkflowEventHandling4PS.RunWorkflowOnPostedPurchaseInvoiceForceApproveApprovalRequestCode));
        DATABASE::"Purch. Cr. Memo Hdr.":
          EXIT(WorkflowManagement.CanExecuteWorkflow(Variant,WorkflowEventHandling4PS.RunWorkflowOnPostedPurchaseCreditMemoForceApproveApprovalRequestCode));
      END;
      EXIT(FALSE);
    END;

    PROCEDURE Approve@1000000040(Variant@1100528200 : Variant);
    VAR
      RecRef@1100528201 : RecordRef;
      PurchInvHeader@1100528203 : Record 122;
      PurchCrMemoHdr@1100528202 : Record 124;
      ApprovalEntryArgument@1100528204 : Record 454;
      ApprovalsMgmt@1000 : Codeunit 1535;
    BEGIN
      RecRef.GETTABLE(Variant);
      ApprovalsMgmt.ApproveRecordApprovalRequest(RecRef.RECORDID);
    END;

    PROCEDURE Reject@13(Variant@1100528200 : Variant);
    VAR
      ApprovalManagement4PSConstr@1100528201 : Codeunit 11125349;
      RecRef@1100528204 : RecordRef;
      PurchInvHeader@1100528203 : Record 122;
      PurchCrMemoHdr@1100528202 : Record 124;
    BEGIN
      RecRef.GETTABLE(Variant);
      CASE RecRef.NUMBER OF
        DATABASE::"Purch. Inv. Header":
          BEGIN
            RecRef.SETTABLE(PurchInvHeader);
            ApprovalManagement4PSConstr.RejectPurchInvHeader4PS(PurchInvHeader);
          END;
        DATABASE::"Purch. Cr. Memo Hdr.":
          BEGIN
            RecRef.SETTABLE(PurchCrMemoHdr);
            ApprovalManagement4PSConstr.RejectPurchCrMemoHdr4PS(PurchCrMemoHdr);
          END;
      END;
    END;

    PROCEDURE Forward@14(Variant@1000 : Variant);
    VAR
      ApprovalEntry@1003 : Record 454;
      ApprovalsMgmt@1002 : Codeunit 1535;
      DCApprovalMgmt@1001 : Codeunit 6085722;
      RecRef@1100528202 : RecordRef;
      PurchInvHeader@1100528201 : Record 122;
      PurchCrMemoHdr@1100528200 : Record 124;
      ApprovalManagement4PSConstr@1100528203 : Codeunit 11125349;
      DCApprovalsBridge@1100528204 : Codeunit 6085790;
    BEGIN
      RecRef.GETTABLE(Variant);
      CASE RecRef.NUMBER OF
        DATABASE::"Purch. Inv. Header":
          BEGIN
            RecRef.SETTABLE(PurchInvHeader);
            FilterApprovalEntry4PS(2,PurchInvHeader."No.",ApprovalEntry,NOT ApprovalManagement4PSConstr.IsApprovalAdminUser(FALSE));
          END;
        DATABASE::"Purch. Cr. Memo Hdr.":
          BEGIN
            RecRef.SETTABLE(PurchCrMemoHdr);
            FilterApprovalEntry4PS(3,PurchCrMemoHdr."No.",ApprovalEntry,NOT ApprovalManagement4PSConstr.IsApprovalAdminUser(FALSE));
          END;
      END;
      DCApprovalMgmt.DelegateApprovalRequest(ApprovalEntry);
      DCApprovalsBridge.OnDelegateRecordApprovalRequested(ApprovalEntry);
    END;

    PROCEDURE OnHold@15(Variant@1000 : Variant);
    VAR
      RecRef@1100528200 : RecordRef;
      PurchInvHeader@1100528202 : Record 122;
      PurchCrMemoHdr@1100528201 : Record 124;
      ApprovalManagement4PSConstr@1100528203 : Codeunit 11125349;
    BEGIN
      RecRef.GETTABLE(Variant);
      CASE RecRef.NUMBER OF
        DATABASE::"Purch. Inv. Header":
          BEGIN
            RecRef.SETTABLE(PurchInvHeader);
            ApprovalManagement4PSConstr.SwitchOnHold(2, PurchInvHeader."No.",TRUE,TRUE);
          END;
        DATABASE::"Purch. Cr. Memo Hdr.":
          BEGIN
            RecRef.SETTABLE(PurchCrMemoHdr);
            ApprovalManagement4PSConstr.SwitchOnHold(3, PurchCrMemoHdr."No.",TRUE,TRUE);
          END;
      END;
    END;

    PROCEDURE InitializeApprovalEntry4PS@80(RecRef@1000 : RecordRef;VAR ApprovalEntryArgument@1001 : Record 454);
    VAR
      Customer@1006 : Record 18;
      GenJournalBatch@1008 : Record 232;
      GenJournalLine@1007 : Record 81;
      PurchHeader@1003 : Record 38;
      SalesHeader@1004 : Record 36;
      IncomingDocument@1010 : Record 130;
      ApprovalAmount@1005 : Decimal;
      ApprovalAmountLCY@1002 : Decimal;
      PurchInvHeader@1100528200 : Record 122;
      PurchCrMemoHdr@1100528201 : Record 124;
    BEGIN
      //Is extend version of Initializeapprovalentry in CodeUnit 6085790 DC Approvals Bridge
      ApprovalEntryArgument.INIT;
      ApprovalEntryArgument."Table ID" := RecRef.NUMBER;
      ApprovalEntryArgument."Record ID to Approve" := RecRef.RECORDID;
      ApprovalEntryArgument."Document Type" := ApprovalEntryArgument."Document Type"::" ";
      ApprovalEntryArgument."Approval Code" := WorkflowStepInstance."Workflow Code";
      ApprovalEntryArgument."Workflow Step Instance ID" := WorkflowStepInstance.ID;

      CASE RecRef.NUMBER OF
        DATABASE::"Purchase Header":
          BEGIN
            RecRef.SETTABLE(PurchHeader);
            CalcPurchDocAmount(PurchHeader,ApprovalAmount,ApprovalAmountLCY);
            ApprovalEntryArgument."Document Type" := PurchHeader."Document Type";
            ApprovalEntryArgument."Document No." := PurchHeader."No.";
            ApprovalEntryArgument."Salespers./Purch. Code" := PurchHeader."Purchaser Code";
            ApprovalEntryArgument.Amount := ApprovalAmount;
            ApprovalEntryArgument."Amount (LCY)" := ApprovalAmountLCY;
            ApprovalEntryArgument."Currency Code" := PurchHeader."Currency Code";
          END;
          DATABASE::"Purch. Inv. Header":
          BEGIN
            RecRef.SETTABLE(PurchInvHeader);
            PurchInvHeader.CALCFIELDS(Amount, "Amount (LCY)");
            ApprovalAmount := PurchInvHeader.Amount;
            ApprovalAmountLCY := PurchInvHeader."Amount (LCY)";
            ApprovalEntryArgument."Document Type" := ApprovalEntryArgument."Document Type"::Invoice;
            ApprovalEntryArgument."Document No." := PurchInvHeader."No.";
            ApprovalEntryArgument."Salespers./Purch. Code" := PurchInvHeader."Purchaser Code";
            ApprovalEntryArgument.Amount := ApprovalAmount;
            ApprovalEntryArgument."Amount (LCY)" := ApprovalAmountLCY;
            ApprovalEntryArgument."Currency Code" := PurchInvHeader."Currency Code";
          END;
          DATABASE::"Purch. Cr. Memo Hdr.":
          BEGIN
            RecRef.SETTABLE(PurchCrMemoHdr);
            PurchCrMemoHdr.CALCFIELDS(Amount, "Amount (LCY)");
            ApprovalAmount := PurchCrMemoHdr.Amount;
            ApprovalAmountLCY := PurchCrMemoHdr."Amount (LCY)";
            ApprovalEntryArgument."Document Type" := ApprovalEntryArgument."Document Type"::"Credit Memo";
            ApprovalEntryArgument."Document No." := PurchCrMemoHdr."No.";
            ApprovalEntryArgument."Salespers./Purch. Code" := PurchCrMemoHdr."Purchaser Code";
            ApprovalEntryArgument.Amount := ApprovalAmount;
            ApprovalEntryArgument."Amount (LCY)" := ApprovalAmountLCY;
            ApprovalEntryArgument."Currency Code" := PurchCrMemoHdr."Currency Code";
          END;
        ELSE
          ERROR(Text003);
      END;

      ApprovalEntryArgument.SETRANGE("Workflow Step Instance ID",WorkflowStepInstance.ID);
    END;

    PROCEDURE CalcPurchDocAmount@24(PurchHeader@1002 : Record 38;VAR ApprovalAmount@1001 : Decimal;VAR ApprovalAmountLCY@1000 : Decimal);
    VAR
      TempPurchLine@1009 : TEMPORARY Record 39;
      TotalPurchLine@1008 : Record 39;
      TotalPurchLineLCY@1007 : Record 39;
      PurchDocMgnt@1003 : Codeunit 6085709;
      PurchPost@1006 : Codeunit 90;
      TempAmount@1005 : Decimal;
      VAtText@1004 : Text[30];
    BEGIN
      PurchHeader.CalcInvDiscForHeader;
      PurchPost.GetPurchLines(PurchHeader,TempPurchLine,0);
      CLEAR(PurchPost);
      PurchPost.SumPurchLinesTemp(
        PurchHeader,TempPurchLine,0,TotalPurchLine,TotalPurchLineLCY,
        TempAmount,VAtText);
      ApprovalAmount := TotalPurchLine.Amount;
      ApprovalAmountLCY := TotalPurchLineLCY.Amount;
    END;

    PROCEDURE InformUserOnStatusChange@22(VAR RecRef@1000 : RecordRef);
    VAR
      PurchHeader@1001 : Record 38;
    BEGIN
      IF NOT GUIALLOWED THEN
        EXIT;

      IF NOT WorkflowStepArgument."Show Confirmation Message" THEN
        EXIT;

      ShowPurchApprovalStatus(RecRef);
    END;

    LOCAL PROCEDURE ShowPurchApprovalStatus@9(Variant@1000 : Variant);
    VAR
      RecRef@1100528200 : RecordRef;
      PurchInvHeader@1100528201 : Record 122;
      PurchCrMemoHdr@1100528202 : Record 124;
      PurchaseHeader@1100528203 : Record 38;
    BEGIN
      RecRef.GETTABLE(Variant);
      CASE RecRef.NUMBER OF
        DATABASE::"Purch. Inv. Header":
          BEGIN
            RecRef.SETTABLE(PurchInvHeader);
            PurchInvHeader.GET;
            IF PurchInvHeader."On Hold" <> '' THEN
              MESSAGE(PendingApprovalMsg, PurchInvHeader."No.")
            ELSE
              MESSAGE(DocStatusChangedMsg, PurchInvHeader."No.");
          END;
        DATABASE::"Purch. Cr. Memo Hdr.":
          BEGIN
            RecRef.SETTABLE(PurchCrMemoHdr);
            PurchCrMemoHdr.GET;
            IF PurchCrMemoHdr."On Hold" <> '' THEN
              MESSAGE(PendingApprovalMsg, PurchCrMemoHdr."No.")
            ELSE
              MESSAGE(DocStatusChangedMsg, PurchCrMemoHdr."No.");
          END;
      END;
    END;

    [EventSubscriber(Codeunit,6085786,OnBeforeExecuteResponseStepInstance)]
    PROCEDURE OnNewWorkflowStepInstance@2(VAR NewWorkflowStepInstance@1000 : Record 1504);
    BEGIN
      WorkflowStepInstance := NewWorkflowStepInstance;
      WorkflowStepArgument.GET(WorkflowStepInstance.Argument);
    END;

    [EventSubscriber(Table,454,OnBeforeInsertEvent,"",Skip)]
    LOCAL PROCEDURE OnBeforeInsertApprovalEntry@38(VAR Rec@1000 : Record 454;RunTrigger@1001 : Boolean);
    VAR
      DCApprovalMgmt@10000 : Codeunit 6085722;
    BEGIN
      IF NOT Rec.ISTEMPORARY THEN
        DCApprovalMgmt.UpdateApprovalEntry(Rec);
    END;

    [EventSubscriber(Codeunit,90,OnAfterPostPurchaseDoc,"",Skip)]
    LOCAL PROCEDURE OnAfterPostPurchaseDoc@1100528207(VAR PurchaseHeader@1100528200 : Record 38;VAR GenJnlPostLine@1100528201 : Codeunit 12;PurchRcpHdrNo@1100528202 : Code[20];RetShptHdrNo@1100528203 : Code[20];PurchInvHdrNo@1100528204 : Code[20];PurchCrMemoHdrNo@1100528205 : Code[20]);
    VAR
      PurchInvHeader@1100528207 : Record 122;
      PurchCrMemoHdr@1100528206 : Record 124;
    BEGIN
      CASE PurchaseHeader."Document Type" OF
        PurchaseHeader."Document Type"::Invoice:
          BEGIN
            IF PurchInvHeader.GET(PurchInvHdrNo) THEN
              SendPurchDocApprovalRequest(PurchInvHeader);
          END;
        PurchaseHeader."Document Type"::"Credit Memo":
          BEGIN
            IF PurchCrMemoHdr.GET(PurchCrMemoHdrNo) THEN
              SendPurchDocApprovalRequest(PurchCrMemoHdr);
          END;
      END;
    END;

    PROCEDURE FilterApprovalEntry4PS@1100528206(lDocType@1100528200 : Integer;lDocNo@1100528201 : Code[20];VAR ApprovalEntry@1000000001 : Record 454;OnlyCurrUser@161024012 : Boolean);
    VAR
      ApprovalManagement4PSConstr@1100528202 : Codeunit 11125349;
    BEGIN
      ApprovalManagement4PSConstr.FilterApprovalEntryOpen(lDocType,lDocNo,ApprovalEntry,OnlyCurrUser);
    END;

    BEGIN
    END.
  }
}

