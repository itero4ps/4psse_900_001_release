OBJECT Codeunit 11125350 Workflow Event Handling 4PS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    EventSubscriberInstance=Static-Automatic;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      WorkflowManagement@1010 : Codeunit 1501;
      EventAlreadyExistErr@1000 : TextConst 'ENU=An event with description %1 already exists. (Continia)';
      SendPurchaseInvoiceForApprovalEventTxt@1100528200 : TextConst 'ENU=An approval request is send. (4PS Invoice)';
      SendPurchaseCreditMemoForApprovalEventTxt@1100528201 : TextConst 'ENU=An approval request is send. (4PS Credit Memo)';
      CancelPurchaseInvoiceRequestEventTxt@1100528202 : TextConst 'ENU=Approval is canceled. (4PS Factuur)';
      CancelPurchaseCreditMemoRequestEventTxt@1100528203 : TextConst 'ENU=Approval is canceled. (4PS Credit Memo)';
      ForceApprovalPurchaseInvoiceRequestEventTxt@1100528204 : TextConst 'ENU=A purchase document is force approved. (4PS Invoice)';
      ForceApprovalPurchaseCreditMemoRequestEventTxt@1100528205 : TextConst 'ENU=A purchase document is force approved. (4PS Credit Memo)';

    [EventSubscriber(Codeunit,1520,OnAddWorkflowEventsToLibrary,"",Skip)]
    PROCEDURE CreateEventsLibrary@13();
    BEGIN
      AddEventToLibrary(
        RunWorkflowOnSendPostedPurchaseInvoiceForApprovalCode,DATABASE::"Purch. Inv. Header", SendPurchaseInvoiceForApprovalEventTxt,0,FALSE);
      AddEventToLibrary(
        RunWorkflowOnSendPostedPurchaseCreditMemoForApprovalCode,DATABASE::"Purch. Cr. Memo Hdr.", SendPurchaseCreditMemoForApprovalEventTxt,0,FALSE);
      AddEventToLibrary(
        RunWorkflowOnCancelPostedPurchaseInvoiceRequestCode,DATABASE::"Purch. Inv. Header", CancelPurchaseInvoiceRequestEventTxt,0,FALSE);
      AddEventToLibrary(
        RunWorkflowOnCancelPostedPurchaseCreditMemoRequestCode,DATABASE::"Purch. Cr. Memo Hdr.", CancelPurchaseCreditMemoRequestEventTxt,0,FALSE);
      AddEventToLibrary(
        RunWorkflowOnPostedPurchaseInvoiceForceApproveApprovalRequestCode,DATABASE::"Purch. Inv. Header", ForceApprovalPurchaseInvoiceRequestEventTxt,0,FALSE);
      AddEventToLibrary(
        RunWorkflowOnPostedPurchaseCreditMemoForceApproveApprovalRequestCode,DATABASE::"Purch. Cr. Memo Hdr.", ForceApprovalPurchaseCreditMemoRequestEventTxt,0,FALSE);
    END;

    LOCAL PROCEDURE AddEventPredecessors@67(EventFunctionName@1001 : Code[128]);
    VAR
      WorkflowEventHandling@1000 : Codeunit 1520;
    BEGIN
      CASE EventFunctionName OF
        RunWorkflowOnSendPostedPurchaseInvoiceForApprovalCode:;// No predecessors. It is a starting event.
        RunWorkflowOnSendPostedPurchaseCreditMemoForApprovalCode:; // No predecessors. It is a starting event.
        RunWorkflowOnCancelPostedPurchaseInvoiceRequestCode:
          AddEventPredecessor(RunWorkflowOnCancelPostedPurchaseInvoiceRequestCode,RunWorkflowOnSendPostedPurchaseInvoiceForApprovalCode);
        RunWorkflowOnCancelPostedPurchaseCreditMemoRequestCode:
          AddEventPredecessor(RunWorkflowOnCancelPostedPurchaseCreditMemoRequestCode,RunWorkflowOnSendPostedPurchaseCreditMemoForApprovalCode);
        RunWorkflowOnPostedPurchaseInvoiceForceApproveApprovalRequestCode:
          AddEventPredecessor(RunWorkflowOnPostedPurchaseInvoiceForceApproveApprovalRequestCode,RunWorkflowOnSendPostedPurchaseInvoiceForApprovalCode);
        RunWorkflowOnPostedPurchaseCreditMemoForceApproveApprovalRequestCode:
          AddEventPredecessor(RunWorkflowOnPostedPurchaseCreditMemoForceApproveApprovalRequestCode,RunWorkflowOnSendPostedPurchaseCreditMemoForApprovalCode);
      END;
    END;

    PROCEDURE AddEventToLibrary@14(FunctionName@1001 : Code[128];TableID@1002 : Integer;Description@1003 : Text[250];RequestPageID@1004 : Integer;UsedForRecordChange@1005 : Boolean);
    VAR
      WorkflowEvent@1000 : Record 1520;
    BEGIN
      IF NOT WorkflowEvent.GET(FunctionName) THEN BEGIN
        WorkflowEvent.SETRANGE(Description,Description);
        IF WorkflowEvent.FINDFIRST THEN
          ERROR(EventAlreadyExistErr,Description);

        WorkflowEvent.INIT;
        WorkflowEvent."Function Name" := FunctionName;
        WorkflowEvent."Table ID" := TableID;
        WorkflowEvent.Description := Description;
        WorkflowEvent."Request Page ID" := RequestPageID;
        WorkflowEvent."Used for Record Change" := UsedForRecordChange;
        WorkflowEvent.INSERT;
      END;
      AddEventPredecessors(WorkflowEvent."Function Name");
    END;

    PROCEDURE AddEventPredecessor@66(FunctionName@1001 : Code[128];PredecessorFunctionName@1002 : Code[128]);
    VAR
      WFEventResponseCombination@1000 : Record 1509;
    BEGIN
      WFEventResponseCombination.INIT;
      WFEventResponseCombination.Type := WFEventResponseCombination.Type::"Event";
      WFEventResponseCombination."Function Name" := FunctionName;
      WFEventResponseCombination."Predecessor Type" := WFEventResponseCombination."Predecessor Type"::"Event";
      WFEventResponseCombination."Predecessor Function Name" := PredecessorFunctionName;
      IF WFEventResponseCombination.INSERT THEN;
    END;

    PROCEDURE RunWorkflowOnSendPostedPurchaseInvoiceForApprovalCode@21() : Code[128];
    BEGIN
      EXIT(UPPERCASE('RunWorkflowOnSendPostedPurchaseInvoiceForApproval'));
    END;

    [EventSubscriber(Codeunit,11125348,OnSendPostedPurchaseInvoiceForApproval)]
    PROCEDURE RunWorkflowOnSendPostedPurchaseInvoiceForApproval@1(VAR PurchInvHeader@1100528200 : Record 122);
    BEGIN
      WorkflowManagement.HandleEvent(RunWorkflowOnSendPostedPurchaseInvoiceForApprovalCode,PurchInvHeader);
    END;

    PROCEDURE RunWorkflowOnSendPostedPurchaseCreditMemoForApprovalCode@1100528203() : Code[128];
    BEGIN
      EXIT(UPPERCASE('RunWorkflowOnSendPostedPurchaseCreditMemoForApproval'));
    END;

    [EventSubscriber(Codeunit,11125348,OnSendPostedPurchaseCreditMemoForApproval)]
    PROCEDURE RunWorkflowOnSendPostedPurchaseCreditMemoForApproval@1100528202(VAR PurchCrMemoHdr@1100528200 : Record 124);
    BEGIN
      WorkflowManagement.HandleEvent(RunWorkflowOnSendPostedPurchaseInvoiceForApprovalCode,PurchCrMemoHdr);
    END;

    PROCEDURE RunWorkflowOnCancelPostedPurchaseInvoiceRequestCode@1100528201() : Code[128];
    BEGIN
      EXIT(UPPERCASE('RunWorkflowOnCancelPostedPurchaseInvoiceRequest'));
    END;

    [EventSubscriber(Codeunit,11125348,OnCancelPostedPurchaseInvoiceApprovalRequest)]
    PROCEDURE RunWorkflowOnCancelPostedPurchaseInvoiceRequest@1100528200(VAR PurchInvHeader@1100528201 : Record 122);
    BEGIN
      WorkflowManagement.HandleEvent(RunWorkflowOnCancelPostedPurchaseInvoiceRequestCode, PurchInvHeader);
    END;

    PROCEDURE RunWorkflowOnCancelPostedPurchaseCreditMemoRequestCode@1100528205() : Code[128];
    BEGIN
      EXIT(UPPERCASE('RunWorkflowOnCancelPostedPurchaseCreditMemoRequest'));
    END;

    [EventSubscriber(Codeunit,11125348,OnCancelPostedPurchaseCreditMemoApprovalRequest)]
    PROCEDURE RunWorkflowOnCancelPostedPurchaseCreditMemoRequest@1100528204(VAR PurchCrMemoHdr@1100529200 : Record 124);
    BEGIN
      WorkflowManagement.HandleEvent(RunWorkflowOnCancelPostedPurchaseCreditMemoRequestCode,PurchCrMemoHdr);
    END;

    PROCEDURE RunWorkflowOnPostedPurchaseInvoiceForceApproveApprovalRequestCode@1100528206() : Code[128];
    BEGIN
      EXIT(UPPERCASE('RunWorkflowOnSendPostedPurchaseInvoiceForForceApproval'));
    END;

    [EventSubscriber(Codeunit,11125348,OnPostedPurchaseInvoiceForceApproveApprovalRequest)]
    PROCEDURE RunWorkflowOnPostedPurchaseInvoiceForceApproveApprovalRequest@1100528210(VAR PurchInvHeader@1100528200 : Record 122);
    BEGIN
      WorkflowManagement.HandleEvent(RunWorkflowOnPostedPurchaseInvoiceForceApproveApprovalRequestCode, PurchInvHeader);
    END;

    PROCEDURE RunWorkflowOnPostedPurchaseCreditMemoForceApproveApprovalRequestCode@1100528207() : Code[128];
    BEGIN
      EXIT(UPPERCASE('RunWorkflowOnSendPostedCreditMemoForForceApprovalCode'));
    END;

    [EventSubscriber(Codeunit,11125348,OnPostedPurchaseCreditMemoForceApproveApprovalRequest)]
    PROCEDURE RunWorkflowOnPostedPurchaseCreditMemoForceApproveApprovalRequest@1100528211(VAR PurchCrMemoHdr@1100528200 : Record 124);
    BEGIN
      WorkflowManagement.HandleEvent(RunWorkflowOnPostedPurchaseCreditMemoForceApproveApprovalRequestCode,PurchCrMemoHdr);
    END;

    BEGIN
    END.
  }
}

