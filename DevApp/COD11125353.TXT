OBJECT Codeunit 11125353 NAS Management 4PS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE NASHandler@1(NASID@1000 : Text[260]);
    VAR
      FPSMobileSetup@1100527000 : Record 11012676;
      Parameter@1008 : Text[260];
      ParamStr@1007 : Text[260];
      SepPosition@1006 : Integer;
      MarketingSetup@1100409003 : Record 5079;
      NASAutomaticExchangeFSA@1100409000 : Codeunit 11020229;
      LIPInterfaceStartup@1100409001 : Codeunit 11012139;
      SharepointInterfaceStartup@1100409002 : Codeunit 11012086;
      NASSendSMTPSOStatusMail@1100409004 : Codeunit 11012824;
      CodeUnitToRun@1100409005 : Integer;
    BEGIN
      ParamStr := UPPERCASE(NASID);
      REPEAT
        SepPosition := STRPOS(ParamStr,',');
        IF SepPosition > 0 THEN
          Parameter := COPYSTR(ParamStr,1,SepPosition - 1)
        ELSE
          Parameter := ParamStr;


        CASE Parameter OF
          'JOBQUEUE':
            CODEUNIT.RUN(CODEUNIT::"Job Queue - NAS Start Up");
        END;

        //**4PS.sn
        IF (COPYSTR(Parameter,1,3) = '2BA') THEN  //db, 09-08-11: M19173
          CODEUNIT.RUN(CODEUNIT::"Process Item Batch");

        IF (COPYSTR(Parameter,1,10) = '4PS-MOBILE') THEN BEGIN //DP00465.c
          FPSMobileSetup.GET;
          IF ((FPSMobileSetup."Receive Message Queue" <> '') AND (FPSMobileSetup."Reply Message Queue" <> '')) THEN BEGIN
            CODEUNIT.RUN(CODEUNIT::"Request Handler (4PS-Mobile)");
          END;
          CODEUNIT.RUN(CODEUNIT::"NAS Process Debrief 4PS-Mobile");
        END;

      // C025983.sn
        IF (COPYSTR(Parameter,1,19) = 'MSMQ-HANDLER-MOBILE') THEN BEGIN
          FPSMobileSetup.GET;
          IF ((FPSMobileSetup."Receive Message Queue" <> '') AND (FPSMobileSetup."Reply Message Queue" <> '')) THEN
            CODEUNIT.RUN(CODEUNIT::"Request Handler (4PS-Mobile)");
        END;
        IF (COPYSTR(Parameter,1,22) = 'PROCESS-DEBRIEF-MOBILE') THEN
          CODEUNIT.RUN(CODEUNIT::"NAS Process Debrief 4PS-Mobile");
      // C025983.en

        IF (COPYSTR(Parameter,1,3) = 'DWH') THEN //* Datawarehouse Replication
          CODEUNIT.RUN(CODEUNIT::"DataWarehouse - Projects v3");

        IF (COPYSTR(Parameter,1,3) = 'FSA') THEN //* Field Service (buitendienst)
          NASAutomaticExchangeFSA.RUN;

        IF (COPYSTR(Parameter,1,5) = 'DONAU') THEN
          CODEUNIT.RUN(CODEUNIT::"Request Handler (Donau)");

        IF (COPYSTR(Parameter,1,4) = 'DOCU') THEN  //* Document beheersysteem Sharepoint of LIP
          IF MarketingSetup.GET THEN
            CASE MarketingSetup."Documentation System" OF
              MarketingSetup."Documentation System"::LIP:
                LIPInterfaceStartup.RUN;
            MarketingSetup."Documentation System"::Sharepoint:
              SharepointInterfaceStartup.RUN;
            END;

        IF (COPYSTR(Parameter,1,11) = 'SO-SMTPMAIL') THEN
          NASSendSMTPSOStatusMail.RUN;

        IF (COPYSTR(Parameter,1,6) = 'CUSTOM') THEN BEGIN
          EVALUATE(CodeUnitToRun,COPYSTR(Parameter,7));
          CODEUNIT.RUN(CodeUnitToRun);
        END;
        //**4PS.en
        ParamStr := COPYSTR(ParamStr,SepPosition + 1);
      UNTIL SepPosition = 0;
    END;

    BEGIN
    END.
  }
}

