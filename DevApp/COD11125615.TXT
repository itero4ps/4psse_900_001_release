OBJECT Codeunit 11125615 EstimateLineOLD-Explode BOM
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    TableNo=11012154;
    OnRun=VAR
            EstimateLine@1100525003 : Record 11012154;
            GenericItem@1100525000 : Record 11125615;
            ExplodeGenericItem@1100525005 : Report 11125617;
          BEGIN
            EstimateLine := Rec;
            EstimateLine.SETRECFILTER;
            EstimateLine.SETRANGE("Line No.");

            IF "Item No." <> '' THEN BEGIN
              OrigLineNo := "Line No.";
              OrigQuantity := Quantity;
              ExplodeItem(EstimateLine);
            END ELSE BEGIN
              GenericItem.SETRANGE(Status,GenericItem.Status::Certified);
              IF PAGE.RUNMODAL(0,GenericItem) = ACTION::LookupOK THEN BEGIN
                GenericItem.TESTFIELD(Status,GenericItem.Status::Certified);
                ExplodeGenericItem.RUNMODAL;
                ExplodeGenItem(EstimateLine,GenericItem,ExplodeGenericItem.GetQuantity);
              END;
            END;
          END;

  }
  CODE
  {
    VAR
      Text001@1100525007 : TextConst 'ENU=Item %1 is not a bill of materials.';
      Text003@1100525005 : TextConst 'ENU=There is not enough space to explode the BOM.';
      OrigLineNo@1100525001 : Integer;
      OrigQuantity@1100525000 : Decimal;
      OrigItem@1100525003 : Code[20];
      LevelBOM@1100525002 : Integer;

    PROCEDURE ExplodeGenItem@1100525000(VAR EstimateLine@1100525000 : Record 11012154;GenericItem@1100525006 : Record 11125615;QuantityGenItem@1100525007 : Decimal);
    VAR
      ToEstimateLine@1100525001 : Record 11012154;
      GenericBOMLine@1100525005 : Record 11125616;
      NoOfBOMComp@1100525003 : Integer;
      NextLineNo@1100525004 : Integer;
    BEGIN
      //explode for generic item
      WITH EstimateLine DO BEGIN
        GenericBOMLine.SETRANGE("Generic Item No.",GenericItem."No.");
        NoOfBOMComp := GenericBOMLine.COUNT;
        IF NoOfBOMComp = 0 THEN
          ERROR(Text001,GenericItem."No.");

        ToEstimateLine.COPY(EstimateLine);
        IF ToEstimateLine.FINDLAST THEN
          NextLineNo := ToEstimateLine."Line No." + 10000
        ELSE
          NextLineNo := 10000;

        IF GenericBOMLine.FINDSET THEN
          REPEAT
            ToEstimateLine.INIT;
            //db.sn, 04-08-10 (PT)
            ToEstimateLine.VALIDATE("Sub-Estimate No.");
            ToEstimateLine.VALIDATE(Part);
            //db.en, 04-08-10 (PT)
            ToEstimateLine."Line No." := NextLineNo;
            ToEstimateLine.VALIDATE("Item No.",GenericBOMLine."Item No.");
            ToEstimateLine.VALIDATE(Quantity,(GenericBOMLine.Quantity * QuantityGenItem));
            ToEstimateLine.VALIDATE("Unit of Measure",GenericBOMLine."Unit of Measure Code");
            ToEstimateLine.Description := GenericBOMLine.Description;
            ToEstimateLine."Part of Line" := OrigLineNo;
            ToEstimateLine.INSERT;
            NextLineNo += 10000;
          UNTIL GenericBOMLine.NEXT = 0;
      END;
    END;

    PROCEDURE ExplodeItem@1100525001(VAR EstimateLine@1100525000 : Record 11012154);
    VAR
      ToEstimateLine@1100525005 : Record 11012154;
      BOMComponent@1100525004 : Record 90;
      NoOfBOMComp@1100525003 : Integer;
      LineSpacing@1100525002 : Integer;
      NextLineNo@1100525001 : Integer;
      FromBOMComp@1100525008 : Record 90;
      BomComp@1100525007 : Record 90;
      TempEstimateLine@1100525006 : TEMPORARY Record 11012154;
    BEGIN
      //explode for item
      WITH EstimateLine DO BEGIN
        FromBOMComp.SETRANGE("Parent Item No.","Item No.");
        FromBOMComp.SETFILTER(Type, '<>%1', FromBOMComp.Type::Resource);
        NoOfBOMComp := FromBOMComp.COUNT;
        IF NoOfBOMComp = 0 THEN BEGIN
          LevelBOM := "Level BOM" + 1;
          ExplodeItemProdBOM(EstimateLine);
        END;

        ToEstimateLine := EstimateLine;
        ToEstimateLine.SETRECFILTER;
        ToEstimateLine.SETRANGE("Line No.");

        OrigLineNo := "Line No.";
        OrigQuantity := Quantity;
        IF OrigItem = '' THEN
          OrigItem := "Item No.";   //db, 21-02-11: M25087

        IF ToEstimateLine.FIND('>') THEN BEGIN
          LineSpacing := (ToEstimateLine."Line No." - "Line No.") DIV (1 + NoOfBOMComp);
          IF LineSpacing = 0 THEN
            ERROR(Text003);
        END ELSE
          LineSpacing := 10000;

        ToEstimateLine := EstimateLine;
        ToEstimateLine.INIT;
        ToEstimateLine.Description := Description;
        ToEstimateLine."Original Quantity" := OrigQuantity;
        ToEstimateLine."Part of Line" := "Part of Line";
        ToEstimateLine."Level BOM" := "Level BOM";
        ToEstimateLine."BOM Item No." := OrigItem;  //db, 21-02-11: M25087
        ToEstimateLine.MODIFY;
        LevelBOM := "Level BOM" + 1;

        IF FromBOMComp.FINDSET THEN BEGIN
          NextLineNo := "Line No.";
          REPEAT
            ToEstimateLine.INIT;
            NextLineNo := NextLineNo + LineSpacing;
            ToEstimateLine.VALIDATE("Sub-Estimate No.");
            ToEstimateLine.VALIDATE(Part);
            ToEstimateLine."Line No." := NextLineNo;

            IF FromBOMComp.Type = FromBOMComp.Type::Item THEN BEGIN
              FromBOMComp.TESTFIELD("No.");
              ToEstimateLine.VALIDATE("Item No.",FromBOMComp."No.");
              ToEstimateLine.VALIDATE(Quantity,Quantity * FromBOMComp."Quantity per");
            END;
            ToEstimateLine."Unit of Measure" := FromBOMComp."Unit of Measure Code";
            ToEstimateLine.Description := FromBOMComp.Description;
            ToEstimateLine."Part of Line" := OrigLineNo;
            ToEstimateLine."Level BOM" := LevelBOM;
            ToEstimateLine."BOM Item No." := OrigItem;  //db, 21-02-11: M25087
            ToEstimateLine.INSERT;

            IF FromBOMComp.Type = FromBOMComp.Type::Item THEN BEGIN
              BomComp.SETRANGE("Parent Item No.", ToEstimateLine."Item No.");
              IF NOT BomComp.ISEMPTY THEN BEGIN
                TempEstimateLine := ToEstimateLine;
                TempEstimateLine.INSERT;
              END;
            END;
          UNTIL FromBOMComp.NEXT = 0;
        END;
      END;

      IF TempEstimateLine.FINDSET THEN BEGIN
        //Explode BOMS
        COMMIT;
        REPEAT
          ExplodeItem(TempEstimateLine);
        UNTIL TempEstimateLine.NEXT = 0;
      END;
    END;

    PROCEDURE ExplodeItemProdBOM@1100525002(VAR EstimateLine@1100525000 : Record 11012154);
    VAR
      Item@1100525006 : Record 27;
      ToEstimateLine@1100525005 : Record 11012154;
      ProductionBOMLine@1100525004 : Record 99000772;
      NoOfBOMComp@1100525003 : Integer;
      LineSpacing@1100525002 : Integer;
      NextLineNo@1100525001 : Integer;
    BEGIN
      //explode for item with prod bom
      WITH EstimateLine DO BEGIN
        Item.GET("Item No.");
        ProductionBOMLine.SETRANGE("Production BOM No.",Item."Production BOM No.");
        ProductionBOMLine.SETRANGE(Type,ProductionBOMLine.Type::Item);
        NoOfBOMComp := ProductionBOMLine.COUNT;
        IF NoOfBOMComp = 0 THEN
          ERROR(Text001,"Item No.");

        ToEstimateLine.COPY(EstimateLine);
        IF ToEstimateLine.FIND('>') THEN BEGIN
          LineSpacing := (ToEstimateLine."Line No." - "Line No.") DIV (1 + NoOfBOMComp);
          IF LineSpacing = 0 THEN
            ERROR(Text003);
        END ELSE
          LineSpacing := 10000;

        NextLineNo := "Line No." + LineSpacing;
        IF ProductionBOMLine.FINDSET THEN
          REPEAT
            ToEstimateLine := EstimateLine;
            //db.sn, 04-08-10 (PT)
            ToEstimateLine.VALIDATE("Sub-Estimate No.");
            ToEstimateLine.VALIDATE(Part);
            //db.en, 04-08-10 (PT)
            ToEstimateLine."Line No." := NextLineNo;
            IF ProductionBOMLine.Type = ProductionBOMLine.Type::Item THEN BEGIN  //db, 12-01-16
              ProductionBOMLine.TESTFIELD("No.");
              ToEstimateLine.VALIDATE("Item No.",ProductionBOMLine."No.");
              ToEstimateLine.VALIDATE(Quantity,Quantity * ProductionBOMLine."Quantity per");
            END;
            //ToEstimateLine."Item No." := ProductionBOMLine."No.";
            //ToEstimateLine.Quantity := (ProductionBOMLine."Quantity per" * Quantity);
            ToEstimateLine."Unit of Measure" := ProductionBOMLine."Unit of Measure Code";
            ToEstimateLine.Description := ProductionBOMLine.Description;
            ToEstimateLine."Part of Line" := OrigLineNo;
            ToEstimateLine."Level BOM" := LevelBOM;
            ToEstimateLine.INSERT;
            NextLineNo += LineSpacing;
          UNTIL ProductionBOMLine.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

