OBJECT Codeunit 11125673 Copy Estimate Elements (RAW)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1100529000 : TextConst 'ENU=First select elements';
      Text002@1100529002 : TextConst 'ENU=''%1'' is already present';
      Text003@1100529003 : TextConst 'ENU=must have the lenght';
      Text004@1100529004 : TextConst 'ENU=last position must be ''0''';
      Text005@1100529005 : TextConst 'ENU=last position may not be ''0''';
      Text006@1210190000 : TextConst 'ENU=(sub element) ''%1'' has no main element ''%2'' in destination estimation ''%3''';

    PROCEDURE BuildEstElemTmpRec@1100529000(VAR MarkedEstElem@1100529000 : Record 11125679;VAR TmpRec@1100529001 : TEMPORARY Record 11125679);
    VAR
      EstElem@1100529003 : Record 11125679;
      LenPrev@1100529006 : Integer;
      LenElem@1100529005 : Integer;
      PrevElem@1100529004 : Text[6];
    BEGIN
      //* Argument TmpRec must be 'Temporary' in the calling process !

      EstElem.COPY(MarkedEstElem);
      EstElem.SETCURRENTKEY("Estimate No.", "Element No.");
      EstElem.SETRANGE("Construction Spec. Line No.", '00');

      IF EstElem.ISEMPTY THEN
        ERROR(Text001);

      EstElem.FINDSET;
      REPEAT
        TmpRec := EstElem;
        TmpRec."Dest. Element No." := '';
        TmpRec.INSERT;
      UNTIL EstElem.NEXT = 0;

      TmpRec.FINDSET;
      REPEAT
        LenPrev := STRLEN(PrevElem);
        LenElem := STRLEN(TmpRec."Element No.");
        IF ((LenElem > LenPrev) AND (LenPrev <> 0) AND (PrevElem = COPYSTR(TmpRec."Element No.", 1, LenPrev))) OR
           ((LenPrev = 6) AND (LenElem = 6) AND
            (COPYSTR(TmpRec."Element No.", 6, 1) <> '0') AND (COPYSTR(PrevElem, 6, 1) = '0') AND
            (COPYSTR(TmpRec."Element No.", 1, 5) = COPYSTR(PrevElem, 1, 5)))
        THEN
          TmpRec.DELETE
        ELSE
          PrevElem := TmpRec."Element No.";
      UNTIL TmpRec.NEXT = 0;
    END;

    PROCEDURE ValidateElemNoDest@1100529004(VAR EstElem@1100529000 : Record 11125679;EstNoDest@1100529002 : Code[20]);
    VAR
      EstElem2@1100529004 : Record 11125679;
      EstElem3@1100529001 : Record 11125679;
      TmpEstElemExpl@1100529003 : TEMPORARY Record 11125679;
    BEGIN
      IF EstElem."Dest. Element No." = '' THEN
        EXIT;

      //* Structure of chapter, (sub)paragraph and elements (ending on 0 and 1..9) must be kept
      IF STRLEN(EstElem."Dest. Element No.") <> STRLEN(EstElem."Element No.") THEN
        EstElem.FIELDERROR("Dest. Element No.", Text003);
      IF STRLEN(EstElem."Element No.") = 6 THEN BEGIN
        IF COPYSTR(EstElem."Element No.",6,1) = '0' THEN BEGIN
          IF COPYSTR(EstElem."Dest. Element No.",6,1) <> '0' THEN
            EstElem.FIELDERROR("Dest. Element No.", Text004);
        END ELSE BEGIN
          IF COPYSTR(EstElem."Dest. Element No.",6,1) = '0' THEN
            EstElem.FIELDERROR("Dest. Element No.", Text005);
          EstElem2.RESET;
          EstElem2.SETRANGE("Estimate No.", EstNoDest);
          EstElem2.SETRANGE("Element No.", COPYSTR(EstElem."Dest. Element No.",1,5) + '0');
          EstElem2.SETRANGE("Construction Spec. Line No.", '00');
          IF EstElem2.ISEMPTY THEN BEGIN
            EstElem.FIELDERROR("Dest. Element No.",
              STRSUBSTNO(Text006, EstElem."Dest. Element No.", COPYSTR(EstElem."Dest. Element No.",1,5) + '0' , EstNoDest));
          END;
        END;
      END;

      //* Check destination element already used on another line (must be done/read in EstElem, because this is a tmp-rec)
      EstElem2.RESET;
      EstElem3.COPY(EstElem);  // Save
      EstElem.FILTERGROUP(9);
      EstElem.SETRANGE("Dest. Element No.", EstElem."Dest. Element No.");
      EstElem.SETFILTER("Element No.", '<>%1', EstElem."Element No.");
      IF EstElem.FINDFIRST THEN
        EstElem2 := EstElem
      ELSE
        EstElem2."Element No." := '';
      EstElem.FILTERGROUP(0);
      EstElem.COPY(EstElem3);  //First restore, then error
      IF EstElem2."Element No." <> '' THEN
        EstElem2.FIELDERROR("Dest. Element No.", STRSUBSTNO(Text002, EstElem2."Dest. Element No."));

      ExplodeEstElem(EstElem, TmpEstElemExpl);
      TmpEstElemExpl.FINDSET;
      EstElem2.RESET;
      REPEAT
        EstElem2.SETRANGE("Estimate No.", EstNoDest);
        EstElem2.SETRANGE("Element No.", TmpEstElemExpl."Dest. Element No.");
        EstElem2.SETRANGE("Construction Spec. Line No.", '00');
        IF EstElem2.FINDFIRST THEN
          EstElem2.FIELDERROR("Element No.", STRSUBSTNO(Text002, EstElem2."Element No."));
      UNTIL TmpEstElemExpl.NEXT = 0;
    END;

    LOCAL PROCEDURE ExplodeEstElem@1100529005(EstElem@1100529000 : Record 11125679;VAR TmpEstElemExpl@1100529002 : TEMPORARY Record 11125679);
    VAR
      EstElem2@1100529003 : Record 11125679;
      LenElem@1100529001 : Integer;
    BEGIN
      LenElem := STRLEN(EstElem."Element No.");
      IF (LenElem = 0) OR ((LenElem = 6) AND (COPYSTR(EstElem."Element No.",6,1) <> '0')) THEN BEGIN
        TmpEstElemExpl := EstElem;
        TmpEstElemExpl.INSERT;
        EXIT;
      END;

      EstElem2.SETRANGE("Estimate No.", EstElem."Estimate No.");
      IF LenElem < 6 THEN
        EstElem2.SETFILTER("Element No.", EstElem."Element No." + '*')
      ELSE
        EstElem2.SETFILTER("Element No.", COPYSTR(EstElem."Element No.",1,5) + '*');
      EstElem2.SETRANGE("Construction Spec. Line No.", '00');
      EstElem2.FINDSET;
      REPEAT
        TmpEstElemExpl := EstElem2;
        TmpEstElemExpl."Dest. Element No." :=
          GetElemNoDestForExploded(EstElem."Element No.",EstElem."Dest. Element No.",EstElem2."Element No.");
        TmpEstElemExpl.INSERT;
      UNTIL EstElem2.NEXT = 0;
    END;

    LOCAL PROCEDURE GetElemNoDestForExploded@1100529015(ElemNoOrig@1100529000 : Text[6];ElemNoDest@1100529001 : Text[6];ElemNoCurr@1100529002 : Text[6]) : Text[6];
    BEGIN
      IF ElemNoCurr = ElemNoOrig THEN
        EXIT(ElemNoDest);

      IF STRLEN(ElemNoDest) = 6 THEN
        EXIT(COPYSTR(ElemNoDest,1,5) + COPYSTR(ElemNoCurr,6,1));

      EXIT(ElemNoDest + COPYSTR(ElemNoCurr,STRLEN(ElemNoDest)+1));
    END;

    PROCEDURE ProcessCopy@1100529007(VAR EstElem@1100529000 : TEMPORARY Record 11125679;EstNoDest@1100529001 : Code[20];CopyQuantities@1100529007 : Boolean;CopyText@1100529006 : Boolean;CopyEstLines@1100529005 : Boolean;CopyVatSpecs@1100529003 : Boolean;CopyComments@1100525000 : Boolean);
    VAR
      Est@1100529009 : Record 11012151;
      EstElem2@1100529010 : Record 11125679;
      EstElemDest@1100529008 : Record 11125679;
      TmpEstElemOrig@1100529002 : TEMPORARY Record 11125679;
      CommentLineRAW@1100525001 : Record 11125684;
      IgnoreElemNoDestEmpty@1100529004 : Boolean;
    BEGIN
      Est.GET(EstNoDest);
      Est.TESTFIELD("No.");
      Est.TESTFIELD("RAW Estimate", TRUE);

      EstElem2.COPY(EstElem);  // Save

      EstElem.FILTERGROUP(8);
      EstElem.SETRANGE("Dest. Element No.", '');
      IF EstElem.FINDFIRST THEN BEGIN
        IF EstElem."Estimate No." <> EstNoDest THEN BEGIN  // Only if Dest. unequal Orig. Estimate
          EstElem.SETFILTER("Dest. Element No.", '<>%1', '');
          IF EstElem.ISEMPTY THEN  // If not filled in all lines
            IgnoreElemNoDestEmpty := TRUE;   // Then use same Element No. as in Orig. Estimate
        END;
      END;
      EstElem.SETRANGE("Dest. Element No.");  // Reset filter
      EstElem.FILTERGROUP(0);

      EstElem.FINDSET;
      REPEAT
        IF NOT IgnoreElemNoDestEmpty THEN
          EstElem.TESTFIELD("Dest. Element No.")
        ELSE BEGIN
          IF EstElem."Dest. Element No." = '' THEN
            EstElem."Dest. Element No." := EstElem."Element No.";
        END;
        ValidateElemNoDest(EstElem, EstNoDest);
        ExplodeEstElem(EstElem, TmpEstElemOrig);
      UNTIL EstElem.NEXT = 0;

      EstElem.COPY(EstElem2);  // Restore

      TmpEstElemOrig.FINDSET;
      REPEAT
        EstElemDest := TmpEstElemOrig;
        EstElemDest."Estimate No." := EstNoDest;
        EstElemDest."Element No." := TmpEstElemOrig."Dest. Element No.";
        EstElemDest."Line No." := 1;
        EstElemDest.INSERT(TRUE);  // With TRUE, check element already exist must be done (for "Construction Spec. Line No." = '00')
        IF (NOT CopyQuantities) AND (EstElemDest.Quantity <> 0) THEN BEGIN
            EstElemDest.VALIDATE(Quantity, 1);
            EstElemDest.MODIFY;
        END;
        IF CopyText THEN
          CopyEstElemText(TmpEstElemOrig."Estimate No.", TmpEstElemOrig."Element No.", EstNoDest, EstElemDest."Element No.");
        IF CopyEstLines THEN
          CopyEstElemLines(TmpEstElemOrig."Estimate No.", TmpEstElemOrig."Element No.", EstNoDest, EstElemDest."Element No.", CopyComments);
        IF CopyVatSpecs THEN
          CopyEstElemVatSpecs(TmpEstElemOrig."Estimate No.", TmpEstElemOrig."Element No.", EstNoDest, EstElemDest."Element No.");
        //DP01086.sn
        IF EstElemDest."Construction Spec. Line No." = '00' THEN BEGIN
          IF CopyComments THEN BEGIN
            CommentLineRAW.CopyComment(
              DATABASE::"Estimate Element (RAW)", '', '', TmpEstElemOrig."Estimate No.", TmpEstElemOrig."Element No.", TmpEstElemOrig."Line No.", 0,
              DATABASE::"Estimate Element (RAW)", '', '', EstElemDest."Estimate No.", EstElemDest."Element No.", EstElemDest."Line No.", 0);
          END;
          IF NOT CommentLineRAW.HasEstElemLineComment(EstElemDest."Estimate No.", EstElemDest."Element No.", EstElemDest."Line No.") THEN
            EstElemDest.CopyActivityCommentRAW('', EstElemDest.Activity, FALSE, '');
        END;
        //DP01086.en
      UNTIL TmpEstElemOrig.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyEstElemText@1100529009(EstNoOrig@1100529002 : Code[20];ElemNoOrig@1100529003 : Text[6];EstNoDest@1100529005 : Code[20];ElemNoDest@1100529004 : Text[6]);
    VAR
      EstElemTextOrig@1100529000 : Record 11125679;
      EstElemTextDest@1100529001 : Record 11125679;
      NextLineNo@1100529006 : Integer;
    BEGIN
      EstElemTextOrig.SETRANGE("Estimate No.", EstNoOrig);
      EstElemTextOrig.SETRANGE("Element No.", ElemNoOrig);
      EstElemTextOrig.SETFILTER("Construction Spec. Line No.", '<>%1', '00');
      IF EstElemTextOrig.FINDSET THEN BEGIN
        NextLineNo := 10000;
        REPEAT
          EstElemTextDest := EstElemTextOrig;
          EstElemTextDest."Estimate No." := EstNoDest;
          EstElemTextDest."Element No." := ElemNoDest;
          EstElemTextDest."Line No." := NextLineNo;
          EstElemTextDest.INSERT;
          NextLineNo := NextLineNo + 10000;
        UNTIL EstElemTextOrig.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CopyEstElemLines@1100529017(EstNoOrig@1100529002 : Code[20];ElemNoOrig@1100529003 : Text[6];EstNoDest@1100529005 : Code[20];ElemNoDest@1100529004 : Text[6];CopyComments@1100525001 : Boolean);
    VAR
      EstLineOrig@1100529000 : Record 11125680;
      EstLineDest@1100529001 : Record 11125680;
      CommentLineRAW@1100525000 : Record 11125684;
      NextLineNo@1100529006 : Integer;
    BEGIN
      EstLineOrig.SETRANGE("Estimate No.", EstNoOrig);
      EstLineOrig.SETRANGE("Element No.", ElemNoOrig);
      IF EstLineOrig.FINDSET THEN BEGIN
        NextLineNo := 10000;
        REPEAT
          EstLineDest := EstLineOrig;
          EstLineDest."Estimate No." := EstNoDest;
          EstLineDest."Element No." := ElemNoDest;
          EstLineDest."Line No." := NextLineNo;
          EstLineDest.InitAdjustmentFactor();
          EstLineDest.InitPurchaseFields();
          EstLineDest.CalculateTotals();
          EstLineDest.INSERT;
          NextLineNo := NextLineNo + 10000;
         //DP01086.sn
         IF CopyComments THEN BEGIN
            CommentLineRAW.CopyComment(
              DATABASE::"Estimate Line (RAW)", '', '', EstLineOrig."Estimate No.", EstLineOrig."Element No.", 0, EstLineOrig."Line No.",
              DATABASE::"Estimate Line (RAW)", '', '', EstLineDest."Estimate No.", EstLineDest."Element No.", 0, EstLineDest."Line No.");
          END;
        //DP01086.en
        UNTIL EstLineOrig.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CopyEstElemVatSpecs@1100529026(EstNoOrig@1100529002 : Code[20];ElemNoOrig@1100529003 : Text[6];EstNoDest@1100529005 : Code[20];ElemNoDest@1100529004 : Text[6]);
    VAR
      EstElemVatSpecOrig@1100529000 : Record 11125678;
      EstElemVatSpecDest@1100529001 : Record 11125678;
    BEGIN
      EstElemVatSpecOrig.SETRANGE("Estimate No.", EstNoOrig);
      EstElemVatSpecOrig.SETRANGE("Element No.", ElemNoOrig);
      IF EstElemVatSpecOrig.FINDSET THEN BEGIN
        REPEAT
          EstElemVatSpecDest := EstElemVatSpecOrig;
          EstElemVatSpecDest."Estimate No." := EstNoDest;
          EstElemVatSpecDest."Element No." := ElemNoDest;
          EstElemVatSpecDest.INSERT;
        UNTIL EstElemVatSpecOrig.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

