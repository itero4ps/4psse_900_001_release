OBJECT Codeunit 11128060 VP pain.001.001.03 Default
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=PE7.01 (12048857);
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      gVPMnm@1100570002 : Codeunit 11128061;
      gTotalAmountHash@1100570000 : Decimal;
      gFromFile@1100570004 : Text;
      gToFile@1100570006 : Text;
      gPostGiroNo@1100570016 : Code[20];
      gCompanyInfo@12047960 : Record 79;
      BA008@12047958 : TextConst 'ENU=This file format does not support Credit memos.\ Try waste Merge lines.\If that does not help Merge is no support for the payment method.';
      gNoOfPayments@12047965 : Integer;
      BA017@12047966 : TextConst 'ENU=All payments are not included in the file, check the currency';
      gNoOfEntries@12047982 : Integer;
      gTotalAmountLCY@12047967 : Decimal;
      BA019@12047968 : TextConst 'ENU=Due Date must not be more than %1 days from today';
      BA020@12047969 : TextConst 'ENU=%1 can not last before today''s date';
      BA025@12047959 : TextConst 'ENU=%1 or %2 must be filled in for %3, %4 %5';
      gMsgId@12047975 : Code[20];
      BA027@12047976 : TextConst 'ENU=length must be %1 chars';
      BA028@12047977 : TextConst 'ENU=Format "%1" is not supported for Danish payment Files';
      BA029@12047987 : TextConst 'ENU=%1 must not be blank for foreign payment';
      BA030@12047979 : TextConst 'ENU=Vendor %1 has none of the following completed: Bankgiro No., Plusgiro No. or Bank Account No.';
      BA031@12047981 : TextConst 'ENU=%1 is not correct for Vendor %2 %3.';
      BA032@12047980 : TextConst 'ENU=%1, "%2" is not valid';
      BA034@12047984 : TextConst 'ENU=System Error: The difference between what is paid and what is in the specification.';
      BA035@12047974 : TextConst 'ENU=Some of the fields %1 or %2 must be completed in %3 for country %4';
      XMLWriter@12047985 : Codeunit 11128077;
      FinInstnIdIsSet@12047986 : Boolean;
      BA036@12047961 : TextConst 'ENU=Merging %1 gives Zero Amounts it is not allowed for the Payment type';
      BA037@12047964 : TextConst 'ENU=Merging %1 gives negative Amounts it is not allowed';
      RmtInfIsSet@12047962 : Boolean;
      Domestic@12047963 : Boolean;
      Bank@1100570001 : ' ,Default,SEB,DNB,Finland,Handelsbanken,Swedbank';
      BA038@1100570003 : TextConst 'ENU=or %1 Must have value';
      BA039@12047970 : TextConst 'ENU=Length of %1 must be 4 characters';
      BA040@12047971 : TextConst 'ENU=" must not be Entered for %1 Payment"';
      SEPAPayment@12047972 : Boolean;
      BA042@12047983 : TextConst 'ENU=Domestic payments are not supported for Country Code %1';
      gRegistrationNo@12047978 : Text;
      BA043@12047973 : TextConst 'ENU=%1, %2 for Vendor %3 is not allowed';
      BA044@12047988 : TextConst 'ENU=It is not allowed to merge Account payment';

    PROCEDURE Create@12047958(pPackEntryNo@12047959 : Integer;pBank@12047960 : Code[20]);
    VAR
      Pack@12047958 : Record 11128079;
      GiroType@12047963 : Record 11128061;
      VPPackEntry@12047962 : Record 11128063;
      tCurrency@12047961 : TEMPORARY Record 4;
    BEGIN
      Pack.GET(pPackEntryNo);

      gCompanyInfo.GET;
      gCompanyInfo.TESTFIELD(Name);
      gCompanyInfo.TESTFIELD("Registration No.");

      GiroType.GET(Pack."Giro Type Code");
      GiroType.TESTFIELD(Filename);
      gToFile := GiroType.Filename;

      OpenFile;

      IF GiroType."Registration No." <> '' THEN
        gRegistrationNo := gVPMnm.ClearText(GiroType."Registration No.")
      ELSE
        gRegistrationNo := gVPMnm.ClearText(gCompanyInfo."Registration No.");

      gVPMnm.SetGiroType(GiroType);

      CreateFile(Pack,pBank);

      CloseFile(Pack);
    END;

    PROCEDURE CreateFile@1070008(pPack@1070000 : Record 11128079;pBank@12047963 : Code[20]);
    VAR
      VPGiroType@12047962 : Record 11128061;
      Vendor@1100570006 : Record 23;
      VendorBankAccount@1100570008 : Record 288;
      VPPackEntry@12047959 : Record 11128063;
      AmountToPay@1100570012 : Decimal;
      AmountToPayLCY@1100570013 : Decimal;
      SpecificationAmount@12047978 : Decimal;
      NoSeriesMgt@12047961 : Codeunit 396;
      LineLevel@12047964 : Integer;
      LineID@12047960 : Code[20];
      Level@12047965 : Integer;
      CreditorLevel@12047986 : Integer;
      ToCountryCode@12047968 : Code[10];
      FromCountryCode@12047982 : Code[10];
      FromIBAN@12047966 : Text;
      FromBankBranchNo@12047969 : Text;
      FromBankAccountNo@12047970 : Text;
      FromSWIFTCode@12047971 : Text;
      FromCurrencyCode@12047979 : Code[10];
      CurrencyAccount@12047972 : Record 11128073;
      LineNo@12047973 : Integer;
      DebitorLineNo@12047985 : Integer;
      PayByAccountNo@12047975 : Code[30];
      tLedgerEntryPayment@12047977 : TEMPORARY Record 11128074;
      tLedgerEntry@12047976 : TEMPORARY Record 11128074;
      tLedgerEntryUpdatePackEntry@12047980 : TEMPORARY Record 11128074;
      IsDebitAmount@12047981 : Boolean;
      ExternalDocumentNo@12047958 : Text;
      PaymentType@12047983 : Code[10];
      NoOfRmtInfLines@12047967 : Integer;
      i@12047974 : Integer;
      tAggregateDebitor@12047984 : TEMPORARY Record 11128077;
      NextAggregateNo@12047987 : Integer;
      MaxForwardDags@12047988 : Integer;
      LongName@12047989 : Text;
    BEGIN
      CASE pBank OF
        'DEFAULT': Bank := Bank::Default;
        'SEB': Bank := Bank::SEB;
        'DNB': Bank := Bank::DNB;
        'HANDELSBANKEN': Bank := Bank::Handelsbanken;
        'SWEDBANK': Bank := Bank::Swedbank;
        ELSE
          ERROR(BA032,'Bank',pBank);
      END;

      VPPackEntry.RESET;
      VPPackEntry.SETRANGE("Pack Entry No.",pPack."Entry No.");
      IF NOT gVPMnm.GetPaymentEntries(VPPackEntry,tLedgerEntryPayment,tLedgerEntry,TRUE,FALSE) THEN
        EXIT;

      VPGiroType.GET(pPack."Giro Type Code");

      FromCountryCode := gVPMnm.GetCountryCode(VPGiroType."Country/Region Code");

      VPGiroType.TESTFIELD("SEPA No. Series for Msg.");
      IF pPack."SEPA Reference" <> '' THEN
        gMsgId := pPack."SEPA Reference"
      ELSE
        gMsgId := NoSeriesMgt.GetNextNo(VPGiroType."SEPA No. Series for Msg.",TODAY,FALSE);

      // Calc totals
      CLEAR(gTotalAmountHash);
      CLEAR(gTotalAmountLCY);
      CLEAR(gNoOfPayments);
      NextAggregateNo := 1;

      tAggregateDebitor.RESET;
      tAggregateDebitor.DELETEALL;

      // SumTotal and prepare Aggregate Debitor
      tLedgerEntryPayment.RESET;
      tLedgerEntryPayment.FINDSET;
      REPEAT
        CheckVendorEntry(tLedgerEntryPayment);
        gNoOfPayments += 1;
        gTotalAmountHash += -tLedgerEntryPayment."Remaining Amount";
        gTotalAmountLCY += -tLedgerEntryPayment."Remaining Amt. (LCY)";

        Vendor.GET(tLedgerEntryPayment."No.");
        Vendor.TESTFIELD(Name);
        VendorBankAccount.GET(tLedgerEntryPayment."No.",tLedgerEntryPayment."Bank Account Code");

        ToCountryCode := gVPMnm.GetCountryCode(VendorBankAccount."Country/Region Code");

        IF ToCountryCode <> 'SE' THEN
          IF (VendorBankAccount."Bank Account No." = '') AND (VendorBankAccount.IBAN = '') THEN
            ERROR(BA025,VendorBankAccount.FIELDCAPTION("Bank Account No."),
                        VendorBankAccount.FIELDCAPTION(IBAN),
                        VendorBankAccount.TABLECAPTION,
                        VendorBankAccount."Vendor No.",
                        VendorBankAccount.Code);

        // Aggregate Debitor
        tAggregateDebitor.INIT;
        tAggregateDebitor.Domestic := IsDomestic(tLedgerEntryPayment,FromCountryCode,ToCountryCode);
        tAggregateDebitor."Due Date" := tLedgerEntryPayment."Due Date";
        tAggregateDebitor."SEPA Payment" := IsSEPACountry(ToCountryCode) AND
                                            (tLedgerEntryPayment."Currency Code" = 'EUR') AND
                                            (VendorBankAccount.IBAN <> '');
        tAggregateDebitor."Payment Method" := VendorBankAccount."Payment Method";

        // Find From-Account
        IF CurrencyAccount.GET(VPGiroType.Code,gVPMnm.GetPayToCurrencyCode(tLedgerEntryPayment."Currency Code")) THEN BEGIN
          IF (CurrencyAccount."Bank Account No." = '') AND (CurrencyAccount.IBAN = '') THEN
            ERROR(BA025,CurrencyAccount.FIELDCAPTION("Bank Account No."),CurrencyAccount.FIELDCAPTION(IBAN),
                        CurrencyAccount.TABLECAPTION,CurrencyAccount."VP Giro Type Code",CurrencyAccount."Currency Code");

          tAggregateDebitor."From Branch No" := CurrencyAccount."Bank Branch No.";
          tAggregateDebitor."From Bank Account No" := CurrencyAccount."Bank Account No.";
          tAggregateDebitor."From IBAN" := DELCHR(CurrencyAccount.IBAN);;
          tAggregateDebitor."From SWIFT Code" := CurrencyAccount."SWIFT Code";
          tAggregateDebitor."From Currency Code" := CurrencyAccount."Currency Code";
          IF tAggregateDebitor."From IBAN" <> '' THEN
            CurrencyAccount.TESTFIELD("SWIFT Code");

          IF Bank = Bank::SEB THEN BEGIN
            IF tAggregateDebitor."From Bank Account No" <> '' THEN
              CurrencyAccount.TESTFIELD("SWIFT Code");
          END ELSE BEGIN
            IF tAggregateDebitor."From Bank Account No" <> '' THEN
              CurrencyAccount.TESTFIELD("Bank Branch No.");
          END;
        END ELSE BEGIN
          IF (VPGiroType."Bank Account No." = '') AND (VPGiroType.IBAN = '') THEN
            ERROR(BA025,VPGiroType.FIELDCAPTION("Bank Account No."),VPGiroType.FIELDCAPTION(IBAN),
                        VPGiroType.TABLECAPTION,VPGiroType.Code,'');

          tAggregateDebitor."From Branch No" := VPGiroType."Bank Branch No.";
          tAggregateDebitor."From Bank Account No" := VPGiroType."Bank Account No.";
          tAggregateDebitor."From IBAN" := DELCHR(VPGiroType.IBAN);
          tAggregateDebitor."From SWIFT Code" := VPGiroType."SWIFT Code";
          tAggregateDebitor."From Currency Code" := gVPMnm.GetLCYCode;
          IF tAggregateDebitor."From IBAN" <> '' THEN
            VPGiroType.TESTFIELD("SWIFT Code");

          IF Bank = Bank::SEB THEN BEGIN
            IF tAggregateDebitor."From Bank Account No" <> '' THEN
              VPGiroType.TESTFIELD("SWIFT Code");
          END ELSE BEGIN
            IF tAggregateDebitor."From Bank Account No" <> '' THEN
              VPGiroType.TESTFIELD("Bank Branch No.");
          END;
        END;

        IF NOT VPGiroType."Aggregate on Debitor Level" THEN
          tAggregateDebitor."No." += 1;

        IF NOT tAggregateDebitor.FIND THEN BEGIN
          tAggregateDebitor."Debitor Aggregate No." := NextAggregateNo;
          tAggregateDebitor.INSERT;
          NextAggregateNo += 1;
        END;
        tLedgerEntryPayment."Debitor Aggregate No." := tAggregateDebitor."Debitor Aggregate No.";
        tLedgerEntryPayment.MODIFY;

      UNTIL tLedgerEntryPayment.NEXT = 0;
      tLedgerEntry.RESET;
      gNoOfEntries := tLedgerEntry.COUNT;

      // Header
      XMLWriter.CreateDomNS('','','Document','urn:iso:std:iso:20022:tech:xsd:pain.001.001.03');
      XMLWriter.SetNotBlankElement(TRUE);
      XMLWriter.AddElementRel(1,'CstmrCdtTrfInitn','');
      XMLWriter.AddElementRel(1,'GrpHdr','');
      XMLWriter.AddElementRel(1,'MsgId',gMsgId);
      XMLWriter.AddElementRel(0,'CreDtTm',CURRENTDATETIME);
      XMLWriter.AddElementRel(0,'NbOfTxs',gNoOfPayments);
      XMLWriter.AddElementRel(0,'CtrlSum',gTotalAmountHash);
      XMLWriter.AddElementRel(0,'InitgPty','');
      XMLWriter.AddElementRel(1,'Nm',gCompanyInfo.Name);
      Level := XMLWriter.GetLevel;
      IF Bank <> Bank::SEB THEN BEGIN // SEB har en bugg s† vi kan inte ha det h„r med (R„ttas 2016-10-30 av SEB)
        XMLWriter.AddElementRel(0,'PstlAdr','');
        XMLWriter.AddElementRel(1,'StrtNm',gCompanyInfo.Address);
        XMLWriter.AddElementRel(0,'PstCd',gCompanyInfo."Post Code");
        XMLWriter.AddElementRel(0,'TwnNm',gCompanyInfo.City);
        XMLWriter.AddElementRel(0,'Ctry',gVPMnm.GetCountryCode(gCompanyInfo."Country/Region Code"));
      END;

      XMLWriter.AddElementAbs(Level,'Id','');
      XMLWriter.AddElementRel(1,'OrgId','');

      Level := XMLWriter.GetLevel + 1;

      IF (Bank = Bank::Swedbank) AND (VPGiroType."Org ID BANK" <> '') THEN BEGIN
        XMLWriter.AddElementAbs(Level,'Othr','');
        XMLWriter.AddElementRel(1,'Id',VPGiroType."Org ID BANK");
        XMLWriter.AddElementRel(0,'SchmeNm','');
        XMLWriter.AddElementRel(1,'Cd','BANK');
      END ELSE BEGIN
        XMLWriter.AddElementAbs(Level,'Othr','');
        XMLWriter.AddElementRel(1,'Id',GetOrgIDCust(VPGiroType));
        XMLWriter.AddElementRel(0,'SchmeNm','');
        XMLWriter.AddElementRel(1,'Cd','CUST');

        IF VPGiroType."Org ID BANK" <> '' THEN BEGIN
          XMLWriter.AddElementAbs(Level,'Othr','');
          XMLWriter.AddElementRel(1,'Id',VPGiroType."Org ID BANK");
          XMLWriter.AddElementRel(0,'SchmeNm','');
          XMLWriter.AddElementRel(1,'Cd','BANK');
        END;
      END;

      LineLevel := XMLWriter.GetLevel - 6;
      DebitorLineNo := 0;

      tAggregateDebitor.RESET;
      tAggregateDebitor.SETCURRENTKEY("Debitor Aggregate No.");
      // Debitor level
      IF tAggregateDebitor.FINDSET THEN REPEAT
        // Check Due Date
        IF tAggregateDebitor.Domestic THEN
          MaxForwardDags := 365
        ELSE
          MaxForwardDags := 30;
        IF (tAggregateDebitor."Due Date" - TODAY) > MaxForwardDags THEN
          ERROR(BA019,MaxForwardDags);

        FromBankBranchNo := tAggregateDebitor."From Branch No";
        FromBankAccountNo := tAggregateDebitor."From Bank Account No";
        FromIBAN := tAggregateDebitor."From IBAN";
        FromSWIFTCode := tAggregateDebitor."From SWIFT Code";
        FromCurrencyCode := tAggregateDebitor."From Currency Code";
        Domestic := tAggregateDebitor.Domestic;
        SEPAPayment := tAggregateDebitor."SEPA Payment";

        DebitorLineNo += 1;
        LineNo := 0;
        LineID := gMsgId + '/' + FORMAT(DebitorLineNo);

        XMLWriter.AddElementAbs(LineLevel,'PmtInf','');
        XMLWriter.AddElementRel(1,'PmtInfId',LineID);

        Level := XMLWriter.GetLevel;
        CreditorLevel := Level;

        XMLWriter.AddElementAbs(Level,'PmtMtd','TRF'); // TRF indicates that it is a Credit Transfer Transaction
        XMLWriter.AddElementAbs(Level,'BtchBookg',VPGiroType."Batch Booking");
        XMLWriter.AddElementAbs(Level,'PmtTpInf','');

        IF Bank = Bank::SEB THEN BEGIN
          IF tAggregateDebitor."Payment Method" = tAggregateDebitor."Payment Method"::Express THEN
            XMLWriter.AddElementAbs(Level + 1,'InstrPrty','HIGH');
        END;

        XMLWriter.AddElementAbs(Level + 1,'SvcLvl','');
        IF SEPAPayment THEN
          XMLWriter.AddElementRel(1,'Cd','SEPA')
        ELSE BEGIN
          IF (Bank = Bank::SEB) AND Domestic THEN BEGIN
            XMLWriter.AddElementRel(1,'Prtry','MPNS');
            XMLWriter.AddElementRel(-1,'LclInstrm','');
            XMLWriter.AddElementRel(1,'Prtry','DO');
          END ELSE
            XMLWriter.AddElementRel(1,'Cd',GetServiceLevelCode(tAggregateDebitor));
        END;

        IF NOT (Bank IN [Bank::SEB,Bank::DNB])  THEN BEGIN
          XMLWriter.AddElementRel(-1,'CtgyPurp','');
          XMLWriter.AddElementRel(1,'Cd','SUPP');
        END;

        XMLWriter.AddElementAbs(Level,'ReqdExctnDt',tAggregateDebitor."Due Date");

        XMLWriter.AddElementAbs(Level,'Dbtr','');
        XMLWriter.AddElementRel(1,'Nm',gCompanyInfo.Name);
        XMLWriter.AddElementRel(0,'PstlAdr','');
        XMLWriter.AddElementRel(1,'Ctry',FromCountryCode);

        IF VPGiroType."Bank Agreement No." <> '' THEN BEGIN
          XMLWriter.AddElementAbs(Level + 1,'Id','');
          XMLWriter.AddElementRel(1,'OrgId','');
          XMLWriter.AddElementRel(1,'Othr','');
          XMLWriter.AddElementRel(1,'Id',VPGiroType."Bank Agreement No.");
          XMLWriter.AddElementRel(0,'SchmeNm','');
          XMLWriter.AddElementRel(1,'Cd','BANK');
        END ELSE BEGIN
          IF Bank = Bank::SEB THEN BEGIN
            VPGiroType.TESTFIELD("SEB Sub Customer No.");
            IF STRLEN(VPGiroType."SEB Sub Customer No.") <> 4 THEN
              ERROR(BA039,VPGiroType.FIELDCAPTION("SEB Sub Customer No."));
            XMLWriter.AddElementAbs(Level + 1,'Id','');
            XMLWriter.AddElementRel(1,'OrgId','');
            XMLWriter.AddElementRel(1,'Othr','');
            XMLWriter.AddElementRel(1,'Id',gRegistrationNo + VPGiroType."SEB Sub Customer No.");
            XMLWriter.AddElementRel(0,'SchmeNm','');
            XMLWriter.AddElementRel(1,'Cd','CUST');
          END ELSE BEGIN
            IF FromSWIFTCode <> '' THEN BEGIN
              IF Bank <> Bank::DNB THEN BEGIN
                XMLWriter.AddElementAbs(Level + 1,'Id','');
                XMLWriter.AddElementRel(1,'OrgId','');
                XMLWriter.AddElementRel(1,'BICOrBEI',FromSWIFTCode);
              END;
            END ELSE BEGIN
              IF FromBankBranchNo = '9960' THEN BEGIN // Nordea
                VPGiroType.TESTFIELD("Plus Giro Customer No.");
                XMLWriter.AddElementAbs(Level + 1,'Id','');
                XMLWriter.AddElementRel(1,'OrgId','');
                XMLWriter.AddElementRel(1,'Othr','');
                XMLWriter.AddElementRel(1,'Id',VPGiroType."Plus Giro Customer No.");
              END;
            END;
          END;
        END;

        XMLWriter.AddElementAbs(Level,'DbtrAcct','');

        IF FromIBAN <> '' THEN BEGIN
          XMLWriter.AddElementRel(1,'Id','');
          XMLWriter.AddElementRel(1,'IBAN',FromIBAN);

          XMLWriter.AddElementAbs(Level +1,'Ccy',FromCurrencyCode);

          XMLWriter.AddElementAbs(Level,'DbtrAgt','');
          XMLWriter.AddElementRel(1,'FinInstnId','');
          XMLWriter.AddElementRel(1,'BIC',FromSWIFTCode);
        END ELSE BEGIN
          XMLWriter.AddElementRel(1,'Id','');
          XMLWriter.AddElementRel(1,'Othr','');
          XMLWriter.AddElementRel(1,'Id',FromBankAccountNo);
          XMLWriter.AddElementRel(0,'SchmeNm','');
          XMLWriter.AddElementRel(1,'Cd','BBAN');

          XMLWriter.AddElementAbs(Level +1,'Ccy',FromCurrencyCode);
          IF Bank = Bank::SEB THEN BEGIN
            XMLWriter.AddElementAbs(Level,'DbtrAgt','');
            XMLWriter.AddElementRel(1,'FinInstnId','');
            XMLWriter.AddElementRel(1,'BIC',FromSWIFTCode);
          END ELSE BEGIN
            XMLWriter.AddElementAbs(Level,'DbtrAgt','');
            XMLWriter.AddElementRel(1,'FinInstnId','');
            XMLWriter.AddElementRel(1,'ClrSysMmbId','');
            XMLWriter.AddElementRel(1,'ClrSysId','');
            XMLWriter.AddElementRel(1,'Cd','SESBA');
            XMLWriter.AddElementRel(-1,'MmbId',FromBankBranchNo);
          END;
        END;

        XMLWriter.AddElementAbs(Level,'ChrgBr',GetChrgBr(VendorBankAccount));

        // Creditor level
        tLedgerEntryPayment.RESET;
        tLedgerEntryPayment.SETCURRENTKEY("Debitor Aggregate No.");
        tLedgerEntryPayment.SETRANGE("Debitor Aggregate No.",tAggregateDebitor."Debitor Aggregate No.");
        tLedgerEntryPayment.FINDSET;
        REPEAT
          Level := CreditorLevel;
          LineNo += 1;
          LineID := gMsgId + '/' + FORMAT(DebitorLineNo) + '/' + FORMAT(LineNo);

          Vendor.GET(tLedgerEntryPayment."No.");
          Vendor.TESTFIELD(Name);
          VendorBankAccount.GET(tLedgerEntryPayment."No.",tLedgerEntryPayment."Bank Account Code");
          ToCountryCode := gVPMnm.GetCountryCode(VendorBankAccount."Country/Region Code");
          PaymentType := ToCountryCode;

          XMLWriter.AddElementAbs(Level,'CdtTrfTxInf','');
          XMLWriter.AddElementRel(1,'PmtId','');
          XMLWriter.AddElementAbs(Level + 2,'InstrId',LineID);

          XMLWriter.AddElementAbs(Level + 2,'EndToEndId',LineID);
          XMLWriter.AddElementRel(-1,'Amt','');
          XMLWriter.AddElementRel(1,'InstdAmt',GetAmountToPay(tLedgerEntryPayment,FALSE));
          XMLWriter.AddAttribute('Ccy',gVPMnm.GetPayToCurrencyCode(tLedgerEntryPayment."Currency Code"));

          Level := XMLWriter.GetLevel -1;

          IF VendorBankAccount."Transit No." <> '' THEN BEGIN
            XMLWriter.AddElementAbs(Level,'IntrmyAgt1','');
            XMLWriter.AddElementRel(1,'FinInstnId','');
            XMLWriter.AddElementRel(1,'BIC',VendorBankAccount."Transit No.");
          END;

          XMLWriter.AddElementAbs(Level,'CdtrAgt','');

          FinInstnIdIsSet := FALSE;

          IF VendorBankAccount.IBAN = '' THEN BEGIN
            CASE ToCountryCode OF
              'SE':
                BEGIN
                  IF Domestic THEN BEGIN
                    FinInstnIdIsSet := TRUE;

                    IF VendorBankAccount."Bank Giro No." <> '' THEN BEGIN
                      PayByAccountNo := gVPMnm.ClearText(VendorBankAccount."Bank Giro No.");
                      IF PayByAccountNo = '' THEN
                        ERROR(BA031,VendorBankAccount.FIELDCAPTION("Bank Giro No."),Vendor."No.", Vendor.Name);
                      IF NOT gVPMnm.SumCheck10Control(PayByAccountNo) THEN
                        ERROR(BA031,VendorBankAccount.FIELDCAPTION("Bank Giro No."),Vendor."No.", Vendor.Name);

                      PaymentType := 'SEBG';
                      XMLWriter.AddElementRel(1,'FinInstnId','');
                      XMLWriter.AddElementRel(1,'ClrSysMmbId','');
                      XMLWriter.AddElementRel(1,'ClrSysId','');
                      XMLWriter.AddElementRel(1,'Cd','SESBA');
                      XMLWriter.AddElementRel(-1,'MmbId','9900');
                    END ELSE
                      IF VendorBankAccount."Plus Giro No." <> '' THEN BEGIN
                        PayByAccountNo := gVPMnm.ClearText(VendorBankAccount."Plus Giro No.");
                        IF PayByAccountNo = '' THEN
                          ERROR(BA031,VendorBankAccount.FIELDCAPTION("Plus Giro No."),Vendor."No.", Vendor.Name);
                        IF NOT gVPMnm.SumCheck10Control(PayByAccountNo) THEN
                          ERROR(BA031,VendorBankAccount.FIELDCAPTION("Plus Giro No."),Vendor."No.", Vendor.Name);

                        PaymentType := 'SEPG';
                        XMLWriter.AddElementRel(1,'FinInstnId','');
                        XMLWriter.AddElementRel(1,'ClrSysMmbId','');
                        XMLWriter.AddElementRel(1,'ClrSysId','');
                        XMLWriter.AddElementRel(1,'Cd','SESBA');
                        IF Bank = Bank::Handelsbanken THEN
                          XMLWriter.AddElementRel(-1,'MmbId','9500')
                        ELSE
                          XMLWriter.AddElementRel(-1,'MmbId','9960');
                      END ELSE
                        IF VendorBankAccount."Bank Account No." <> '' THEN BEGIN
                          PayByAccountNo := gVPMnm.ClearText(VendorBankAccount."Bank Account No.");
                          IF PayByAccountNo = '' THEN
                            ERROR(BA031,VendorBankAccount.FIELDCAPTION("Bank Account No."),Vendor."No.", Vendor.Name);
                          PaymentType := 'SEBA';
                          InsertNodeFinInstnId(VendorBankAccount,'SESBA');
                        END ELSE
                          ERROR(BA030,VendorBankAccount."Vendor No.");
                  END;
                END;
              'GB':
                BEGIN
                  CheckBankBranchNoOrSWIFTCode(VendorBankAccount,ToCountryCode);
                  IF Domestic AND (VendorBankAccount."Bank Branch No." <> '') THEN
                    InsertNodeFinInstnId(VendorBankAccount,'GBDSC');
                END;
              'US': InsertNodeFinInstnId(VendorBankAccount,'USABA');
              'AU': InsertNodeFinInstnId(VendorBankAccount,'AUBSB');
              'CA': InsertNodeFinInstnId(VendorBankAccount,'CACPA');
              'CN': InsertNodeFinInstnId(VendorBankAccount,'CNAPS');
              'HK': InsertNodeFinInstnId(VendorBankAccount,'HKNCC');
              'IN': InsertNodeFinInstnId(VendorBankAccount,'INFSC');
              'NZ': InsertNodeFinInstnId(VendorBankAccount,'NZNCC');
              'RU': InsertNodeFinInstnId(VendorBankAccount,'RUCBC');
              'ZA': InsertNodeFinInstnId(VendorBankAccount,'ZANCC');

              'DK':
                BEGIN
                  IF Domestic THEN BEGIN
                    FinInstnIdIsSet := TRUE;

                    IF COPYSTR(VendorBankAccount."Bank Account No.",1,1) = '+' THEN BEGIN
                      CASE COPYSTR(VendorBankAccount."Bank Account No.",1,3) OF
                        '+01':
                          BEGIN
                            PaymentType := 'DK01';
                          END;
                        '+04':
                          BEGIN
                            PaymentType := 'DK04';
                            tLedgerEntryPayment.TESTFIELD("OCR No.");
                            IF STRLEN(tLedgerEntryPayment."OCR No.") <> 16 THEN
                              tLedgerEntryPayment.FIELDERROR("OCR No.",STRSUBSTNO(BA027,16));
                          END;
                        '+15':
                          BEGIN
                            PaymentType := 'DK15';
                            tLedgerEntryPayment.TESTFIELD("OCR No.");
                            IF STRLEN(tLedgerEntryPayment."OCR No.") <> 16 THEN
                              tLedgerEntryPayment.FIELDERROR("OCR No.",STRSUBSTNO(BA027,16));
                          END;
                        '+71':
                          BEGIN
                            PaymentType := 'DK71';
                            tLedgerEntryPayment.TESTFIELD("OCR No.");
                            IF STRLEN(tLedgerEntryPayment."OCR No.") <> 15 THEN
                              tLedgerEntryPayment.FIELDERROR("OCR No.",STRSUBSTNO(BA027,15));
                          END;
                        '+73':
                          BEGIN
                            PaymentType := 'DK73';
                          END;
                        '+75':
                          BEGIN
                            PaymentType := 'DK75';
                            tLedgerEntryPayment.TESTFIELD("OCR No.");
                            IF STRLEN(tLedgerEntryPayment."OCR No.") <> 16 THEN
                              tLedgerEntryPayment.FIELDERROR("OCR No.",STRSUBSTNO(BA027,16));
                          END;

                        ELSE
                          ERROR(BA028,VendorBankAccount."Bank Account No.");
                      END;
                    END;
                  END;
                END;
              'NO':
                BEGIN
                  IF Domestic THEN
                    FinInstnIdIsSet := TRUE;
                END;
            END;
          END;

          IF NOT FinInstnIdIsSet THEN BEGIN
            VendorBankAccount.TESTFIELD("SWIFT Code");
            XMLWriter.AddElementRel(1,'FinInstnId','');
            XMLWriter.AddElementRel(1,'BIC',VendorBankAccount."SWIFT Code");
          END;

          XMLWriter.AddElementAbs(Level,'Cdtr','');

          LongName := Vendor.Name + Vendor."Name 2";
          XMLWriter.AddElementRel(1,'Nm',COPYSTR(LongName,1,70));

          IF Domestic THEN BEGIN
            // Structured Address
            XMLWriter.AddElementRel(0,'PstlAdr','');
            XMLWriter.AddElementRel(1,'StrtNm',Vendor.Address);
            XMLWriter.AddElementRel(0,'PstCd',Vendor."Post Code");
            XMLWriter.AddElementRel(0,'TwnNm',Vendor.City);
            XMLWriter.AddElementRel(0,'Ctry',gVPMnm.GetCountryCode(Vendor."Country/Region Code"));
          END ELSE BEGIN
            // Unstructured Address
            Vendor.TESTFIELD(Address);
            XMLWriter.AddElementRel(0,'PstlAdr','');
            XMLWriter.AddElementRel(1,'Ctry',gVPMnm.GetCountryCode(Vendor."Country/Region Code"));

            // If Name is more than 70 characters place the rest in AdrLine, by specifications from SEB
            XMLWriter.AddElementRel(0,'AdrLine',COPYSTR(LongName,71));

            XMLWriter.AddElementRel(0,'AdrLine',Vendor.Address);
          END;

          XMLWriter.AddElementAbs(Level,'CdtrAcct','');
          IF VendorBankAccount.IBAN <> '' THEN BEGIN
            XMLWriter.AddElementRel(1,'Id','');
            XMLWriter.AddElementRel(1,'IBAN',DELCHR(VendorBankAccount.IBAN));
          END ELSE BEGIN
            CASE PaymentType OF
              'DK01','DK04','DK15','DK71','DK73','DK75':
                BEGIN
                  XMLWriter.AddElementRel(1,'Id','');
                  XMLWriter.AddElementRel(1,'Othr','');
                  XMLWriter.AddElementRel(1,'Id',gVPMnm.ClearText(COPYSTR(VendorBankAccount."Bank Account No.",4)));
                  XMLWriter.AddElementRel(0,'SchmeNm','');
                  XMLWriter.AddElementRel(1,'Prtry','OCR');
                END;
              'SEBG':
                BEGIN
                  XMLWriter.AddElementRel(1,'Id','');
                  XMLWriter.AddElementRel(1,'Othr','');
                  XMLWriter.AddElementRel(1,'Id',gVPMnm.ClearText(VendorBankAccount."Bank Giro No."));
                  XMLWriter.AddElementRel(0,'SchmeNm','');
                  IF Bank = Bank::DNB THEN
                    XMLWriter.AddElementRel(1,'Cd','BBAN')
                  ELSE
                    XMLWriter.AddElementRel(1,'Prtry','BGNR');
                END;
              'SEPG':
                BEGIN
                  XMLWriter.AddElementRel(1,'Id','');
                  XMLWriter.AddElementRel(1,'Othr','');
                  XMLWriter.AddElementRel(1,'Id',gVPMnm.ClearText(VendorBankAccount."Plus Giro No."));
                  XMLWriter.AddElementRel(0,'SchmeNm','');
                  XMLWriter.AddElementRel(1,'Cd','BBAN');
                END;
              ELSE BEGIN
                VendorBankAccount.TESTFIELD("Bank Account No.");
                XMLWriter.AddElementRel(1,'Id','');
                XMLWriter.AddElementRel(1,'Othr','');
                XMLWriter.AddElementRel(1,'Id',gVPMnm.ClearText(VendorBankAccount."Bank Account No."));
                XMLWriter.AddElementRel(0,'SchmeNm','');
                XMLWriter.AddElementRel(1,'Cd','BBAN');
              END;
            END;
          END;

          // Regul Reporting Cross Border
          IF NOT Domestic OR SEPAPayment THEN BEGIN
            IF FromCountryCode IN ['SE','NO'] THEN
              IF tLedgerEntryPayment."Code for Bank of Sweden" = '' THEN
                ERROR(BA029,tLedgerEntryPayment.FIELDCAPTION("Code for Bank of Sweden"));
            IF tLedgerEntryPayment."Code for Bank of Sweden" <> '' THEN BEGIN
              XMLWriter.AddElementAbs(Level,'RgltryRptg','');
              IF Bank = Bank::SEB THEN
                XMLWriter.AddElementRel(1,'DbtCdtRptgInd','DEBT');
              XMLWriter.AddElementAbs(Level + 1,'Dtls','');
              XMLWriter.AddElementRel(1,'Cd',tLedgerEntryPayment."Code for Bank of Sweden");
              XMLWriter.AddElementRel(0,'Inf',GetRegulRepCrossBorderDesc(tLedgerEntryPayment."Code for Bank of Sweden"));
            END;
          END;

          // Invoice reference must be shorter for some destinations
          CASE PaymentType OF
            'SEBA':
              BEGIN
                MergingIsNotAllowed(tLedgerEntryPayment,STRSUBSTNO('%1/%2',FromCountryCode,PaymentType));
                ExternalDocumentNo := COPYSTR(tLedgerEntryPayment."External Document No.",1,12);
              END;
            ELSE BEGIN
              // If more then one invoice for this payment, add all External doc No in same field
              IF tLedgerEntryPayment."Merging No." <> 0 THEN BEGIN
                  ExternalDocumentNo := '';

                  tLedgerEntry.RESET;
                  tLedgerEntry.SETRANGE("Merging No.",tLedgerEntryPayment."Merging No.");
                  tLedgerEntry.FINDSET;
                  REPEAT
                    IF ExternalDocumentNo = '' THEN
                      ExternalDocumentNo += tLedgerEntry."External Document No."
                    ELSE
                      ExternalDocumentNo += ', ' + tLedgerEntry."External Document No.";
                  UNTIL tLedgerEntry.NEXT = 0;
              END ELSE
                ExternalDocumentNo := tLedgerEntryPayment."External Document No.";
            END;
          END;

          // Set RmtInf Invoice reference
          RmtInfIsSet := FALSE;
          CASE FromCountryCode OF
            'FI':
              BEGIN
                MergingIsNotAllowed(tLedgerEntryPayment,PaymentType);
                XMLWriter.AddElementAbs(Level,RmtInf,'');
                XMLWriter.AddElementRel(1,'Strd','');
                XMLWriter.AddElementRel(1,'CdtrRefInf','');
                XMLWriter.AddElementRel(1,'Tp','');
                XMLWriter.AddElementRel(1,'CdOrPrtry','');
                XMLWriter.AddElementRel(1,'Cd','SCOR');
                IF NOT Domestic THEN
                  XMLWriter.AddElementRel(-1,'Issr','ISO');
                XMLWriter.AddElementAbs(Level + 3,'Ref',GetOcrOrExteranlDocNo(tLedgerEntryPayment."OCR No.",ExternalDocumentNo));
              END;
            'DK':
              BEGIN
                IF Domestic THEN BEGIN
                  MergingIsNotAllowed(tLedgerEntryPayment,STRSUBSTNO('%1/%2',FromCountryCode,PaymentType));
                  CASE PaymentType OF
                  'DK01':
                    BEGIN
                      XMLWriter.AddElementAbs(Level,RmtInf,'');
                      XMLWriter.AddElementRel(1,'Ustrd',ExternalDocumentNo);
                      XMLWriter.AddElementRel(0,'Strd','');
                      XMLWriter.AddElementRel(1,'CdtrRefInf','');
                      XMLWriter.AddElementRel(1,'Tp','');
                      XMLWriter.AddElementRel(1,'CdOrPrtry','');
                      XMLWriter.AddElementRel(1,'Cd','SCOR');
                      XMLWriter.AddElementRel(-2,'Ref','01/');
                    END;
                  'DK04':
                    BEGIN
                      XMLWriter.AddElementAbs(Level,RmtInf,'');
                      XMLWriter.AddElementRel(1,'Strd','');
                      XMLWriter.AddElementRel(1,'CdtrRefInf','');
                      XMLWriter.AddElementRel(1,'Tp','');
                      XMLWriter.AddElementRel(1,'CdOrPrtry','');
                      XMLWriter.AddElementRel(1,'Cd','SCOR');
                      XMLWriter.AddElementRel(-2,'Ref','04/' + tLedgerEntryPayment."OCR No.");
                    END;
                  'DK15':
                    BEGIN
                      XMLWriter.AddElementAbs(Level,RmtInf,'');
                      XMLWriter.AddElementRel(1,'Strd','');
                      XMLWriter.AddElementRel(1,'CdtrRefInf','');
                      XMLWriter.AddElementRel(1,'Tp','');
                      XMLWriter.AddElementRel(1,'CdOrPrtry','');
                      XMLWriter.AddElementRel(1,'Cd','SCOR');
                      XMLWriter.AddElementRel(-2,'Ref','15/' + tLedgerEntryPayment."OCR No.");
                    END;
                  'DK71':
                    BEGIN
                      XMLWriter.AddElementAbs(Level,RmtInf,'');
                      XMLWriter.AddElementRel(1,'Strd','');
                      XMLWriter.AddElementRel(1,'CdtrRefInf','');
                      XMLWriter.AddElementRel(1,'Tp','');
                      XMLWriter.AddElementRel(1,'CdOrPrtry','');
                      XMLWriter.AddElementRel(1,'Cd','SCOR');
                      XMLWriter.AddElementRel(-2,'Ref','71/' + tLedgerEntryPayment."OCR No.");
                    END;
                  'DK73':
                    BEGIN
                      XMLWriter.AddElementAbs(Level,RmtInf,'');
                      XMLWriter.AddElementRel(1,'Ustrd',ExternalDocumentNo);
                      XMLWriter.AddElementRel(0,'Strd','');
                      XMLWriter.AddElementRel(1,'CdtrRefInf','');
                      XMLWriter.AddElementRel(1,'Tp','');
                      XMLWriter.AddElementRel(1,'CdOrPrtry','');
                      XMLWriter.AddElementRel(1,'Cd','SCOR');
                      XMLWriter.AddElementRel(-2,'Ref','73/');
                    END;
                  'DK75':
                    BEGIN
                      XMLWriter.AddElementAbs(Level,RmtInf,'');
                      XMLWriter.AddElementRel(1,'Strd','');
                      XMLWriter.AddElementRel(1,'CdtrRefInf','');
                      XMLWriter.AddElementRel(1,'Tp','');
                      XMLWriter.AddElementRel(1,'CdOrPrtry','');
                      XMLWriter.AddElementRel(1,'Cd','SCOR');
                      XMLWriter.AddElementRel(-2,'Ref','75/' + tLedgerEntryPayment."OCR No.");
                    END;
                  END;
                END;
              END;
            'NO':
              BEGIN
                IF Domestic THEN BEGIN
                  MergingIsNotAllowed(tLedgerEntryPayment,PaymentType);
                  IF tLedgerEntryPayment."OCR No." <> '' THEN BEGIN
                    XMLWriter.AddElementAbs(Level,RmtInf,'');
                    XMLWriter.AddElementRel(1,'Strd','');

                    IF Bank = Bank::DNB THEN BEGIN
                      XMLWriter.AddElementAbs(Level + 2,'RfrdDocInf','');
                      XMLWriter.AddElementRel(1,'Tp','');
                      XMLWriter.AddElementRel(1,'CdOrPrtry','');
                      XMLWriter.AddElementRel(1,'Cd','CINV');

                      XMLWriter.AddElementAbs(Level + 3,'RltdDt', tLedgerEntryPayment."Due Date");

                      XMLWriter.AddElementAbs(Level + 2,'RfrdDocAmt','');
                      XMLWriter.AddElementRel(1,'RmtdAmt',GetAmountToPay(tLedgerEntryPayment,FALSE));
                      XMLWriter.AddAttribute('Ccy',gVPMnm.GetPayToCurrencyCode(tLedgerEntryPayment."Currency Code"));
                    END;

                    XMLWriter.AddElementAbs(Level + 2,'CdtrRefInf','');
                    XMLWriter.AddElementRel(1,'Tp','');
                    XMLWriter.AddElementRel(1,'CdOrPrtry','');
                    XMLWriter.AddElementRel(1,'Cd','SCOR');
                    XMLWriter.AddElementRel(-2,'Ref',tLedgerEntryPayment."OCR No.");
                  END;
                END;
              END;
            'SE':
              BEGIN

                // Merging Entries
                IF (tLedgerEntryPayment."Merging No." <> 0) AND Domestic AND NOT SEPAPayment THEN BEGIN
                  IF PaymentType = 'SEBA' THEN
                    ERROR(BA044);
                  XMLWriter.AddElementAbs(Level,RmtInf,'');
                  CLEAR(SpecificationAmount);
                  tLedgerEntry.RESET;
                  tLedgerEntry.SETRANGE("Merging No.",tLedgerEntryPayment."Merging No.");
                  tLedgerEntry.FINDSET;
                  REPEAT
                    // Check
                    tLedgerEntry.TESTFIELD("Currency Code",tLedgerEntryPayment."Currency Code");
                    tLedgerEntry.TESTFIELD("Due Date",tLedgerEntryPayment."Due Date");
                    tLedgerEntry.TESTFIELD(Type,tLedgerEntryPayment.Type);
                    tLedgerEntry.TESTFIELD("No.",tLedgerEntryPayment."No.");

                    // Credit / Debit
                    IsDebitAmount := tLedgerEntry."Remaining Amount" < 0;

                    XMLWriter.AddElementAbs(Level + 1,'Strd','');
                    XMLWriter.AddElementRel(1,'RfrdDocInf','');
                    XMLWriter.AddElementRel(1,'Tp','');
                    XMLWriter.AddElementRel(1,'CdOrPrtry','');

                    IF  IsDebitAmount THEN
                      XMLWriter.AddElementRel(1,'Cd','CINV')
                    ELSE
                      XMLWriter.AddElementRel(1,'Cd','CREN');

                    XMLWriter.AddElementRel(-2,'Nb',tLedgerEntry."External Document No."); // Invoice Ref.
                    XMLWriter.AddElementRel(-1,'RfrdDocAmt','');

                    IF  IsDebitAmount THEN
                      XMLWriter.AddElementRel(1,'RmtdAmt',ABS(tLedgerEntry."Remaining Amount")) // Invoice Amount
                    ELSE
                      XMLWriter.AddElementRel(1,'CdtNoteAmt',ABS(tLedgerEntry."Remaining Amount")); // Credit Amount

                    XMLWriter.AddAttribute('Ccy',gVPMnm.GetPayToCurrencyCode(tLedgerEntry."Currency Code")); // Currency Code
                    SpecificationAmount += tLedgerEntry."Remaining Amount";
                  UNTIL tLedgerEntry.NEXT = 0;
                  // Check specification Amount
                  IF SpecificationAmount <> tLedgerEntryPayment."Remaining Amount" THEN
                    ERROR(BA034);
                END ELSE BEGIN
                  IF tLedgerEntryPayment."OCR No." <> '' THEN BEGIN
                    XMLWriter.AddElementAbs(Level,RmtInf,'');
                    XMLWriter.AddElementRel(1,'Strd','');

                    IF Bank = Bank::DNB THEN BEGIN
                      XMLWriter.AddElementAbs(Level + 2,'RfrdDocInf','');
                      XMLWriter.AddElementRel(1,'Tp','');
                      XMLWriter.AddElementRel(1,'CdOrPrtry','');
                      XMLWriter.AddElementRel(1,'Cd','CINV');

                      XMLWriter.AddElementAbs(Level + 3,'RltdDt', tLedgerEntryPayment."Due Date");

                      XMLWriter.AddElementAbs(Level + 2,'RfrdDocAmt','');
                      XMLWriter.AddElementRel(1,'RmtdAmt',GetAmountToPay(tLedgerEntryPayment,FALSE));
                      XMLWriter.AddAttribute('Ccy',gVPMnm.GetPayToCurrencyCode(tLedgerEntryPayment."Currency Code"));
                    END;

                    XMLWriter.AddElementAbs(Level + 2,'CdtrRefInf','');
                    XMLWriter.AddElementRel(1,'Tp','');
                    XMLWriter.AddElementRel(1,'CdOrPrtry','');
                    XMLWriter.AddElementRel(1,'Cd','SCOR');
                    XMLWriter.AddElementRel(-2,'Ref',tLedgerEntryPayment."OCR No.");
                  END;
                END;
              END;
          END;

          IF NOT RmtInfIsSet THEN BEGIN
            XMLWriter.AddElementAbs(Level,RmtInf,'');
            InsertRmtInfUstrdNode(Level,ExternalDocumentNo,1,140);
          END;

          tLedgerEntryUpdatePackEntry.RESET;
          tLedgerEntryUpdatePackEntry.DELETEALL;
          IF tLedgerEntryPayment."Merging No." = 0 THEN BEGIN
            tLedgerEntryUpdatePackEntry := tLedgerEntryPayment;
            tLedgerEntryUpdatePackEntry.INSERT;
          END ELSE BEGIN
            tLedgerEntry.RESET;
            tLedgerEntry.SETRANGE("Merging No.", tLedgerEntryPayment."Merging No.");
            IF tLedgerEntry.FINDSET THEN REPEAT
              tLedgerEntryUpdatePackEntry := tLedgerEntry;
              tLedgerEntryUpdatePackEntry.INSERT;
            UNTIL tLedgerEntry.NEXT = 0;
          END;

          tLedgerEntryUpdatePackEntry.RESET;
          IF tLedgerEntryUpdatePackEntry.FINDSET THEN REPEAT
            VendorBankAccount.GET(tLedgerEntryUpdatePackEntry."No.",tLedgerEntryUpdatePackEntry."Bank Account Code");
            VPPackEntry.GET(pPack."Entry No.", tLedgerEntryUpdatePackEntry.Type, tLedgerEntryUpdatePackEntry."Entry No.");
            VPPackEntry."Payment from Currency Code" := FromCurrencyCode;
            VPPackEntry."To Currency Code" := gVPMnm.GetPayToCurrencyCode(tLedgerEntryUpdatePackEntry."Currency Code");
            VPPackEntry."To Bank Branch No." := VendorBankAccount."Bank Branch No.";
            VPPackEntry."To Bank Account No." := VendorBankAccount."Bank Account No.";
            VPPackEntry."To IBAN" := VendorBankAccount.IBAN;
            VPPackEntry."To SWIFT Code" := VendorBankAccount."SWIFT Code";
            VPPackEntry."From Bank Branch No." := FromBankBranchNo;
            VPPackEntry."From Bank Account No." := FromBankAccountNo;
            VPPackEntry."From IBAN" := FromIBAN;
            VPPackEntry."From SWIFT Code" := FromSWIFTCode;
            VPPackEntry."SEPA Line Reference" := LineID;
            VPPackEntry.MODIFY;
          UNTIL tLedgerEntryUpdatePackEntry.NEXT = 0;
        UNTIL tLedgerEntryPayment.NEXT = 0;
      UNTIL tAggregateDebitor.NEXT = 0;

      XMLWriter.SaveToFile(gFromFile);

      NoSeriesMgt.SaveNoSeries;
    END;

    PROCEDURE CheckVendorEntry@12047959(ptLedgerEntryPayment@12047961 : TEMPORARY Record 11128074);
    VAR
      VendorLedgerEntry@12047958 : Record 25;
    BEGIN
      WITH ptLedgerEntryPayment DO BEGIN
        VendorLedgerEntry.GET("Entry No.");  // Get Vendor Leadger Entry
        VendorLedgerEntry.TESTFIELD(Open,TRUE);
        VendorLedgerEntry.CALCFIELDS("Remaining Amount","Remaining Amt. (LCY)");
        IF ptLedgerEntryPayment."Merging No." = 0 THEN
          IF VendorLedgerEntry."Remaining Amount" > 0  THEN
            ERROR(BA008);

        IF VendorLedgerEntry."Due Date" < TODAY THEN
          ERROR(BA020,VendorLedgerEntry.FIELDCAPTION("Due Date"));
      END;
    END;

    PROCEDURE OpenFile@1070005();
    VAR
      RBMgt@1100570001 : Codeunit 419;
    BEGIN
      gFromFile := RBMgt.ServerTempFileName('xml');
    END;

    PROCEDURE CloseFile@1070009(pPack@1070000 : Record 11128079);
    VAR
      RBMgt@1100570000 : Codeunit 419;
      VPPackEntry@12047958 : Record 11128063;
    BEGIN
      VPPackEntry.RESET;
      VPPackEntry.SETRANGE("Pack Entry No.",pPack."Entry No.");
      IF VPPackEntry.COUNT <> gNoOfEntries THEN
        ERROR(BA017);

      pPack.LOCKTABLE;
      pPack.FIND;
      pPack."Total Amount (LCY)" := gTotalAmountLCY;
      pPack."SEPA Reference" := gMsgId;
      pPack.MODIFY;

      RBMgt.DownloadToFile(gFromFile,gVPMnm.SetFileName(pPack,gToFile));
    END;

    LOCAL PROCEDURE GetRegulRepCrossBorderDesc@12047960(pCode@12047959 : Code[10]) : Text;
    VAR
      RegulReportingCrossBorder@12047958 : Record 11128075;
      PaymentTypeCodeAbroad@1100285300 : Record 11128511;
      GLSetup@1100285301 : Record 98;
    BEGIN
      //RFC155
      GLSetup.GET;
      //IF GLSetup."Norwegian Localization Active" AND PaymentTypeCodeAbroad.GET(pCode) THEN
      IF GLSetup."Norwegian Localization Active" THEN
        IF PaymentTypeCodeAbroad.GET(COPYSTR(pCode,1,2)) THEN
          EXIT(PaymentTypeCodeAbroad.Description);
      //RFC155

      IF RegulReportingCrossBorder.GET(pCode) THEN
        EXIT(RegulReportingCrossBorder.Description)
      ELSE
        EXIT('');
    END;

    LOCAL PROCEDURE CheckBankBranchNoOrSWIFTCode@12047972(pVendorBankAccount@12047958 : Record 288;pCountryCode@12047959 : Code[10]);
    BEGIN
      IF (pVendorBankAccount."Bank Branch No." = '') AND (pVendorBankAccount."SWIFT Code" = '') THEN
        ERROR(BA035,pVendorBankAccount.FIELDCAPTION("Bank Branch No."),pVendorBankAccount.FIELDCAPTION("SWIFT Code"),pVendorBankAccount.TABLECAPTION,pCountryCode);
    END;

    LOCAL PROCEDURE InsertNodeFinInstnId@12047967(pVendorBankAccount@12047958 : Record 288;pExternalClearing@12047959 : Code[20]);
    VAR
      Level@1100570000 : Integer;
    BEGIN
      IF (pVendorBankAccount."SWIFT Code" = '') AND (pVendorBankAccount."Bank Branch No." = '') THEN
        pVendorBankAccount.FIELDERROR("SWIFT Code",STRSUBSTNO(BA038,pVendorBankAccount.FIELDCAPTION("Bank Branch No.")));
      IF NOT Domestic AND (Bank = Bank::SEB) THEN
        pVendorBankAccount.TESTFIELD("SWIFT Code");
      FinInstnIdIsSet := TRUE;

      XMLWriter.AddElementRel(1,'FinInstnId','');
      Level := XMLWriter.GetLevel;
      XMLWriter.AddElementRel(1,'BIC',pVendorBankAccount."SWIFT Code");
      IF gVPMnm.ClearText(pVendorBankAccount."Bank Branch No.") <> '' THEN BEGIN
        XMLWriter.AddElementAbs(Level + 1,'ClrSysMmbId','');
        XMLWriter.AddElementRel(1,'ClrSysId','');
        XMLWriter.AddElementRel(1,'Cd',pExternalClearing);
        XMLWriter.AddElementRel(-1,'MmbId',gVPMnm.ClearText(pVendorBankAccount."Bank Branch No."));
      END;
    END;

    LOCAL PROCEDURE GetAmountToPay@12047962(ptLedgerEntryPayment@12047958 : TEMPORARY Record 11128074;pNotZeroAmount@12047959 : Boolean) : Decimal;
    BEGIN
      IF ptLedgerEntryPayment."Remaining Amount" > 0 THEN
        ERROR(BA037,ptLedgerEntryPayment."Merging No.");
      IF pNotZeroAmount AND (ptLedgerEntryPayment."Remaining Amount" = 0) THEN
        ERROR(BA036,ptLedgerEntryPayment."Merging No.");
      EXIT(-ptLedgerEntryPayment."Remaining Amount");
    END;

    LOCAL PROCEDURE RmtInf@12047965() : Text;
    BEGIN
      RmtInfIsSet := TRUE;
      EXIT('RmtInf');
    END;

    LOCAL PROCEDURE GetOcrOrExteranlDocNo@12047963(pOCR@12047958 : Text;pExternalDocNo@12047959 : Text) : Text;
    BEGIN
      IF pOCR <> '' THEN
        EXIT(pOCR)
      ELSE
        EXIT(pExternalDocNo);
    END;

    LOCAL PROCEDURE GetOrgIDCust@12047964(pVPGiroType@12047958 : Record 11128061) : Code[20];
    BEGIN
      IF pVPGiroType."Org ID CUST" <> '' THEN
        EXIT(pVPGiroType."Org ID CUST")
      ELSE
        EXIT(gRegistrationNo);
    END;

    LOCAL PROCEDURE GetServiceLevelCode@12047966(pAggregateDebitor@12047958 : Record 11128077) : Code[20];
    BEGIN
      IF Domestic THEN
        EXIT('NURG')
      ELSE
        CASE pAggregateDebitor."Payment Method" OF
          pAggregateDebitor."Payment Method"::Normal: EXIT('NURG');
          pAggregateDebitor."Payment Method"::Express: EXIT('URGP');
          ELSE
            ERROR(BA032,pAggregateDebitor.FIELDCAPTION("Payment Method"),pAggregateDebitor."Payment Method");
        END;
    END;

    PROCEDURE GetChrgBr@1100570007(pVendorBankAccount@1100570000 : Record 288) : Text[30];
    BEGIN
      IF SEPAPayment THEN
        EXIT('SLEV');

      IF Domestic THEN BEGIN
        IF Bank = Bank::SEB THEN
          EXIT('DEBT')
        ELSE
          EXIT('SHAR');
      END ELSE BEGIN
        //Foreign
        CASE pVendorBankAccount."Payment Charge Paid by" OF
          pVendorBankAccount."Payment Charge Paid by"::"Sender - Swedish Cost":
            EXIT('SHAR');
          pVendorBankAccount."Payment Charge Paid by"::Receiver:
            EXIT('CRED');
          pVendorBankAccount."Payment Charge Paid by"::"Sender - All Cost":
            EXIT('DEBT');
          ELSE
            ERROR(BA043,pVendorBankAccount.FIELDCAPTION("Payment Charge Paid by"),
                        pVendorBankAccount."Payment Charge Paid by",pVendorBankAccount."Vendor No.");
        END;
      END;
    END;

    LOCAL PROCEDURE InsertRmtInfUstrdNode@12047970(pLevel@12047961 : Integer;pExternalDocumentNo@12047958 : Text;pNoOfRmtInfLines@12047959 : Integer;pFieldLength@12047963 : Integer);
    VAR
      i@12047960 : Integer;
      pos@12047964 : Integer;
      AddDots@12047962 : Boolean;
    BEGIN
      // If string is more than "pFieldLength" chr breake to multi lines
      IF STRLEN(pExternalDocumentNo) > pFieldLength THEN BEGIN
        i := 1;
        WHILE (STRLEN(pExternalDocumentNo) > pFieldLength) AND (i <= pNoOfRmtInfLines - 1) DO BEGIN
          XMLWriter.AddElementAbs(pLevel + 1,'Ustrd',COPYSTR(pExternalDocumentNo,1,pFieldLength));
          pExternalDocumentNo := COPYSTR(pExternalDocumentNo,pFieldLength + 1);
          i += 1;
        END;

        pExternalDocumentNo  := COPYSTR(pExternalDocumentNo,1,pFieldLength);
        IF (pExternalDocumentNo <> '')  THEN BEGIN
          // Remove all txt include and after chr "," if last chr is not blank
          IF (i = pNoOfRmtInfLines) AND (STRPOS(pExternalDocumentNo,',') <> 0) AND (COPYSTR(pExternalDocumentNo,pFieldLength - 1) <> '') THEN BEGIN
            AddDots := FALSE;
            FOR i := STRLEN(pExternalDocumentNo) DOWNTO 1 DO BEGIN
              pos := i;
              IF COPYSTR(pExternalDocumentNo,i,1) = ',' THEN
                i := 1; //To make a braek in the loop
              AddDots := TRUE;
            END;
            pExternalDocumentNo := COPYSTR(pExternalDocumentNo,1,pos-1);
          END;
          IF AddDots THEN
            pExternalDocumentNo := COPYSTR(pExternalDocumentNo + '...',1,pFieldLength);
          IF pExternalDocumentNo <> '' THEN
            XMLWriter.AddElementAbs(pLevel + 1,'Ustrd',pExternalDocumentNo);
        END;
      END ELSE
        XMLWriter.AddElementRel(1,'Ustrd',pExternalDocumentNo);
    END;

    LOCAL PROCEDURE MergingIsNotAllowed@12047976(ptLedgerEntryPayment@12047958 : TEMPORARY Record 11128074;pPaymentType@12047959 : Text);
    BEGIN
      IF SEPAPayment AND (ptLedgerEntryPayment."Merging No." <> 0 ) THEN
        ptLedgerEntryPayment.FIELDERROR("Merging No.",STRSUBSTNO(BA040,pPaymentType));
    END;

    LOCAL PROCEDURE IsSEPACountry@12047968(pCountryCode@12047958 : Code[10]) : Boolean;
    BEGIN
      EXIT(
      pCountryCode IN [
      'AT','BE','BG','BL','CH','CY','CZ','DE','DK','EE','ES','FI',
      'FR','GB','GF','GG','GI','GR','HR','HU','IE','IS','IT','LI',
      'LT','IM','JE','LU','LV','MF','MC','MT','MQ','NL','NO','PL',
      'PT','PM','RE','RO','SE','SI','SK','SM','YT'
      ]);
    END;

    LOCAL PROCEDURE IsDomestic@12047969(ptLedgerEntryPayment@12047960 : TEMPORARY Record 11128074;pFromCountryCode@12047958 : Code[10];pToCountryCode@12047959 : Code[10]) : Boolean;
    VAR
      CurrencyCode@12047961 : Code[10];
    BEGIN
      IF pFromCountryCode <> pToCountryCode THEN
        EXIT(FALSE);

      IF gVPMnm.GetPayToCurrencyCode(ptLedgerEntryPayment."Currency Code") = 'EUR' THEN
        EXIT(TRUE);

      CASE pFromCountryCode OF
        'SE': CurrencyCode := 'SEK';
        'NO': CurrencyCode := 'NOK';
        'DK': CurrencyCode := 'DKK';
        'GB': CurrencyCode := 'GBP';
        ELSE
          ERROR(BA042,pFromCountryCode);
      END;

      EXIT(gVPMnm.GetPayToCurrencyCode(ptLedgerEntryPayment."Currency Code") = CurrencyCode);
    END;

    BEGIN
    {
      PEB *** Begin ***
      PEB0091 Banking
      PEB
      PEB *** End ***
      160929 ITERO.DL RFC155 The new file format (pain001) for supplier payments does not support Norwegian requirements
    }
    END.
  }
}

