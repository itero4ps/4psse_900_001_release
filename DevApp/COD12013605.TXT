OBJECT Codeunit 12013605 ExFlow Dim Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF350003,NAV2013;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text032@1100285003 : TextConst 'ENU=The combination of dimensions used in %1 %2 is blocked. %3';
      Text033@1100285002 : TextConst 'ENU=The combination of dimensions used in %1 %2, line no. %3 is blocked. %4';
      Text034@1100285001 : TextConst 'ENU=The dimensions used in %1 %2 are invalid. %3';
      Text035@1100285000 : TextConst 'ENU=The dimensions used in %1 %2, line no. %3 are invalid. %4';
      Text100@1100285004 : TextConst 'ENU=Table %1 is not supported!';
      EXF01@1100285005 : TextConst 'ENU=The purchase document %1 does not have a new row no.';
      GlobalTempPurchLine@1100285006 : TEMPORARY Record 39;

    PROCEDURE GetDimFromPurchLine@1100285000(PurchLine@1100285000 : Record 39;VAR DimCodeTab@1100285002 : ARRAY [8] OF Code[20];VAR DimValueTab@1100285001 : ARRAY [8] OF Code[20];VAR CurrentDocDimFound@1100285003 : Boolean;VAR CurrentDocDimCode@1100285005 : Code[20];VAR CurrentDocDimValue@1100285008 : Code[20]);
    VAR
      DimSetEntry@1100285004 : Record 480;
      i@1100285006 : Integer;
    BEGIN
      i := 1;

      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID", PurchLine."Dimension Set ID");
      IF DimSetEntry.FINDSET THEN
        REPEAT
          IF (DimSetEntry."Dimension Code" = CurrentDocDimCode) THEN BEGIN
            DimCodeTab[i] := CurrentDocDimCode;
            DimValueTab[i] := CurrentDocDimValue;
            i := i + 1;
            CurrentDocDimFound := TRUE;
          END
          ELSE BEGIN
            DimCodeTab[i] := DimSetEntry."Dimension Code";
            DimValueTab[i] := DimSetEntry."Dimension Value Code";
            i := i + 1;
          END;
        UNTIL DimSetEntry.NEXT = 0;

      IF (NOT CurrentDocDimFound) AND (CurrentDocDimCode <> '') THEN BEGIN
        DimCodeTab[i] := CurrentDocDimCode;
        DimValueTab[i] := CurrentDocDimValue;
        i := i + 1;
      END;
    END;

    PROCEDURE GetDimFromExFPurchLine@1100285017(ExFPurchLine@1100285000 : Record 12013588;VAR DimCodeTab@1100285002 : ARRAY [8] OF Code[20];VAR DimValueTab@1100285001 : ARRAY [8] OF Code[20];VAR CurrentDocDimFound@1100285003 : Boolean;VAR CurrentDocDimCode@1100285005 : Code[20];VAR CurrentDocDimValue@1100285008 : Code[20]);
    VAR
      DimSetEntry@1100285004 : Record 480;
      i@1100285006 : Integer;
    BEGIN
      i := 1;

      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID", ExFPurchLine."Dimension Set ID");
      IF DimSetEntry.FINDSET THEN
        REPEAT
          IF (DimSetEntry."Dimension Code" = CurrentDocDimCode) THEN BEGIN
            DimCodeTab[i] := CurrentDocDimCode;
            DimValueTab[i] := CurrentDocDimValue;
            i := i + 1;
            CurrentDocDimFound := TRUE;
          END
          ELSE BEGIN
            DimCodeTab[i] := DimSetEntry."Dimension Code";
            DimValueTab[i] := DimSetEntry."Dimension Value Code";
            i := i + 1;
          END;
        UNTIL DimSetEntry.NEXT = 0;

      IF (NOT CurrentDocDimFound) AND (CurrentDocDimCode <> '') THEN BEGIN
        DimCodeTab[i] := CurrentDocDimCode;
        DimValueTab[i] := CurrentDocDimValue;
        i := i + 1;
      END;
    END;

    PROCEDURE InsertExDocDim@1100285001(PurchLine@1100285002 : Record 39;DocLine@1100285000 : Record 12013609);
    VAR
      DimSetEntry@1100285006 : Record 480;
      DocLineDim@1100285005 : Record 12013611;
      Column@1100285001 : Record 12013661;
    BEGIN
      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID", PurchLine."Dimension Set ID");
      IF DimSetEntry.FINDSET THEN
        REPEAT
          Column.RESET;
          Column.SETCURRENTKEY("Company Name", Code);
          Column.SETRANGE("Company Name",COMPANYNAME);
          Column.SETRANGE(Code, DimSetEntry."Dimension Code");
          Column.SETRANGE(Dimension, TRUE);
          IF Column.FINDFIRST THEN BEGIN
            DocLineDim.INIT;
            DocLineDim."Company Name" := COMPANYNAME;
            DocLineDim."Document Type" := DocLine."Document Type";
            DocLineDim."Document No." := DocLine."Document No.";
            DocLineDim."Line No." := DocLine."Line No.";
            DocLineDim."Dimension Code" := DimSetEntry."Dimension Code";
            DocLineDim."Dimension Value" := DimSetEntry."Dimension Value Code";
            DocLineDim."Column ID" := Column.ID;
            DocLineDim."Document Line ID" := DocLine.ID;
            DocLineDim."Document ID" := DocLine."Document ID";
            DocLineDim.ID := 0;
            IF DocLineDim."Dimension Value" <> '' THEN
              DocLineDim.INSERT;
          END;
        UNTIL DimSetEntry.NEXT = 0;
    END;

    PROCEDURE CopyDocDimToJnlLineDim@1100285002(_PurchaseLine@1100285000 : Record 39;VAR GenJnlLine@1100285003 : Record 81;RetrieveLinesFrom@1100285004 : 'Purchase Line,Ex Document Line');
    VAR
      ExDocLineDim@1100285005 : Record 12013611;
      ExColumn@1100285006 : Record 12013661;
      TempDimSetEntry@1100285002 : TEMPORARY Record 480;
      DimValue@1100285007 : Record 349;
      DimMgt@1100285001 : Codeunit 408;
    BEGIN
      IF RetrieveLinesFrom = RetrieveLinesFrom::"Purchase Line" THEN BEGIN
        GenJnlLine."Dimension Set ID" := _PurchaseLine."Dimension Set ID";
        DimMgt.UpdateGlobalDimFromDimSetID(GenJnlLine."Dimension Set ID",GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Shortcut Dimension 2 Code");
        GenJnlLine.MODIFY;
      END
      ELSE BEGIN
        TempDimSetEntry.RESET;
        TempDimSetEntry.DELETEALL;

        ExDocLineDim.RESET;
        ExDocLineDim.SETCURRENTKEY("Document No.");
        ExDocLineDim.SETRANGE("Company Name", COMPANYNAME);
        ExDocLineDim.SETRANGE("Document Type", _PurchaseLine."Document Type");
        ExDocLineDim.SETRANGE("Document No.", _PurchaseLine."Document No.");
        ExDocLineDim.SETRANGE("Line No.", _PurchaseLine."Line No.");
        ExDocLineDim.SETFILTER("Dimension Value", '<>%1', '');
        IF ExDocLineDim.FINDSET THEN BEGIN
          REPEAT
            IF ExDocLineDim."Dimension Value" <> '' THEN BEGIN
              ExColumn.SETRANGE("Company Name", COMPANYNAME);
              ExColumn.SETRANGE(Code, ExDocLineDim."Dimension Code");
              ExColumn.SETRANGE(Dimension, TRUE);
              IF ExColumn.FINDFIRST THEN BEGIN
                DimValue.GET(ExDocLineDim."Dimension Code",ExDocLineDim."Dimension Value");
                IF TempDimSetEntry.GET(TempDimSetEntry."Dimension Set ID",DimValue."Dimension Code") THEN
                  TempDimSetEntry.DELETE;

                TempDimSetEntry."Dimension Code" := DimValue."Dimension Code";
                TempDimSetEntry."Dimension Value Code" := DimValue.Code;
                TempDimSetEntry."Dimension Value ID" := DimValue."Dimension Value ID";
                TempDimSetEntry.INSERT;
              END;
            END;
          UNTIL ExDocLineDim.NEXT = 0;

          GenJnlLine."Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
          DimMgt.UpdateGlobalDimFromDimSetID(GenJnlLine."Dimension Set ID",GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Shortcut Dimension 2 Code");
          GenJnlLine.MODIFY;
        END;
      END;
    END;

    PROCEDURE DeleteJnlDim@1100285003(GenJnlLine@1100285000 : Record 81);
    VAR
      DimMgt@1100285001 : Codeunit 408;
    BEGIN
      GenJnlLine."Dimension Set ID" := 0;
      DimMgt.UpdateGlobalDimFromDimSetID(GenJnlLine."Dimension Set ID",GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Shortcut Dimension 2 Code");
      GenJnlLine.MODIFY;
    END;

    PROCEDURE CopyDimFromImpHeader@1100285004(FromTableNo@1100285012 : Integer;FromDocType@1100285011 : Integer;FromDocNo@1100285010 : Code[20];ToTableNo@1100285008 : Integer;ToDocType@1100285007 : Integer;ToDocNo@1100285006 : Code[20]);
    BEGIN
      // No code required, dimension set id is handled through transferfields
    END;

    PROCEDURE CopyDimFromImpLine@1100285005(FromTableNo@1100285005 : Integer;FromDocType@1100285006 : Integer;FromDocNo@1100285007 : Code[20];FromLineNo@1100285003 : Integer;ToTableNo@1100285010 : Integer;ToDocType@1100285009 : Integer;ToDocNo@1100285008 : Code[20];ToLineNo@1100285004 : Integer);
    BEGIN
      // No code required, dimension set id is handled through transferfields
    END;

    PROCEDURE ExPurchCodeUpDateDim@2(VAR ExPurchLine@1000 : Record 12013588;ExPurchCodeLine@1001 : Record 12013683);
    VAR
      DimMgt@1100285000 : Codeunit 408;
    BEGIN
      ExPurchLine."Dimension Set ID" := ExPurchCodeLine."Dimension Set ID";
      DimMgt.UpdateGlobalDimFromDimSetID(ExPurchLine."Dimension Set ID",ExPurchLine."Shortcut Dimension 1 Code",ExPurchLine."Shortcut Dimension 2 Code");
      ExPurchLine.MODIFY;
    END;

    PROCEDURE CopyAndCheckDocDimToTempDocDim@34(VAR PurchHeader@1100285000 : Record 38;VAR ErrText@1100285001 : Text[1024]);
    VAR
      PurchLine2@1000 : Record 39;
    BEGIN
      PurchLine2."Line No." := 0;
      CheckDimComb(PurchLine2, PurchHeader, ErrText);
      CheckDimValuePosting(PurchLine2, PurchHeader, ErrText);

      PurchLine2.SETRANGE("Document Type",PurchHeader."Document Type");
      PurchLine2.SETRANGE("Document No.",PurchHeader."No.");
      PurchLine2.SETFILTER(Type,'<>%1',PurchLine2.Type::" ");
      IF PurchLine2.FINDSET THEN
        REPEAT
          IF (PurchHeader.Receive AND (PurchLine2."Qty. to Receive" <> 0)) OR
             (PurchHeader.Invoice AND (PurchLine2."Qty. to Invoice" <> 0)) OR
             (PurchHeader.Ship AND (PurchLine2."Return Qty. to Ship" <> 0))
          THEN BEGIN
            CheckDimComb(PurchLine2, PurchHeader, ErrText);
            CheckDimValuePosting(PurchLine2, PurchHeader, ErrText);
          END
        UNTIL PurchLine2.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckDimComb@30(PurchLine@1000 : Record 39;VAR PurchHeader@1100285002 : Record 38;VAR ErrText@1100285001 : Text[1024]);
    VAR
      DimMgt@1100285000 : Codeunit 408;
    BEGIN
      IF PurchLine."Line No." = 0 THEN
        IF NOT DimMgt.CheckDimIDComb(PurchHeader."Dimension Set ID") THEN
          ErrText :=
            COPYSTR(STRSUBSTNO(Text032,
              PurchHeader."Document Type",PurchHeader."No.",DimMgt.GetDimCombErr), 1, MAXSTRLEN(ErrText));

      IF PurchLine."Line No." <> 0 THEN
        IF NOT DimMgt.CheckDimIDComb(PurchLine."Dimension Set ID") THEN
          ErrText :=
            COPYSTR(STRSUBSTNO(Text033,
              PurchHeader."Document Type",PurchHeader."No.",PurchLine."Line No.",DimMgt.GetDimCombErr), 1, MAXSTRLEN(ErrText));
    END;

    LOCAL PROCEDURE CheckDimValuePosting@28(VAR PurchLine2@1000 : Record 39;VAR PurchHeader@1100285000 : Record 38;VAR ErrText@1100285001 : Text[1024]);
    VAR
      TableIDArr@1002 : ARRAY [10] OF Integer;
      NumberArr@1003 : ARRAY [10] OF Code[20];
      DimMgt@1100285002 : Codeunit 408;
    BEGIN
      IF PurchLine2."Line No." = 0 THEN BEGIN
        TableIDArr[1] := DATABASE::Vendor;
        NumberArr[1] := PurchHeader."Pay-to Vendor No.";
        TableIDArr[3] := DATABASE::"Salesperson/Purchaser";
        NumberArr[3] := PurchHeader."Purchaser Code";
        TableIDArr[4] := DATABASE::Campaign;
        NumberArr[4] := PurchHeader."Campaign No.";
        TableIDArr[5] := DATABASE::"Responsibility Center";
        NumberArr[5] := PurchHeader."Responsibility Center";
        IF NOT DimMgt.CheckDimValuePosting(TableIDArr,NumberArr,PurchHeader."Dimension Set ID") THEN
          ErrText := COPYSTR(STRSUBSTNO(Text034, PurchHeader."Document Type",PurchHeader."No.",DimMgt.GetDimValuePostingErr), 1,
                     MAXSTRLEN(ErrText));
      END ELSE BEGIN
        TableIDArr[1] := DimMgt.TypeToTableID3(PurchLine2.Type);
        NumberArr[1] := PurchLine2."No.";
        TableIDArr[2] := DATABASE::Job;
        NumberArr[2] := PurchLine2."Job No.";
        TableIDArr[3] := DATABASE::"Work Center";
        NumberArr[3] := PurchLine2."Work Center No.";
        IF NOT DimMgt.CheckDimValuePosting(TableIDArr,NumberArr,PurchLine2."Dimension Set ID") THEN
          ErrText := COPYSTR(STRSUBSTNO(Text035, PurchHeader."Document Type",PurchHeader."No.",PurchLine2."Line No.",
                                         DimMgt.GetDimValuePostingErr), 1, MAXSTRLEN(ErrText));
      END;
    END;

    PROCEDURE ImpHeadCCDocDimToTempDocDim@1100285011(ExfPurchDocHead@1100285000 : Record 12013587);
    VAR
      ExfPurchDocLine2@1100285001 : Record 12013588;
    BEGIN
      ExfPurchDocLine2."Line No." := 0;
      ImpHeadCheckDimComb(ExfPurchDocHead,ExfPurchDocLine2);
      ImpHeadCheckDimValuePosting(ExfPurchDocHead,ExfPurchDocLine2);

      ExfPurchDocLine2.SETRANGE("Inbound Document No.",ExfPurchDocHead."Inbound Document No.");
      ExfPurchDocLine2.SETFILTER(Type,'<>%1', ExfPurchDocLine2.Type::" ");
      IF ExfPurchDocLine2.FINDSET THEN
        REPEAT
          ImpHeadCheckDimComb(ExfPurchDocHead,ExfPurchDocLine2);
          ImpHeadCheckDimValuePosting(ExfPurchDocHead,ExfPurchDocLine2);
        UNTIL ExfPurchDocLine2.NEXT = 0;
    END;

    LOCAL PROCEDURE ImpHeadCheckDimComb@1100285010(ExfPurchDocHead@1100285002 : Record 12013587;ExfPurchDocLine2@1100285001 : Record 12013588);
    VAR
      DimMgt@1100285000 : Codeunit 408;
    BEGIN
      IF ExfPurchDocLine2."Line No." = 0 THEN
        IF NOT DimMgt.CheckDimIDComb(ExfPurchDocHead."Dimension Set ID") THEN
          ERROR(Text032,
            ExfPurchDocHead."Document Type",FORMAT(ExfPurchDocHead."Inbound Document No."),DimMgt.GetDimCombErr);

      IF ExfPurchDocLine2."Line No." <> 0 THEN
        IF NOT DimMgt.CheckDimIDComb(ExfPurchDocLine2."Dimension Set ID") THEN
          ERROR(Text033,
            ExfPurchDocHead."Document Type",FORMAT(ExfPurchDocHead."Inbound Document No."),ExfPurchDocLine2."Line No.",DimMgt.GetDimCombErr);
    END;

    LOCAL PROCEDURE ImpHeadCheckDimValuePosting@1100285009(ExfPurchDocHead@1100285000 : Record 12013587;ExfPurchDocLine2@1100285001 : Record 12013588);
    VAR
      DimMgt@1100285002 : Codeunit 408;
      TableIDArr@1002 : ARRAY [10] OF Integer;
      NumberArr@1003 : ARRAY [10] OF Code[20];
    BEGIN
      IF ExfPurchDocLine2."Line No." = 0 THEN BEGIN
        TableIDArr[1] := DATABASE::Vendor;
        NumberArr[1] := ExfPurchDocHead."Pay-to Vendor No.";
        TableIDArr[2] := DATABASE::"Salesperson/Purchaser";
        NumberArr[2] := ExfPurchDocHead."Purchaser Code";
        TableIDArr[3] := 0;
        NumberArr[3] := '';
        TableIDArr[4] := 0;
        NumberArr[4] := '';
        IF NOT DimMgt.CheckDimValuePosting(TableIDArr,NumberArr,ExfPurchDocHead."Dimension Set ID") THEN
          ERROR(Text034,
            ExfPurchDocHead."Document Type",ExfPurchDocHead."Inbound Document No.",DimMgt.GetDimValuePostingErr);
      END ELSE BEGIN
        TableIDArr[1] := DimMgt.TypeToTableID3(ExfPurchDocLine2.Type);
        NumberArr[1] := ExfPurchDocLine2."No.";
        TableIDArr[2] := DATABASE::Job;
        NumberArr[2] := ExfPurchDocLine2."Job No.";
        TableIDArr[3] := 0;
        NumberArr[3] := '';
        IF NOT DimMgt.CheckDimValuePosting(TableIDArr,NumberArr,ExfPurchDocLine2."Dimension Set ID") THEN
          ERROR(Text035,
            ExfPurchDocHead."Document Type",ExfPurchDocHead."Inbound Document No.",ExfPurchDocLine2."Line No.",DimMgt.GetDimValuePostingErr);
      END;
    END;

    PROCEDURE SaveDocDim@1100285006(VAR ExPurchDocLine@1100285007 : Record 12013588;DimNo@1100285005 : Integer;DimValueCode@1100285006 : Code[20]);
    VAR
      DimValue@1100285003 : Record 349;
      GLSetup@1100285004 : Record 98;
      DimCode@1100285000 : Code[20];
    BEGIN
      GLSetup.GET;

      CASE DimNo OF
        1 : DimCode := GLSetup."Shortcut Dimension 1 Code";
        2 : DimCode := GLSetup."Shortcut Dimension 2 Code";
        3 : DimCode := GLSetup."Shortcut Dimension 3 Code";
        4 : DimCode := GLSetup."Shortcut Dimension 4 Code";
        5 : DimCode := GLSetup."Shortcut Dimension 5 Code";
        6 : DimCode := GLSetup."Shortcut Dimension 6 Code";
        7 : DimCode := GLSetup."Shortcut Dimension 7 Code";
        8 : DimCode := GLSetup."Shortcut Dimension 8 Code"
        ELSE
          EXIT;
      END;

      DimValue.GET(DimCode,DimValueCode);
      UpdateDimFromPurchLine(ExPurchDocLine."Dimension Set ID",DimValue,ExPurchDocLine."Shortcut Dimension 1 Code",ExPurchDocLine."Shortcut Dimension 2 Code");
    END;

    PROCEDURE UpdateDimFromUser@1100285007(VAR PurchLine@1100285000 : Record 39;ExFlowSetup@1100285001 : Record 12013601;ExUserGroup@1100285002 : Record 12013606);
    VAR
      DimValue@1100285003 : Record 349;
    BEGIN
      IF ExUserGroup."Dimension Value" <> '' THEN BEGIN
        DimValue.GET(ExFlowSetup."User Dimension Code",ExUserGroup."Dimension Value");
        UpdateDimFromPurchLine(PurchLine."Dimension Set ID",DimValue,PurchLine."Shortcut Dimension 1 Code",PurchLine."Shortcut Dimension 2 Code");
        PurchLine.MODIFY;
      END;
    END;

    PROCEDURE ShowShortcutDimCode@27(CompName@1100285003 : Text[50];DocType@1100285004 : Integer;DocNo@1100285005 : Code[20];LineNo@1100285006 : Integer;VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20];VAR GLSetupShortcutDimCode@1100285002 : ARRAY [8] OF Code[20]);
    VAR
      ExDocLineDim@1100285000 : Record 12013611;
      i@1100285001 : Integer;
    BEGIN
      ExDocLineDim.RESET;
      ExDocLineDim.SETCURRENTKEY("Document No.");
      ExDocLineDim.SETRANGE("Company Name", CompName);
      ExDocLineDim.SETRANGE("Document Type", DocType);
      ExDocLineDim.SETRANGE("Document No.", DocNo);
      ExDocLineDim.SETRANGE("Line No.", LineNo);
      FOR i := 1 TO 8 DO BEGIN
        ShortcutDimCode[i] := '';
        IF GLSetupShortcutDimCode[i] <> '' THEN BEGIN
          ExDocLineDim.SETRANGE("Dimension Code", GLSetupShortcutDimCode[i]);
          IF ExDocLineDim.FINDFIRST THEN
            ShortcutDimCode[i] := ExDocLineDim."Dimension Value";
        END;
      END;
    END;

    PROCEDURE ShowShortcutDimCodePL@1100285008(TableNo@1100285006 : Integer;DocType@1100285005 : Integer;DocNo@1100285004 : Code[20];LineNo@1100285003 : Integer;VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20];VAR GLSetupShortcutDimCode@1100285002 : ARRAY [8] OF Code[20]);
    VAR
      PurchLine@1100285000 : Record 39;
      DimMgt@1100285001 : Codeunit 408;
    BEGIN
      // Change Journal
      IF TableNo <> DATABASE::"Purchase Line" THEN
        ERROR(Text100,TableNo);

      CLEAR(ShortcutDimCode);
      IF PurchLine.GET(DocType,DocNo,LineNo) THEN
        DimMgt.GetShortcutDimensions(PurchLine."Dimension Set ID",ShortcutDimCode);
    END;

    PROCEDURE CreateQuoteCopyDimension@1100285012(DocLine@1100285014 : Record 12013609;VAR PurchLine@1100285015 : Record 39;VAR DimCodeTab@1100285006 : ARRAY [8] OF Code[20];VAR DimValueTab@1100285005 : ARRAY [8] OF Code[20]);
    VAR
      DimSetEntry@1100285000 : Record 480;
      PurchHeader@1100285012 : Record 38;
      DimValue@1100285020 : Record 349;
      DocLine2@1100285013 : Record 12013609;
      DocLineDim2@1100285001 : Record 12013611;
      GLSetup@1100285002 : Record 98;
      DocLineDim@1100285016 : Record 12013611;
      Column@1100285019 : Record 12013661;
      ImportData@1100285010 : Codeunit 12013623;
      ExFlow@1100285017 : Codeunit 12013601;
      i@1100285007 : Integer;
      ColumnID@1100285011 : Integer;
      DoNotInsert@1100285003 : Boolean;
      RecordRef@1100285008 : RecordRef;
      FldRef@1100285009 : FieldRef;
    BEGIN
      GLSetup.GET;

      DocLine2.RetrieveRecord(DocLine2,DocLine."Company Name",PurchLine."Document Type",PurchLine."Document No.",
                              PurchLine."Line No.");

      PurchHeader.GET(PurchLine."Document Type",PurchLine."Document No.");

      DimSetEntry.SETRANGE("Dimension Set ID", PurchHeader."Dimension Set ID");
      IF DimSetEntry.FINDSET THEN
        REPEAT
          DocLineDim.RESET;
          DocLineDim.SETCURRENTKEY("Document Line ID");
          DocLineDim.SETRANGE("Document Line ID", DocLine2.ID);
          DocLineDim.SETRANGE("Dimension Code", DimSetEntry."Dimension Code");
          IF NOT DocLineDim.FINDFIRST THEN BEGIN
            // Copy Dimension from Header to line
            DimValue.GET(DimSetEntry."Dimension Code",DimSetEntry."Dimension Value Code");
            UpdateDimFromPurchLine(PurchLine."Dimension Set ID",DimValue,PurchLine."Shortcut Dimension 1 Code",PurchLine."Shortcut Dimension 2 Code");

            ColumnID := ExFlow.GetColumnID(DimValue."Dimension Code");

            IF ColumnID <> 0 THEN
              IF Column.GET(COMPANYNAME,ColumnID) THEN
                IF Column.Dimension THEN BEGIN
                  DocLineDim2.INIT;
                  DocLineDim2.ID := 0;
                  DocLineDim2."Company Name" := DocLine2."Company Name";
                  DocLineDim2."Document Type" := PurchLine."Document Type";
                  DocLineDim2."Document No." := PurchLine."Document No.";
                  DocLineDim2."Line No." := PurchLine."Line No.";
                  DocLineDim2."Dimension Code" := DimValue."Dimension Code";
                  DocLineDim2."Dimension Value" := DimValue.Code;
                  DocLineDim2."Column ID" := ColumnID;
                  DocLineDim2."Document Line ID" := DocLine2.ID;
                  DocLineDim2."Document ID" := DocLine2."Document ID";
                  IF DocLineDim2."Dimension Value" <> '' THEN
                    DocLineDim2.INSERT(FALSE);
                END;
          END;
        UNTIL DimSetEntry.NEXT = 0;

      CLEAR(DimCodeTab);
      CLEAR(DimValueTab);
      i := 1;

      DocLineDim.RESET;
      DocLineDim.SETCURRENTKEY("Document Line ID");
      DocLineDim.SETRANGE("Document Line ID",DocLine.ID);
      IF DocLineDim.FINDSET THEN BEGIN
        REPEAT
          DoNotInsert := FALSE;

          Column.GET(COMPANYNAME,DocLineDim."Column ID");
          IF Column.Dimension THEN BEGIN
            DimCodeTab[i] := DocLineDim."Dimension Code";
            DimValueTab[i] := DocLineDim."Dimension Value";
            i := i + 1;

            InsertDocDimFromDocLineDim(DocLineDim,PurchLine,DoNotInsert,GLSetup);
          END
          ELSE BEGIN
            IF (DocLineDim."Dimension Value" <> '') AND (Column."Purchase Line Field ID" <> 0) THEN BEGIN
              PurchLine.MODIFY;
              RecordRef.GETTABLE(PurchLine);
              FldRef := RecordRef.FIELD(Column."Purchase Line Field ID");
              IF Column."Is Free Text" THEN
                ImportData.UpdateRecord(FldRef,DocLineDim."Dimension Desc.",FALSE,
                                      DATABASE::"Purchase Line",Column."Purchase Line Field ID",-1)
              ELSE
              ImportData.UpdateRecord(FldRef,DocLineDim."Dimension Value",FALSE,
                                      DATABASE::"Purchase Line",Column."Purchase Line Field ID",-1);
              RecordRef.MODIFY;
              PurchLine.GET(PurchLine."Document Type",PurchLine."Document No.",PurchLine."Line No.");
            END;
          END;

          IF DoNotInsert THEN BEGIN
            IF DocLineDim2.RetrieveRecord(DocLineDim2,COMPANYNAME, PurchLine."Document Type", PurchLine."Document No.",
                                          PurchLine."Line No.", DocLineDim."Dimension Code") THEN
              DocLineDim2.DELETE;
          END
          ELSE
            IF DocLineDim2.RetrieveRecord(DocLineDim2,COMPANYNAME, PurchLine."Document Type", PurchLine."Document No.",
                                          PurchLine."Line No.", DocLineDim."Dimension Code") THEN
              BEGIN
                DocLineDim2."Dimension Value" := DocLineDim."Dimension Value";
                DocLineDim2."Column ID" := DocLineDim."Column ID";
                DocLineDim2."Document Line ID" := DocLine2.ID;
                DocLineDim2."Document ID" := DocLine2."Document ID";
                DocLineDim2."Line No." := PurchLine."Line No.";
                DocLineDim2.MODIFY(FALSE);
              END
            ELSE
              BEGIN
                DocLineDim2.INIT;
                DocLineDim2.ID := 0;
                DocLineDim2."Company Name" := DocLineDim."Company Name";
                DocLineDim2."Document Type" := PurchLine."Document Type";
                DocLineDim2."Document No." := PurchLine."Document No.";
                DocLineDim2."Line No." := PurchLine."Line No.";
                DocLineDim2."Dimension Code" := DocLineDim."Dimension Code";
                DocLineDim2."Dimension Value" := DocLineDim."Dimension Value";
                DocLineDim2."Column ID" := DocLineDim."Column ID";
                DocLineDim2."Document Line ID" := DocLine2.ID;
                DocLineDim2."Document ID" := DocLine2."Document ID";
                IF DocLineDim2."Dimension Value" <> '' THEN
                  DocLineDim2.INSERT(FALSE);
              END;
        UNTIL DocLineDim.NEXT = 0;

        PurchLine.MODIFY(FALSE);
      END;
    END;

    PROCEDURE InsertDocDimFromDocLineDim@1100285022(DocLineDim@1100285000 : Record 12013611;VAR PurchLine@1100285003 : Record 39;VAR DoNotInsert@1100285001 : Boolean;GLSetup@1100285005 : Record 98);
    VAR
      DimToGLMapping@1100285002 : Record 12013646;
      DimValue@1100285004 : Record 349;
    BEGIN
      IF DocLineDim."Dimension Value" = '' THEN
        EXIT;

      DimToGLMapping.RESET;
      DimToGLMapping.SETRANGE("Dimension Code", DocLineDim."Dimension Code");
      DimToGLMapping.SETRANGE("Dimension Value Code", DocLineDim."Dimension Value");
      DimToGLMapping.SETRANGE("Do Not Insert Into NAV", TRUE);
      DoNotInsert := DimToGLMapping.FINDFIRST;
      IF NOT DoNotInsert THEN BEGIN
        DimValue.GET(DocLineDim."Dimension Code",DocLineDim."Dimension Value");

        UpdateDimFromPurchLine(PurchLine."Dimension Set ID",DimValue,PurchLine."Shortcut Dimension 1 Code",PurchLine."Shortcut Dimension 2 Code");
      END;
    END;

    PROCEDURE ShowDocLineDim@1100285013(DocType@1100285005 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,Posted Invoice,Posted Credit Memo,Requisition';DocNo@1100285006 : Code[20];LineNo@1100285007 : Integer);
    VAR
      DocLineDim@1100285000 : Record 12013611;
      TempDocLineDim@1100285002 : TEMPORARY Record 12013611;
      Column@1100285003 : Record 12013661;
      DocLine@1100285004 : Record 12013609;
      DimSetEntry@1100285001 : Record 480;
      PurchLine@1100285010 : Record 39;
      ExFlow@1100285009 : Codeunit 12013601;
      ColumnID@1100285008 : Integer;
      TempID@1100285011 : Integer;
    BEGIN
      // Update Purchase Line
      TempDocLineDim.RESET;
      TempDocLineDim.DELETEALL;

      PurchLine.GET(DocType,DocNo,LineNo);

      DocLineDim.RESET;
      DocLineDim.SETCURRENTKEY("Document No.");
      DocLineDim.SETRANGE("Company Name", COMPANYNAME);
      DocLineDim.SETRANGE("Document Type", DocType);
      DocLineDim.SETRANGE("Document No.", DocNo);
      DocLineDim.SETRANGE("Line No.", LineNo);
      IF DocLineDim.FINDSET THEN
        REPEAT
          IF Column.GET(COMPANYNAME,DocLineDim."Column ID") THEN
            IF Column.Dimension THEN
              BEGIN
                TempDocLineDim.INIT;
                TempDocLineDim.TRANSFERFIELDS(DocLineDim);
                TempDocLineDim.INSERT;
              END;
        UNTIL DocLineDim.NEXT = 0;

      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID", PurchLine."Dimension Set ID");
      IF DimSetEntry.FINDSET THEN
        REPEAT
          ColumnID := ExFlow.GetColumnID(DimSetEntry."Dimension Code");
          IF ColumnID <> 0 THEN BEGIN
            TempDocLineDim.SETRANGE("Dimension Code",DimSetEntry."Dimension Code");
            IF NOT TempDocLineDim.FIND('-') THEN BEGIN
              TempDocLineDim.INIT;
              TempDocLineDim.ID := TempID;
              TempID := TempID - 1;
              TempDocLineDim."Company Name" := COMPANYNAME;
              TempDocLineDim."Document Type" := DocType;
              TempDocLineDim."Document No." := DocNo;
              TempDocLineDim."Line No." := LineNo;
              TempDocLineDim."Dimension Code" := DimSetEntry."Dimension Code";
              TempDocLineDim.INSERT;
            END;
          END;
        UNTIL DimSetEntry.NEXT = 0;

      TempDocLineDim.RESET;
      IF NOT TempDocLineDim.FIND('-') THEN BEGIN
        TempDocLineDim."Document Type" := DocType;
        TempDocLineDim."Document No." := DocNo;
        TempDocLineDim."Line No." := LineNo;
      END;
      PAGE.RUNMODAL(PAGE::"Show Document Line Dimensions",TempDocLineDim);
    END;

    PROCEDURE DimensionChanged@1100285014(PurchLine@1100285000 : Record 39) : Boolean;
    VAR
      DimSetEntry@1100285002 : Record 480;
      DocLineDim@1100285001 : Record 12013611;
      Column@1100285003 : Record 12013661;
    BEGIN
      DocLineDim.RESET;
      DocLineDim.SETCURRENTKEY("Document No.");
      DocLineDim.SETRANGE("Company Name",COMPANYNAME);
      DocLineDim.SETRANGE("Document Type",PurchLine."Document Type");
      DocLineDim.SETRANGE("Document No.",PurchLine."Document No.");
      DocLineDim.SETRANGE("Line No.",PurchLine."Line No.");
      IF DocLineDim.FINDSET THEN
        REPEAT
          IF Column.GET(COMPANYNAME,DocLineDim."Column ID") THEN
            IF Column.Dimension THEN
              BEGIN
                IF DimSetEntry.GET(PurchLine."Dimension Set ID",DocLineDim."Dimension Code") THEN
                  BEGIN
                    IF DimSetEntry."Dimension Value Code" <> DocLineDim."Dimension Value" THEN
                      EXIT(TRUE);
                  END
                ELSE
                  EXIT(TRUE);
              END;
        UNTIL DocLineDim.NEXT = 0;

      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID", PurchLine."Dimension Set ID");
      IF DimSetEntry.FINDSET THEN
        REPEAT
          IF NOT DocLineDim.RetrieveRecord(DocLineDim,COMPANYNAME, PurchLine."Document Type", PurchLine."Document No.",
                                           PurchLine."Line No.", DimSetEntry."Dimension Code") THEN
            BEGIN
              Column.RESET;
              Column.SETCURRENTKEY("Company Name", Code);
              Column.SETFILTER("Company Name", COMPANYNAME);
              Column.SETFILTER(Code, DimSetEntry."Dimension Code");
              IF Column.FIND('-') THEN
                EXIT(TRUE);
            END;
        UNTIL DimSetEntry.NEXT = 0;

      EXIT(FALSE);
    END;

    PROCEDURE UpdateDocLineDimensions@1100285015(ChangeJournalLine@1100285003 : Record 12013618;NewLineNo@1100285007 : Integer;VAR PurchHeader@1100285008 : Record 38;AppSetup@1100285017 : Record 12013601);
    VAR
      DimSetEntry@1100285002 : Record 480;
      DimValue@1100285011 : Record 349;
      DocLineDim@1100285000 : Record 12013611;
      DocLineDim2@1100285012 : Record 12013611;
      ImportData@1100285015 : Codeunit 12013623;
      Index@1100285001 : Integer;
      PurchLine@1100285004 : Record 39;
      ColumnID@1100285006 : Integer;
      Column@1100285005 : Record 12013661;
      DimToGLMapping@1100285010 : Record 12013646;
      DoNotInsert@1100285009 : Boolean;
      RecordRef@1100285013 : RecordRef;
      FldRef@1100285014 : FieldRef;
      ExFlow@1100285016 : Codeunit 12013601;
    BEGIN
      // Update purchase line
      PurchLine.GET(ChangeJournalLine."Document Type",ChangeJournalLine."Document No.",NewLineNo);

      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID", PurchLine."Dimension Set ID");
      IF DimSetEntry.FINDSET THEN
        REPEAT
          DocLineDim.RESET;
          DocLineDim.SETCURRENTKEY("Document No.");
          DocLineDim.SETRANGE("Company Name",ChangeJournalLine."Company Name");
          DocLineDim.SETRANGE("Document Type",ChangeJournalLine."Document Type");
          DocLineDim.SETRANGE("Document No.",ChangeJournalLine."Document No.");
          DocLineDim.SETRANGE("Line No.", NewLineNo);
          DocLineDim.SETRANGE("Dimension Code", DimSetEntry."Dimension Code");
          IF NOT DocLineDim.FINDFIRST THEN BEGIN
            ColumnID := ExFlow.GetColumnID(DimSetEntry."Dimension Code");
            IF ColumnID <> 0 THEN BEGIN
              Column.GET(COMPANYNAME,ColumnID);
              IF Column.Dimension THEN BEGIN
                DimValue."Dimension Code" := DimSetEntry."Dimension Code";
                DimValue.Code := '';
                UpdateDimFromPurchLine(PurchLine."Dimension Set ID",DimValue,PurchLine."Shortcut Dimension 1 Code",PurchLine."Shortcut Dimension 2 Code");
                PurchLine.MODIFY(FALSE);
              END;
            END;
          END;
        UNTIL DimSetEntry.NEXT = 0;

      DocLineDim.RESET;
      DocLineDim.SETCURRENTKEY("Document No.");
      DocLineDim.SETRANGE("Company Name",ChangeJournalLine."Company Name");
      DocLineDim.SETRANGE("Document Type",ChangeJournalLine."Document Type");
      DocLineDim.SETRANGE("Document No.",ChangeJournalLine."Document No.");
      DocLineDim.SETRANGE("Line No.", NewLineNo);
      IF DocLineDim.FINDSET(TRUE) THEN
        REPEAT
          ExFlow.GetDimIndex(DocLineDim."Dimension Code", Index);
          ColumnID := ExFlow.GetColumnID(DocLineDim."Dimension Code");
          Column.GET(COMPANYNAME,ColumnID);

          IF Column.Dimension THEN
            BEGIN
              DoNotInsert := FALSE;

              IF DimSetEntry.GET(PurchLine."Dimension Set ID",DocLineDim."Dimension Code") THEN
                BEGIN
                  IF DimSetEntry."Dimension Value Code" <> DocLineDim."Dimension Value" THEN
                    BEGIN
                      IF DocLineDim."Dimension Value" = '' THEN
                        BEGIN
                          DimValue."Dimension Code" := DimSetEntry."Dimension Code";
                          DimValue.Code := '';
                          UpdateDimFromPurchLine(PurchLine."Dimension Set ID",DimValue,PurchLine."Shortcut Dimension 1 Code",PurchLine."Shortcut Dimension 2 Code");
                          PurchLine.MODIFY;

                          DocLineDim2.GET(DocLineDim.ID);
                          DocLineDim2.DELETE;
                        END
                      ELSE
                        BEGIN
                          DimValue.GET(DocLineDim."Dimension Code", DocLineDim."Dimension Value");
                          UpdateDimFromPurchLine(PurchLine."Dimension Set ID",DimValue,PurchLine."Shortcut Dimension 1 Code",PurchLine."Shortcut Dimension 2 Code");
                          PurchLine.MODIFY;
                        END;
                    END;
                END
              ELSE
                IF DocLineDim."Dimension Value" <> '' THEN
                  BEGIN
                    DimToGLMapping.RESET;
                    DimToGLMapping.SETRANGE("Dimension Code", DocLineDim."Dimension Code");
                    DimToGLMapping.SETRANGE("Dimension Value Code", DocLineDim."Dimension Value");
                    DimToGLMapping.SETRANGE("Do Not Insert Into NAV", TRUE);
                    DoNotInsert := DimToGLMapping.FINDFIRST;
                    IF NOT DoNotInsert THEN BEGIN
                      DimValue.GET(DocLineDim."Dimension Code", DocLineDim."Dimension Value");
                      UpdateDimFromPurchLine(PurchLine."Dimension Set ID",DimValue,PurchLine."Shortcut Dimension 1 Code",PurchLine."Shortcut Dimension 2 Code");
                      PurchLine.MODIFY;
                    END;
                  END;
            END
            ELSE BEGIN
              IF (DocLineDim."Dimension Value" <> '') AND (Column."Purchase Line Field ID" <> 0) THEN BEGIN
                RecordRef.GETTABLE(PurchLine);
                FldRef := RecordRef.FIELD(Column."Purchase Line Field ID");
                ImportData.UpdateRecord(FldRef,DocLineDim."Dimension Value",FALSE,
                                        DATABASE::"Purchase Line",Column."Purchase Line Field ID",-1);
                RecordRef.MODIFY;
              END;
            END;
        UNTIL DocLineDim.NEXT = 0;

      IF AppSetup."Copy Line Dimensions to Header" THEN
        UpdateDimHeader(PurchLine,PurchHeader);

      ExFlow.UpdateLine(PurchLine,PurchLine,TRUE);
    END;

    LOCAL PROCEDURE UpdateDimHeader@1100285016(PurchLine@1100285000 : Record 39;VAR PurchHeader@1100285006 : Record 38);
    VAR
      DimSetEntry@1100285002 : Record 480;
      TempDimValue@1100285001 : TEMPORARY Record 349;
      DimValue@1100285003 : Record 349;
    BEGIN
      TempDimValue.RESET;
      TempDimValue.DELETEALL;

      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID", PurchLine."Dimension Set ID");
      IF DimSetEntry.FINDSET THEN BEGIN
        REPEAT
          TempDimValue.SETRANGE("Dimension Code", DimSetEntry."Dimension Code");
          IF NOT TempDimValue.FIND('-') THEN BEGIN
            TempDimValue."Dimension Code" := DimSetEntry."Dimension Code";
            TempDimValue.Code := DimSetEntry."Dimension Value Code";
            TempDimValue.INSERT;
          END;
        UNTIL DimSetEntry.NEXT = 0;

        TempDimValue.RESET;
        TempDimValue.FIND('-');
        REPEAT
          // Make sure all the lines have the same dim value
          DimSetEntry.RESET;
          DimSetEntry.SETRANGE("Dimension Set ID", PurchLine."Dimension Set ID");
          DimSetEntry.SETRANGE("Dimension Code", TempDimValue."Dimension Code");
          DimSetEntry.SETFILTER("Dimension Value Code", '<>%1', TempDimValue.Code);
          IF NOT DimSetEntry.FINDFIRST THEN BEGIN
            DimValue.GET(TempDimValue."Dimension Code", TempDimValue.Code);
            UpdateDimFromPurchLine(PurchHeader."Dimension Set ID",DimValue,PurchHeader."Shortcut Dimension 1 Code",PurchHeader."Shortcut Dimension 2 Code");
            PurchHeader.MODIFY;
          END;
        UNTIL TempDimValue.NEXT = 0;
      END;
    END;

    PROCEDURE UpdateDimFromPurchLine@1100285021(VAR DimSetID@1100285000 : Integer;DimValue@1100285001 : Record 349;VAR ShortCutDimCode1@1100285004 : Code[20];VAR ShortCutDimCode2@1100285005 : Code[20]);
    VAR
      TempDimSetEntry@1100285003 : TEMPORARY Record 480;
      DimMgt@1100285002 : Codeunit 408;
    BEGIN
      DimMgt.GetDimensionSet(TempDimSetEntry,DimSetID);
      IF TempDimSetEntry.GET(TempDimSetEntry."Dimension Set ID",DimValue."Dimension Code") THEN
        TempDimSetEntry.DELETE;

      IF DimValue.Code <> '' THEN BEGIN
        TempDimSetEntry."Dimension Code" := DimValue."Dimension Code";
        TempDimSetEntry."Dimension Value Code" := DimValue.Code;
        TempDimSetEntry."Dimension Value ID" := DimValue."Dimension Value ID";
        TempDimSetEntry.INSERT;
      END;

      DimSetID := DimMgt.GetDimensionSetID(TempDimSetEntry);
      DimMgt.UpdateGlobalDimFromDimSetID(DimSetID,ShortCutDimCode1,ShortCutDimCode2);
    END;

    PROCEDURE CopyPurchLineDimToTemp@1100285018(PurchLine@1100285000 : Record 39);
    BEGIN
      GlobalTempPurchLine.RESET;
      GlobalTempPurchLine.SETRANGE("Document Type", PurchLine."Document Type");
      GlobalTempPurchLine.SETRANGE("Document No.", PurchLine."Document No.");
      GlobalTempPurchLine.SETRANGE("Line No.", PurchLine."Line No.");
      IF GlobalTempPurchLine.FIND('-') THEN BEGIN
        GlobalTempPurchLine."Dimension Set ID" := PurchLine."Dimension Set ID";
        GlobalTempPurchLine.MODIFY;
      END
      ELSE BEGIN
        GlobalTempPurchLine."Document Type" := PurchLine."Document Type";
        GlobalTempPurchLine."Document No." := PurchLine."Document No.";
        GlobalTempPurchLine."Line No." := PurchLine."Line No.";
        GlobalTempPurchLine."Dimension Set ID" := PurchLine."Dimension Set ID";
        GlobalTempPurchLine.INSERT;
      END;
    END;

    PROCEDURE UpdateDimHeaderImportWorksheet@1100285025(ExFPurchLine@1100285000 : Record 12013588;VAR ExFPurchHeader@1100285006 : Record 12013587);
    VAR
      DimSetEntry@1100285003 : Record 480;
      TempDimValue@1100285002 : TEMPORARY Record 349;
      DimValue@1100285001 : Record 349;
    BEGIN
      TempDimValue.RESET;
      TempDimValue.DELETEALL;

      ExFPurchLine.RESET;
      IF ExFPurchLine.FINDSET THEN BEGIN
        REPEAT
          DimSetEntry.RESET;
          DimSetEntry.SETRANGE("Dimension Set ID", ExFPurchLine."Dimension Set ID");
          IF DimSetEntry.FINDSET THEN
            REPEAT
              TempDimValue.SETRANGE("Dimension Code", DimSetEntry."Dimension Code");
              IF NOT TempDimValue.FIND('-') THEN BEGIN
                TempDimValue."Dimension Code" := DimSetEntry."Dimension Code";
                TempDimValue.Code := DimSetEntry."Dimension Value Code";
                TempDimValue.INSERT;
              END;
            UNTIL DimSetEntry.NEXT = 0;
        UNTIL ExFPurchLine.NEXT = 0;

        TempDimValue.RESET;
        TempDimValue.FIND('-');
        REPEAT
          // Make sure all the lines have the same dim value
          DimSetEntry.RESET;
          DimSetEntry.SETRANGE("Dimension Set ID", ExFPurchLine."Dimension Set ID");
          DimSetEntry.SETRANGE("Dimension Code", TempDimValue."Dimension Code");
          DimSetEntry.SETFILTER("Dimension Value Code", '<>%1', TempDimValue.Code);
          IF NOT DimSetEntry.FINDFIRST THEN BEGIN
            DimValue.GET(TempDimValue."Dimension Code", TempDimValue.Code);
            UpdateDimFromPurchLine(ExFPurchHeader."Dimension Set ID",DimValue,ExFPurchHeader."Shortcut Dimension 1 Code",ExFPurchHeader."Shortcut Dimension 2 Code");
            ExFPurchHeader.MODIFY;
          END;
        UNTIL TempDimValue.NEXT = 0;
      END;
    END;

    PROCEDURE CopyTempDimToPurchLine@1100285019(VAR PurchLine@1100285000 : Record 39;CopyAllFromTemp@1100285002 : Boolean);
    BEGIN
      GlobalTempPurchLine.RESET;
      IF NOT CopyAllFromTemp THEN BEGIN
        GlobalTempPurchLine.SETRANGE("Document Type", PurchLine."Document Type");
        GlobalTempPurchLine.SETRANGE("Document No.", PurchLine."Document No.");
        GlobalTempPurchLine.SETRANGE("Line No.", PurchLine."Line No.");
      END;
      IF NOT GlobalTempPurchLine.FIND('-') THEN
        EXIT;

      PurchLine."Dimension Set ID" := GlobalTempPurchLine."Dimension Set ID";
      PurchLine.MODIFY;
    END;

    PROCEDURE AddDocDimToJnlLineDim@1100285020(VAR GenJnlLine@1100285003 : Record 81;DimCode@1100285000 : Code[20];DimValue@1100285004 : Code[20]);
    VAR
      TempDimSetEntry@1100285001 : TEMPORARY Record 480;
      DimValueRec@1100285002 : Record 349;
      DimMgt@1100285005 : Codeunit 408;
      TempDimSetID@1100285006 : Integer;
    BEGIN
      IF NOT DimValueRec.GET(DimCode,DimValue) THEN
        EXIT;

      IF GenJnlLine."Dimension Set ID" = 0 THEN BEGIN
        IF DimValueRec.Code <> '' THEN BEGIN
          TempDimSetEntry.INIT;
          TempDimSetEntry."Dimension Set ID" := 0;
          TempDimSetEntry."Dimension Code" := DimValueRec."Dimension Code";
          TempDimSetEntry."Dimension Value Code" := DimValueRec.Code;
          TempDimSetEntry."Dimension Value ID" := DimValueRec."Dimension Value ID";
          TempDimSetEntry.INSERT;
        END;
      END
      ELSE BEGIN
        DimMgt.GetDimensionSet(TempDimSetEntry,GenJnlLine."Dimension Set ID");
        IF TempDimSetEntry.GET(TempDimSetEntry."Dimension Set ID",DimCode) THEN
          TempDimSetEntry.DELETE;

        IF DimValueRec.Code <> '' THEN BEGIN
          TempDimSetEntry."Dimension Code" := DimValueRec."Dimension Code";
          TempDimSetEntry."Dimension Value Code" := DimValueRec.Code;
          TempDimSetEntry."Dimension Value ID" := DimValueRec."Dimension Value ID";
          TempDimSetEntry.INSERT;
        END;
      END;

      TempDimSetID := DimMgt.GetDimensionSetID(TempDimSetEntry);
      DimMgt.UpdateGlobalDimFromDimSetID(TempDimSetID,GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Shortcut Dimension 2 Code");
      GenJnlLine."Dimension Set ID" := TempDimSetID;
      GenJnlLine.MODIFY;
    END;

    PROCEDURE OpenExcel@1100285030(VAR TempExcelBuffer@1100285000 : Record 370;ReportName@1100285001 : Text[50]);
    BEGIN
      TempExcelBuffer.CreateBook(ReportName,ReportName);
      TempExcelBuffer.GiveUserControl;
    END;

    PROCEDURE ClearFixedDimCode@1100285023(PurchaseLine@1100285003 : Record 39;VAR FixedDimCode@1100285002 : ARRAY [8] OF Code[20];VAR FixedDimValue@1100285001 : ARRAY [8] OF Code[20];VAR i@1100285004 : Integer);
    VAR
      DimSetEntry@1100285000 : Record 480;
    BEGIN
      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID", PurchaseLine."Dimension Set ID");
      DimSetEntry.SETRANGE("Dimension Code",FixedDimCode[i]);
      IF DimSetEntry.FINDFIRST THEN BEGIN
        CLEAR(FixedDimCode);
        CLEAR(FixedDimValue);
        i := 8;
      END;
    END;

    PROCEDURE CreatePurchLine@1100285034(VAR PurchLine@1100285010 : Record 39;VAR OldPurchLine@1100285009 : Record 39);
    VAR
      ExFlowSetup@1100285003 : Record 12013601;
      NewPurchLine@1100285000 : Record 39;
      ExDocLineApprover@1100285004 : Record 12013610;
      NewExDocLineApprover@1100285005 : Record 12013610;
      ExDocLine@1100285006 : Record 12013609;
      OldExDocLine@1100285008 : Record 12013609;
      TotalAmount@1100285002 : Decimal;
      TempLineNo@1100285001 : Integer;
      ExDocument@1100285012 : Record 12013608;
    BEGIN
      ExFlowSetup.GET(COMPANYNAME,0);

      NewPurchLine.RESET;
      NewPurchLine.LOCKTABLE;
      NewPurchLine.SETRANGE("Document Type", OldPurchLine."Document Type");
      NewPurchLine.SETRANGE("Document No.", OldPurchLine."Document No.");
      NewPurchLine.FINDLAST;
      TempLineNo := NewPurchLine."Line No." + 10000;

      PurchLine.CALCSUMS("Line Amount");
      TotalAmount := PurchLine."Line Amount";
      IF ExFlowSetup."Block Amt change Approved Line" THEN BEGIN
        IF OldPurchLine."Line Amount" < TotalAmount THEN
          ERROR(EXF01);
      END;
      TotalAmount := OldPurchLine."Line Amount";

      ExDocLineApprover.RESET;
      ExDocLineApprover.SETCURRENTKEY("Document No.");
      ExDocLineApprover.SETRANGE("Company Name", COMPANYNAME);
      ExDocLineApprover.SETRANGE("Document Type", OldPurchLine."Document Type");
      ExDocLineApprover.SETRANGE("Document No.", OldPurchLine."Document No.");
      ExDocLineApprover.SETRANGE("Line No.", OldPurchLine."Line No.");

      OldExDocLine.SETCURRENTKEY("Document No.");
      OldExDocLine.SETRANGE("Company Name", COMPANYNAME);
      OldExDocLine.SETRANGE("Document Type", OldPurchLine."Document Type");
      OldExDocLine.SETRANGE("Document No.", OldPurchLine."Document No.");
      OldExDocLine.SETRANGE("Line No.", OldPurchLine."Line No.");
      OldExDocLine.FIND('-');

      PurchLine.RESET;
      IF PurchLine.FINDSET THEN BEGIN
        REPEAT
          NewPurchLine.INIT;
          NewPurchLine.TRANSFERFIELDS(OldPurchLine);
          NewPurchLine."Line No." := TempLineNo;
          TempLineNo := TempLineNo + 10000;
          NewPurchLine.VALIDATE(Type, PurchLine.Type);
          NewPurchLine.VALIDATE("No.", PurchLine."No.");
          NewPurchLine.VALIDATE(Quantity, PurchLine.Quantity);
          NewPurchLine.VALIDATE("Direct Unit Cost", PurchLine."Direct Unit Cost");
          NewPurchLine.Description := PurchLine.Description;
          NewPurchLine.INSERT(TRUE);

          ExDocLine.SETCURRENTKEY("Document No.");
          ExDocLine.SETRANGE("Company Name", COMPANYNAME);
          ExDocLine.SETRANGE("Document Type", NewPurchLine."Document Type");
          ExDocLine.SETRANGE("Document No.", NewPurchLine."Document No.");
          ExDocLine.SETRANGE("Line No.", NewPurchLine."Line No.");
          ExDocLine.FIND('-');
          ExDocLine.Flowstatus := OldExDocLine.Flowstatus;
          ExDocLine.MODIFY;

          NewExDocLineApprover.RESET;
          NewExDocLineApprover.SETRANGE("Company Name", COMPANYNAME);
          NewExDocLineApprover.SETRANGE("Document Type", NewPurchLine."Document Type");
          NewExDocLineApprover.SETRANGE("Document No.", NewPurchLine."Document No.");
          NewExDocLineApprover.SETRANGE("Line No.", NewPurchLine."Line No.");
          NewExDocLineApprover.DELETEALL;

          IF ExDocLineApprover.FINDSET THEN
            REPEAT
              NewExDocLineApprover.INIT;
              NewExDocLineApprover.TRANSFERFIELDS(ExDocLineApprover);
              NewExDocLineApprover.ID := 0;
              NewExDocLineApprover."Document Line ID" := ExDocLine.ID;
              NewExDocLineApprover."Document ID" := ExDocLine."Document ID";
              NewExDocLineApprover."Document Type" := NewPurchLine."Document Type";
              NewExDocLineApprover."Document No." := NewPurchLine."Document No.";
              NewExDocLineApprover."Line No." := NewPurchLine."Line No.";
              NewExDocLineApprover.INSERT;
            UNTIL ExDocLineApprover.NEXT = 0;

          TotalAmount := TotalAmount - NewPurchLine."Line Amount";
        UNTIL PurchLine.NEXT = 0;

        IF TotalAmount = 0 THEN BEGIN
          ExDocLineApprover.RESET;
          ExDocLineApprover.SETCURRENTKEY("Document No.");
          ExDocLineApprover.SETRANGE("Company Name", COMPANYNAME);
          ExDocLineApprover.SETRANGE("Document Type", OldPurchLine."Document Type");
          ExDocLineApprover.SETRANGE("Document No.", OldPurchLine."Document No.");
          ExDocLineApprover.SETRANGE("Line No.", OldPurchLine."Line No.");
          ExDocLineApprover.DELETEALL;

          ExDocLine.RESET;
          ExDocLine.SETCURRENTKEY("Document No.");
          ExDocLine.SETRANGE("Company Name", COMPANYNAME);
          ExDocLine.SETRANGE("Document Type", OldPurchLine."Document Type");
          ExDocLine.SETRANGE("Document No.", OldPurchLine."Document No.");
          ExDocLine.SETRANGE("Line No.", OldPurchLine."Line No.");
          ExDocLine.DELETEALL;

          OldPurchLine.DELETE(TRUE);
        END
        ELSE BEGIN
          OldPurchLine.VALIDATE("Direct Unit Cost", TotalAmount);
          OldPurchLine.MODIFY;
          OldExDocLine."Direct Unit Cost" := OldPurchLine."Direct Unit Cost";
          OldExDocLine.Amount := OldPurchLine."Line Amount";
          OldExDocLine."Line Discount %" := OldPurchLine."Line Discount %";
          OldExDocLine.MODIFY;
        END;
      END;

      ExDocument.RetrieveRecord(ExDocument,COMPANYNAME,OldPurchLine."Document Type",OldPurchLine."Document No.");
      IF ExDocument.Status <> ExDocument.Status::"Ready for posting" THEN BEGIN
        ExDocument.Status := ExDocument.Status::"Ready for posting";
        ExDocument.MODIFY(FALSE);
      END;
    END;

    PROCEDURE InsertDimToEXPurchHeader@1100285024(VAR pvarExfPurchHead@1100285002 : Record 12013587;DimCode@1100285001 : Code[20];DimValue@1100285000 : Code[20]);
    VAR
      DimensionValue@1100285004 : Record 349;
    BEGIN
      //348882
      IF DimCode = '' THEN
        EXIT;

      IF NOT DimensionValue.GET(DimCode,DimValue) THEN
        EXIT;

      UpdateDimFromPurchLine(pvarExfPurchHead."Dimension Set ID",DimensionValue,
                             pvarExfPurchHead."Shortcut Dimension 1 Code",pvarExfPurchHead."Shortcut Dimension 2 Code");
      pvarExfPurchHead.MODIFY;
    END;

    PROCEDURE InsertDimToEXPurchLine@1100285026(VAR pvarExfPurchLine@1100285002 : Record 12013588;DimCode@1100285001 : Code[20];DimValue@1100285000 : Code[20]);
    VAR
      DimensionValue@1100285004 : Record 349;
    BEGIN
      //348882
      IF DimCode = '' THEN
        EXIT;

      IF NOT DimensionValue.GET(DimCode,DimValue) THEN
        EXIT;

      UpdateDimFromPurchLine(pvarExfPurchLine."Dimension Set ID",DimensionValue,
                             pvarExfPurchLine."Shortcut Dimension 1 Code",pvarExfPurchLine."Shortcut Dimension 2 Code");
      pvarExfPurchLine.MODIFY;
    END;

    PROCEDURE UpdateDocLineDimQuote@1100285027(VAR PurchQuoteLine@1100285003 : Record 39;VAR PurchHeader@1100285008 : Record 38;AppSetup@1100285017 : Record 12013601);
    VAR
      DimSetEntry@1100285002 : Record 480;
      DimValue@1100285010 : Record 349;
      DocLineDim@1100285000 : Record 12013611;
      DocLineDim2@1100285012 : Record 12013611;
      Column@1100285007 : Record 12013661;
      DimToGLMapping@1100285005 : Record 12013646;
      ImportData@1100285015 : Codeunit 12013623;
      ExFlow@1100285004 : Codeunit 12013601;
      Index@1100285001 : Integer;
      ColumnID@1100285006 : Integer;
      DoNotInsert@1100285009 : Boolean;
      RecordRef@1100285013 : RecordRef;
      FldRef@1100285014 : FieldRef;
    BEGIN
      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Set ID", PurchQuoteLine."Dimension Set ID");
      IF DimSetEntry.FINDSET THEN
        REPEAT
          DocLineDim.RESET;
          DocLineDim.SETCURRENTKEY("Document No.");
          DocLineDim.SETRANGE("Company Name", COMPANYNAME);
          DocLineDim.SETRANGE("Document Type", PurchQuoteLine."Document Type");
          DocLineDim.SETRANGE("Document No.",PurchQuoteLine."Document No.");
          DocLineDim.SETRANGE("Line No.", PurchQuoteLine."Line No.");
          DocLineDim.SETRANGE("Dimension Code", DimSetEntry."Dimension Code");
          IF NOT DocLineDim.FINDFIRST THEN BEGIN
            ColumnID := ExFlow.GetColumnID(DimSetEntry."Dimension Code");
            IF ColumnID <> 0 THEN BEGIN
              Column.GET(COMPANYNAME,ColumnID);
              IF Column.Dimension THEN BEGIN
                DimValue."Dimension Code" := DimSetEntry."Dimension Code";
                DimValue.Code := '';
                UpdateDimFromPurchLine(PurchQuoteLine."Dimension Set ID",DimValue,PurchQuoteLine."Shortcut Dimension 1 Code",PurchQuoteLine."Shortcut Dimension 2 Code");
                PurchQuoteLine.MODIFY;
              END;
            END;
          END;
        UNTIL DimSetEntry.NEXT = 0;

      DocLineDim.RESET;
      DocLineDim.SETCURRENTKEY("Document No.");
      DocLineDim.SETRANGE("Company Name",COMPANYNAME);
      DocLineDim.SETRANGE("Document Type",PurchQuoteLine."Document Type");
      DocLineDim.SETRANGE("Document No.",PurchQuoteLine."Document No.");
      DocLineDim.SETRANGE("Line No.",PurchQuoteLine."Line No.");
      IF DocLineDim.FINDSET(TRUE) THEN
        REPEAT
          ExFlow.GetDimIndex(DocLineDim."Dimension Code", Index);
          ColumnID := ExFlow.GetColumnID(DocLineDim."Dimension Code");
          Column.GET(COMPANYNAME,ColumnID);

          IF Column.Dimension THEN
            BEGIN
              DoNotInsert := FALSE;

              IF DimSetEntry.GET(PurchQuoteLine."Dimension Set ID",DocLineDim."Dimension Code") THEN
                BEGIN
                  IF DimSetEntry."Dimension Value Code" <> DocLineDim."Dimension Value" THEN
                    BEGIN
                      IF DocLineDim."Dimension Value" = '' THEN
                        BEGIN
                          DimValue."Dimension Code" := DimSetEntry."Dimension Code";
                          DimValue.Code := '';
                          UpdateDimFromPurchLine(PurchQuoteLine."Dimension Set ID",DimValue,PurchQuoteLine."Shortcut Dimension 1 Code",PurchQuoteLine."Shortcut Dimension 2 Code");
                          PurchQuoteLine.MODIFY;

                          DocLineDim2.GET(DocLineDim.ID);
                          DocLineDim2.DELETE;
                        END
                      ELSE
                        BEGIN
                          DimValue.GET(DocLineDim."Dimension Code", DocLineDim."Dimension Value");
                          UpdateDimFromPurchLine(PurchQuoteLine."Dimension Set ID",DimValue,PurchQuoteLine."Shortcut Dimension 1 Code",PurchQuoteLine."Shortcut Dimension 2 Code");
                          PurchQuoteLine.MODIFY;
                        END;
                    END;
                END
              ELSE
                IF DocLineDim."Dimension Value" <> '' THEN
                  BEGIN
                    DimToGLMapping.RESET;
                    DimToGLMapping.SETRANGE("Dimension Code", DocLineDim."Dimension Code");
                    DimToGLMapping.SETRANGE("Dimension Value Code", DocLineDim."Dimension Value");
                    DimToGLMapping.SETRANGE("Do Not Insert Into NAV", TRUE);
                    DoNotInsert := DimToGLMapping.FINDFIRST;
                    IF NOT DoNotInsert THEN BEGIN
                      DimValue.GET(DocLineDim."Dimension Code", DocLineDim."Dimension Value");
                      UpdateDimFromPurchLine(PurchQuoteLine."Dimension Set ID",DimValue,PurchQuoteLine."Shortcut Dimension 1 Code",PurchQuoteLine."Shortcut Dimension 2 Code");
                      PurchQuoteLine.MODIFY;
                    END;
                  END;
            END
            ELSE BEGIN
              IF (DocLineDim."Dimension Value" <> '') AND (Column."Purchase Line Field ID" <> 0) THEN BEGIN
                RecordRef.GETTABLE(PurchQuoteLine);
                FldRef := RecordRef.FIELD(Column."Purchase Line Field ID");
                ImportData.UpdateRecord(FldRef,DocLineDim."Dimension Value",FALSE,
                                        DATABASE::"Purchase Line",Column."Purchase Line Field ID",-1);
                RecordRef.MODIFY;
              END;
            END;
        UNTIL DocLineDim.NEXT = 0;

      IF AppSetup."Copy Line Dimensions to Header" THEN
        UpdateDimHeader(PurchQuoteLine,PurchHeader);
    END;

    PROCEDURE CheckDimConflicts@1100285028(_ChangJnlLine@1100285001 : Record 12013618;DocLine@1100285009 : Record 12013609;Priority@1100285000 : Integer;NoChange@1100285005 : Boolean;JobChange@1100285006 : Boolean);
    VAR
      DimMgt@1100285002 : Codeunit 408;
      DefaultDim@1100285003 : Record 352;
      ExDimMgt@1100285004 : Codeunit 12013605;
      DocLineDim@1100285007 : Record 12013611;
      ColumnID@1100285008 : Integer;
      ExFlow@1100285010 : Codeunit 12013601;
    BEGIN
      IF Priority = 0 THEN
        EXIT;

      IF NoChange AND JobChange THEN BEGIN
        DefaultDim.RESET;
        IF Priority = 1 THEN  BEGIN
           //Insert from Job first which will give it priority
          DefaultDim.SETRANGE("Table ID",167);
          JobChange := FALSE; // LG: Why?
        END;
        IF Priority = 2 THEN BEGIN
          CASE _ChangJnlLine."New Type" OF
            //Insert from No first which will give Job priority
            _ChangJnlLine."New Type"::"G/L Account": DefaultDim.SETRANGE("Table ID",15);
            _ChangJnlLine."New Type"::Item         : DefaultDim.SETRANGE("Table ID",27);
            _ChangJnlLine."New Type"::"Fixed Asset": DefaultDim.SETRANGE("Table ID",5600);
            _ChangJnlLine."New Type"::"Charge (Item)": DefaultDim.SETRANGE("Table ID",5800);
          END;
          NoChange := FALSE; // LG: Why?
        END;

        DefaultDim.SETRANGE("No.",_ChangJnlLine."New No.");
        DefaultDim.SETFILTER("Dimension Value Code",'<>%1','');
        IF DefaultDim.FINDSET THEN
          REPEAT
            ColumnID := ExFlow.GetColumnID(DefaultDim."Dimension Code");
            IF ColumnID <> 0 THEN BEGIN
              DocLineDim.RESET;
              DocLineDim.SETCURRENTKEY("Document No.");
              DocLineDim.SETRANGE("Company Name",_ChangJnlLine."Company Name");
              DocLineDim.SETRANGE("Document Type",_ChangJnlLine."Document Type");
              DocLineDim.SETRANGE("Document No.",_ChangJnlLine."Document No.");
              DocLineDim.SETRANGE("Line No.", _ChangJnlLine."New Line No.");
              DocLineDim.SETRANGE("Dimension Code", DefaultDim."Dimension Code");
              IF DocLineDim.FINDFIRST THEN BEGIN
                IF Priority = 2 THEN BEGIN
                  DocLineDim."Dimension Value" := DefaultDim."Dimension Value Code";
                  DocLineDim.MODIFY;
                END;
              END ELSE BEGIN
                  DocLineDim.INIT;
                  DocLineDim."Company Name" := COMPANYNAME;
                  DocLineDim."Document Type" := _ChangJnlLine."Document Type";
                  DocLineDim."Document No." := _ChangJnlLine."Document No.";
                  DocLineDim."Line No." :=_ChangJnlLine."New Line No.";
                  DocLineDim."Dimension Code" := DefaultDim."Dimension Code";
                  DocLineDim."Dimension Value" := DefaultDim."Dimension Value Code";
                  DocLineDim."Column ID" := ColumnID;
                  DocLineDim."Document Line ID" := DocLine.ID;
                  DocLineDim."Document ID" := DocLine."Document ID";
                  DocLineDim.ID := 0;
                  IF DocLineDim."Dimension Value" <> '' THEN
                    DocLineDim.INSERT;
              END;
            END;
        UNTIL DefaultDim.NEXT = 0;
      END;

      DefaultDim.RESET;
      DefaultDim.SETRANGE("Table ID",0); // LG: added. If both NoChange and JobChange above, both will
        //be false down here (why?) resulting in a open filter regarding "Table ID" and
        //will trigger on anything having "New Job No." as "No" (and "New Job No." can be anything, not only Job)
      IF NoChange THEN
        CASE _ChangJnlLine."New Type" OF
          _ChangJnlLine."New Type"::"G/L Account": DefaultDim.SETRANGE("Table ID",15);
          _ChangJnlLine."New Type"::Item         : DefaultDim.SETRANGE("Table ID",27);
          _ChangJnlLine."New Type"::"Fixed Asset": DefaultDim.SETRANGE("Table ID",5600);
          _ChangJnlLine."New Type"::"Charge (Item)": DefaultDim.SETRANGE("Table ID",5800);
        END;

      IF JobChange THEN
        DefaultDim.SETRANGE("Table ID",167);

      DefaultDim.SETRANGE("No.",_ChangJnlLine."New Job No.");
      DefaultDim.SETFILTER("Dimension Value Code",'<>%1','');
      IF DefaultDim.FINDSET THEN
        REPEAT
          ColumnID := ExFlow.GetColumnID(DefaultDim."Dimension Code");
          IF ColumnID <> 0 THEN BEGIN
            DocLineDim.RESET;
            DocLineDim.SETCURRENTKEY("Document No.");
            DocLineDim.SETRANGE("Company Name",_ChangJnlLine."Company Name");
            DocLineDim.SETRANGE("Document Type",_ChangJnlLine."Document Type");
            DocLineDim.SETRANGE("Document No.",_ChangJnlLine."Document No.");
            DocLineDim.SETRANGE("Line No.", _ChangJnlLine."New Line No.");
            DocLineDim.SETRANGE("Dimension Code", DefaultDim."Dimension Code");
            IF DocLineDim.FINDFIRST THEN BEGIN
              IF Priority = 2 THEN BEGIN
                DocLineDim."Dimension Value" := DefaultDim."Dimension Value Code";
                DocLineDim.MODIFY;
              END;
            END ELSE BEGIN
                DocLineDim.INIT;
                DocLineDim."Company Name" := COMPANYNAME;
                DocLineDim."Document Type" := _ChangJnlLine."Document Type";
                DocLineDim."Document No." := _ChangJnlLine."Document No.";
                DocLineDim."Line No." :=_ChangJnlLine."New Line No.";
                DocLineDim."Dimension Code" := DefaultDim."Dimension Code";
                DocLineDim."Dimension Value" := DefaultDim."Dimension Value Code";
                DocLineDim."Column ID" := ColumnID;
                DocLineDim."Document Line ID" := DocLine.ID;
                DocLineDim."Document ID" := DocLine."Document ID";
                DocLineDim.ID := 0;
                IF DocLineDim."Dimension Value" <> '' THEN // LG: not needed as this comes from DefaultDim."Dimension Value Code" (filte
                  DocLineDim.INSERT;
            END;
          END;
        UNTIL DefaultDim.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

