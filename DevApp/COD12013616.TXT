OBJECT Codeunit 12013616 EX User Dimension Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF350000;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      SeparationValue@1100285001 : Text[30];

    PROCEDURE OpenUserDimTable@1100285000(ExUserCompany@1100285000 : Record 12013641);
    VAR
      EXUserDimSelection@1100285001 : Page 12013670;
      ExFlowSetup@1100285002 : Record 12013601;
    BEGIN
      ExFlowSetup.GET(COMPANYNAME);
      ExFlowSetup.TESTFIELD("User Dimension Code");

      CLEAR(EXUserDimSelection);
      EXUserDimSelection.SetEXUser(ExUserCompany."User ID");
      EXUserDimSelection.LOOKUPMODE := TRUE;
      EXUserDimSelection.RUNMODAL;
    END;

    PROCEDURE PopulateTempTable@1100285001(VAR TempDimensionValue@1100285006 : TEMPORARY Record 349;ExUserCode@1100285005 : Code[50]);
    VAR
      TempDimensionValue2@1100285004 : TEMPORARY Record 349;
      DimensionValue@1100285008 : Record 349;
      EXUser@1100285007 : Record 12013641;
      ExFlowSetup@1100285010 : Record 12013601;
      TempDimValue@1100285000 : Code[250];
      UserDimCode@1100285001 : ARRAY [999] OF Code[20];
      i@1100285002 : Integer;
      TempPos@1100285003 : Integer;
    BEGIN
      ExFlowSetup.GET(COMPANYNAME);

      SeparationValue := '|';

      IF ExUserCode = '' THEN BEGIN
        EXUser.SETRANGE("Company Name", COMPANYNAME);
        EXUser.FINDFIRST;
        ExUserCode := EXUser."User ID";
      END
      ELSE
        EXUser.GET(COMPANYNAME,ExUserCode);

      TempDimensionValue.RESET;
      TempDimensionValue.DELETEALL;

      CLEAR(UserDimCode);
      TempDimensionValue2.RESET;
      TempDimensionValue2.DELETEALL;

      IF EXUser."Dimension 1" <> '' THEN BEGIN
        i := 1;
        TempDimValue := UPPERCASE(EXUser."Dimension 1");
        REPEAT
          TempPos := STRPOS(TempDimValue,SeparationValue);
          IF TempPos > 0 THEN BEGIN
            UserDimCode[i] := COPYSTR(TempDimValue,1,TempPos-1);
            TempDimValue := DELSTR(TempDimValue,1,TempPos);
          END
          ELSE BEGIN
            UserDimCode[i] := COPYSTR(TempDimValue,1);
            TempDimValue := '';
          END;

          UserDimCode[i] := COPYSTR(UserDimCode[i],STRPOS(UserDimCode[i],':')+1);
          TempDimensionValue2.Code := UserDimCode[i];
          TempDimensionValue2.INSERT;

          i += 1;
        UNTIL TempDimValue = '';
      END;

      DimensionValue.SETRANGE("Dimension Code", ExFlowSetup."User Dimension Code");
      IF DimensionValue.FINDSET THEN
        REPEAT
          TempDimensionValue.INIT;
          TempDimensionValue.TRANSFERFIELDS(DimensionValue);

          TempDimensionValue2.SETRANGE(Code, DimensionValue.Code);
          TempDimensionValue.Blocked := TempDimensionValue2.FIND('-');
          TempDimensionValue.INSERT;
        UNTIL DimensionValue.NEXT = 0;
    END;

    PROCEDURE UpdateUser@1100285002(VAR TempDimensionValue@1100285001 : TEMPORARY Record 349;ExUserCode@1100285002 : Code[50]);
    VAR
      EXUser@1100285000 : Record 12013641;
    BEGIN
      EXUser.GET(COMPANYNAME,ExUserCode);

      SeparationValue := '|';

      EXUser."Dimension 1" := '';

      TempDimensionValue.SETRANGE(Blocked, TRUE);
      IF TempDimensionValue.FIND('-') THEN BEGIN
        EXUser."Dimension 1" := TempDimensionValue."Dimension Code" + ':' +
                                TempDimensionValue.Code + SeparationValue;
        IF TempDimensionValue.NEXT <> 0 THEN
          REPEAT
            EXUser."Dimension 1" := EXUser."Dimension 1"  + TempDimensionValue.Code + SeparationValue;
          UNTIL TempDimensionValue.NEXT = 0;

        EXUser."Dimension 1" := COPYSTR(EXUser."Dimension 1",1,STRLEN(EXUser."Dimension 1")-STRLEN(SeparationValue));
      END;

      EXUser.MODIFY;

      TempDimensionValue.RESET;
    END;

    BEGIN
    END.
  }
}

