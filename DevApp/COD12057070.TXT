OBJECT Codeunit 12057070 ExFlow Web Service Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF348000;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      EXF14@1100285002 : TextConst 'ENU=PDF Preview correctly installed on current computer';
      EXF15@1100285001 : TextConst 'ENU=Can not verify access to %1, please verify that the file is placed in %2';

    PROCEDURE SetPDF@1100285000(ExternalPrimaryKey@1100285000 : Code[50];PDFImage@1100285001 : BigText);
    VAR
      ExBlob@1100285002 : Record 12013591;
      Bytes@1100285005 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array";
      Convert@1100285006 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Convert";
      MemoryStream@1100285007 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
      OutStream@1100285008 : OutStream;
      recExfPurchImpHeader@1100285010 : Record 12013648;
      recExfPurchImpLine@1100285009 : Record 12013649;
      recExfPurchImpLine2@1100285011 : Record 12013649;
      TempLineNo@1100285012 : Integer;
    BEGIN
      ExBlob.INIT;
      ExBlob."No." := 0;
      ExBlob."File Extension" := 'PDF';
      ExBlob."External Primary Key" := ExternalPrimaryKey;

      Bytes := Convert.FromBase64String(PDFImage);
      MemoryStream := MemoryStream.MemoryStream(Bytes);
      ExBlob.BLOB.CREATEOUTSTREAM(OutStream);
      MemoryStream.WriteTo(OutStream);

      ExBlob.INSERT(TRUE);

      // Fix since line does not have all the fields at the time of OnInsert in the page.
      recExfPurchImpHeader.RESET;
      recExfPurchImpHeader.SETRANGE("External Primary Key", ExternalPrimaryKey);
      IF recExfPurchImpHeader.FIND('-') THEN BEGIN
        recExfPurchImpHeader."Image Buffer ID" := ExBlob."No.";
        recExfPurchImpHeader.MODIFY;

        recExfPurchImpLine.RESET;
        recExfPurchImpLine.SETRANGE("Import Document No.", 0);
        recExfPurchImpLine.SETRANGE("External Primary Key", recExfPurchImpHeader."External Primary Key");
        IF recExfPurchImpLine.FINDSET(TRUE,TRUE) THEN BEGIN
          recExfPurchImpLine2.SETRANGE("Import Document No.", recExfPurchImpHeader."Import Document No.");
          IF recExfPurchImpLine2.FINDLAST THEN
            TempLineNo := recExfPurchImpLine2."Line No." + 10000
          ELSE
            TempLineNo := 10000;

          REPEAT
            recExfPurchImpLine2.INIT;
            recExfPurchImpLine2.TRANSFERFIELDS(recExfPurchImpLine);
            recExfPurchImpLine2."Import Document No." := recExfPurchImpHeader."Import Document No.";
            recExfPurchImpLine2."Line No." := TempLineNo;
            recExfPurchImpLine2.INSERT;
            TempLineNo += 10000;

            recExfPurchImpLine2.GET(recExfPurchImpLine."Import Document No.",recExfPurchImpLine."Line No.");
            recExfPurchImpLine2.DELETE;
          UNTIL recExfPurchImpLine.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE InitRTCPDFprev@1100285007();
    VAR
      ClientAddin@1100285000 : Record 2000000069;
    BEGIN
      IF NOT ClientAddin.GET('SignUp.ExFlow.PdfPreview','624e54b0c38953fa') THEN BEGIN
        ClientAddin.INIT;
        ClientAddin."Add-in Name" := 'SignUp.ExFlow.PdfPreview';
        ClientAddin."Public Key Token" := '624e54b0c38953fa';
        ClientAddin.Description := 'ExFlow PDF Viewer';
        ClientAddin.INSERT;
      END;
    END;

    PROCEDURE ClientTestPDFprev@1100285006();
    VAR
      AddinPath@1100285004 : Text[1024];
      ExFileMan@1100285003 : Codeunit 12013602;
      msg@1100285001 : Text[1024];
    BEGIN
      InitRTCPDFprev;

      AddinPath := GetAddInPath;
      IF NOT ExFileMan.FileExist(AddinPath,'SignUp_PDF_Preview.dll') THEN BEGIN
        msg := STRSUBSTNO(EXF15,'SignUp_PDF_Preview.dll',AddinPath);
        MESSAGE(msg);
      END ELSE
        MESSAGE(EXF14);
    END;

    PROCEDURE DownloadDLL@1100285004();
    VAR
      URL@1100285001 : Text[1024];
    BEGIN
      URL := 'http://www.signup.se/download/SignUpAddIn.zip';
      HYPERLINK(URL);
    END;

    PROCEDURE GetAddInPath@1100285001() : Text[250];
    BEGIN
      EXIT(COPYSTR(APPLICATIONPATH,1,STRPOS(APPLICATIONPATH,'Service')-1) + 'RoleTailored Client\Add-ins\');
    END;

    BEGIN
    END.
  }
}

