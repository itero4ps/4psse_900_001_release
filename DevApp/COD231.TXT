OBJECT Codeunit 231 Gen. Jnl.-Post
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00.00.44974,4PS9.00,4PSSE;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=BEGIN
            GenJnlLine.COPY(Rec);
            Code;
            COPY(GenJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.';
      Text004@1004 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.';
      Text005@1009 : TextConst 'ENU=Using %1 for Declining Balance can result in misleading numbers for subsequent years. You should manually check the postings and correct them if necessary. Do you want to continue?';
      GenJnlTemplate@1005 : Record 80;
      GenJnlLine@1006 : Record 81;
      GenJnlPostBatch@1007 : Codeunit 13;
      TempJnlBatchName@1008 : Code[10];
      Text006@1010 : TextConst '@@@=Source Code in Genenral Journal Template must not be equal to Job G/L WIP in Source Code Setup.;ENU=%1 in %2 must not be equal to %3 in %4.';
      PreviewMode@1011 : Boolean;
      gBlnBackground@1210190000 : Boolean;
      gBlnNoCommit@1210190001 : Boolean;
      gReplacePostingDatePeriodic@1100285100 : Boolean;

    [TryFunction]
    LOCAL PROCEDURE Code@1();
    VAR
      FALedgEntry@1000 : Record 5601;
      SourceCodeSetup@1001 : Record 242;
      GenJournalTemplatePerRole@1100525000 : Record 11020419;
    BEGIN
      WITH GenJnlLine DO BEGIN
        GenJournalTemplatePerRole.CheckPermission(TABLECAPTION, "Journal Template Name",TRUE); //**4PS.n

        GenJnlTemplate.GET("Journal Template Name");
        IF GenJnlTemplate.Type = GenJnlTemplate.Type::Jobs THEN BEGIN
          SourceCodeSetup.GET;
          IF GenJnlTemplate."Source Code" = SourceCodeSetup."Job G/L WIP" THEN
            ERROR(Text006,GenJnlTemplate.FIELDCAPTION("Source Code"),GenJnlTemplate.TABLECAPTION,
              SourceCodeSetup.FIELDCAPTION("Job G/L WIP"),SourceCodeSetup.TABLECAPTION);
        END;
        GenJnlTemplate.TESTFIELD("Force Posting Report",FALSE);
        IF GenJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text000);

        IF (NOT gBlnBackground) THEN  //**4PS.n
          IF NOT PreviewMode THEN
            IF NOT CONFIRM(Text001,FALSE) THEN
              EXIT;

        IF "Account Type" = "Account Type"::"Fixed Asset" THEN BEGIN
          FALedgEntry.SETRANGE("FA No.","Account No.");
          FALedgEntry.SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
          IF FALedgEntry.FINDFIRST AND "Depr. Acquisition Cost" THEN
            IF NOT CONFIRM(Text005,FALSE,FIELDCAPTION("Depr. Acquisition Cost")) THEN
              EXIT;
        END;

        TempJnlBatchName := "Journal Batch Name";

        GenJnlPostBatch.ReplacePostingDatePeriodic(gReplacePostingDatePeriodic);  //RFC135
        GenJnlPostBatch.NoCommitAllowed(gBlnNoCommit);  //**4PS.n
        GenJnlPostBatch.RUN(GenJnlLine);

        IF PreviewMode THEN
          EXIT;

        IF (NOT gBlnBackground) THEN BEGIN  //**4PS.n
          IF "Line No." = 0 THEN
            MESSAGE(Text002)
          ELSE
            IF TempJnlBatchName = "Journal Batch Name" THEN
              MESSAGE(Text003)
            ELSE
              MESSAGE(
                Text004,
                "Journal Batch Name");
        END;  //**4PS.n

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    PROCEDURE Preview@2(VAR GenJournalLine@1000 : Record 81);
    VAR
      GenJnlPostPreview@1002 : Codeunit 19;
    BEGIN
      PreviewMode := TRUE;
      GenJnlPostBatch.SetPreviewMode(TRUE);
      GenJnlLine.COPY(GenJournalLine);
      GenJnlPostPreview.Start;
      IF NOT Code THEN BEGIN
        GenJnlPostPreview.Finish;
        IF GETLASTERRORTEXT <> GenJnlPostPreview.GetPreviewModeErrMessage THEN
          ERROR(GETLASTERRORTEXT);
        GenJnlPostPreview.ShowAllEntries;
        ERROR('');
      END;
    END;

    PROCEDURE Background@1210190001(iBlnBackground@1210190000 : Boolean);
    BEGIN
      //**4PS
      // If true, no messages to- nor confirmations from- the user will be generated
      gBlnBackground := iBlnBackground;
    END;

    PROCEDURE NoCommitAllowed@1210190000(iBlnNoCommit@1210190000 : Boolean);
    BEGIN
      //**4PS
      // If true, no commit allowed during the process (post-batch)
      gBlnNoCommit := iBlnNoCommit;
    END;

    PROCEDURE ReplacePostingDatePeriodic@1100285100(lv_replace@1100285100 : Boolean);
    BEGIN
      //RFC135
      gReplacePostingDatePeriodic := lv_replace;
    END;

    BEGIN
    {
      4PS MVOS 20100810: Check permission Journal Template before posting.
      160923 ITERO.DL RFC135 Supplier invoices with accrual entries in a closed period should be booked in open period, passover variable to next function
    }
    END.
  }
}

