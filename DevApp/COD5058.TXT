OBJECT Codeunit 5058 BankCont-Update
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW17.00,4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      RMSetup@1000 : Record 5079;
      ContBusRel@1001 : Record 5054;
      Cont@1002 : Record 5050;
      OldCont@1003 : Record 5050;
      NoSeriesMgt@1004 : Codeunit 396;
      ContNo@1005 : Code[20];
      NoSeries@1006 : Code[10];
      SalespersonCode@1007 : Code[10];
      CentralDataMngmnt@1100485001 : Codeunit 11012010;

    PROCEDURE OnInsert@3(VAR BankAcc@1000 : Record 270);
    BEGIN
      RMSetup.GET;
      IF RMSetup."Bus. Rel. Code for Bank Accs." = '' THEN
        EXIT;

      InsertNewContact(BankAcc,TRUE);
    END;

    PROCEDURE OnModify@1(VAR BankAcc@1000 : Record 270);
    VAR
      CurrencyCode@1100528700 : Code[10];
    BEGIN
      WITH ContBusRel DO BEGIN
        SETCURRENTKEY("Link to Table","No.");
        SETRANGE("Link to Table","Link to Table"::"Bank Account");
        SETRANGE("No.",BankAcc."No.");
        IF NOT FINDFIRST THEN
          EXIT;
        Cont.GET("Contact No.");
        OldCont := Cont;
      END;

      ContNo := Cont."No.";
      NoSeries := Cont."No. Series";
      SalespersonCode := Cont."Salesperson Code";
      CurrencyCode := Cont."Currency Code"; //**4PS.n
      Cont.VALIDATE("E-Mail",BankAcc."E-Mail");
      Cont.TRANSFERFIELDS(BankAcc);
      Cont."No." := ContNo ;
      Cont."No. Series" := NoSeries;
      Cont."Salesperson Code" := SalespersonCode;
      Cont."Currency Code" := CurrencyCode; //**4PS.n
      Cont.VALIDATE(Name);
      Cont.OnModify(OldCont,TRUE);
      Cont.MODIFY(TRUE);
    END;

    PROCEDURE OnDelete@2(VAR BankAcc@1000 : Record 270);
    BEGIN
      WITH ContBusRel DO BEGIN
        SETCURRENTKEY("Link to Table","No.");
        SETRANGE("Link to Table","Link to Table"::"Bank Account");
        SETRANGE("No.",BankAcc."No.");
        DELETEALL(TRUE);
      END;
    END;

    PROCEDURE InsertNewContact@4(VAR BankAcc@1000 : Record 270;LocalCall@1001 : Boolean);
    BEGIN
      //**4PS.sn
      IF NOT CentralDataMngmnt.InsertingCompanyIsAllowed THEN
        EXIT;
      //**4PS.en

      IF NOT LocalCall THEN BEGIN
        RMSetup.GET;
        RMSetup.TESTFIELD("Bus. Rel. Code for Bank Accs.");
      END;

      WITH Cont DO BEGIN
        INIT;
        TRANSFERFIELDS(BankAcc);

        VALIDATE(Name);
        VALIDATE("E-Mail");
        "No." := '';
        "No. Series" := '';
        "Currency Code" := ''; //**4PS.n
      //**4PS.so
      //  RMSetup.TESTFIELD("Contact Nos.");
      //  NoSeriesMgt.InitSeries(RMSetup."Contact Nos.",'',0D,"No.","No. Series");
      //**4PS.eo
      //**4PS.sn
        RMSetup.TESTFIELD("Company Nos.");
        NoSeriesMgt.InitSeries(RMSetup."Company Nos.",'',0D,"No.","No. Series");
      //**4PS.en
        Type := Type::Company;
        TypeChange;
        SetSkipDefault(FALSE);
        INSERT(TRUE);
      END;

      WITH ContBusRel DO BEGIN
        INIT;
        "Contact No." := Cont."No.";
        "Business Relation Code" := RMSetup."Bus. Rel. Code for Bank Accs.";
        "Link to Table" := "Link to Table"::"Bank Account";
        "No." := BankAcc."No.";
        INSERT(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

