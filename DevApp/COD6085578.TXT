OBJECT Codeunit 6085578 Document Management (DC)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.05,4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      SupplyingCompany@1100528400 : Text[30];

    PROCEDURE GetNoOfDocForRecRef@1000000000(RecRef@1000000001 : RecordRef) : Integer;
    VAR
      Document@1000000002 : Record 6085590;
      Value@1000000003 : Record 6085593;
      RecIDMgt@161024012 : Codeunit 6085604;
      SourceRecIDTreeID@161024013 : Integer;
    BEGIN
      IF FORMAT(RecRef) = '' THEN
        EXIT;

      SourceRecIDTreeID := RecIDMgt.GetRecIDTreeID(RecRef,FALSE);
      IF SourceRecIDTreeID = 0 THEN
        EXIT;

      Document.SETCURRENTKEY("Source Record ID Tree ID");
      Document.SETRANGE("Source Record ID Tree ID",SourceRecIDTreeID);

      Value.SETCURRENTKEY("Value (Record ID Tree ID)");
      Value.SETRANGE("Value (Record ID Tree ID)",SourceRecIDTreeID);

      EXIT(Document.COUNT + Value.COUNT);
    END;

    PROCEDURE LookupDocsForRecRef@1000000001(RecRef@161024012 : RecordRef) : Integer;
    VAR
      DocListForSource@161024015 : Page 6085606;
      DocCapMgt@161024013 : Codeunit 6085603;
    BEGIN
      IF FORMAT(RecRef) = '' THEN
        EXIT;

      DocCapMgt.IsLogActive(RecRef.NUMBER,TRUE);
      DocListForSource.LoadDocuments(RecRef.RECORDID,0,0,'',0);
      DocListForSource.RUN;
    END;

    PROCEDURE GetNoOfDocForDoc@161024014(CreatedDocType@161024019 : Integer;CreatedDocSubtype@161024018 : Integer;CreatedDocID@161024017 : Code[20];CreatedDocRefNo@161024016 : Integer) : Integer;
    VAR
      Document@1000000002 : Record 6085590;
    BEGIN
      //**4PS.sn
      IF SupplyingCompany <> '' THEN
        Document.CHANGECOMPANY(SupplyingCompany);
      //**4PS.en
      Document.SETCURRENTKEY("Created Doc. Type","Created Doc. Subtype","Created Doc. ID","Created Doc. Ref. No.");
      Document.SETRANGE("Created Doc. Type",CreatedDocType);
      Document.SETRANGE("Created Doc. Subtype",CreatedDocSubtype);
      Document.SETRANGE("Created Doc. ID",CreatedDocID);
      Document.SETRANGE("Created Doc. Ref. No.",CreatedDocRefNo);
      //**4PS.so
      // Document.SETRANGE("File Type",Document."File Type"::Miscellaneous);
      //**4PS.eon
      EXIT(Document.COUNT);
    END;

    PROCEDURE LookupDocsForDoc@161024012(CreatedDocType@161024019 : Integer;CreatedDocSubtype@161024018 : Integer;CreatedDocID@161024017 : Code[20];CreatedDocRefNo@161024016 : Integer) : Integer;
    VAR
      DocListForSource@161024015 : Page 6085606;
      EmptyRecID@161024012 : RecordID;
    BEGIN
      DocListForSource.SetSupplyingCompany(SupplyingCompany); //**4PS.n
      DocListForSource.LoadDocuments(EmptyRecID,CreatedDocType,CreatedDocSubtype,CreatedDocID,CreatedDocRefNo);
      DocListForSource.RUN;
    END;

    PROCEDURE MoveGenJnlLineDocToLedgEntry@1160040000(GenJnlLine@1160040000 : Record 81;GLEntry@1160040001 : Record 17);
    VAR
      Document@1160040005 : Record 6085590;
      Document2@1160040006 : Record 6085590;
      RecIDMgt@1160040004 : Codeunit 6085604;
      RecRef@1160040002 : RecordRef;
      SourceID@1160040003 : Integer;
    BEGIN
      IF GenJnlLine."Line No." = 0 THEN
        EXIT;

      IF GenJnlLine."Journal Batch Name" = '' THEN
        EXIT;

      RecRef.GETTABLE(GenJnlLine);
      SourceID := RecIDMgt.GetRecIDTreeID(RecRef,FALSE);
      IF SourceID = 0 THEN
        EXIT;

      Document.SETCURRENTKEY("Source Record ID Tree ID");
      Document.SETRANGE("Source Record ID Tree ID",SourceID);
      IF Document.FINDSET THEN
        REPEAT
          Document2 := Document;
          Document2."Created Doc. Type" := DATABASE::"G/L Entry";
          Document2."Created Doc. ID" := GenJnlLine."Document No.";
          Document2."Source Record ID Tree ID" := 0;
          Document2.MODIFY;
        UNTIL Document.NEXT = 0;
    END;

    PROCEDURE SetSupplyingCompany@1100528400(SupplyingCompany2@1100528400 : Text[30]);
    BEGIN
      //**4PS
      SupplyingCompany := SupplyingCompany2;
    END;

    BEGIN
    {
      4PS, (DC) added to object name
    }
    END.
  }
}

