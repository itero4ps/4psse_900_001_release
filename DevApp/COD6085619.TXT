OBJECT Codeunit 6085619 Export Continia Users
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.05;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            Code;
            SendWelcomeEmail;
          END;

  }
  CODE
  {
    VAR
      Text000@1160040020 : TextConst 'ENU=Welcome to Continia Online';
      Text001@1160040012 : TextConst 'ENU=Dear %1';
      Text002@1160040013 : TextConst 'ENU=You have now been created as a user for Continia Document Capture. Before you get started please use the link below to activate your user account.';
      Text003@1160040014 : TextConst 'ENU=Account Details';
      Text004@1160040015 : TextConst 'ENU=Username: %1';
      Text005@1160040016 : TextConst 'ENU=Password:';
      Text006@1160040018 : TextConst 'ENU=Click here to specify and activate your account';
      Text007@1160040017 : TextConst 'ENU=When your account has been activated you can access Continia Online here: %1';
      Text008@1160040019 : TextConst 'ENU=Yours sincerely,';
      Text009@1160040010 : TextConst 'ENU=%1 users exported.';
      Text010@1160040001 : TextConst 'ENU=Exporting Users...\Step   #1#######################\Status @2@@@@@@@@@@@@@@@@@@@@@@@';
      Text011@1160040006 : TextConst 'ENU=Searching for active companies';
      Text012@1160040005 : TextConst 'ENU=Searching for active users';
      Text013@1160040004 : TextConst 'ENU=Exporting Expense Management users';
      Text014@1160040003 : TextConst 'ENU=Exporting Web Approval Users';
      Text015@1160040002 : TextConst 'ENU=No users were exported';
      Text016@1160040000 : TextConst 'ENU=%1 in %2 is missing for company %3.';
      COMgt@1160040007 : Codeunit 6086500;
      XMLLib@1160040011 : Codeunit 6085573;
      HttpActions@1160040009 : 'PUT,DELETE,GET';
      Protocol@1160040008 : 'IMAP,ContiniaMail';
      Text017@1160040021 : TextConst 'ENU=You have now been created as a user for Continia Online.';
      Text018@1160040022 : TextConst 'ENU=Your Windows password';
      Text019@1160040023 : TextConst 'ENU=Your account has already been activated and you can access Continia Online here: %1';
      Text020@1160040024 : TextConst 'ENU=Sending Welcome E-mails...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@';

    LOCAL PROCEDURE Code@1160040037() : Boolean;
    VAR
      Company@1160040012 : Record 2000000006;
      ContiniaUser@1160040011 : Record 6086001;
      ContiniaUserSetup@1160040014 : Record 6086002;
      COSetup@1160040017 : Record 6086500;
      AboutDC@1160040015 : Codeunit 6085589;
      CODC@1160040000 : Codeunit 6085617;
      CEM@1160040010 : Codeunit 6085782;
      CloudXmlDoc@1160040021 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      OnPremXmlDoc@1160040022 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      CloudUsersNode@1160040019 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      OnPremUsersNode@1160040008 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      CloudDatabaseNode@1160040020 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      OnPremDatabaseNode@1160040007 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      CloudWindowsNode@1160040023 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      OnPremWindowsNode@1160040006 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      CloudUserNode@1160040024 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      OnPremUserNode@1160040005 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      CloudCompaniesNode@1160040025 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      OnPremCompaniesNode@1160040016 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      Window@1160040013 : Dialog;
      TotalCount@1160040028 : Integer;
      RecCount@1160040001 : Integer;
      ExportCount@1160040029 : Integer;
      UseCloudApproval@1160040004 : Boolean;
      ExportOnPrem@1160040002 : Boolean;
      ExportCloud@1160040003 : Boolean;
      DCBuildVersion@1160040009 : Text[30];
      CEMBuildVersion@1160040018 : Text[30];
    BEGIN
      IF GUIALLOWED THEN BEGIN
        Window.OPEN(Text010);
        Window.UPDATE(1,Text011);
        TotalCount := Company.COUNT;
      END;

      Company.FINDSET;
      REPEAT
        IF GUIALLOWED THEN BEGIN
          RecCount += 1;
          Window.UPDATE(2,ROUND(RecCount / TotalCount * 10000,1,'>'));
        END;

        IF COSetup.ValidateSetups(Company.Name) THEN BEGIN
          CheckCompanySetup(Company.Name);
          Company.MARK(TRUE);
        END;
      UNTIL Company.NEXT = 0;

      Company.MARKEDONLY(TRUE);
      IF Company.COUNT > 0 THEN BEGIN
        DCBuildVersion := AboutDC.BuildVersion;
        CEMBuildVersion := CEM.CEMBuildVersion;

        IF ContiniaUser.FINDSET THEN BEGIN
          IF GUIALLOWED THEN BEGIN
            Window.UPDATE(1,Text012);
            RecCount := 0;
            TotalCount := ContiniaUser.COUNT;
          END;

          COSetup.GET;
          UseCloudApproval := COSetup."Enable Web Approval" AND COSetup."Use Continia Online Web Portal";
          ExportCloud := UseCloudApproval OR CEM.IsCEMEnabled(FALSE);
          ExportOnPrem := NOT UseCloudApproval;
          REPEAT
            IF GUIALLOWED THEN BEGIN
              RecCount += 1;
              Window.UPDATE(2,ROUND(RecCount / TotalCount * 10000,1,'>'));
            END;

            CLEAR(CloudUserNode);
            CLEAR(OnPremUserNode);
            IF Company.FINDSET THEN
              REPEAT
                ContiniaUserSetup.CHANGECOMPANY(Company.Name);
                IF ContiniaUserSetup.GET(ContiniaUser."User ID") THEN BEGIN
                  IF ContiniaUserSetup."Expense Management User" OR ContiniaUserSetup."Web Approval User" THEN BEGIN
                    IF ExportCloud THEN BEGIN
                      IF ISNULL(CloudXmlDoc) THEN
                        CreateUsersXml(COSetup,DCBuildVersion,CEMBuildVersion,
                          CloudXmlDoc,CloudUsersNode,CloudWindowsNode,CloudDatabaseNode);

                      IF ISNULL(CloudUserNode) THEN
                        IF ContiniaUser."NAV Login Type" = ContiniaUser."NAV Login Type"::Windows THEN
                          CreateUserXml(CloudWindowsNode,COSetup,ContiniaUser,CloudUserNode,CloudCompaniesNode)
                        ELSE
                          CreateUserXml(CloudDatabaseNode,COSetup,ContiniaUser,CloudUserNode,CloudCompaniesNode);

                      CreateCompanyXml(CloudCompaniesNode,Company,
                        ContiniaUserSetup."Expense Management User",ContiniaUserSetup."Web Approval User" AND UseCloudApproval);
                    END;

                    // Export users to OnPrem Web Approval if needed
                    IF ExportOnPrem AND ContiniaUserSetup."Web Approval User" THEN BEGIN
                      IF ISNULL(OnPremXmlDoc) THEN
                        CreateUsersXml(COSetup,DCBuildVersion,CEMBuildVersion,
                          OnPremXmlDoc,OnPremUsersNode,OnPremWindowsNode,OnPremDatabaseNode);

                      IF ISNULL(OnPremUserNode) THEN
                        IF ContiniaUser."NAV Login Type" = ContiniaUser."NAV Login Type"::Windows THEN
                          CreateUserXml(OnPremWindowsNode,COSetup,ContiniaUser,OnPremUserNode,OnPremCompaniesNode)
                        ELSE
                          CreateUserXml(OnPremDatabaseNode,COSetup,ContiniaUser,OnPremUserNode,OnPremCompaniesNode);

                      CreateCompanyXml(OnPremCompaniesNode,Company,FALSE,ContiniaUserSetup."Web Approval User");
                    END;
                  END;
                END;
              UNTIL Company.NEXT = 0;

            IF NOT (ISNULL(OnPremUserNode) AND ISNULL(CloudUserNode)) THEN
              ExportCount += 1;
          UNTIL ContiniaUser.NEXT = 0;

          IF NOT ISNULL(CloudXmlDoc) THEN BEGIN
            IF GUIALLOWED THEN
              Window.UPDATE(1,Text013);
            COMgt.ExecuteRequest(CODC.GetCOUserUrl,HttpActions::PUT,TRUE,CloudXmlDoc);
          END;

          IF NOT ISNULL(OnPremXmlDoc) THEN BEGIN
            IF GUIALLOWED THEN
              Window.UPDATE(1,Text014);
            COMgt.ExecuteRequest(CODC.GetUserUrl,HttpActions::PUT,TRUE,OnPremXmlDoc);
            SaveActivateLinks(OnPremXmlDoc);
          END;
        END;
      END;

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ExportCount = 0 THEN
        MESSAGE(Text015)
      ELSE
        MESSAGE(Text009,ExportCount);
    END;

    LOCAL PROCEDURE CheckCompanySetup@1160040057(_CompanyName@1160040000 : Text[30]);
    VAR
      COSetup@1160040003 : Record 6086500;
      ContiniaUser@1160040005 : Record 6086001;
      ContiniaUserSetup@1160040002 : Record 6086002;
      EmptyGUID@1160040004 : GUID;
      Stop@1160040006 : Boolean;
    BEGIN
      ContiniaUserSetup.CHANGECOMPANY(_CompanyName);
      ContiniaUserSetup.SETRANGE("Web Approval User",TRUE);
      IF ContiniaUserSetup.ISEMPTY THEN
        EXIT;

      COSetup.CHANGECOMPANY(_CompanyName);
      COSetup.GET;

      IF COSetup."Company GUID" = EmptyGUID THEN
        ERROR(Text016,COSetup.FIELDCAPTION("Company GUID"),COSetup.TABLECAPTION,_CompanyName);

      ContiniaUserSetup.FINDFIRST;
      REPEAT
        ContiniaUser.GET(ContiniaUserSetup."Continia User ID");
        IF ContiniaUser."NAV Login Type" = ContiniaUser."NAV Login Type"::Windows THEN BEGIN
          IF COSetup."Windows Web Service URL" = '' THEN
            ERROR(Text016,COSetup.FIELDCAPTION("Windows Web Service URL"),COSetup.TABLECAPTION,_CompanyName);

          IF COSetup."Local Domain Name" = '' THEN
            ERROR(Text016,COSetup.FIELDCAPTION("Local Domain Name"),COSetup.TABLECAPTION,_CompanyName);

          Stop := TRUE;
        END;
      UNTIL (ContiniaUserSetup.NEXT = 0) OR Stop;


      Stop := FALSE;
      ContiniaUserSetup.FINDFIRST;
      REPEAT
        ContiniaUser.GET(ContiniaUserSetup."Continia User ID");
        IF ContiniaUser."NAV Login Type" = ContiniaUser."NAV Login Type"::Database THEN BEGIN
          IF COSetup."Database Web Service URL" = '' THEN
            ERROR(Text016,COSetup.FIELDCAPTION("Database Web Service URL"),COSetup.TABLECAPTION,_CompanyName);
          Stop := TRUE;
        END;
      UNTIL (ContiniaUserSetup.NEXT = 0) OR Stop;
    END;

    LOCAL PROCEDURE CreateUsersXml@1160040042(COSetup@1160040002 : Record 6086500;DCBuildVersion@1160040003 : Text[30];CEMBuildVersion@1160040006 : Text[30];VAR XmlDoc@1160040001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";VAR UsersNode@1160040000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";VAR WindowsNode@1160040004 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";VAR DatabaseNode@1160040005 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    BEGIN
      XmlDoc := XmlDoc.XmlDocument;
      UsersNode := XmlDoc.CreateNode('element','Users','');
      XMLLib.SetXmlNodeAttr(UsersNode,'Version','2');
      XmlDoc.AppendChild(UsersNode);

      XMLLib.CreateXmlNode(UsersNode,'DatabaseLogins','',DatabaseNode);
      XMLLib.SetXmlNodeAttr(DatabaseNode,'WebServiceUrl',COSetup."Database Web Service URL");
      XMLLib.SetXmlNodeAttr(DatabaseNode,'Tenant',COSetup.Tenant);
      XMLLib.SetXmlNodeAttr(DatabaseNode,'DCVersion',DCBuildVersion);
      XMLLib.SetXmlNodeAttr(DatabaseNode,'EMVersion',CEMBuildVersion);

      XMLLib.CreateXmlNode(UsersNode,'WindowsLogins','',WindowsNode);
      XMLLib.SetXmlNodeAttr(WindowsNode,'WebServiceUrl',COSetup."Windows Web Service URL");
      XMLLib.SetXmlNodeAttr(WindowsNode,'Tenant',COSetup.Tenant);
      XMLLib.SetXmlNodeAttr(WindowsNode,'DCVersion',DCBuildVersion);
      XMLLib.SetXmlNodeAttr(WindowsNode,'EMVersion',CEMBuildVersion);
    END;

    LOCAL PROCEDURE CreateUserXml@1160040047(VAR ParentNode@1160040001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";COSetup@1160040004 : Record 6086500;ContiniaUser@1160040003 : Record 6086001;VAR UserNode@1160040000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";VAR CompaniesNode@1160040002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode") : Boolean;
    VAR
      WebServiceKey@1160040005 : Text[250];
    BEGIN
      IF ContiniaUser."E-Mail" = '' THEN
        EXIT;

      XMLLib.CreateXmlNode(ParentNode,'User','',UserNode);
      XMLLib.CreateXmlNode(UserNode,'Companies','',CompaniesNode);

      IF ContiniaUser."NAV Login Type" = ContiniaUser."NAV Login Type"::Windows THEN BEGIN
        IF STRPOS(ContiniaUser."User ID",'\') > 0 THEN
          XMLLib.SetXmlNodeAttr(UserNode,'WindowsUsername',ContiniaUser."User ID")
        ELSE
          XMLLib.SetXmlNodeAttr(UserNode,'WindowsUsername',COSetup."Local Domain Name" + '\' + ContiniaUser."User ID");
      END ELSE BEGIN
        WebServiceKey := COMgt.GetUserWebServiceKey(ContiniaUser."User ID");
        XMLLib.SetXmlNodeAttr(UserNode,'WebServiceKey',WebServiceKey);
      END;

      XMLLib.SetXmlNodeAttr(UserNode,'Email',ContiniaUser."E-Mail");
      XMLLib.SetXmlNodeAttr(UserNode,'Username',ContiniaUser."User ID");
      XMLLib.SetXmlNodeAttr(UserNode,'FullName',ContiniaUser.GetName);
    END;

    LOCAL PROCEDURE CreateCompanyXml@1160040048(VAR CompaniesNode@1160040000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";Company@1160040002 : Record 2000000006;IsEMUser@1160040004 : Boolean;IsWAUser@1160040003 : Boolean) : Boolean;
    VAR
      CompanyNode@1160040001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      COSetup@1160040005 : Record 6086500;
    BEGIN
      COSetup.CHANGECOMPANY(Company.Name);
      COSetup.GET;

      XMLLib.CreateXmlNode(CompaniesNode,'Company','',CompanyNode);
      XMLLib.SetXmlNodeAttr(CompanyNode,'Name',Company.Name);
      XMLLib.SetXmlNodeAttr(CompanyNode,'Code',GetCompanyCode(Company));
      XMLLib.SetXmlNodeAttr(CompanyNode,'Guid',COSetup."Company GUID");
      IF IsWAUser THEN
        XMLLib.SetXmlNodeAttr(CompanyNode,'WAUser',XMLLib.Bool2Text(IsWAUser));
      IF IsEMUser THEN
        XMLLib.SetXmlNodeAttr(CompanyNode,'EMUser',XMLLib.Bool2Text(IsEMUser));
    END;

    PROCEDURE GetCompanyCode@1160040001(Company@1160040004 : Record 2000000006) : Code[10];
    VAR
      DCSetup@1160040000 : Record 6085573;
      Obj@1160040003 : Record 2000000001;
      RecRef@1160040001 : RecordRef;
      FieldRef@1160040002 : FieldRef;
    BEGIN
      DCSetup.CHANGECOMPANY(Company.Name);
      IF DCSetup.GET THEN
        EXIT(DCSetup."Company Code");

      IF NOT Obj.GET(Obj.Type::Table,'',6086300) THEN
        EXIT;

      RecRef.OPEN(6086300,FALSE,Company.Name);
      IF NOT RecRef.FINDFIRST THEN
        EXIT;

      FieldRef := RecRef.FIELD(2);
      EXIT(FieldRef.VALUE);
    END;

    PROCEDURE SaveActivateLinks@1160040000(VAR XmlDoc@1160040001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument");
    VAR
      ContiniaUser@1160040000 : Record 6086001;
      XMLNodeList@1160040004 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
      ActivationLink@1160040006 : Text[1024];
      Username@1160040007 : Text[1024];
      i@1160040005 : Integer;
    BEGIN
      XMLNodeList := XmlDoc.DocumentElement.SelectNodes('User');

      FOR i := 0 TO XMLNodeList.Count - 1 DO BEGIN
        COMgt.GetAttribAsText(XMLNodeList,'Link',i,ActivationLink);
        COMgt.GetAttribAsText(XMLNodeList,'Username',i,Username);

        IF ContiniaUser.GET(Username) THEN BEGIN
          ContiniaUser."Activation Link" := ActivationLink;
          ContiniaUser.MODIFY(TRUE);
        END;
      END;
    END;

    PROCEDURE SendWelcomeEmail@1160040002();
    VAR
      COSetup@1160040002 : Record 6086500;
      ContiniaUser@1160040001 : Record 6086001;
      ContiniaUser2@1160040004 : Record 6086001;
      SmtpMailMgt@1160040000 : Codeunit 6085598;
      ContiniaUserMgt@1160040007 : Codeunit 6085781;
      Window@1160040005 : Dialog;
      RecCount@1160040003 : Integer;
      i@1160040006 : Integer;
    BEGIN
      COSetup.GET;

      ContiniaUser.SETCURRENTKEY("Send Welcome E-mail");
      ContiniaUser.SETRANGE("Send Welcome E-mail",TRUE);
      IF ContiniaUser.FINDSET THEN BEGIN
        IF GUIALLOWED THEN BEGIN
          Window.OPEN(Text020);
          RecCount := ContiniaUser.COUNT;
        END;

        REPEAT
          IF GUIALLOWED THEN BEGIN
            i := i + 1;
            Window.UPDATE(1,CalcProgress(RecCount,i));
          END;

          SmtpMailMgt.AddTo(ContiniaUser.Name,ContiniaUser."E-Mail");
          SmtpMailMgt.SetSubject(Text000);

          SmtpMailMgt.AppendToBody(STRSUBSTNO('<b>%1</b>',STRSUBSTNO(Text001,ContiniaUser.Name)),TRUE);
          SmtpMailMgt.AppendLineBrakToBody;

          IF ContiniaUser."NAV Login Type" = ContiniaUser."NAV Login Type"::Windows THEN BEGIN
            SmtpMailMgt.AppendToBody(Text017,TRUE);
            SmtpMailMgt.AppendLineBrakToBody;
            SmtpMailMgt.AppendToBody(STRSUBSTNO('<b>%1</b>',Text003),TRUE);
            IF COSetup."Use Continia Online Web Portal" THEN
              SmtpMailMgt.AppendToBody(STRSUBSTNO(Text004,ContiniaUser."E-Mail"),TRUE)
            ELSE
              SmtpMailMgt.AppendToBody(STRSUBSTNO(Text004,ContiniaUserMgt.GetNAVUserID(ContiniaUser."User ID")),TRUE);
            SmtpMailMgt.AppendToBody(STRSUBSTNO('%1 %2',Text005,Text018),TRUE);
            SmtpMailMgt.AppendLineBrakToBody;
            SmtpMailMgt.AppendToBody(STRSUBSTNO(Text019,COSetup."Web Site Url"),TRUE);
          END ELSE BEGIN
            SmtpMailMgt.AppendToBody(Text002,TRUE);
            SmtpMailMgt.AppendLineBrakToBody;
            SmtpMailMgt.AppendToBody(STRSUBSTNO('<b>%1</b>',Text003),TRUE);
            IF COSetup."Use Continia Online Web Portal" THEN
              SmtpMailMgt.AppendToBody(STRSUBSTNO(Text004,ContiniaUser."E-Mail"),TRUE)
            ELSE
              SmtpMailMgt.AppendToBody(STRSUBSTNO(Text004,ContiniaUserMgt.GetNAVUserID(ContiniaUser."User ID")),TRUE);
            SmtpMailMgt.AppendToBody(STRSUBSTNO('%1<a href=%3>%2</a>',Text005,Text006,ContiniaUser."Activation Link"),TRUE);
            SmtpMailMgt.AppendLineBrakToBody;
            SmtpMailMgt.AppendToBody(STRSUBSTNO(Text007,COSetup."Web Site Url"),TRUE);
          END;

          SmtpMailMgt.AppendLineBrakToBody;
          SmtpMailMgt.AppendToBody(Text008,TRUE);
          SmtpMailMgt.AppendToBody('Continia Software A/S',TRUE);

          IF NOT SmtpMailMgt.Send THEN
            SmtpMailMgt.DisplayLastError;

          CLEAR(SmtpMailMgt);

          ContiniaUser2.GET(ContiniaUser."User ID");
          ContiniaUser2."Send Welcome E-mail" := FALSE;
          ContiniaUser2.MODIFY(TRUE);
          COMMIT;
        UNTIL ContiniaUser.NEXT = 0;

        IF GUIALLOWED THEN
          Window.CLOSE;
      END;
    END;

    PROCEDURE CalcProgress@161024017(VAR TotalCount@161024012 : Integer;VAR Index@161024013 : Integer) : Integer;
    BEGIN
      EXIT(ROUND(Index / TotalCount * 10000,1,'>'));
    END;

    BEGIN
    END.
  }
}

