OBJECT Codeunit 6085701 Doc. - Register (Log Interact)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.3.50,4PS9.00;
  }
  PROPERTIES
  {
    TableNo=6085590;
    OnRun=VAR
            Cont@1000000008 : Record 5050;
            Template@1000000000 : Record 6085579;
            TempSegLine@1000000001 : TEMPORARY Record 11072040;
            Attachment@1000000002 : Record 11072030;
            InteractLogEntry@1000000007 : Record 11072027;
            Field@1000000010 : Record 6085580;
            MarketingSetup@161024013 : Record 5079;
            Setup@161024012 : Record 6085573;
            CaptureMgnt@1000000009 : Codeunit 6085576;
            SegManagement@1000000005 : Codeunit 5051;
            AttachmentNo@1000000003 : Integer;
            NextInteractLogEntryNo@1000000004 : Integer;
          BEGIN
            Template.GET(Rec."Template No.");
            IF Template."Codeunit ID: Doc. Validation" <> 0 THEN BEGIN
              CODEUNIT.RUN(Template."Codeunit ID: Doc. Validation",Rec);
              COMMIT;
            END;

            TESTFIELD(Rec.OK);

            IF Rec.Description = '' THEN
              Rec.Description := COPYSTR(CaptureMgnt.GetText(Rec,Field.Type::Header,Text001,0),1,MAXSTRLEN(Rec.Description));
            IF Rec.Description = '' THEN
              Rec.Description := Template.Description;

            MarketingSetup.GET;
            Setup.GET;

            Attachment.LOCKTABLE;
            IF Attachment.FINDLAST THEN
              AttachmentNo := Attachment."No." + 1
            ELSE
              AttachmentNo := 1;
            Attachment.INIT;
            Attachment."No." := AttachmentNo;

            IF MarketingSetup."Attachment Storage Type" = MarketingSetup."Attachment Storage Type"::Embedded THEN BEGIN
              Rec.CALCFIELDS("PDF File");
              IF Rec."PDF File".HASVALUE THEN
                Attachment.Attachment := Rec."PDF File"
              ELSE
                IF FILE.EXISTS(Setup."PDF File Path" + '\' + Rec."No." + '.' + Text002) THEN
                  Attachment.Attachment.IMPORT(Setup."PDF File Path" + '\' + Rec."No." + '.' + Text002);
            END ELSE BEGIN
              IF NOT FILE.EXISTS(MarketingSetup."Attachment Storage Location" + '\' + Rec."No.") THEN BEGIN
                Rec.CALCFIELDS("PDF File");
                IF Rec."PDF File".HASVALUE THEN BEGIN
                  Attachment.Attachment.EXPORT(MarketingSetup."Attachment Storage Location" + '\' + Rec."No.");
                  Attachment."Storage Type" := Attachment."Storage Type"::"Disk File";
                  Attachment."Storage Pointer" := MarketingSetup."Attachment Storage Location";
                END ELSE
                  IF FILE.EXISTS(Setup."PDF File Path" + '\' + Rec."No." + '.' + Text002) THEN BEGIN
                    FILE.COPY(Setup."PDF File Path" + '\' + Rec."No." + '.' + Text002,
                      MarketingSetup."Attachment Storage Location" + '\' + FORMAT(Attachment."No."));
                    Attachment."Storage Type" := Attachment."Storage Type"::"Disk File";
                    Attachment."Storage Pointer" := MarketingSetup."Attachment Storage Location";
                  END;
                END;
            END;

            Attachment."File Extension" := Text002;
            Attachment."Read Only" := TRUE;
            Attachment.INSERT;


            Cont.GET(GetSourceID);
            TempSegLine."Contact No." := Cont."No.";
            TempSegLine."Contact Company No." := Cont."Company No.";
            TempSegLine.INSERT;
            TempSegLine.VALIDATE("Interaction Template Code",Template."Interaction Template Code");
            TempSegLine."Salesperson Code" := Cont."Salesperson Code";
            TempSegLine.Description := COPYSTR(Rec.Description,1,MAXSTRLEN(TempSegLine.Description));
            TempSegLine.Subject := COPYSTR(Rec.Description,1,MAXSTRLEN(TempSegLine.Description));
            TempSegLine.Date := CaptureMgnt.GetDate(Rec,Field.Type::Header,Text004,0);
            TempSegLine."Time of Interaction" := TIME;
            TempSegLine."Attachment No." := Attachment."No.";

            InteractLogEntry.LOCKTABLE;
            IF InteractLogEntry.FINDLAST THEN
              NextInteractLogEntryNo := InteractLogEntry."Entry No." + 1
            ELSE
              NextInteractLogEntryNo := 1;
            InteractLogEntry.INIT;
            InteractLogEntry."Entry No." := NextInteractLogEntryNo;
            SegManagement.CopyFieldsToInteractLogEntry(InteractLogEntry,TempSegLine);
            InteractLogEntry."Attachment No." := Attachment."No.";
            InteractLogEntry.INSERT;

            IF "Show after Register" THEN BEGIN
              InteractLogEntry.RESET;
              InteractLogEntry.SETCURRENTKEY("Contact Company No.",Date,"Contact No.",Canceled);
              InteractLogEntry.SETRANGE("Contact Company No.",Cont."Company No.");
              InteractLogEntry.SETRANGE("Contact No.",Cont."No.");
              InteractLogEntry.GET(NextInteractLogEntryNo);
              PAGE.RUN(PAGE::"Interaction Log Entries",InteractLogEntry);
            END;

            Rec.Status := Rec.Status::Registered;
            Rec.MODIFY;


            IF Template."Codeunit ID: After Step 1" <> 0 THEN
              CODEUNIT.RUN(Template."Codeunit ID: After Step 1",Rec);

            IF Template."Codeunit ID: After Step 2" <> 0 THEN
              CODEUNIT.RUN(Template."Codeunit ID: After Step 2",Rec);
          END;

  }
  CODE
  {
    VAR
      Text001@1000000000 : TextConst 'ENU=DESCRIPTION';
      Text002@1000000001 : TextConst 'ENU=PDF';
      Text003@1000000002 : TextConst 'ENU=TIFF';
      Text004@6085573 : TextConst 'ENU=RECEIPTDATE';

    BEGIN
    {
      4PS, 4PS specific due to renumbered objects
    }
    END.
  }
}

