OBJECT Codeunit 6085746 Advanced Appvl. Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.4.04,4PS9.00;
  }
  PROPERTIES
  {
    Permissions=TableData 6085744=rimd,
                TableData 6085750=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      PurchHeader@1000000000 : Record 38;
      AppvlGroup@1000000002 : Record 6085743;
      Text004@161024012 : TextConst 'ENU=No %1 exists which fullfil the criterias. Please setup a new %1.';
      Text005@161024016 : TextConst 'ENU=No approver found';
      Text006@161024017 : TextConst 'ENU=Line %1 has not been approved. Please make sure that one or more users can approve amount and dimension combination of this line.';
      Text007@1000000003 : TextConst 'ENU=Dimension %1 must be specified on all purchase lines';
      Text008@161024018 : TextConst 'ENU=%1 must be specified on %2: %3.';
      DimMgt@6085573 : Codeunit 408;
      PurchInvHeader@1100528201 : Record 122;
      PurchCrMemoHdr@1100528200 : Record 124;

    PROCEDURE SubmitForApproval@161024013(PurchHeader@1002 : Record 38);
    VAR
      ApprovalEntry@1003 : Record 454;
      DtldAppvlEntry@161024021 : Record 6085744;
      UserSetup@161024013 : Record 91;
      RecRef@1001 : RecordRef;
      ApprovalsBridge@1000 : Codeunit 6085790;
      DCApprovalsMgmt@1004 : Codeunit 6085722;
      ActionToPerform@161024017 : 'ApproveLines,FindNextApprover';
      NextApproverID@161024016 : Code[50];
      ApprovalAmount@161024015 : Decimal;
      ApprovalAmountLCY@161024014 : Decimal;
    BEGIN
      SetPurchHeader(PurchHeader);

      DeleteAppvlEntries(DATABASE::"Purchase Header",PurchHeader."Document Type",PurchHeader."No.");
      DeleteAppvlEntries(DATABASE::"Purchase Line",PurchHeader."Document Type",PurchHeader."No.");

      FindAndSetAppvlGroup;

      SyncDtldAppvlEntries;

      IF AppvlGroup."First Entry Created by" = AppvlGroup."First Entry Created by"::"Approver ID" THEN
        UpdateDtldAppvlEntries(ActionToPerform::FindNextApprover,NextApproverID)
      ELSE BEGIN
        UserSetup.SETRANGE("Salespers./Purch. Code",PurchHeader."Purchaser Code");
        UserSetup.FINDFIRST;

        DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purchase Line");
        DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type");
        DtldAppvlEntry.SETRANGE("Document No.",PurchHeader."No.");
        DtldAppvlEntry.MODIFYALL("User ID",UserSetup."User ID");

        NextApproverID := UserSetup."User ID";
      END;

      IF NextApproverID <> '' THEN BEGIN
        UserSetup.GET(NextApproverID);
        RecRef.GETTABLE(PurchHeader);
        ApprovalsBridge.InitializeApprovalEntry(RecRef,ApprovalEntry);
        DCApprovalsMgmt.MakeApprovalEntry(ApprovalEntry,0,NextApproverID);
      END ELSE
        ERROR(Text005);
    END;

    PROCEDURE ApproveApprovalRequest@161024012(ApprovalEntry@1003 : Record 454;_UserID@1000 : Code[50]);
    VAR
      ApprovalEntry2@161024014 : Record 454;
      UserSetup@161024019 : Record 91;
      ApprovalSharing@161024023 : Record 6085749;
      DCApprovalMgmt@1006 : Codeunit 6085722;
      ApprovalsBridge@161024022 : Codeunit 6085790;
      WebSearchMgnt@161024021 : Codeunit 6085748;
      PurchRecRef@1005 : RecordRef;
      NextApproverID@1000000000 : Code[50];
      ActionToPerform@161024013 : 'Approve,FindNextApprover';
      ApprovalAmount@161024017 : Decimal;
      ApprovalAmountLCY@161024016 : Decimal;
      Approved@161024024 : Boolean;
    BEGIN
      // *********************************************************************************************************************************
      // This function is called when an approver approves an approval request
      // *********************************************************************************************************************************
      PurchHeader.GET(ApprovalEntry."Document Type",ApprovalEntry."Document No.");
      SetPurchHeader(PurchHeader);

      FindAndSetAppvlGroup;

      SyncDtldAppvlEntries;

      IF _UserID = '' THEN
        _UserID := USERID;

      IF ApprovalEntry."Approver ID" <> _UserID THEN BEGIN
        DCApprovalMgmt.FilterApprovalSharingToUser(ApprovalSharing,_UserID);
        ApprovalSharing.SETRANGE("Owner User ID",ApprovalEntry."Approver ID");
        ApprovalSharing.SETRANGE("Use Owners Limts & Permissions",TRUE);
        IF ApprovalSharing.FINDFIRST THEN BEGIN
          UpdateDtldAppvlEntries(ActionToPerform::Approve,ApprovalEntry."Approver ID");
          Approved := TRUE;
        END ELSE
          UpdateDtldAppvlEntries(ActionToPerform::FindNextApprover,_UserID);
      END;

      IF NOT Approved THEN
        UpdateDtldAppvlEntries(ActionToPerform::Approve,_UserID);

      // If the next approver have already been created in the Approval Entry table, then keep this person as the next approver
      ApprovalEntry2.SETRANGE("Table ID",ApprovalEntry."Table ID");
      ApprovalEntry2.SETRANGE("Document Type",ApprovalEntry."Document Type");
      ApprovalEntry2.SETRANGE("Document No.",ApprovalEntry."Document No.");
      ApprovalEntry2.SETRANGE(Status,ApprovalEntry2.Status::Created,ApprovalEntry2.Status::Open);
      IF ApprovalEntry2.FINDFIRST THEN BEGIN
        UpdateDtldAppvlEntries(ActionToPerform::FindNextApprover,ApprovalEntry2."Approver ID");
        EXIT;
      END;

      UpdateDtldAppvlEntries(ActionToPerform::FindNextApprover,NextApproverID);

      IF NextApproverID <> '' THEN BEGIN
        UserSetup.GET(NextApproverID);

        PurchRecRef.GETTABLE(PurchHeader);
        ApprovalsBridge.InitializeApprovalEntry(PurchRecRef,ApprovalEntry);
        DCApprovalMgmt.MakeApprovalEntry(ApprovalEntry,0,NextApproverID);

        IF AppvlGroup."Four Eyes Approval" = AppvlGroup."Four Eyes Approval"::Invoice THEN
          UpdateDocDtldAppvlEntries(ActionToPerform::Approve,_UserID);

        WebSearchMgnt.UpdateWebSearchFromPurchHeader(PurchHeader,FALSE);
      END ELSE BEGIN
        CheckMandatoryDim;

        // All lines have been approved. Now the whole document must be approved.
        UpdateDocDtldAppvlEntries(ActionToPerform::Approve,_UserID);

        IF NOT UpdateDocDtldAppvlEntries(ActionToPerform::FindNextApprover,NextApproverID) THEN
          IF NextApproverID = '' THEN
            ERROR(Text005);

        IF NextApproverID <> '' THEN BEGIN
          UserSetup.GET(NextApproverID);
          PurchRecRef.GETTABLE(PurchHeader);
          ApprovalsBridge.InitializeApprovalEntry(PurchRecRef,ApprovalEntry);
          DCApprovalMgmt.MakeApprovalEntry(ApprovalEntry,0,NextApproverID);
        END ELSE BEGIN
          ApprovalsBridge.AdvancedApprovalFlowComplete(ApprovalEntry);
        END;
      END;
    END;

    PROCEDURE CalcNextApprover@161024017(ApprovalEntry@161024012 : Record 454;CurrUserId@1000000002 : Code[50]) NextApproverID : Code[50];
    VAR
      ApprovalEntry2@161024014 : Record 454;
      ApprovalSharing@161024023 : Record 6085749;
      DtldAppvlEntry@161024019 : Record 6085744;
      DtldAppvlEntryDim@161024017 : Record 6085750;
      TempDtldAppvlEntry@161024016 : TEMPORARY Record 6085744;
      TempDtldAppvlEntryDim@161024015 : TEMPORARY Record 6085750;
      DCAppMgnt@161024022 : Codeunit 6085722;
      ActionToPerform@161024013 : 'Approve,FindNextApprover';
      Approved@161024024 : Boolean;
    BEGIN
      // *********************************************************************************************************************************
      // This function returns who the next approver will be if the current approver approves the document as it is
      // *********************************************************************************************************************************

      PurchHeader.GET(ApprovalEntry."Document Type",ApprovalEntry."Document No.");
      SetPurchHeader(PurchHeader);

      FindAndSetAppvlGroup;

      DtldAppvlEntry.SETFILTER("Table ID",'%1|%2',DATABASE::"Purchase Header",DATABASE::"Purchase Line");
      DtldAppvlEntry.SETRANGE("Document Type",ApprovalEntry."Document Type");
      DtldAppvlEntry.SETRANGE("Document No.",ApprovalEntry."Document No.");
      IF DtldAppvlEntry.FINDSET THEN
        REPEAT
          TempDtldAppvlEntry := DtldAppvlEntry;
          TempDtldAppvlEntry.INSERT;
        UNTIL DtldAppvlEntry.NEXT = 0;

      DtldAppvlEntryDim.SETFILTER("Table ID",'%1|%2',DATABASE::"Purchase Header",DATABASE::"Purchase Line");
      DtldAppvlEntryDim.SETRANGE("Document Type",ApprovalEntry."Document Type");
      DtldAppvlEntryDim.SETRANGE("Document No.",ApprovalEntry."Document No.");
      IF DtldAppvlEntryDim.FINDSET THEN
        REPEAT
          TempDtldAppvlEntryDim := DtldAppvlEntryDim;
          TempDtldAppvlEntryDim.INSERT;
        UNTIL DtldAppvlEntryDim.NEXT = 0;


      SyncDtldAppvlEntries;

      IF CurrUserId = '' THEN
        CurrUserId := USERID;

      IF ApprovalEntry."Approver ID" <> CurrUserId THEN BEGIN
        DCAppMgnt.FilterApprovalSharingToUser(ApprovalSharing,CurrUserId);
        ApprovalSharing.SETRANGE("Owner User ID",ApprovalEntry."Approver ID");
        ApprovalSharing.SETRANGE("Use Owners Limts & Permissions",TRUE);
        IF ApprovalSharing.FINDFIRST THEN BEGIN
          UpdateDtldAppvlEntries(ActionToPerform::Approve,ApprovalEntry."Approver ID");
          Approved := TRUE;
        END ELSE
          UpdateDtldAppvlEntries(ActionToPerform::FindNextApprover,CurrUserId);
      END;

      IF NOT Approved THEN
        UpdateDtldAppvlEntries(ActionToPerform::Approve,CurrUserId);

      // If the next approver have already been created in the Approval Entry table, then keep this person as the next approver
      ApprovalEntry2.SETRANGE("Table ID",ApprovalEntry."Table ID");
      ApprovalEntry2.SETRANGE("Document Type",ApprovalEntry."Document Type");
      ApprovalEntry2.SETRANGE("Document No.",ApprovalEntry."Document No.");
      ApprovalEntry2.SETFILTER("Sequence No.",'>%1',ApprovalEntry."Sequence No.");
      ApprovalEntry2.SETRANGE(Status,ApprovalEntry2.Status::Created,ApprovalEntry2.Status::Open);
      IF ApprovalEntry2.FINDFIRST THEN
        NextApproverID := ApprovalEntry2."Approver ID"
      ELSE
        UpdateDtldAppvlEntries(ActionToPerform::FindNextApprover,NextApproverID);

      IF NextApproverID = '' THEN BEGIN
        CheckMandatoryDim;

        // All lines have been approved. Now the whole document must be approved.
        UpdateDocDtldAppvlEntries(ActionToPerform::Approve,CurrUserId);
        IF NOT UpdateDocDtldAppvlEntries(ActionToPerform::FindNextApprover,NextApproverID) THEN
          IF NextApproverID = '' THEN
            ERROR(Text005);
      END;

      DtldAppvlEntry.SETFILTER("Table ID",'%1|%2',DATABASE::"Purchase Header",DATABASE::"Purchase Line");
      DtldAppvlEntry.SETRANGE("Document Type",ApprovalEntry."Document Type");
      DtldAppvlEntry.SETRANGE("Document No.",ApprovalEntry."Document No.");
      DtldAppvlEntry.DELETEALL;
      IF TempDtldAppvlEntry.FINDSET THEN
        REPEAT
          DtldAppvlEntry := TempDtldAppvlEntry;
          DtldAppvlEntry.INSERT;
        UNTIL TempDtldAppvlEntry.NEXT = 0;

      DtldAppvlEntryDim.SETFILTER("Table ID",'%1|%2',DATABASE::"Purchase Header",DATABASE::"Purchase Line");
      DtldAppvlEntryDim.SETRANGE("Document Type",ApprovalEntry."Document Type");
      DtldAppvlEntryDim.SETRANGE("Document No.",ApprovalEntry."Document No.");
      DtldAppvlEntryDim.DELETEALL;
      IF TempDtldAppvlEntryDim.FINDSET THEN
        REPEAT
          DtldAppvlEntryDim := TempDtldAppvlEntryDim;
          DtldAppvlEntryDim.INSERT;
        UNTIL TempDtldAppvlEntryDim.NEXT = 0;

      IF NextApproverID = CurrUserId THEN
        NextApproverID := '';
    END;

    PROCEDURE DelegateApprovalRequest@161024016(ApprovalEntry@161024012 : Record 454;ApproverId@161024013 : Code[50];Approve@6085573 : Boolean);
    VAR
      ActionToPerform@161024014 : 'Approve,FindNextApprover';
    BEGIN
      PurchHeader.GET(ApprovalEntry."Document Type",ApprovalEntry."Document No.");
      SetPurchHeader(PurchHeader);

      FindAndSetAppvlGroup;

      SyncDtldAppvlEntries;

      UpdateDtldAppvlEntries(ActionToPerform::FindNextApprover,ApproverId);

      IF (Approve) AND (AppvlGroup."Four Eyes Approval" = AppvlGroup."Four Eyes Approval"::Invoice) THEN
        UpdateDocDtldAppvlEntries(ActionToPerform::Approve,ApprovalEntry."Approver ID");
    END;

    LOCAL PROCEDURE SyncDtldAppvlEntries@1000000000();
    VAR
      PurchLine@1000000000 : Record 39;
      DtldAppvlEntry@1000000001 : Record 6085744;
    BEGIN
      DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purchase Line");
      DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type");
      DtldAppvlEntry.SETRANGE("Document No.",PurchHeader."No.");

      PurchLine.SETRANGE("Document Type",PurchHeader."Document Type");
      PurchLine.SETRANGE("Document No.",PurchHeader."No.");

      //**4PS.sn
      IF PurchHeader."Amounts only" THEN
        PurchLine.SETFILTER("Line Amount", '<>%1', 0)
      ELSE
      //**4PS.en
        PurchLine.SETFILTER(Quantity,'<>%1',0);

      IF NOT PurchLine.FINDSET THEN BEGIN
        DtldAppvlEntry.DELETEALL(TRUE);

        RecreateDocApproval;
      END ELSE BEGIN
        REPEAT
          IF NOT DtldAppvlEntry.GET(DATABASE::"Purchase Line",PurchLine."Document Type",
            PurchLine."Document No.",PurchLine."Line No.")
          THEN BEGIN
            CreateDtldAppvlEntry(PurchLine);
            RecreateDocApproval;
          END ELSE BEGIN
            // This function check to see if information that controls who needs to approve the line on the Purchase Line has changed.
            // If so, we will recreate the line which needs to be reapproved.
            IF HasPurchLineChanged(PurchLine,DtldAppvlEntry) THEN BEGIN
              DtldAppvlEntry.DELETE(TRUE);
              CreateDtldAppvlEntry(PurchLine);
              RecreateDocApproval;
            END;
          END;
        UNTIL PurchLine.NEXT = 0;
      END;

      // Check if any Detailed Approval Entry exists, which dosent have an corresponding Purchase Line
      IF DtldAppvlEntry.FINDSET THEN
        REPEAT
          IF NOT PurchLine.GET(DtldAppvlEntry."Document Type",DtldAppvlEntry."Document No.",DtldAppvlEntry."Line No.") THEN BEGIN
            DtldAppvlEntry.DELETE(TRUE);
            RecreateDocApproval;
          END;
        UNTIL DtldAppvlEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateDtldAppvlEntries@1000000004(ActionToPerform@161024018 : 'Approve,FindNextApprover';VAR CurrApproverId@161024013 : Code[50]);
    VAR
      AppvlUser@1000000003 : Record 6085746;
      AppvlUserDim@1000000005 : Record 6085748;
      DtldAppvlEntry@161024015 : Record 6085744;
      TempSumDtldAppvlEntry@1000000001 : TEMPORARY Record 6085744;
      TempSumDtldAppvlEntryDim@1000000000 : TEMPORARY Record 6085750;
      EntryRelationship@161024014 : TEMPORARY Record 6085747;
      CompleteDimMatch@161024012 : Boolean;
    BEGIN
      // This will sum all the approval lines based on the the information that controls who needs to approve the line
      SumApprovalLines(TRUE,TempSumDtldAppvlEntry,TempSumDtldAppvlEntryDim,EntryRelationship);

      AppvlUser.SETRANGE("Approval Group Code",AppvlGroup.Code);

      DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purchase Line");
      DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type");
      DtldAppvlEntry.SETRANGE("Document No.",PurchHeader."No.");
      DtldAppvlEntry.SETRANGE("Approval Group Code",AppvlGroup.Code);
      DtldAppvlEntry.SETRANGE(Approved,FALSE);
      DtldAppvlEntry.MODIFYALL("User ID",'');
      DtldAppvlEntry.SETRANGE(Approved);

      // Loop through each of the sum lines
      IF TempSumDtldAppvlEntry.FINDSET THEN
        REPEAT
          // Filter the Approval User to find all user records (of the current user) that has the right limit
          IF CurrApproverId <> '' THEN BEGIN
            AppvlUser.SETCURRENTKEY("Approval Group Code","User ID","Approval Amount Limit");
            AppvlUser.SETRANGE("User ID",CurrApproverId);
          END ELSE
            AppvlUser.SETCURRENTKEY("Approval Group Code","Approval Amount Limit");

          AppvlUser.SETFILTER("Approval Amount Limit",'>=%1',TempSumDtldAppvlEntry."Amount (LCY)");
          IF AppvlUser.FINDSET THEN
            REPEAT
              CompleteDimMatch := TRUE;

              DtldAppvlEntry.SETCURRENTKEY("Table ID","Document Type","Document No.","Approval Group Code","User ID");
              DtldAppvlEntry.SETRANGE("User ID",AppvlUser."User ID");
              DtldAppvlEntry.CALCSUMS("Amount (LCY)");
              IF AppvlUser."Approval Amount Limit" - DtldAppvlEntry."Amount (LCY)" < TempSumDtldAppvlEntry."Amount (LCY)" THEN
                CompleteDimMatch := FALSE;

              IF CompleteDimMatch THEN BEGIN
                DtldAppvlEntry.GET(DATABASE::"Purchase Line",PurchHeader."Document Type",PurchHeader."No.",
                  EntryRelationship."Approval Entry Line No.");

                // Filter the dimensions associated to the current sum line. Then make sure that the current Approval User record have the
                // same dimensions.
                TempSumDtldAppvlEntryDim.SETRANGE("Table ID",TempSumDtldAppvlEntry."Table ID");
                IF TempSumDtldAppvlEntryDim.FINDSET THEN BEGIN
                  REPEAT
                    IF NOT AppvlUserDim.GET(AppvlUser."Approval Group Code",AppvlUser."User ID",AppvlUser."Entry No.",
                      TempSumDtldAppvlEntryDim."Dimension Code",TempSumDtldAppvlEntryDim."Dimension Value Code")
                    THEN
                      CompleteDimMatch := FALSE;
                  UNTIL (TempSumDtldAppvlEntryDim.NEXT = 0) OR (NOT CompleteDimMatch);
                END;

                // CompleteDimMatch is TRUE if the current Approval User record have all the dimensions used on the sum line
                IF CompleteDimMatch THEN BEGIN
                  CurrApproverId := AppvlUser."User ID";

                  // The EntryRelationship variable holds the relationship between the sum line and the Detailed Approval Entries
                  // So now we need to flag the Detailed Approval Entries that are associated to this sum line.
                  EntryRelationship.SETRANGE("Sum Line Id",TempSumDtldAppvlEntry."Table ID");
                  IF EntryRelationship.FINDSET THEN
                    REPEAT
                      DtldAppvlEntry.GET(DATABASE::"Purchase Line",PurchHeader."Document Type",PurchHeader."No.",
                        EntryRelationship."Approval Entry Line No.");

                      IF ActionToPerform = ActionToPerform::Approve THEN BEGIN
                        IF NOT DtldAppvlEntry.Approved THEN BEGIN
                          DtldAppvlEntry."User ID" := CurrApproverId;
                          DtldAppvlEntry.Approved := TRUE;
                          DtldAppvlEntry.MODIFY;
                        END;
                      END ELSE BEGIN
                        DtldAppvlEntry."User ID" := CurrApproverId;
                        DtldAppvlEntry.MODIFY;
                      END;
                    UNTIL EntryRelationship.NEXT = 0;
                END;
              END;
            UNTIL (AppvlUser.NEXT = 0) OR (CompleteDimMatch);
        UNTIL TempSumDtldAppvlEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE SumApprovalLines@1000000007(OnlyUnApprovedLines@161024012 : Boolean;VAR TempSumDtldAppvlEntry@1000000000 : TEMPORARY Record 6085744;VAR TempSumDtldAppvlEntryDim@1000000002 : TEMPORARY Record 6085750;VAR EntryRelationship@161024013 : TEMPORARY Record 6085747);
    VAR
      DtldAppvlEntry@1000000004 : Record 6085744;
      DtldAppvlEntryDim@1000000003 : Record 6085750;
      AppvlGroupDim@161024014 : Record 6085751;
      CompleteDimMatch@1000000005 : Boolean;
    BEGIN
      // *********************************************************************************************************************************
      // This function will sum all the approval lines based on the the information that controls who needs to approve the line
      // *********************************************************************************************************************************

      DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purchase Line");
      DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type");
      DtldAppvlEntry.SETRANGE("Document No.",PurchHeader."No.");
      IF OnlyUnApprovedLines THEN
        DtldAppvlEntry.SETRANGE(Approved,FALSE);
      IF DtldAppvlEntry.FINDSET THEN
        REPEAT
          // Loop through all the summed Detailed Approval Entry and try to find a line which have the same dimensions as the current
          // Detailed Approval Entry line. If a sum line already exists then we will update this line with the amount. Otherwise we will
          // create a new sum line.
          IF TempSumDtldAppvlEntry.FINDSET THEN
            REPEAT
              CompleteDimMatch := TRUE;

              TempSumDtldAppvlEntryDim.SETRANGE("Table ID",TempSumDtldAppvlEntry."Table ID");
              IF TempSumDtldAppvlEntryDim.FINDSET THEN
                REPEAT
                  IF NOT DtldAppvlEntryDim.GET(DtldAppvlEntry."Table ID",DtldAppvlEntry."Document Type",
                    DtldAppvlEntry."Document No.",DtldAppvlEntry."Line No.",TempSumDtldAppvlEntryDim."Dimension Code")
                  THEN
                    CompleteDimMatch := FALSE;

                  IF TempSumDtldAppvlEntryDim."Dimension Value Code" <> DtldAppvlEntryDim."Dimension Value Code" THEN
                    CompleteDimMatch := FALSE;
                UNTIL (TempSumDtldAppvlEntryDim.NEXT = 0) OR (NOT CompleteDimMatch);

              // If all the dimensions that are on the sum line also are on the current Detailed Approval Entry line, then check
              // that there are not any dimensions on the current Detailed Approval Entry line which are not on the sum line.
              IF CompleteDimMatch THEN BEGIN
                DtldAppvlEntryDim.SETRANGE("Document Line No.",DtldAppvlEntry."Line No.");
                IF DtldAppvlEntryDim.FINDSET THEN
                  REPEAT
                    IF NOT TempSumDtldAppvlEntryDim.GET(TempSumDtldAppvlEntry."Table ID",0,'',0,DtldAppvlEntryDim."Dimension Code") THEN
                      CompleteDimMatch := FALSE;

                    IF TempSumDtldAppvlEntryDim."Dimension Value Code" <> DtldAppvlEntryDim."Dimension Value Code" THEN
                      CompleteDimMatch := FALSE;
                  UNTIL (DtldAppvlEntryDim.NEXT = 0) OR (NOT CompleteDimMatch);
              END;
            UNTIL CompleteDimMatch OR (TempSumDtldAppvlEntry.NEXT = 0);

          IF CompleteDimMatch THEN BEGIN
            // A sum line with the correct active dimensions were found so we will update the amount on that line
            TempSumDtldAppvlEntry."Amount (LCY)" := TempSumDtldAppvlEntry."Amount (LCY)" + DtldAppvlEntry."Amount (LCY)";
            TempSumDtldAppvlEntry.MODIFY;

            EntryRelationship."Sum Line Id" := TempSumDtldAppvlEntry."Table ID";
            EntryRelationship."Approval Entry Line No." := DtldAppvlEntry."Line No.";
            EntryRelationship.INSERT;
          END ELSE BEGIN
            // No sum line with the correct active dimensions were found we need to create it.
            TempSumDtldAppvlEntry.INIT;
            TempSumDtldAppvlEntry."Table ID" := TempSumDtldAppvlEntry.COUNT;
            TempSumDtldAppvlEntry."Amount (LCY)" := DtldAppvlEntry."Amount (LCY)";
            TempSumDtldAppvlEntry.INSERT;

            AppvlGroupDim.SETRANGE("Approval Group Code", AppvlGroup.Code);
            IF AppvlGroupDim.FINDSET THEN
              REPEAT
                IF NOT DtldAppvlEntryDim.GET(DtldAppvlEntry."Table ID", DtldAppvlEntry."Document Type",
                  DtldAppvlEntry."Document No.", DtldAppvlEntry."Line No.",AppvlGroupDim."Dimension Code")
              THEN
                DtldAppvlEntryDim.INIT;
              TempSumDtldAppvlEntryDim."Table ID" := TempSumDtldAppvlEntry."Table ID";
              TempSumDtldAppvlEntryDim."Dimension Code" := AppvlGroupDim."Dimension Code";
              TempSumDtldAppvlEntryDim."Dimension Value Code" := DtldAppvlEntryDim."Dimension Value Code";
              TempSumDtldAppvlEntryDim.INSERT;
            UNTIL AppvlGroupDim.NEXT = 0;

            EntryRelationship."Sum Line Id" := TempSumDtldAppvlEntry."Table ID";
            EntryRelationship."Approval Entry Line No." := DtldAppvlEntry."Line No.";
            EntryRelationship.INSERT;
          END;

        UNTIL DtldAppvlEntry.NEXT = 0;

      IF TempSumDtldAppvlEntry.FINDSET THEN
        REPEAT
          TempSumDtldAppvlEntry."Amount (LCY)" := ABS(TempSumDtldAppvlEntry."Amount (LCY)");
          TempSumDtldAppvlEntry.MODIFY;
        UNTIL TempSumDtldAppvlEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE CreateDtldAppvlEntry@1000000002(PurchLine@1000000000 : Record 39);
    VAR
      NewDtldAppvlEntry@1000000001 : Record 6085744;
      NewDtldAppvlEntryDim@1000000002 : Record 6085750;
      CurrExchRate@161024012 : Record 330;
      DimSetEntry@6085573 : TEMPORARY Record 480;
    BEGIN
      // *********************************************************************************************************************************
      // This function will create a new Detailed Approval Entry based on the Purchase Line
      // *********************************************************************************************************************************

      WITH NewDtldAppvlEntry DO BEGIN
        "Table ID" := DATABASE::"Purchase Line";
        "Document Type" := PurchLine."Document Type";
        "Document No." := PurchLine."Document No.";
        "Line No." := PurchLine."Line No.";
        Amount := GetAmountExclVAT(PurchLine);
        //**4PS.so
        //"Amount (LCY)" := CurrExchRate.ExchangeAmtFCYToLCY(PurchHeader."Posting Date",PurchHeader."Currency Code",
        //  Amount,PurchHeader."Currency Factor");
        //**4PS.eo
        //**4PS.sn
        "Amount (LCY)" := CurrExchRate.ExchangeAmtFCYToLCY(1,PurchHeader."Job No.",PurchHeader."Posting Date",
          PurchHeader."Currency Code",
          Amount,PurchHeader."Currency Factor",FALSE);
        //**4PS.en

        "Approval Group Code" := AppvlGroup.Code;
        INSERT;

        // Create the Detailed Approval Entry Dimensions
        DimMgt.GetDimensionSet(DimSetEntry,PurchLine."Dimension Set ID");
        IF DimSetEntry.FINDSET THEN
          REPEAT
            IF IsActiveDimension(DimSetEntry."Dimension Code") THEN BEGIN
              NewDtldAppvlEntryDim."Table ID" := "Table ID";
              NewDtldAppvlEntryDim."Document Type" := "Document Type";
              NewDtldAppvlEntryDim."Document No." := "Document No.";
              NewDtldAppvlEntryDim."Document Line No." := "Line No.";
              NewDtldAppvlEntryDim."Dimension Code" := DimSetEntry."Dimension Code";
              NewDtldAppvlEntryDim."Dimension Value Code" := DimSetEntry."Dimension Value Code";
              NewDtldAppvlEntryDim.INSERT;
            END;
          UNTIL DimSetEntry.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE HasPurchLineChanged@1000000001(PurchLine@1000000000 : Record 39;DtldAppvlEntry@1000000001 : Record 6085744) : Boolean;
    VAR
      DimSetEntry@1000000002 : TEMPORARY Record 480;
      DtldAppvlEntryDim@1000000003 : Record 6085750;
    BEGIN
      // *********************************************************************************************************************************
      // This function check to see if information that controls who needs to approve the line on the Purchase Line has changed.
      // If so, it will return TRUE.
      // *********************************************************************************************************************************

      // We check to see if the amount has changed. We do not consider the LCY value as a small currency variance should not result
      // in a new approval. The original LCY amount remains on the Detailed Approval Entry
      IF GetAmountExclVAT(PurchLine) <> DtldAppvlEntry.Amount THEN
        EXIT(TRUE);

      // This function will check if any dimensions have been changed on the Purchase Line compared to the
      // Dtld. Approval Entry Dimension
      DimMgt.GetDimensionSet(DimSetEntry,PurchLine."Dimension Set ID");

      DtldAppvlEntryDim.SETRANGE("Table ID",DtldAppvlEntry."Table ID");
      DtldAppvlEntryDim.SETRANGE("Document Type",DtldAppvlEntry."Document Type");
      DtldAppvlEntryDim.SETRANGE("Document No.",DtldAppvlEntry."Document No.");
      DtldAppvlEntryDim.SETRANGE("Document Line No.",DtldAppvlEntry."Line No.");

      IF DimSetEntry.FINDSET THEN
        REPEAT
          IF IsActiveDimension(DimSetEntry."Dimension Code") THEN BEGIN
            IF NOT DtldAppvlEntryDim.GET(DtldAppvlEntry."Table ID",DtldAppvlEntry."Document Type",DtldAppvlEntry."Document No.",
              DtldAppvlEntry."Line No.",DimSetEntry."Dimension Code")
            THEN
              EXIT(TRUE);

            IF DtldAppvlEntryDim."Dimension Value Code" <> DimSetEntry."Dimension Value Code" THEN
              EXIT(TRUE);
          END;
        UNTIL DimSetEntry.NEXT = 0;

      IF DtldAppvlEntryDim.FINDSET THEN
        REPEAT
          IF IsActiveDimension(DtldAppvlEntryDim."Dimension Code") THEN BEGIN
            IF NOT DimSetEntry.GET(PurchLine."Dimension Set ID",DtldAppvlEntryDim."Dimension Code") THEN
              EXIT(TRUE);

            IF DtldAppvlEntryDim."Dimension Value Code" <> DimSetEntry."Dimension Value Code" THEN
              EXIT(TRUE);
          END;
        UNTIL DtldAppvlEntryDim.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateDocDtldAppvlEntries@161024021(ActionToPerform@161024018 : 'Approve,FindNextApprover';VAR CurrApproverId@161024013 : Code[50]) : Boolean;
    VAR
      AppvlUser@1000000003 : Record 6085746;
      AppvlUserDim@1000000005 : Record 6085748;
      DtldAppvlEntry@161024015 : Record 6085744;
      DtldAppvlEntry2@1160040000 : Record 6085744;
      DtldAppvlEntryDim@1000000000 : Record 6085750;
      ExcludeApproverID@161024014 : Code[50];
      AmountLCY@161024016 : Decimal;
      CompleteDimMatch@161024012 : Boolean;
    BEGIN
      // *********************************************************************************************************************************
      // This function are used in two ways:
      // 1) Called with ActionToPerform = Approve:
      //    The function will sum/group the 'Line Amount (LCY)' of all unapproved Detailed Approval Entries with the same dimensions and
      //    see if the CurrApproverId has the nesseary Approval Limit. If so, it will approve these Detailed Approval Entries.
      // 2) Called with ActionToPerform = FindNextApprover and CurrApproverId = '':
      //    The function will sum/group the 'Line Amount (LCY)' of all unapproved Detailed Approval Entries with the same dimensions.
      //    Next, it will find the first approver who can approve the sum/group of the first uapproved dimension combination.
      //    The User Id of this approver is updated on the Detailed Approval Entries for this dimension combination.
      //    Next, it will see if it can find other sum/groups that this user can approve, and if so, it will update the user id on these
      //    entries as well.
      // *********************************************************************************************************************************


      // See if this document have already been approved on a total level. This can be the cause if two persons needs
      // to approve the full amount. If so, find the first approver and save it in a variable to check when we find the
      // next approver (which must be different than the first).
      DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purchase Header");
      DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type");
      DtldAppvlEntry.SETRANGE("Document No.",PurchHeader."No.");
      DtldAppvlEntry.SETRANGE(Approved,TRUE);
      IF DtldAppvlEntry.FINDFIRST THEN
        ExcludeApproverID := DtldAppvlEntry."User ID";


      DtldAppvlEntry.SETRANGE(Approved,FALSE);
      IF NOT DtldAppvlEntry.FINDFIRST THEN BEGIN
        DtldAppvlEntry2.SETRANGE("Table ID",DATABASE::"Purchase Line");
        DtldAppvlEntry2.SETRANGE("Document Type",PurchHeader."Document Type");
        DtldAppvlEntry2.SETRANGE("Document No.",PurchHeader."No.");
        DtldAppvlEntry2.SETRANGE(Approved,FALSE);
        IF DtldAppvlEntry2.ISEMPTY THEN
          EXIT(TRUE);
      END;


      IF (ActionToPerform = ActionToPerform::Approve) AND
        (AppvlGroup."Four Eyes Approval" = AppvlGroup."Four Eyes Approval"::Invoice) AND
        (ExcludeApproverID <> CurrApproverId)
      THEN BEGIN
        DtldAppvlEntry."User ID" := CurrApproverId;
        DtldAppvlEntry.Approved := TRUE;
        DtldAppvlEntry.MODIFY;
        EXIT(TRUE);
      END;

      DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purchase Line");

      // No Approver ID have been excluded so far. If it is the same approver on all lines, then this user can not approve the
      // full amount and therefore we need to exclude this user.
      IF AppvlGroup."Four Eyes Approval" = AppvlGroup."Four Eyes Approval"::Invoice THEN
        IF ExcludeApproverID = '' THEN BEGIN
          DtldAppvlEntry.FINDFIRST;
          DtldAppvlEntry.SETFILTER("User ID",'<>%1',DtldAppvlEntry."User ID");
          IF DtldAppvlEntry.ISEMPTY THEN
            ExcludeApproverID := DtldAppvlEntry."User ID";

          DtldAppvlEntry.SETRANGE("User ID");
        END;

      AppvlUser.SETRANGE("Approval Group Code",AppvlGroup.Code);


      IF  AppvlGroup."Four Eyes Approval" = AppvlGroup."Four Eyes Approval"::"Invoice Full Amount" THEN BEGIN
        DtldAppvlEntry.GET(DATABASE::"Purchase Header",PurchHeader."Document Type",PurchHeader."No.",0);
        AmountLCY := DtldAppvlEntry."Amount (LCY)";
      END;

      IF CurrApproverId <> '' THEN BEGIN
        AppvlUser.SETCURRENTKEY("Approval Group Code","User ID","Approval Amount Limit");
        AppvlUser.SETRANGE("User ID",CurrApproverId);
      END ELSE
        AppvlUser.SETCURRENTKEY("Approval Group Code","Approval Amount Limit");

      AppvlUser.SETRANGE("Approval Amount Limit",AmountLCY,9999999999.0);
      IF AppvlUser.FINDSET THEN
        REPEAT
          IF ExcludeApproverID <> AppvlUser."User ID" THEN BEGIN
            CompleteDimMatch := TRUE;

            DtldAppvlEntryDim.SETRANGE("Table ID",DATABASE::"Purchase Line");
            DtldAppvlEntryDim.SETRANGE("Document Type",PurchHeader."Document Type");
            DtldAppvlEntryDim.SETRANGE("Document No.",PurchHeader."No.");
            IF DtldAppvlEntryDim.FINDSET THEN
              REPEAT
                IF NOT AppvlUserDim.GET(AppvlUser."Approval Group Code",AppvlUser."User ID",AppvlUser."Entry No.",
                  DtldAppvlEntryDim."Dimension Code",DtldAppvlEntryDim."Dimension Value Code")
                THEN
                  CompleteDimMatch := FALSE;
              UNTIL (DtldAppvlEntryDim.NEXT = 0) OR (NOT CompleteDimMatch);
            IF CompleteDimMatch THEN
              CurrApproverId := AppvlUser."User ID";
          END;
        UNTIL (AppvlUser.NEXT = 0) OR (CompleteDimMatch);

      IF CompleteDimMatch THEN BEGIN
        DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purchase Header");
        DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type");
        DtldAppvlEntry.SETRANGE("Document No.",PurchHeader."No.");
        DtldAppvlEntry.SETRANGE(Approved,FALSE);
        DtldAppvlEntry.FINDFIRST;

        IF (ExcludeApproverID <> CurrApproverId) THEN BEGIN
          IF ActionToPerform = ActionToPerform::Approve THEN BEGIN
            DtldAppvlEntry."User ID" := CurrApproverId;
            DtldAppvlEntry.Approved := TRUE;
            DtldAppvlEntry.MODIFY;
            EXIT(TRUE);
          END ELSE BEGIN
            DtldAppvlEntry."User ID" := CurrApproverId;
            DtldAppvlEntry.MODIFY;
            EXIT(TRUE);
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE RecreateDocApproval@161024015();
    VAR
      DtldAppvlEntry@161024012 : Record 6085744;
      Amount@161024013 : Decimal;
      AmountLCY@161024014 : Decimal;
    BEGIN
      IF AppvlGroup."Four Eyes Approval" = AppvlGroup."Four Eyes Approval"::" " THEN
        EXIT;

      DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purchase Line");
      DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type");
      DtldAppvlEntry.SETRANGE("Document No.",PurchHeader."No.");
      IF DtldAppvlEntry.FINDSET THEN
        REPEAT
          Amount := Amount + DtldAppvlEntry.Amount;
          AmountLCY := AmountLCY + DtldAppvlEntry."Amount (LCY)";
        UNTIL DtldAppvlEntry.NEXT = 0;

      DeleteAppvlEntries(DATABASE::"Purchase Header",PurchHeader."Document Type",PurchHeader."No.");

      DtldAppvlEntry.INIT;
      DtldAppvlEntry."Table ID" := DATABASE::"Purchase Header";
      DtldAppvlEntry."Document Type" := PurchHeader."Document Type";
      DtldAppvlEntry."Document No." := PurchHeader."No.";
      DtldAppvlEntry."Line No." := 0;
      DtldAppvlEntry."Approval Group Code" := AppvlGroup.Code;
      DtldAppvlEntry.Amount := Amount;
      DtldAppvlEntry."Amount (LCY)" := AmountLCY;
      DtldAppvlEntry.INSERT;

      DtldAppvlEntry."Line No." := 1;
      DtldAppvlEntry.INSERT;
    END;

    LOCAL PROCEDURE FindAndSetAppvlGroup@1103308000();
    VAR
      TempPurchHeader@1103308000 : TEMPORARY Record 38;
      Vend@1103308004 : Record 23;
      TempVend@1103308007 : TEMPORARY Record 23;
      Purchaser@1103308005 : Record 13;
      TempPurchaser@1103308006 : TEMPORARY Record 13;
      DtldAppvlEntry@1000000001 : Record 6085744;
      AppvlGroup2@1000000002 : Record 6085743;
      CriterionsFulfilled@1103308003 : Boolean;
    BEGIN
      IF GetLastDtldAppvlEntry(DtldAppvlEntry) THEN BEGIN
        IF AppvlGroup2.GET(DtldAppvlEntry."Approval Group Code") THEN BEGIN
          SetAppvlGroup(AppvlGroup2);
          EXIT;
        END;
      END;

      TempPurchHeader := PurchHeader;
      TempPurchHeader.INSERT;

      IF Vend.GET(PurchHeader."Buy-from Vendor No.") THEN BEGIN
        TempVend := Vend;
        TempVend.INSERT;
      END;

      IF Purchaser.GET(PurchHeader."Purchaser Code") THEN BEGIN
        TempPurchaser := Purchaser;
        TempPurchaser.INSERT;
      END;

      AppvlGroup2.SETCURRENTKEY(Priority);
      IF AppvlGroup2.FINDSET THEN
        REPEAT
          CriterionsFulfilled := TRUE;

          IF FORMAT(AppvlGroup2."Purchase Header Filter") <> '' THEN BEGIN
            TempPurchHeader.SETVIEW(TableFilter2View(DATABASE::"Purchase Header",
              FORMAT(AppvlGroup2."Purchase Header Filter"),TempPurchHeader.GETVIEW));
            IF TempPurchHeader.ISEMPTY THEN
              CriterionsFulfilled := FALSE;
          END;

          IF FORMAT(AppvlGroup2."Vendor Filter") <> '' THEN BEGIN
            TempVend.SETVIEW(TableFilter2View(DATABASE::Vendor,FORMAT(AppvlGroup2."Vendor Filter"),TempVend.GETVIEW));
            IF TempVend.ISEMPTY THEN
              CriterionsFulfilled := FALSE;
          END;

          IF FORMAT(AppvlGroup2."Purchaser Filter") <> '' THEN BEGIN
            TempPurchaser.SETVIEW(TableFilter2View(DATABASE::"Salesperson/Purchaser",
              FORMAT(AppvlGroup2."Purchaser Filter"),TempPurchaser.GETVIEW));
            IF TempPurchaser.ISEMPTY THEN
              CriterionsFulfilled := FALSE;
          END;
        UNTIL (AppvlGroup2.NEXT = 0) OR CriterionsFulfilled;

      IF CriterionsFulfilled THEN
        SetAppvlGroup(AppvlGroup2)
      ELSE
        ERROR(Text004,AppvlGroup.TABLECAPTION);
    END;

    LOCAL PROCEDURE GetLastDtldAppvlEntry@1000000014(VAR DtldAppvlEntry@1000000000 : Record 6085744) : Boolean;
    BEGIN
      WITH DtldAppvlEntry DO BEGIN
        SETRANGE("Table ID",DATABASE::"Purchase Header",DATABASE::"Purchase Line");
        SETRANGE("Document Type",PurchHeader."Document Type");
        SETRANGE("Document No.",PurchHeader."No.");
        EXIT(DtldAppvlEntry.FINDLAST);
      END;
    END;

    LOCAL PROCEDURE SetPurchHeader@1000000022(NewPurchHeader@1000000000 : Record 38);
    BEGIN
      PurchHeader := NewPurchHeader;
    END;

    LOCAL PROCEDURE SetAppvlGroup@1000000023(NewAppvlGroup@1000000000 : Record 6085743);
    BEGIN
      AppvlGroup := NewAppvlGroup;
    END;

    PROCEDURE MoveEntriesToPosted@1000000005(TableId@1000000004 : Integer;DocumentType@1000000003 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';DocumentNo@1000000002 : Code[20];ToTableId@1000000001 : Integer;ToNo@1000000005 : Code[20]);
    VAR
      DtldAppvlEntry@1000000000 : Record 6085744;
      DtldAppvlEntryDim@161024014 : Record 6085750;
      PostedDtldAppvlEntry@161024012 : Record 6085752;
      PostedDtldAppvlEntryDim@161024013 : Record 6085753;
    BEGIN
      IF (TableId <> DATABASE::"Purchase Header") OR (ToTableId <> DATABASE::"Purch. Inv. Header") THEN
        EXIT;

      DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purchase Header",DATABASE::"Purchase Line");
      DtldAppvlEntry.SETRANGE("Document Type",DocumentType);
      DtldAppvlEntry.SETRANGE("Document No.",DocumentNo);
      IF DtldAppvlEntry.FINDSET THEN
        REPEAT
          PostedDtldAppvlEntry.TRANSFERFIELDS(DtldAppvlEntry);
          PostedDtldAppvlEntry."Table ID" := ToTableId;
          PostedDtldAppvlEntry."Document No." := ToNo;
          PostedDtldAppvlEntry.INSERT;
        UNTIL DtldAppvlEntry.NEXT = 0;

      DtldAppvlEntryDim.SETRANGE("Table ID",TableId);
      DtldAppvlEntryDim.SETRANGE("Document Type",DocumentType);
      DtldAppvlEntryDim.SETRANGE("Document No.",DocumentNo);
      IF DtldAppvlEntryDim.FINDSET THEN
        REPEAT
          PostedDtldAppvlEntryDim.TRANSFERFIELDS(DtldAppvlEntryDim);
          PostedDtldAppvlEntryDim."Table ID" := ToTableId;
          PostedDtldAppvlEntryDim."Document No." := ToNo;
          PostedDtldAppvlEntryDim.INSERT;
        UNTIL DtldAppvlEntryDim.NEXT = 0;
    END;

    PROCEDURE DeleteAppvlEntries@1000000006(TableId@1000000002 : Integer;DocumentType@1000000001 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';DocumentNo@1000000000 : Code[20]);
    VAR
      DtldAppvlEntry@1000000003 : Record 6085744;
    BEGIN
      DtldAppvlEntry.SETRANGE("Table ID",TableId);
      DtldAppvlEntry.SETRANGE("Document Type",DocumentType);
      DtldAppvlEntry.SETRANGE("Document No.",DocumentNo);
      DtldAppvlEntry.DELETEALL(TRUE);
    END;

    PROCEDURE GetAmountExclVAT@1000000009(PurchLine@1000000001 : Record 39) : Decimal;
    VAR
      TempVATAmountLine@1000000002 : TEMPORARY Record 290;
    BEGIN
      IF NOT PurchHeader."Prices Including VAT" THEN
        EXIT(PurchLine."Line Amount")
      ELSE BEGIN
        PurchLine.SETRANGE("Line No.",PurchLine."Line No.");
        PurchLine.CalcVATAmountLines(0,PurchHeader,PurchLine,TempVATAmountLine);
        EXIT(TempVATAmountLine."Line Amount" - TempVATAmountLine."VAT Amount");
      END;
    END;

    LOCAL PROCEDURE TableFilterToFilters@1103308006(TableFilter@1103308000 : Text[1024]) FilterString : Text[1024];
    VAR
      Position@1103308001 : Integer;
      RecRef@1103308002 : RecordRef;
      FieldRef@1103308003 : FieldRef;
    BEGIN
      TableFilter := COPYSTR(TableFilter,STRPOS(TableFilter,': '));
      Position := STRPOS(TableFilter,'=');
      WHILE Position > 0 DO BEGIN
        TableFilter := DELSTR(TableFilter,Position,1);
        TableFilter := INSSTR(TableFilter,': ',Position);
        Position := STRPOS(TableFilter,'=');
      END;
    END;

    LOCAL PROCEDURE TableFilter2View@1103308005(TableNo@1160830001 : Integer;TableFilter@1160830000 : Text[1024];CurrTableView@1160830005 : Text[1024]) : Text[1024];
    VAR
      AllObj@1160830004 : Record 2000000038;
      TableView@1160830003 : Text[1024];
      Pos@1160830002 : Integer;
    BEGIN
      TableView := '';
      Pos := STRPOS(CurrTableView,'WHERE(');
      IF Pos > 0 THEN
        TableView := COPYSTR(CurrTableView,1,Pos-1)
      ELSE
        TableView := CurrTableView + ' ';

      Pos := STRPOS(TableFilter,': ');
      IF Pos = 0 THEN
        EXIT(TableView);

      AllObj.GET(AllObj."Object Type"::Table,TableNo);

      IF COPYSTR(TableFilter,1,Pos-1) <> AllObj."Object Name" THEN
        EXIT(TableView);

      TableFilter := COPYSTR(TableFilter,Pos+2);

      Pos := STRPOS(TableFilter,'=');
      IF Pos = 0 THEN
        EXIT(TableView);

      TableView := TableView + 'WHERE(';

      WHILE Pos > 0 DO BEGIN
        TableView := TableView + COPYSTR(TableFilter,1,Pos) + 'FILTER(';
        TableFilter := COPYSTR(TableFilter,Pos+1);

        IF TableFilter[1] = '"' THEN BEGIN
          TableFilter := COPYSTR(TableFilter,2);
          Pos := STRPOS(TableFilter,'"');
          WHILE Pos > 0 DO BEGIN
            TableView := TableView + COPYSTR(TableFilter,1,Pos-1);
            IF COPYSTR(TableFilter,Pos,2) = '""' THEN BEGIN
              TableView := TableView + '"';
              TableFilter := COPYSTR(TableFilter,Pos+2);
              Pos := STRPOS(TableFilter,'"');
            END ELSE BEGIN
              TableFilter := COPYSTR(TableFilter,Pos+1);
              Pos := 0;
            END;
          END;
        END;

        Pos := STRPOS(TableFilter,',');
        IF Pos = 0 THEN
          Pos := STRLEN(TableFilter) + 1;

        TableView := TableView + COPYSTR(TableFilter,1,Pos-1) + ')' + COPYSTR(TableFilter,Pos,1);
        TableFilter := COPYSTR(TableFilter,Pos+1);
        Pos := STRPOS(TableFilter,'=');
      END;

      TableView := TableView + ')';

      EXIT(TableView);
    END;

    LOCAL PROCEDURE IsActiveDimension@1000000003(DimCode@1000000000 : Code[20]) : Boolean;
    VAR
      AppvlGroupDim@1000000001 : Record 6085751;
    BEGIN
      EXIT(AppvlGroupDim.GET(AppvlGroup.Code,DimCode));
    END;

    PROCEDURE CheckMandatoryDim@1000000008();
    VAR
      PurchLine@1000000000 : Record 39;
      DimSetEntry@1000000001 : TEMPORARY Record 480;
      AppvlGroupDim@1000000002 : Record 6085751;
    BEGIN
      AppvlGroupDim.SETRANGE("Approval Group Code",AppvlGroup.Code);
      AppvlGroupDim.SETRANGE(Mandatory,TRUE);
      IF AppvlGroupDim.ISEMPTY THEN
        EXIT;

      PurchLine.SETRANGE("Document Type",PurchHeader."Document Type");
      PurchLine.SETRANGE("Document No.",PurchHeader."No.");
      PurchLine.SETFILTER(Quantity,'<>%1',0);
      IF PurchLine.FINDSET THEN
        REPEAT
          IF AppvlGroupDim.FINDSET THEN
            REPEAT
              DimSetEntry.RESET;
              DimMgt.GetDimensionSet(DimSetEntry,PurchLine."Dimension Set ID");
              DimSetEntry.SETRANGE("Dimension Code",AppvlGroupDim."Dimension Code");
              IF DimSetEntry.ISEMPTY THEN
                ERROR(Text007,AppvlGroupDim."Dimension Code");
            UNTIL AppvlGroupDim.NEXT = 0;
        UNTIL PurchLine.NEXT = 0;
    END;

    PROCEDURE MarkOnWeb@1000000010(PurchLine@1000000001 : Record 39;CurrUserId@1000000000 : Code[50]) : Boolean;
    VAR
      DtldAppvlEntry@1000000002 : Record 6085744;
    BEGIN
      DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purchase Line");
      DtldAppvlEntry.SETRANGE("Document Type",PurchLine."Document Type");
      DtldAppvlEntry.SETRANGE("Document No.",PurchLine."Document No.");
      DtldAppvlEntry.SETRANGE("Line No.",PurchLine."Line No.");
      DtldAppvlEntry.SETRANGE("User ID",CurrUserId);
      DtldAppvlEntry.SETRANGE(Approved,FALSE);
      EXIT(NOT DtldAppvlEntry.ISEMPTY);
    END;

    PROCEDURE ApproveApprovalRequest4PS@1100528201(ApprovalEntry@161024012 : Record 454;_UserId@1000000002 : Code[50]);
    VAR
      ApprovalEntry2@161024014 : Record 454;
      UserSetup@161024019 : Record 91;
      ApprovalSharing@161024023 : Record 6085749;
      DCAppMgnt@161024022 : Codeunit 6085722;
      WebSearchMgnt@161024021 : Codeunit 6085748;
      NextApproverID@1000000000 : Code[20];
      ActionToPerform@161024013 : 'Approve,FindNextApprover';
      ApprovalAmount@161024017 : Decimal;
      ApprovalAmountLCY@161024016 : Decimal;
      Approved@161024024 : Boolean;
      lDocType@1100528200 : Integer;
      RecRef@1100528201 : RecordRef;
      ApprovalsBridge4PSConstruct@1100528202 : Codeunit 11125348;
      DCApprovalManagement@1100528203 : Codeunit 6085722;
      DCApprovalsBridge@1100528204 : Codeunit 6085790;
    BEGIN
      //**4PS
      // *********************************************************************************************************************************
      // This function is called when an approver approves an approval request
      // *********************************************************************************************************************************
      CASE ApprovalEntry."Table ID" OF
        DATABASE::"Purch. Inv. Header":
        BEGIN
          SetPurchInvHeader(ApprovalEntry."Document No.");
          lDocType := 2;
        END;
        DATABASE::"Purch. Cr. Memo Hdr.":
        BEGIN
          SetCreditMemoHeader(ApprovalEntry."Document No.");
          lDocType := 3;
        END;
      END;

      FindAndSetAppvlGroup4PS(lDocType);

      SyncDtldAppvlEntries4PS(lDocType);

      IF _UserId = '' THEN
        _UserId := USERID;

      IF ApprovalEntry."Approver ID" <> _UserId THEN BEGIN
        DCAppMgnt.FilterApprovalSharingToUser(ApprovalSharing,_UserId);
        ApprovalSharing.SETRANGE("Owner User ID",ApprovalEntry."Approver ID");
        ApprovalSharing.SETRANGE("Use Owners Limts & Permissions",TRUE);
        IF ApprovalSharing.FINDFIRST THEN BEGIN
          UpdateDtldAppvlEntries4PS(lDocType,ActionToPerform::Approve,ApprovalEntry."Approver ID");
          Approved := TRUE;
        END ELSE
          UpdateDtldAppvlEntries4PS(lDocType,ActionToPerform::FindNextApprover,_UserId);
      END;

      IF NOT Approved THEN
        UpdateDtldAppvlEntries4PS(lDocType,ActionToPerform::Approve,_UserId);

      // If the next approver have already been created in the Approval Entry table, then keep this person as the next approver
      ApprovalEntry2.SETRANGE("Table ID",ApprovalEntry."Table ID");
      ApprovalEntry2.SETRANGE("Document Type",ApprovalEntry."Document Type");
      ApprovalEntry2.SETRANGE("Document No.",ApprovalEntry."Document No.");
      ApprovalEntry2.SETRANGE(Status,ApprovalEntry2.Status::Created,ApprovalEntry2.Status::Open);
      IF ApprovalEntry2.FINDFIRST THEN BEGIN
        UpdateDtldAppvlEntries4PS(lDocType,ActionToPerform::FindNextApprover,ApprovalEntry2."Approver ID");
        EXIT;
      END;

      UpdateDtldAppvlEntries4PS(lDocType,ActionToPerform::FindNextApprover,NextApproverID);

      CASE lDocType OF
        2:
          BEGIN
            PurchInvHeader.CALCFIELDS(Amount);
            ApprovalAmount := PurchInvHeader.Amount;
            ApprovalAmountLCY := PurchInvHeader.Amount;

            IF NextApproverID <> '' THEN BEGIN
              UserSetup.GET(NextApproverID);

              RecRef.GETTABLE(PurchInvHeader);
              ApprovalsBridge4PSConstruct.InitializeApprovalEntry4PS(RecRef,ApprovalEntry);
              DCApprovalManagement.MakeApprovalEntry(ApprovalEntry,0,NextApproverID);

              IF AppvlGroup."Four Eyes Approval" = AppvlGroup."Four Eyes Approval"::Invoice THEN
                UpdateDocDtldAppvlEntries4PS(lDocType, ActionToPerform::Approve,ApprovalEntry."Original Approver ID");

              WebSearchMgnt.UpdateWebSearchFromInvoice(PurchInvHeader);   //Werkt dit??

            END ELSE BEGIN
              CheckMandatoryDim4PS(lDocType);

              // All lines have been approved. Now the whole document must be approved.
              UpdateDocDtldAppvlEntries4PS(lDocType, ActionToPerform::Approve,ApprovalEntry."Original Approver ID");
              IF NOT UpdateDocDtldAppvlEntries4PS(lDocType, ActionToPerform::FindNextApprover,NextApproverID) THEN
                IF NextApproverID = '' THEN
                  ERROR(Text005);

              IF NextApproverID <> '' THEN BEGIN
                UserSetup.GET(NextApproverID);
                RecRef.GETTABLE(PurchInvHeader);
                ApprovalsBridge4PSConstruct.InitializeApprovalEntry4PS(RecRef,ApprovalEntry);
                DCApprovalManagement.MakeApprovalEntry(ApprovalEntry,0,NextApproverID);
              END ELSE BEGIN
                DCApprovalsBridge.AdvancedApprovalFlowComplete(ApprovalEntry);
              END;
            END;

          END;
        3:
          BEGIN
             PurchCrMemoHdr.CALCFIELDS(Amount);
             ApprovalAmount := PurchCrMemoHdr.Amount;
             ApprovalAmountLCY := PurchCrMemoHdr.Amount;

            IF NextApproverID <> '' THEN BEGIN
              UserSetup.GET(NextApproverID);

              RecRef.GETTABLE(PurchCrMemoHdr);
              ApprovalsBridge4PSConstruct.InitializeApprovalEntry4PS(RecRef,ApprovalEntry);
              DCApprovalManagement.MakeApprovalEntry(ApprovalEntry,0,NextApproverID);

              IF AppvlGroup."Four Eyes Approval" = AppvlGroup."Four Eyes Approval"::Invoice THEN
                UpdateDocDtldAppvlEntries4PS(lDocType, ActionToPerform::Approve,ApprovalEntry."Original Approver ID");

              WebSearchMgnt.UpdateWebSearchFromCrMemo(PurchCrMemoHdr);   //Werkt dit??

            END ELSE BEGIN
              CheckMandatoryDim4PS(lDocType);

              // All lines have been approved. Now the whole document must be approved.
              UpdateDocDtldAppvlEntries4PS(lDocType, ActionToPerform::Approve,ApprovalEntry."Original Approver ID");
              IF NOT UpdateDocDtldAppvlEntries4PS(lDocType, ActionToPerform::FindNextApprover,NextApproverID) THEN
                IF NextApproverID = '' THEN
                  ERROR(Text005);

              IF NextApproverID <> '' THEN BEGIN
                UserSetup.GET(NextApproverID);
                RecRef.GETTABLE(PurchCrMemoHdr);
                ApprovalsBridge4PSConstruct.InitializeApprovalEntry4PS(RecRef,ApprovalEntry);
                DCApprovalManagement.MakeApprovalEntry(ApprovalEntry,0,NextApproverID);
              END ELSE BEGIN
                DCApprovalsBridge.AdvancedApprovalFlowComplete(ApprovalEntry);
              END;
            END;

          END;
      END;
    END;

    LOCAL PROCEDURE SetPurchInvHeader@1100528202(NewPurchHeaderCode@1000000000 : Code[20]);
    BEGIN
      //**4PS
      PurchInvHeader.GET(NewPurchHeaderCode);
    END;

    LOCAL PROCEDURE SetCreditMemoHeader@1100528203(NewPurchCreditMemoCode@1000000000 : Code[20]);
    BEGIN
      //**4PS
      PurchCrMemoHdr.GET(NewPurchCreditMemoCode);
    END;

    LOCAL PROCEDURE FindAndSetAppvlGroup4PS@1100528206(lDocType@1100528200 : Integer);
    VAR
      TempPurchInvHeader@1103308000 : TEMPORARY Record 122;
      TempPurchCrMemoHdr@1100528201 : TEMPORARY Record 124;
      Vend@1103308004 : Record 23;
      TempVend@1103308007 : TEMPORARY Record 23;
      Purchaser@1103308005 : Record 13;
      TempPurchaser@1103308006 : TEMPORARY Record 13;
      DtldAppvlEntry@1000000001 : Record 6085744;
      AppvlGroup2@1000000002 : Record 6085743;
      CriterionsFulfilled@1103308003 : Boolean;
    BEGIN
      //**4PS
      IF GetLastDtldAppvlEntry(DtldAppvlEntry) THEN BEGIN
        IF AppvlGroup2.GET(DtldAppvlEntry."Approval Group Code") THEN BEGIN
          SetAppvlGroup(AppvlGroup2);
          EXIT;
        END;
      END;


      CASE lDocType OF
        2:
          BEGIN
            TempPurchInvHeader := PurchInvHeader;
            TempPurchInvHeader.INSERT;

            IF Vend.GET(PurchInvHeader."Buy-from Vendor No.") THEN BEGIN
              TempVend := Vend;
              TempVend.INSERT;
            END;

            IF Purchaser.GET(PurchInvHeader."Purchaser Code") THEN BEGIN
              TempPurchaser := Purchaser;
              TempPurchaser.INSERT;
            END;

            AppvlGroup2.SETCURRENTKEY(Priority);
            IF AppvlGroup2.FINDSET THEN
            REPEAT
              CriterionsFulfilled := TRUE;

              IF FORMAT(AppvlGroup2."Purchase Header Filter") <> '' THEN BEGIN
                TempPurchInvHeader.SETVIEW(TableFilter2View(DATABASE::"Purch. Inv. Header",
                FORMAT(AppvlGroup2."Purchase Header Filter"),TempPurchInvHeader.GETVIEW));
                IF TempPurchInvHeader.ISEMPTY THEN
                  CriterionsFulfilled := FALSE;
              END;

              IF FORMAT(AppvlGroup2."Vendor Filter") <> '' THEN BEGIN
                TempVend.SETVIEW(TableFilter2View(DATABASE::Vendor,FORMAT(AppvlGroup2."Vendor Filter"),TempVend.GETVIEW));
                IF TempVend.ISEMPTY THEN
                  CriterionsFulfilled := FALSE;
              END;

              IF FORMAT(AppvlGroup2."Purchaser Filter") <> '' THEN BEGIN
                TempPurchaser.SETVIEW(TableFilter2View(DATABASE::"Salesperson/Purchaser",
                  FORMAT(AppvlGroup2."Purchaser Filter"),TempPurchaser.GETVIEW));
                IF TempPurchaser.ISEMPTY THEN
                  CriterionsFulfilled := FALSE;
              END;

              IF CriterionsFulfilled THEN BEGIN
                SetAppvlGroup(AppvlGroup2);
                EXIT;
              END;
            UNTIL AppvlGroup2.NEXT = 0;

            ERROR(Text004,AppvlGroup.TABLECAPTION);

          END;
        3:
          BEGIN
            TempPurchCrMemoHdr := PurchCrMemoHdr;
            TempPurchCrMemoHdr.INSERT;

            IF Vend.GET(PurchCrMemoHdr."Buy-from Vendor No.") THEN BEGIN
              TempVend := Vend;
              TempVend.INSERT;
            END;

            IF Purchaser.GET(PurchCrMemoHdr."Purchaser Code") THEN BEGIN
              TempPurchaser := Purchaser;
              TempPurchaser.INSERT;
            END;

            AppvlGroup2.SETCURRENTKEY(Priority);
            IF AppvlGroup2.FINDSET THEN
              REPEAT
                CriterionsFulfilled := TRUE;

                IF FORMAT(AppvlGroup2."Purchase Header Filter") <> '' THEN BEGIN
                  TempPurchCrMemoHdr.SETVIEW(TableFilter2View(DATABASE::"Purch. Cr. Memo Hdr.",
                  FORMAT(AppvlGroup2."Purchase Header Filter"),TempPurchCrMemoHdr.GETVIEW));
                  IF TempPurchCrMemoHdr.ISEMPTY THEN
                    CriterionsFulfilled := FALSE;
                END;

                IF FORMAT(AppvlGroup2."Vendor Filter") <> '' THEN BEGIN
                  TempVend.SETVIEW(TableFilter2View(DATABASE::Vendor,FORMAT(AppvlGroup2."Vendor Filter"),TempVend.GETVIEW));
                  IF TempVend.ISEMPTY THEN
                    CriterionsFulfilled := FALSE;
                END;

                IF FORMAT(AppvlGroup2."Purchaser Filter") <> '' THEN BEGIN
                  TempPurchaser.SETVIEW(TableFilter2View(DATABASE::"Salesperson/Purchaser",
                    FORMAT(AppvlGroup2."Purchaser Filter"),TempPurchaser.GETVIEW));
                  IF TempPurchaser.ISEMPTY THEN
                    CriterionsFulfilled := FALSE;
                END;

                IF CriterionsFulfilled THEN BEGIN
                  SetAppvlGroup(AppvlGroup2);
                  EXIT;
                END;
              UNTIL AppvlGroup2.NEXT = 0;

              ERROR(Text004,AppvlGroup.TABLECAPTION);

          END;
      END;
    END;

    LOCAL PROCEDURE SyncDtldAppvlEntries4PS@1100528210(lDocType@1100528200 : Integer);
    VAR
      PurchInvLine@1000000000 : Record 123;
      PurchCrMemoLine@1100528201 : Record 125;
      DtldAppvlEntry@1000000001 : Record 6085744;
    BEGIN
      //**4PS
      CASE lDocType OF
        2:
          BEGIN

            DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purch. Inv. Line");
            DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type"::Invoice);
            DtldAppvlEntry.SETRANGE("Document No.",PurchInvHeader."No.");

            PurchInvLine.SETRANGE("Document No.",PurchHeader."No.");

            //**4PS.sn
            IF PurchInvHeader."Amounts only" THEN
              PurchInvLine.SETFILTER("Line Amount", '<>%1', 0)
            ELSE
            //**4PS.en
              PurchInvLine.SETFILTER(Quantity,'<>%1',0);

            IF NOT PurchInvLine.FINDSET THEN BEGIN
              DtldAppvlEntry.DELETEALL(TRUE);
              RecreateDocApproval4PS(lDocType);
            END ELSE BEGIN
              REPEAT
                IF NOT DtldAppvlEntry.GET(DATABASE::"Purch. Inv. Line",PurchHeader."Document Type"::Invoice,
                  PurchInvLine."Document No.",PurchInvLine."Line No.")
                THEN BEGIN
                  CreateDtldAppvlEntry4PS(lDocType,PurchInvLine,PurchCrMemoLine);
                  RecreateDocApproval4PS(lDocType);
                END ELSE BEGIN
                  // This function check to see if information that controls who
                  // needs to approve the line on the Purchase Line has changed.
                  // If so, we will recreate the line which needs to be reapproved.
                  IF HasPurchLineChanged4PS(lDocType, PurchInvLine,PurchCrMemoLine,DtldAppvlEntry) THEN BEGIN
                    DtldAppvlEntry.DELETE(TRUE);
                    CreateDtldAppvlEntry4PS(lDocType, PurchInvLine,PurchCrMemoLine);
                    RecreateDocApproval4PS(lDocType);
                 END;
                END;
             UNTIL PurchInvLine.NEXT = 0;
            END;

            // Check if any Detailed Approval Entry exists, which dosent have an corresponding Purchase Line
            IF DtldAppvlEntry.FINDSET THEN
            REPEAT
              IF NOT PurchInvLine.GET(DtldAppvlEntry."Document No.",DtldAppvlEntry."Line No.") THEN BEGIN
                DtldAppvlEntry.DELETE(TRUE);
                RecreateDocApproval4PS(lDocType);
              END;
            UNTIL DtldAppvlEntry.NEXT = 0;

          END;
        3:
          BEGIN

            DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purch. Cr. Memo Line");
            DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type"::Invoice);
            DtldAppvlEntry.SETRANGE("Document No.",PurchCrMemoHdr."No.");

            PurchCrMemoLine.SETRANGE("Document No.",PurchCrMemoHdr."No.");

            //**4PS.sn
            IF PurchCrMemoHdr."Amounts only" THEN
              PurchCrMemoLine.SETFILTER("Line Amount", '<>%1', 0)
            ELSE
            //**4PS.en
              PurchCrMemoLine.SETFILTER(Quantity,'<>%1',0);

            IF NOT PurchCrMemoLine.FINDSET THEN BEGIN
              DtldAppvlEntry.DELETEALL(TRUE);
              RecreateDocApproval4PS(lDocType);
            END ELSE BEGIN
              REPEAT
                IF NOT DtldAppvlEntry.GET(DATABASE::"Purch. Cr. Memo Line",PurchHeader."Document Type"::Invoice,
                  PurchCrMemoLine."Document No.",PurchCrMemoLine."Line No.")
                THEN BEGIN
                  CreateDtldAppvlEntry4PS(lDocType,PurchInvLine,PurchCrMemoLine);
                  RecreateDocApproval4PS(lDocType);
                END ELSE BEGIN
                  // This function check to see if information that controls who
                  // needs to approve the line on the Purchase Line has changed.
                  // If so, we will recreate the line which needs to be reapproved.
                  IF HasPurchLineChanged4PS(lDocType, PurchInvLine, PurchCrMemoLine,DtldAppvlEntry) THEN BEGIN
                    DtldAppvlEntry.DELETE(TRUE);
                    CreateDtldAppvlEntry4PS(lDocType,PurchInvLine, PurchCrMemoLine);
                    RecreateDocApproval4PS(lDocType);
                 END;
                END;
             UNTIL PurchCrMemoLine.NEXT = 0;
            END;

            // Check if any Detailed Approval Entry exists, which dosent have an corresponding Purchase Line
            IF DtldAppvlEntry.FINDSET THEN
            REPEAT
              IF NOT PurchCrMemoLine.GET(DtldAppvlEntry."Document No.",DtldAppvlEntry."Line No.") THEN BEGIN
                DtldAppvlEntry.DELETE(TRUE);
                RecreateDocApproval4PS(lDocType);
              END;
            UNTIL DtldAppvlEntry.NEXT = 0;

          END;
      END;
    END;

    LOCAL PROCEDURE RecreateDocApproval4PS@1100528211(lDocType@1100528200 : Integer);
    VAR
      DtldAppvlEntry@161024012 : Record 6085744;
      Amount@161024013 : Decimal;
      AmountLCY@161024014 : Decimal;
    BEGIN
      //**4PS
      IF AppvlGroup."Four Eyes Approval" = AppvlGroup."Four Eyes Approval"::" " THEN
        EXIT;

      CASE lDocType OF
        2:
          BEGIN
            DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purch. Inv. Line");
            DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type"::Invoice);
            DtldAppvlEntry.SETRANGE("Document No.",PurchInvHeader."No.");
            IF DtldAppvlEntry.FINDFIRST THEN
              REPEAT
                Amount := Amount + DtldAppvlEntry.Amount;
                AmountLCY := AmountLCY + DtldAppvlEntry."Amount (LCY)";
              UNTIL DtldAppvlEntry.NEXT = 0;

            DeleteAppvlEntries(DATABASE::"Purch. Inv. Header",PurchHeader."Document Type"::Invoice,PurchInvHeader."No.");

            DtldAppvlEntry.INIT;
            DtldAppvlEntry."Table ID" := DATABASE::"Purch. Inv. Header";
            DtldAppvlEntry."Document Type" := PurchHeader."Document Type"::Invoice;
            DtldAppvlEntry."Document No." := PurchInvHeader."No.";
            DtldAppvlEntry."Line No." := 0;
            DtldAppvlEntry."Approval Group Code" := AppvlGroup.Code;
            DtldAppvlEntry.Amount := Amount;
            DtldAppvlEntry."Amount (LCY)" := AmountLCY;
            DtldAppvlEntry.INSERT;

            DtldAppvlEntry."Line No." := 1;
            DtldAppvlEntry.INSERT;

          END;
        3:
          BEGIN

            DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purch. Cr. Memo Line");
            DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type"::"Credit Memo");
            DtldAppvlEntry.SETRANGE("Document No.",PurchCrMemoHdr."No.");
            IF DtldAppvlEntry.FINDFIRST THEN
              REPEAT
                Amount := Amount + DtldAppvlEntry.Amount;
                AmountLCY := AmountLCY + DtldAppvlEntry."Amount (LCY)";
              UNTIL DtldAppvlEntry.NEXT = 0;

            DeleteAppvlEntries(DATABASE::"Purch. Cr. Memo Hdr.",PurchHeader."Document Type"::"Credit Memo",PurchCrMemoHdr."No.");

            DtldAppvlEntry.INIT;
            DtldAppvlEntry."Table ID" := DATABASE::"Purch. Cr. Memo Hdr.";
            DtldAppvlEntry."Document Type" := PurchHeader."Document Type"::"Credit Memo";
            DtldAppvlEntry."Document No." := PurchCrMemoHdr."No.";
            DtldAppvlEntry."Line No." := 0;
            DtldAppvlEntry."Approval Group Code" := AppvlGroup.Code;
            DtldAppvlEntry.Amount := Amount;
            DtldAppvlEntry."Amount (LCY)" := AmountLCY;
            DtldAppvlEntry.INSERT;

            DtldAppvlEntry."Line No." := 1;
            DtldAppvlEntry.INSERT;

          END;
      END;
    END;

    LOCAL PROCEDURE CreateDtldAppvlEntry4PS@1100528212(lDocType@1100528200 : Integer;PurchInvLine@1000000000 : Record 123;PurchCrMemoLine@1100528201 : Record 125);
    VAR
      NewDtldAppvlEntry@1000000001 : Record 6085744;
      NewDtldAppvlEntryDim@1000000002 : Record 6085750;
      CurrExchRate@161024012 : Record 330;
      DimSetEntry@1100528202 : TEMPORARY Record 480;
    BEGIN
      //**4PS
      // *********************************************************************************************************************************
      // This function will create a new Detailed Approval Entry based on the posted lines
      // *********************************************************************************************************************************
      WITH NewDtldAppvlEntry DO BEGIN

        CASE lDocType OF
          2:
            BEGIN

              "Table ID" := DATABASE::"Purch. Inv. Line";
              "Document Type" := PurchHeader."Document Type"::Invoice;
              "Document No." := PurchInvLine."Document No.";
              "Line No." := PurchInvLine."Line No.";
              Amount := GetAmountExclVAT4PS(lDocType, PurchInvLine, PurchCrMemoLine);

            //"Amount (LCY)" := CurrExchRate.ExchangeAmtFCYToLCY(PurchInvHeader."Job No.",PurchInvHeader."Posting Date", //**4PS.o
              "Amount (LCY)" := CurrExchRate.ExchangeAmtFCYToLCY(1,PurchInvHeader."Job No.",PurchInvHeader."Posting Date", //**4PS.n
                 PurchInvHeader."Currency Code",
                Amount,PurchInvHeader."Currency Factor",FALSE);

              "Approval Group Code" := AppvlGroup.Code;
              INSERT;

              // Create the Detailed Approval Entry Dimensions
              DimMgt.GetDimensionSet(DimSetEntry,PurchInvLine."Dimension Set ID");
              IF DimSetEntry.FINDFIRST THEN
                REPEAT
                  IF IsActiveDimension(DimSetEntry."Dimension Code") THEN BEGIN
                    NewDtldAppvlEntryDim."Table ID" := "Table ID";
                    NewDtldAppvlEntryDim."Document Type" := "Document Type";
                    NewDtldAppvlEntryDim."Document No." := "Document No.";
                    NewDtldAppvlEntryDim."Document Line No." := "Line No.";
                    NewDtldAppvlEntryDim."Dimension Code" := DimSetEntry."Dimension Code";
                    NewDtldAppvlEntryDim."Dimension Value Code" := DimSetEntry."Dimension Value Code";
                    NewDtldAppvlEntryDim.INSERT;
                  END;
                UNTIL DimSetEntry.NEXT = 0;


            END;
          3:
            BEGIN

              "Table ID" := DATABASE::"Purch. Cr. Memo Line";
              "Document Type" := PurchHeader."Document Type"::"Credit Memo";
              "Document No." := PurchCrMemoLine."Document No.";
              "Line No." := PurchCrMemoLine."Line No.";
              Amount := GetAmountExclVAT4PS(lDocType, PurchInvLine, PurchCrMemoLine);

            //"Amount (LCY)" := CurrExchRate.ExchangeAmtFCYToLCY(PurchCrMemoHdr."Job No.",PurchCrMemoHdr."Posting Date", //**4PS.o
              "Amount (LCY)" := CurrExchRate.ExchangeAmtFCYToLCY(1,PurchCrMemoHdr."Job No.",PurchCrMemoHdr."Posting Date", //**4PS.n
                PurchCrMemoHdr."Currency Code",
                Amount,PurchCrMemoHdr."Currency Factor",FALSE);

              "Approval Group Code" := AppvlGroup.Code;
              INSERT;

              // Create the Detailed Approval Entry Dimensions
              DimMgt.GetDimensionSet(DimSetEntry,PurchCrMemoLine."Dimension Set ID");
              IF DimSetEntry.FINDFIRST THEN
                REPEAT
                  IF IsActiveDimension(DimSetEntry."Dimension Code") THEN BEGIN
                    NewDtldAppvlEntryDim."Table ID" := "Table ID";
                    NewDtldAppvlEntryDim."Document Type" := "Document Type";
                    NewDtldAppvlEntryDim."Document No." := "Document No.";
                    NewDtldAppvlEntryDim."Document Line No." := "Line No.";
                    NewDtldAppvlEntryDim."Dimension Code" := DimSetEntry."Dimension Code";
                    NewDtldAppvlEntryDim."Dimension Value Code" := DimSetEntry."Dimension Value Code";
                    NewDtldAppvlEntryDim.INSERT;
                  END;
                UNTIL DimSetEntry.NEXT = 0;

            END;
        END;

      END;
    END;

    LOCAL PROCEDURE HasPurchLineChanged4PS@1100528213(lDocType@1100528200 : Integer;PurchInvLine@1000000000 : Record 123;PurchCrMemoLine@1100528201 : Record 125;DtldAppvlEntry@1000000001 : Record 6085744) : Boolean;
    VAR
      DtldAppvlEntryDim@1000000003 : Record 6085750;
      DimSetEntry@1100528202 : Record 480;
    BEGIN
      //**4PS
      // *********************************************************************************************************************************
      // This function checks to see if information that controls who needs to approve the line on the Purchase Line has changed.
      // If so, it will return TRUE.
      // *********************************************************************************************************************************

      // We check to see if the amount has changed. We do not consider the LCY value as a small currency variance should not result
      // in a new approval. The original LCY amount remains on the Detailed Approval Entry
      IF GetAmountExclVAT4PS(lDocType,PurchInvLine,PurchCrMemoLine) <> DtldAppvlEntry.Amount THEN
        EXIT(TRUE);

      // This function will check if any dimensions have been changed on the Purchase Line compared to the
      // Dtld. Approval Entry Dimension
      CASE lDocType OF
        2: BEGIN
             DimMgt.GetDimensionSet(DimSetEntry,PurchInvLine."Dimension Set ID");
           END;
        3: BEGIN
             DimMgt.GetDimensionSet(DimSetEntry,PurchCrMemoLine."Dimension Set ID");
           END;
      END;

      DtldAppvlEntryDim.SETRANGE("Table ID",DtldAppvlEntry."Table ID");
      DtldAppvlEntryDim.SETRANGE("Document Type",DtldAppvlEntry."Document Type");
      DtldAppvlEntryDim.SETRANGE("Document No.",DtldAppvlEntry."Document No.");
      DtldAppvlEntryDim.SETRANGE("Document Line No.",DtldAppvlEntry."Line No.");

      IF DimSetEntry.FINDSET THEN
        REPEAT
          IF IsActiveDimension(DimSetEntry."Dimension Code") THEN BEGIN
            IF NOT DtldAppvlEntryDim.GET(DtldAppvlEntry."Table ID",DtldAppvlEntry."Document Type",DtldAppvlEntry."Document No.",
              DtldAppvlEntry."Line No.",DimSetEntry."Dimension Code")
            THEN
              EXIT(TRUE);

            IF DtldAppvlEntryDim."Dimension Value Code" <> DimSetEntry."Dimension Value Code" THEN
              EXIT(TRUE);
          END;
        UNTIL DimSetEntry.NEXT = 0;

      IF DtldAppvlEntryDim.FINDSET THEN
        REPEAT
          IF IsActiveDimension(DtldAppvlEntryDim."Dimension Code") THEN BEGIN
            IF NOT DimSetEntry.GET(DtldAppvlEntryDim."Table ID",DtldAppvlEntryDim."Document Type",DtldAppvlEntryDim."Document No.",
              DtldAppvlEntryDim."Document Line No.",DtldAppvlEntryDim."Dimension Code")
            THEN
              EXIT(TRUE);

            IF DtldAppvlEntryDim."Dimension Value Code" <> DimSetEntry."Dimension Value Code" THEN
              EXIT(TRUE);
          END;
        UNTIL DtldAppvlEntryDim.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateDtldAppvlEntries4PS@1100528217(lDocType@1100528200 : Integer;ActionToPerform@161024018 : 'Approve,FindNextApprover';VAR CurrApproverId@161024013 : Code[20]);
    VAR
      AppvlUser@1000000003 : Record 6085746;
      AppvlUserDim@1000000005 : Record 6085748;
      DtldAppvlEntry@161024015 : Record 6085744;
      TempSumDtldAppvlEntry@1000000001 : TEMPORARY Record 6085744;
      TempSumDtldAppvlEntryDim@1000000000 : TEMPORARY Record 6085750;
      EntryRelationship@161024014 : TEMPORARY Record 6085747;
      CompleteDimMatch@161024012 : Boolean;
    BEGIN
      //**4PS
      // This will sum all the approval lines based on the the information that controls who needs to approve the line
      SumApprovalLines(TRUE,TempSumDtldAppvlEntry,TempSumDtldAppvlEntryDim,EntryRelationship);

      AppvlUser.SETRANGE("Approval Group Code",AppvlGroup.Code);

      CASE lDocType OF
        2:
        BEGIN
          DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purch. Inv. Line");
          DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type"::Invoice);
          DtldAppvlEntry.SETRANGE("Document No.",PurchInvHeader."No.");
        END;
        3:
        BEGIN
          DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purch. Cr. Memo Line");
          DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type"::"Credit Memo");
          DtldAppvlEntry.SETRANGE("Document No.",PurchCrMemoHdr."No.");
        END;
      END;

      DtldAppvlEntry.SETRANGE("Approval Group Code",AppvlGroup.Code);
      DtldAppvlEntry.SETRANGE(Approved,FALSE);
      DtldAppvlEntry.MODIFYALL("User ID",'');
      DtldAppvlEntry.SETRANGE(Approved);

      // Loop through each of the sum lines
      IF TempSumDtldAppvlEntry.FINDSET THEN
        REPEAT
          // Filter the Approval User to find all user records (of the current user) that has the right limit
          IF CurrApproverId <> '' THEN BEGIN
            AppvlUser.SETCURRENTKEY("Approval Group Code","User ID","Approval Amount Limit");
            AppvlUser.SETRANGE("User ID",CurrApproverId);
          END ELSE
            AppvlUser.SETCURRENTKEY("Approval Group Code","Approval Amount Limit");

          AppvlUser.SETRANGE("Approval Amount Limit",TempSumDtldAppvlEntry."Amount (LCY)",9999999999.0);
          IF AppvlUser.FINDSET THEN
            REPEAT
              CompleteDimMatch := TRUE;

              DtldAppvlEntry.SETCURRENTKEY("Table ID","Document Type","Document No.","Approval Group Code","User ID");
              DtldAppvlEntry.SETRANGE("User ID",AppvlUser."User ID");
              DtldAppvlEntry.CALCSUMS("Amount (LCY)");
              IF AppvlUser."Approval Amount Limit" - DtldAppvlEntry."Amount (LCY)" < TempSumDtldAppvlEntry."Amount (LCY)" THEN
                CompleteDimMatch := FALSE;

              IF CompleteDimMatch THEN BEGIN
                DtldAppvlEntry.GET(DATABASE::"Purchase Line",PurchHeader."Document Type",PurchHeader."No.",
                  EntryRelationship."Approval Entry Line No.");

      // Filter the dimensions associated to the current sum line. Then make sure that the current Approval User record have the
                // same dimensions.
                TempSumDtldAppvlEntryDim.SETRANGE("Table ID",TempSumDtldAppvlEntry."Table ID");
                IF TempSumDtldAppvlEntryDim.FINDFIRST THEN BEGIN
                  REPEAT
                    IF NOT AppvlUserDim.GET(AppvlUser."Approval Group Code",AppvlUser."User ID",AppvlUser."Entry No.",
                      TempSumDtldAppvlEntryDim."Dimension Code",TempSumDtldAppvlEntryDim."Dimension Value Code")
                    THEN
                      CompleteDimMatch := FALSE;
                  UNTIL (TempSumDtldAppvlEntryDim.NEXT = 0) OR (NOT CompleteDimMatch);
                END;

                // CompleteDimMatch is TRUE if the current Approval User record have all the dimensions used on the sum line
                IF CompleteDimMatch THEN BEGIN
                  CurrApproverId := AppvlUser."User ID";

                  // The EntryRelationship variable holds the relationship between the sum line and the Detailed Approval Entries
                  // So now we need to flag the Detailed Approval Entries that are associated to this sum line.
                  EntryRelationship.SETRANGE("Sum Line Id",TempSumDtldAppvlEntry."Table ID");
                  IF EntryRelationship.FINDFIRST THEN
                    REPEAT
                      DtldAppvlEntry.GET(DATABASE::"Purchase Line",PurchHeader."Document Type",PurchHeader."No.",
                        EntryRelationship."Approval Entry Line No.");

                      IF ActionToPerform = ActionToPerform::Approve THEN BEGIN
                        IF NOT DtldAppvlEntry.Approved THEN BEGIN
                          DtldAppvlEntry."User ID" := CurrApproverId;
                          DtldAppvlEntry.Approved := TRUE;
                          DtldAppvlEntry.MODIFY;
                        END;
                      END ELSE BEGIN
                        DtldAppvlEntry."User ID" := CurrApproverId;
                        DtldAppvlEntry.MODIFY;
                      END;
                    UNTIL EntryRelationship.NEXT = 0;
                END;
              END;
            UNTIL (AppvlUser.NEXT = 0) OR (CompleteDimMatch);
        UNTIL TempSumDtldAppvlEntry.NEXT = 0;
    END;

    PROCEDURE GetAmountExclVAT4PS@1100528227(lDocType@1100528202 : Integer;PurchInvLine@1100528201 : Record 123;PurchCrMemoLine@1100528200 : Record 125) : Decimal;
    VAR
      TempVATAmountLine@1000000002 : TEMPORARY Record 290;
    BEGIN
      //**4PS
      CASE lDocType OF
        2:
        BEGIN
          IF NOT PurchInvHeader."Prices Including VAT" THEN
            EXIT(PurchInvLine."Line Amount")
          ELSE BEGIN
            PurchInvLine.SETRANGE("Line No.",PurchInvLine."Line No.");
            PurchInvLine.CalcVATAmountLines(PurchInvHeader, TempVATAmountLine);
            EXIT(TempVATAmountLine."Line Amount" - TempVATAmountLine."VAT Amount");
          END;
        END;
        3:
        BEGIN
          IF NOT PurchCrMemoHdr."Prices Including VAT" THEN
            EXIT(PurchCrMemoLine."Line Amount")
          ELSE BEGIN
            PurchCrMemoLine.SETRANGE("Line No.",PurchCrMemoLine."Line No.");
            PurchCrMemoLine.CalcVATAmountLines(PurchCrMemoHdr,TempVATAmountLine);
            EXIT(TempVATAmountLine."Line Amount" - TempVATAmountLine."VAT Amount");
          END;
        END;

      END;
    END;

    LOCAL PROCEDURE UpdateDocDtldAppvlEntries4PS@1100528204(lDocType@1100528200 : Integer;ActionToPerform@161024018 : 'Approve,FindNextApprover';VAR CurrApproverId@161024013 : Code[20]) : Boolean;
    VAR
      AppvlUser@1000000003 : Record 6085746;
      AppvlUserDim@1000000005 : Record 6085748;
      DtldAppvlEntry@161024015 : Record 6085744;
      DtldAppvlEntryDim@1000000000 : Record 6085750;
      ExcludeApproverID@161024014 : Code[20];
      AmountLCY@161024016 : Decimal;
      CompleteDimMatch@161024012 : Boolean;
    BEGIN
      // *********************************************************************************************************************************
      // This function are used in two ways:
      // 1) Called with ActionToPerform = Approve:
      //    The function will sum/group the 'Line Amount (LCY)' of all unapproved Detailed Approval Entries with the same dimensions and
      //    see if the CurrApproverId has the nesseary Approval Limit. If so, it will approve these Detailed Approval Entries.
      // 2) Called with ActionToPerform = FindNextApprover and CurrApproverId = '':
      //    The function will sum/group the 'Line Amount (LCY)' of all unapproved Detailed Approval Entries with the same dimensions.
      //    Next, it will find the first approver who can approve the sum/group of the first uapproved dimension combination.
      //    The User Id of this approver is updated on the Detailed Approval Entries for this dimension combination.
      //    Next, it will see if it can find other sum/groups that this user can approve, and if so, it will update the user id on these
      //    entries as well.
      // *********************************************************************************************************************************

      // See if this document have already been approved on a total level. This can be the cause if two persons needs
      // to approve the full amount. If so, find the first approver and save it in a variable to check when we find the
      // next approver (which must be different than the first).
      CASE lDocType OF
        2:
        BEGIN
          DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purch. Inv. Header");
          DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type"::Invoice);
          DtldAppvlEntry.SETRANGE("Document No.",PurchInvHeader."No.");
          DtldAppvlEntry.SETRANGE(Approved,TRUE);
          IF DtldAppvlEntry.FINDFIRST THEN
            ExcludeApproverID := DtldAppvlEntry."User ID";
        END;
        3:
        BEGIN
          DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purch. Cr. Memo Hdr.");
          DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type"::"Credit Memo");
          DtldAppvlEntry.SETRANGE("Document No.",PurchCrMemoHdr."No.");
          DtldAppvlEntry.SETRANGE(Approved,TRUE);
          IF DtldAppvlEntry.FINDFIRST THEN
            ExcludeApproverID := DtldAppvlEntry."User ID";
        END;
      END;

      DtldAppvlEntry.SETRANGE(Approved,FALSE);
      IF NOT DtldAppvlEntry.FINDFIRST THEN
        EXIT(TRUE); // Completely approved

      IF (ActionToPerform = ActionToPerform::Approve) AND
        (AppvlGroup."Four Eyes Approval" = AppvlGroup."Four Eyes Approval"::Invoice)
      THEN BEGIN
        DtldAppvlEntry."User ID" := CurrApproverId;
        DtldAppvlEntry.Approved := TRUE;
        DtldAppvlEntry.MODIFY;
        EXIT;
      END;

      CASE lDocType OF
        2:
        BEGIN
          DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purch. Inv. Line");
        END;
        3:
        BEGIN
          DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purch. Cr. Memo Line");
        END;
      END;


      // No Approver ID have been excluded so far. If it is the same approver on all lines, then this user can not approve the
      // full amount and therefore we need to exclude this user.
      IF AppvlGroup."Four Eyes Approval" = AppvlGroup."Four Eyes Approval"::Invoice THEN
        IF ExcludeApproverID = '' THEN BEGIN
          DtldAppvlEntry.FINDFIRST;
          DtldAppvlEntry.SETFILTER("User ID",'<>%1',DtldAppvlEntry."User ID");
          IF DtldAppvlEntry.ISEMPTY THEN
            ExcludeApproverID := DtldAppvlEntry."User ID";

          DtldAppvlEntry.SETRANGE("User ID");
        END;

      AppvlUser.SETRANGE("Approval Group Code",AppvlGroup.Code);

      IF DtldAppvlEntry.FINDSET THEN
        REPEAT
          AmountLCY := AmountLCY + DtldAppvlEntry."Amount (LCY)";
        UNTIL DtldAppvlEntry.NEXT = 0;

      IF CurrApproverId <> '' THEN BEGIN
        AppvlUser.SETCURRENTKEY("Approval Group Code","User ID","Approval Amount Limit");
        AppvlUser.SETRANGE("User ID",CurrApproverId);
      END ELSE
        AppvlUser.SETCURRENTKEY("Approval Group Code","Approval Amount Limit");

      AppvlUser.SETRANGE("Approval Amount Limit",AmountLCY,9999999999.0);
      IF AppvlUser.FINDSET THEN
        REPEAT
          IF ExcludeApproverID <> AppvlUser."User ID" THEN BEGIN
            CompleteDimMatch := TRUE;

            CASE lDocType OF
              2:
              BEGIN
                DtldAppvlEntryDim.SETRANGE("Table ID",DATABASE::"Purch. Inv. Line");
                DtldAppvlEntryDim.SETRANGE("Document Type",PurchHeader."Document Type"::Invoice);
                DtldAppvlEntryDim.SETRANGE("Document No.",PurchInvHeader."No.");
              END;
              3:
              BEGIN
                DtldAppvlEntryDim.SETRANGE("Table ID",DATABASE::"Purch. Cr. Memo Line");
                DtldAppvlEntryDim.SETRANGE("Document Type",PurchHeader."Document Type"::"Credit Memo");
                DtldAppvlEntryDim.SETRANGE("Document No.",PurchCrMemoHdr."No.");
              END;
            END;

            IF DtldAppvlEntryDim.FINDFIRST THEN
              REPEAT
                IF NOT AppvlUserDim.GET(AppvlUser."Approval Group Code",AppvlUser."User ID",AppvlUser."Entry No.",
                  DtldAppvlEntryDim."Dimension Code",DtldAppvlEntryDim."Dimension Value Code")
                THEN
                  CompleteDimMatch := FALSE;
              UNTIL (DtldAppvlEntryDim.NEXT = 0) OR (NOT CompleteDimMatch);
            IF CompleteDimMatch THEN
              CurrApproverId := AppvlUser."User ID";
          END;
        UNTIL (AppvlUser.NEXT = 0) OR (CompleteDimMatch);

      IF CompleteDimMatch THEN BEGIN

        CASE lDocType OF
          2:
          BEGIN
            DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purch. Inv. Header");
            DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type"::Invoice);
            DtldAppvlEntry.SETRANGE("Document No.",PurchInvHeader."No.");
          END;
          3:
          BEGIN
            DtldAppvlEntry.SETRANGE("Table ID",DATABASE::"Purch. Cr. Memo Hdr.");
            DtldAppvlEntry.SETRANGE("Document Type",PurchHeader."Document Type"::"Credit Memo");
            DtldAppvlEntry.SETRANGE("Document No.",PurchCrMemoHdr."No.");
          END;
        END;
        DtldAppvlEntry.SETRANGE(Approved,FALSE);
        DtldAppvlEntry.FINDFIRST;

        IF ActionToPerform = ActionToPerform::Approve THEN BEGIN
          DtldAppvlEntry."User ID" := CurrApproverId;
          DtldAppvlEntry.Approved := TRUE;
          DtldAppvlEntry.MODIFY;
          EXIT(FALSE);
        END ELSE BEGIN
          DtldAppvlEntry."User ID" := CurrApproverId;
          DtldAppvlEntry.MODIFY;
          EXIT(TRUE);
        END;
      END ELSE
        EXIT(FALSE);
    END;

    PROCEDURE CheckMandatoryDim4PS@1100528205(lDocType@1100528202 : Integer);
    VAR
      PurchInvLine@1000000000 : Record 123;
      AppvlGroupDim@1000000002 : Record 6085751;
      PurchCrMemoLine@1100528201 : Record 125;
      DimSetEntry@1100528200 : Record 480;
    BEGIN
      AppvlGroupDim.SETRANGE("Approval Group Code",AppvlGroup.Code);
      AppvlGroupDim.SETRANGE(Mandatory,TRUE);
      IF AppvlGroupDim.ISEMPTY THEN
        EXIT;

        CASE lDocType OF
          2:
          BEGIN
            PurchInvLine.SETRANGE("Document No.",PurchInvHeader."No.");
            PurchInvLine.SETFILTER(Quantity,'<>%1',0);
            IF PurchInvLine.FINDSET THEN
              REPEAT
                IF AppvlGroupDim.FINDSET THEN
                  REPEAT
                    DimMgt.GetDimensionSet(DimSetEntry,PurchInvLine."Dimension Set ID");
                    DimSetEntry.SETRANGE("Dimension Code",AppvlGroupDim."Dimension Code");
                    IF DimSetEntry.ISEMPTY THEN
                      ERROR(Text007,AppvlGroupDim."Dimension Code");
                  UNTIL AppvlGroupDim.NEXT = 0;

              UNTIL PurchInvLine.NEXT = 0;
          END;
          3:
          BEGIN
            PurchCrMemoLine.SETRANGE("Document No.",PurchCrMemoHdr."No.");
            PurchCrMemoLine.SETFILTER(Quantity,'<>%1',0);
            IF PurchCrMemoLine.FINDSET THEN
              REPEAT
                IF AppvlGroupDim.FINDSET THEN
                  REPEAT
                    DimMgt.GetDimensionSet(DimSetEntry,PurchCrMemoLine."Dimension Set ID");
                    DimSetEntry.SETRANGE("Dimension Code",AppvlGroupDim."Dimension Code");
                    IF DimSetEntry.ISEMPTY THEN
                      ERROR(Text007,AppvlGroupDim."Dimension Code");
                  UNTIL AppvlGroupDim.NEXT = 0;
              UNTIL PurchCrMemoLine.NEXT = 0;
          END;
        END;
    END;

    BEGIN
    {
      *  There is a one-to-one relationship between a 'Detailed Approval Line' and a 'Purchase Line'. The Purchase Line dimensions
         are also stored in a table called 'Dtld. Approval Entry Dimension'.

         The engine always start by making sure that the values that are used to deside who needs to approve an a document,
         are the same on the Detailed Approval Line and Purchase Line and the. This is information like the Line Amount
         and the activated Dimension and Dimension Values for the paticualar Approval Group. IF any of these have changed
         when the process is started then the engine will recreate the changed lines in the 'Detailed Approval Line' table and
         'Dtld. Approval Entry Dimension' table, based on the Purchase Lines. These new 'Detailed Approval Line' will be flagged
         as not Approved.

         When a user approve a document the engine will start by executing the logic described above. After that it will try to
         approve as many lines as possible, based on the Amount and Dimension allowance specified on the Approval User.

         Next, the engine will find the next person to approve the document and create the Approval Entry to this person. At the same
         time it will stamp the user id to the approver to the 'Detailed Approval Line'.
    }
    END.
  }
}

