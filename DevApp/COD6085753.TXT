OBJECT Codeunit 6085753 Purch. Rcpt. - Validation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.3.50.06;
  }
  PROPERTIES
  {
    TableNo=6085590;
    OnRun=VAR
            Field@161024017 : Record 6085580;
            PurchRcptHeader@161024016 : Record 120;
            Comment@161024018 : Record 6085594;
            CaptureMgnt@161024015 : Codeunit 6085576;
            PurchRcptNo@161024013 : Code[20];
            OrderNo@161024014 : Code[20];
            IsValid@161024012 : Boolean;
          BEGIN
            CODEUNIT.RUN(CODEUNIT::"Doc. - Field Validation",Rec);

            PurchRcptNo := COPYSTR(CaptureMgnt.GetText(Rec,Field.Type::Header,Text001,0),1,MAXSTRLEN(PurchRcptNo));
            OrderNo := COPYSTR(CaptureMgnt.GetText(Rec,Field.Type::Header,Text002,0),1,MAXSTRLEN(OrderNo));

            IF (PurchRcptNo <> '') AND (OrderNo <> '') THEN BEGIN
              IF NOT PurchRcptHeader.GET(PurchRcptNo) THEN BEGIN
                Comment.Add(Rec,Field,0,Comment.Area::Validation,Comment."Comment Type"::Error,STRSUBSTNO(Text003,PurchRcptNo));
                OK := FALSE;
              END ELSE BEGIN
                IF OrderNo <> PurchRcptHeader."Order No." THEN BEGIN
                  Field.GET("Template No.",Field.Type::Header,Text002);
                  Comment.Add(Rec,Field,0,Comment.Area::Validation,Comment."Comment Type"::Error,
                    STRSUBSTNO(Text004,Field."Field Name",OrderNo,PurchRcptHeader."Order No."));
                  OK := FALSE;
                END;
              END;
            END;

            MODIFY;
          END;

  }
  CODE
  {
    VAR
      Text001@161024012 : TextConst 'ENU=RCPTNO';
      Text002@161024013 : TextConst 'ENU=ORDERNO';
      Text003@161024014 : TextConst 'ENU=Purchase Receipt %1 does not exist.';
      Text004@161024015 : TextConst 'ENU=%1 %2 is different from %3 on Purchase Receipt.';

    BEGIN
    END.
  }
}

