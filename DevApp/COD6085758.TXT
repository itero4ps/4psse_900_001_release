OBJECT Codeunit 6085758 Doc. - Move to Company
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.00.03,4PS9.00;
  }
  PROPERTIES
  {
    TableNo=6085590;
    OnRun=BEGIN
            CopyDocuments(Rec);
          END;

  }
  CODE
  {
    VAR
      Text001@1160040004 : TextConst 'ENU=You cannot assign numbers greater than %1 from the number series %2.';
      Text002@1160040003 : TextConst 'ENU=The number %1 cannot be extended to more than 20 characters.';
      Text003@1160040002 : TextConst 'ENU=%1 on the %2 cannot be empty in company %3.';
      Text004@1160040009 : TextConst 'ENU=%1 not supported for %2 when copying documents between companies.';
      Text005@1160040010 : TextConst 'ENU=Only one %1 is supported when copying documents between companies.';
      Text006@1160040000 : TextConst 'ENU=%1 document(s) were moved to company %2.';
      Text007@1160040005 : TextConst 'ENU=%1 on the %2 must be the same in both companies.';
      HideWindow@1160040001 : Boolean;
      CompName@1160040006 : Text[30];
      Text008@1160040007 : TextConst 'ENU=" ,.-;:/\*+-"';

    PROCEDURE CopyDocuments@1160040002(VAR Document@1160040002 : Record 6085590);
    VAR
      Comp@1160040001 : Record 2000000006;
      OldSetup@1160040004 : Record 6085573;
      NewSetup@1160040003 : Record 6085573;
      NoOfRec@1160040000 : Integer;
    BEGIN
      IF NOT HideWindow THEN BEGIN
        Comp.FILTERGROUP(4);
        Comp.SETFILTER(Name,'<>%1',COMPANYNAME);
        Comp.FILTERGROUP(0);
        IF PAGE.RUNMODAL(PAGE::Companies,Comp) <> ACTION::LookupOK THEN
          EXIT;
      END ELSE
        Comp.GET(CompName);

      OldSetup.GET;
      NewSetup.CHANGECOMPANY(Comp.Name);
      NewSetup.GET;

      IF NewSetup."Document Nos." = '' THEN
        ERROR(Text003,NewSetup.FIELDCAPTION("Document Nos."),NewSetup.TABLECAPTION,Comp.Name);
      IF NewSetup."TIFF File Path" = '' THEN
        ERROR(Text003,NewSetup.FIELDCAPTION("TIFF File Path"),NewSetup.TABLECAPTION,Comp.Name);
      IF NewSetup."PDF File Path" = '' THEN
        ERROR(Text003,NewSetup.FIELDCAPTION("PDF File Path"),NewSetup.TABLECAPTION,Comp.Name);

      Document.SETRANGE(Status,Document.Status::Open);
      NoOfRec := Document.COUNT;
      IF Document.FINDSET THEN
        REPEAT
          CopyDocument(Document,NewSetup,Comp.Name);
        UNTIL Document.NEXT = 0;

      IF NOT HideWindow THEN
        MESSAGE(Text006,NoOfRec,Comp.Name);
    END;

    PROCEDURE CopyDocument@1160040000(VAR OldDocument@1160040011 : Record 6085590;NewSetup@1160040008 : Record 6085573;NewCompName@1160040000 : Text[30]);
    VAR
      OldDocCat@1160040013 : Record 6085575;
      NewDocCat@1160040002 : Record 6085575;
      OldDocuments@1160040007 : Record 6085590;
      NewDocument@1160040001 : Record 6085590;
      OldDocPage@1160040003 : Record 6085591;
      NewDocPage@1160040004 : Record 6085591;
      OldDocWord@1160040005 : Record 6085592;
      NewDocWord@1160040006 : Record 6085592;
      EmptyGuid@1160040009 : GUID;
    BEGIN
      OldDocument.TESTFIELD("File Type",OldDocument."File Type"::OCR);
      OldDocument.TESTFIELD(Status,OldDocument.Status::Open);

      OldDocCat.GET(OldDocument."Document Category Code");
      NewDocCat.CHANGECOMPANY(NewCompName);
      NewDocCat.GET(OldDocument."Document Category Code");
      IF NewDocCat."Archive E-mails on Document" <> OldDocCat."Archive E-mails on Document" THEN
        ERROR(Text007,NewDocCat.FIELDCAPTION("Archive E-mails on Document"),NewDocCat.TABLECAPTION);

      IF (NewDocCat."Archive E-mails on Document") AND (NewSetup."E-mail File Path" = '') THEN
        ERROR(Text003,NewSetup.FIELDCAPTION("E-mail File Path"),NewSetup.TABLECAPTION,NewCompName);

      NewDocument.CHANGECOMPANY(NewCompName);
      NewDocument.TRANSFERFIELDS(OldDocument,TRUE);
      NewDocument."No." := GetNextDocNo(NewSetup,NewCompName);
      NewDocument.VALIDATE("Source Record ID Tree ID",0);
      NewDocument.INSERT(TRUE);

      OldDocPage.SETRANGE("Document No.",OldDocument."No.");
      IF OldDocPage.FINDSET THEN
        REPEAT
          NewDocPage.CHANGECOMPANY(NewCompName);
          NewDocPage := OldDocPage;
          NewDocPage."Document No." := NewDocument."No.";
          NewDocPage.INSERT;
        UNTIL OldDocPage.NEXT = 0;

      OldDocWord.SETRANGE("Document No.",OldDocument."No.");
      IF OldDocWord.FINDSET THEN
        REPEAT
          NewDocWord.CHANGECOMPANY(NewCompName);
          NewDocWord := OldDocWord;
          NewDocWord."Document No." := NewDocument."No.";
          NewDocWord.INSERT;
        UNTIL OldDocWord.NEXT = 0;

      NewDocument.SetDCSetup(NewSetup);
      CopyFile(OldDocument.GetTIFServerFileName,NewDocument.GetTIFServerFileName);
      CopyFile(OldDocument.GetPDFServerFileName,NewDocument.GetPDFServerFileName);
      IF (NewDocCat."Archive E-mails on Document") AND (OldDocument."E-Mail GUID" <> EmptyGuid) THEN
        CopyFile(OldDocument.GetEMailServerFileName,NewDocument.GetEMailServerFileName);

      OldDocument.DELETE(TRUE);

      COMMIT;
    END;

    PROCEDURE GetNextDocNo@12(NewSetup@1160040000 : Record 6085573;NewCompName@1160040001 : Text[30]) : Code[20];
    VAR
      NoSeries@1160040002 : Record 308;
      NoSeriesLine@1160040004 : Record 309;
      NoSeriesRelationship@1160040003 : Record 310;
    BEGIN
      NoSeriesRelationship.CHANGECOMPANY(NewCompName);
      NoSeriesRelationship.SETRANGE(Code,NewSetup."Document Nos.");
      IF NOT NoSeriesRelationship.ISEMPTY THEN
        ERROR(Text004,NoSeriesRelationship.TABLECAPTION,NewSetup.FIELDCAPTION("Document Nos."));

      NoSeries.CHANGECOMPANY(NewCompName);
      NoSeries.GET(NewSetup."Document Nos.");
      NoSeries.TESTFIELD("Default Nos.",TRUE);

      NoSeriesLine.CHANGECOMPANY(NewCompName);
      NoSeriesLine.LOCKTABLE;
      NoSeriesLine.SETRANGE("Series Code",NewSetup."Document Nos.");
      //**4PS.so
      //NoSeriesLine.FINDFIRST;
      // IF NoSeriesLine.COUNT > 1 THEN
      //   ERROR(Text005,NoSeriesLine.TABLECAPTION);
      //**4PS.eo
      //**4PS.sn
      NoSeriesLine.SETCURRENTKEY("Series Code", "Starting Date");
      NoSeriesLine.SETFILTER("Starting Date", '<=%1|%2', WORKDATE, 0D);
      NoSeriesLine.FINDLAST;
      //**4PS.en

      IF NoSeriesLine."Starting Date" > WORKDATE THEN
        NoSeriesLine.TESTFIELD("Starting Date",0D);

      NoSeriesLine.TESTFIELD(Open,TRUE);
      NoSeriesLine."Last Date Used" := WORKDATE;

      IF NoSeriesLine."Last No. Used" = '' THEN BEGIN
        NoSeriesLine.TESTFIELD("Starting No.");
        NoSeriesLine."Last No. Used" := NoSeriesLine."Starting No.";
      END ELSE
        IF NoSeriesLine."Increment-by No." <= 1 THEN
          NoSeriesLine."Last No. Used" := INCSTR(NoSeriesLine."Last No. Used")
        ELSE
          IncrementNoText(NoSeriesLine."Last No. Used",NoSeriesLine."Increment-by No.");
      IF (NoSeriesLine."Ending No." <> '') AND
         (NoSeriesLine."Last No. Used" > NoSeriesLine."Ending No.")
      THEN
        ERROR(Text001,NoSeriesLine."Ending No.",NewSetup."Document Nos.");

      NoSeriesLine.VALIDATE(Open);
      NoSeriesLine.MODIFY;

      EXIT(NoSeriesLine."Last No. Used");
    END;

    LOCAL PROCEDURE IncrementNoText@20(VAR No@1000 : Code[20];IncrementByNo@1001 : Decimal);
    VAR
      DecimalNo@1002 : Decimal;
      StartPos@1003 : Integer;
      EndPos@1004 : Integer;
      NewNo@1005 : Text[30];
    BEGIN
      GetIntegerPos(No,StartPos,EndPos);
      EVALUATE(DecimalNo,COPYSTR(No,StartPos,EndPos - StartPos + 1));
      NewNo := FORMAT(DecimalNo + IncrementByNo,0,1);
      ReplaceNoText(No,NewNo,0,StartPos,EndPos);
    END;

    LOCAL PROCEDURE ReplaceNoText@7(VAR No@1000 : Code[20];NewNo@1001 : Code[20];FixedLength@1002 : Integer;StartPos@1003 : Integer;EndPos@1004 : Integer);
    VAR
      StartNo@1005 : Code[20];
      EndNo@1006 : Code[20];
      ZeroNo@1007 : Code[20];
      NewLength@1008 : Integer;
      OldLength@1009 : Integer;
    BEGIN
      IF StartPos > 1 THEN
        StartNo := COPYSTR(No,1,StartPos - 1);
      IF EndPos < STRLEN(No) THEN
        EndNo := COPYSTR(No,EndPos + 1);
      NewLength := STRLEN(NewNo);
      OldLength := EndPos - StartPos + 1;
      IF FixedLength > OldLength THEN
        OldLength := FixedLength;
      IF OldLength > NewLength THEN
        ZeroNo := PADSTR('',OldLength - NewLength,'0');
      IF STRLEN(StartNo) + STRLEN(ZeroNo) + STRLEN(NewNo) + STRLEN(EndNo)  > 20 THEN
        ERROR(Text002,No);
      No := StartNo + ZeroNo + NewNo + EndNo;
    END;

    LOCAL PROCEDURE GetIntegerPos@4(No@1000 : Code[20];VAR StartPos@1001 : Integer;VAR EndPos@1002 : Integer);
    VAR
      IsDigit@1003 : Boolean;
      i@1004 : Integer;
    BEGIN
      StartPos := 0;
      EndPos := 0;
      IF No <> '' THEN BEGIN
        i := STRLEN(No);
        REPEAT
          IsDigit := No[i] IN ['0'..'9'];
          IF IsDigit THEN BEGIN
            IF EndPos = 0 THEN
              EndPos := i;
            StartPos := i;
          END;
          i := i - 1;
        UNTIL (i = 0) OR (StartPos <> 0) AND NOT IsDigit;
      END;
    END;

    PROCEDURE CopyFile@1160040004(FromFile@1160040001 : Text[1024];ToFile@1160040002 : Text[1024]);
    VAR
      FileSystem@1160040003 : Codeunit 6085584;
    BEGIN
      IF FILE.EXISTS(ToFile) THEN
        FILE.ERASE(ToFile);

      FILE.COPY(FromFile,ToFile);
    END;

    PROCEDURE AutoCopyDocument@1160040003(Document@1160040000 : Record 6085590) : Boolean;
    VAR
      Comp@1160040001 : Record 2000000006;
      CompIdentText@1160040002 : Record 6085611;
      DocWord@1160040004 : Record 6085592;
      BigString@1160040003 : Codeunit 6085587;
      TextFound@1160040005 : Boolean;
    BEGIN
      Comp.SETFILTER(Name,'<>%1',COMPANYNAME);
      IF NOT Comp.FINDSET THEN
        EXIT;

      CompIdentText.SETFILTER("Company Name",'<>%1',COMPANYNAME);
      IF CompIdentText.ISEMPTY THEN
        EXIT;

      DocWord.SETRANGE("Document No.",Document."No.");
      DocWord.SETRANGE("Page No.",1);
      IF NOT DocWord.FINDSET(FALSE,FALSE) THEN
        EXIT;

      REPEAT
        BigString.Append(UPPERCASE(DELCHR(DocWord.Word,'=',Text008)));
      UNTIL DocWord.NEXT = 0;


      CompIdentText.SETCURRENTKEY("Identification Text Length");
      CompIdentText.ASCENDING(FALSE);
      REPEAT
        TextFound := FALSE;

        CompIdentText.SETRANGE("Company Name",Comp.Name);
        IF CompIdentText.FINDFIRST THEN
          REPEAT
            TextFound := BigString.IndexOf(UPPERCASE(DELCHR(CompIdentText."Identification Text",'=',Text008))) <> -1;
          UNTIL TextFound OR (CompIdentText.NEXT = 0);
      UNTIL TextFound OR (Comp.NEXT = 0);

      IF TextFound THEN BEGIN
        HideWindow := TRUE;
        CompName := CompIdentText."Company Name";

        Document.SETRECFILTER;
        CopyDocuments(Document);

        EXIT(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

