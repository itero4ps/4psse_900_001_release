OBJECT Codeunit 6085774 Sales Doc. - Line Validation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.00;
  }
  PROPERTIES
  {
    TableNo=6085596;
    OnRun=VAR
            Document@1000000000 : Record 6085590;
            Comment@1000000001 : Record 6085594;
            Template@1000000010 : Record 6085579;
            Field@1000000009 : Record 6085580;
            CaptureMgnt@1000000008 : Codeunit 6085576;
            Quantity@1000000003 : Decimal;
            UnitCost@1000000004 : Decimal;
            LineAmount@1000000005 : Decimal;
            DiscAmount@1000000006 : Decimal;
            DiscPct@1000000007 : Decimal;
            LineAmount2@1000000002 : Decimal;
          BEGIN
            Field.SETRANGE("Template No.",Rec."Template No.");
            Field.SETRANGE(Type,Field.Type::Line);

            IF Field.FINDSET THEN
              REPEAT
                IF NOT CaptureMgnt.IsValidValue(Field,Rec."Document No.",Rec."Line No.") THEN BEGIN
                  Rec.OK := FALSE;
                  EXIT;
                END;
              UNTIL Field.NEXT = 0;

            Document.GET(Rec."Document No.");
            Template.GET(Rec."Template No.");

            Quantity := SalesDocMgnt.GetLineQuantity(Document,Rec."Line No.");
            UnitCost := SalesDocMgnt.GetLineUnitCost(Document,Rec."Line No.");
            DiscPct := SalesDocMgnt.GetLineDiscPct(Document,Rec."Line No.");
            DiscAmount := SalesDocMgnt.GetLineDiscAmount(Document,Rec."Line No.");
            LineAmount := SalesDocMgnt.GetLineAmount(Document,Rec."Line No.");

            IF (Quantity = 0) AND (UnitCost = 0) AND (LineAmount = 0) AND (DiscPct = 0) AND (DiscAmount = 0) THEN BEGIN
              Rec.OK := TRUE;
              EXIT;
            END;

            LineAmount2 := Quantity * UnitCost;

            IF DiscAmount <> 0 THEN
              LineAmount2 := ROUND(LineAmount2 - DiscAmount)
            ELSE IF DiscPct <> 0 THEN
              LineAmount2 := ROUND(LineAmount2 * ((100-DiscPct) / 100));

            Rec.OK := LineAmount = LineAmount2;
          END;

  }
  CODE
  {
    VAR
      SalesDocMgnt@1000000001 : Codeunit 6085770;

    BEGIN
    {
      This codeunit validates lines on sales documents
    }
    END.
  }
}

