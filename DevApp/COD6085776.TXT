OBJECT Codeunit 6085776 Sales Doc. - Register
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.4.04;
  }
  PROPERTIES
  {
    TableNo=6085590;
    OnRun=VAR
            DocCat@1160040000 : Record 6085575;
            Template@1000000012 : Record 6085579;
            SalesHeader@1000000002 : Record 36;
            SalesLine@1000000003 : Record 37;
            Field@1000000004 : Record 6085580;
            Alias@1000000014 : Record 6085584;
            SalesInvHeader@1000000000 : Record 112;
            SalesCrMemoHeader@1000000001 : Record 114;
            SalesRcptHeader@161024015 : Record 110;
            ShptHeader@161024014 : Record 110;
            CaptureMgnt@1000000009 : Codeunit 6085576;
            OrderNo@161024016 : Code[100];
            NextLineNo@1000000018 : Integer;
            DocType@1160040001 : Integer;
          BEGIN
            Template.GET(Rec."Template No.");

            CODEUNIT.RUN(Template."Codeunit ID: Doc. Validation",Rec);
            COMMIT;

            TESTFIELD(Rec.OK);

            DocCat.GET(Rec."Document Category Code");

            IF DocCat."Destination Header Table No." <> 0 THEN
              DocCat.TESTFIELD("Destination Header Table No.",DATABASE::"Sales Header");

            IF DocCat."Destination Line Table No." <> 0 THEN
              DocCat.TESTFIELD("Destination Line Table No.",DATABASE::"Sales Line");

            DocType := SalesDocMgnt.GetDocType(Rec);


            // *********************************************************************************************************************************
            // IDENTIFY THE SALESPERSON CODE FOR THIS DOCUMENT
            // *********************************************************************************************************************************
            IF NOT SetupAlias(Alias,Template,GetSourceID,SalesDocMgnt.GetOurContact(Rec)) THEN
              ERROR('');
            COMMIT;

            HasPostingError := FALSE;
            CreateWithoutMatch(Rec,SalesHeader,DocType,Alias);


            Rec.VALIDATE(Status,Rec.Status::Registered);
            Rec.MODIFY;

            COMMIT;

            IF "Show after Register" OR HasPostingError THEN BEGIN
              IF SalesHeader.FIND('=') THEN BEGIN
                CASE SalesHeader."Document Type" OF
                  SalesHeader."Document Type"::Order:
                    PAGE.RUNMODAL(PAGE::"Sales Order",SalesHeader);
                  SalesHeader."Document Type"::Invoice:
                    PAGE.RUNMODAL(PAGE::"Sales Invoice",SalesHeader);
                  SalesHeader."Document Type"::"Return Order":
                    PAGE.RUNMODAL(PAGE::"Sales Return Order",SalesHeader);
                  SalesHeader."Document Type"::"Credit Memo":
                    PAGE.RUNMODAL(PAGE::"Sales Credit Memo",SalesHeader);
                END;
              END ELSE BEGIN
                IF SalesHeader."Document Type" IN [SalesHeader."Document Type"::Order,SalesHeader."Document Type"::Invoice] THEN BEGIN
                  IF SalesHeader."Last Posting No." <> '' THEN
                    SalesInvHeader.GET(SalesHeader."Last Posting No.")
                  ELSE
                    SalesInvHeader.GET(SalesHeader."No.");
                  PAGE.RUNMODAL(PAGE::"Posted Sales Invoice",SalesInvHeader);
                END ELSE BEGIN
                  IF SalesHeader."Last Posting No." <> '' THEN
                    SalesCrMemoHeader.GET(SalesHeader."Last Posting No.")
                  ELSE
                    SalesCrMemoHeader.GET(SalesHeader."No.");
                  PAGE.RUNMODAL(PAGE::"Posted Sales Credit Memo",SalesCrMemoHeader);
                END;
              END;
            END;
          END;

  }
  CODE
  {
    VAR
      Text001@1000000004 : TextConst 'ENU=Select Salesperson from List,Translate ''%1'' to Salesperson,Use Salesperson from Customer (%2),Always use Salesperson from Customer (%2)';
      Text002@1000000005 : TextConst 'ENU=Select Salesperson from List,Use Salesperson from Customer (%1),Always use Salesperson from Customer (%1),Setup default Salesperson Code';
      Text003@1000000009 : TextConst 'ENU="No line translations exists with %1 = ''%2''\\This must be create in order to register the document."';
      CaptureMgnt@1000000008 : Codeunit 6085576;
      SalesDocMgnt@1000000010 : Codeunit 6085770;
      Text004@161024017 : TextConst 'ENU=No Account has been configured for %1.';
      HasPostingError@1000000012 : Boolean;
      Text005@1000000014 : TextConst 'ENU=Select Salesperson from List,Change Salesperson on Customer,Setup default Salesperson Code';
      Text006@161024013 : TextConst 'ENU=There is a difference between the recognized and amounts on the Order that was created. Registration Step 2 has therefore not been carried out.\\Please check the document and perform Registration Step 2 manually.';
      Text007@161024012 : TextConst 'ENU=There is a difference between the recognized and amounts on the Order that was created. Registration Step 2 has therefore not been carried out.\\Please check the document manually.';

    PROCEDURE SetupAlias@1000000004(VAR DataTransl@1000000003 : Record 6085584;VAR Template@1000000001 : Record 6085579;CustNo@1000000007 : Code[20];ContactName@1000000000 : Text[200]) : Boolean;
    VAR
      Field@1000000005 : Record 6085580;
      Salesperson@1000000002 : Record 13;
      Cust@1000000006 : Record 18;
      Selection@1000000004 : ' ,UseCustSalesCode,AlwaysUseCustSalesCode,ShowTranslation,SelectSalesperson';
    BEGIN
      WHILE TRUE DO BEGIN
        Cust.GET(CustNo);
        IF Template."Purch. Use Vend. Purch. Code" THEN BEGIN
          DataTransl.RESET;
          DataTransl.INIT;
          DataTransl."Translate to No." := Cust."Salesperson Code";
          EXIT(TRUE);
        END;

        DataTransl.RESET;
        DataTransl.SETRANGE("Template No.",Template."No.");
        DataTransl.SETRANGE(Type,Field.Type::Header);
        DataTransl.SETRANGE("Field Code",'OURCONTACT');
        DataTransl.SETRANGE("Translate From",UPPERCASE(ContactName));
        DataTransl.SETRANGE("Translate to Type",DataTransl."Translate to Type"::"Salesperson/Purchaser");
        IF DataTransl.FINDFIRST AND (DataTransl."Translate to No." <> '') THEN
          EXIT(TRUE);

        IF ContactName <> '' THEN BEGIN
          Salesperson.SETFILTER(Name,COPYSTR('@' + ContactName,1,MAXSTRLEN(Salesperson.Name)));
          IF Salesperson.FINDFIRST THEN BEGIN
            DataTransl.RESET;
            DataTransl.INIT;
            DataTransl."Translate to No." := Salesperson.Code;
            EXIT(TRUE);
          END ELSE BEGIN
            Salesperson.SETRANGE(Name);
            Salesperson.SETRANGE(Code,COPYSTR(ContactName,1,MAXSTRLEN(Salesperson.Code)));
            IF Salesperson.FINDFIRST THEN BEGIN
              DataTransl.RESET;
              DataTransl.INIT;
              DataTransl."Translate to No." := Salesperson.Code;
              EXIT(TRUE);
            END;
          END;
        END;

        IF NOT Field.GET(Template."No.",Field.Type::Header,'OURCONTACT') THEN BEGIN
          DataTransl.INIT;
          DataTransl."Translate to No." := '';
          EXIT(TRUE);
        END;

        CASE TRUE OF
          (ContactName = '') AND (Cust."Salesperson Code" <> ''):
            CASE STRMENU(STRSUBSTNO(Text002,Cust."Salesperson Code"),1) OF
              1:
                Selection := Selection::SelectSalesperson;
              2:
                Selection := Selection::UseCustSalesCode;
              3:
                Selection := Selection::AlwaysUseCustSalesCode;
              4:
                Selection := Selection::ShowTranslation;
              ELSE
                Selection := Selection::" ";
            END;

          Cust."Salesperson Code" <> '':
            CASE STRMENU(STRSUBSTNO(Text001,ContactName,Cust."Salesperson Code"),1) OF
              1:
                Selection := Selection::SelectSalesperson;
              2:
                Selection := Selection::ShowTranslation;
              3:
                Selection := Selection::UseCustSalesCode;
              4:
                Selection := Selection::AlwaysUseCustSalesCode;
              ELSE
                Selection := Selection::" ";
            END;

          ELSE BEGIN
            CASE STRMENU(Text005) OF
              1:
                Selection := Selection::SelectSalesperson;
              2:
                BEGIN
                  PAGE.RUNMODAL(PAGE::"Customer Card",Cust);
                  COMMIT;
                  Cust.GET(CustNo);
                  IF Cust."Salesperson Code" <> '' THEN
                    Selection := Selection::AlwaysUseCustSalesCode
                  ELSE
                    Selection := 99;
                END;
              3:
                Selection := Selection::ShowTranslation;
              ELSE
                Selection := Selection::" ";
            END;
          END;
        END;

        CASE Selection OF
          Selection::" ":
            EXIT(FALSE);

          Selection::ShowTranslation:
            BEGIN
              DataTransl.SETRANGE("Translate From");
              DataTransl.INIT;
              DataTransl."Template No." := Template."No.";
              DataTransl.Type := Field.Type::Header;
              DataTransl."Field Code" := 'OURCONTACT';
              DataTransl."Translate From" := UPPERCASE(ContactName);
              DataTransl."Translate to Type" := DataTransl."Translate to Type"::"Salesperson/Purchaser";
              IF DataTransl.INSERT(TRUE) THEN;
              COMMIT;
              PAGE.RUNMODAL(0,DataTransl);
            END;

          Selection::UseCustSalesCode:
            BEGIN
              DataTransl.RESET;
              DataTransl.INIT;
              DataTransl."Translate to No." := Cust."Salesperson Code";
              EXIT(TRUE);
            END;

          Selection::AlwaysUseCustSalesCode:
            BEGIN
              Template."Purch. Use Vend. Purch. Code" := TRUE;
              Template.MODIFY;
              COMMIT;
            END;

          Selection::SelectSalesperson:
            BEGIN
              Salesperson.RESET;
              IF PAGE.RUNMODAL(0,Salesperson) = ACTION::LookupOK THEN BEGIN
                DataTransl.RESET;
                DataTransl.INIT;
                DataTransl."Translate to No." := Salesperson.Code;
                EXIT(TRUE);
              END;
            END;
        END;
      END;

      CLEAR(DataTransl);
      EXIT(FALSE);
    END;

    PROCEDURE CreateSalesHeader@1000000001(VAR Document@1000000003 : Record 6085590;VAR SalesHeader@1000000002 : Record 36;DocType@1160040003 : Integer;Alias@1000000000 : Record 6085584);
    VAR
      GLSetup@1000000006 : Record 98;
      DCSetup@1000000007 : Record 6085573;
      Template@1000000004 : Record 6085579;
      Field@1000000005 : Record 6085580;
      DimValueCode@161024012 : Code[20];
      CustContact@1160040000 : Text[80];
      CurrCode@6085576 : Code[10];
      RespCenter@6085575 : Code[10];
      OldDimId@6085573 : Integer;
    BEGIN
      // *********************************************************************************************************************************
      // CREATE SALES HEADER
      // *********************************************************************************************************************************
      GLSetup.GET;
      DCSetup.GET;

      Template.GET(Document."Template No.");
      SalesHeader.SetHideValidationDialog(TRUE);

      IF SalesDocMgnt.GetOurDocNo(Document) = '' THEN BEGIN
        SalesHeader.INIT;
        SalesHeader."No. Series" := CaptureMgnt.GetText(Document,Field.Type::Header,'NOSERIES',0);
        SalesHeader.VALIDATE("Document Type",DocType);
        SalesHeader.INSERT(TRUE);

        SalesHeader.VALIDATE("Sell-to Customer No.",Document.GetSourceID);
      END ELSE
        SalesHeader.GET(DocType,SalesDocMgnt.GetOurDocNo(Document));

      CASE TRUE OF
        SalesDocMgnt.GetPostingDate(Document) <> 0D:
          SalesHeader.VALIDATE("Posting Date",SalesDocMgnt.GetPostingDate(Document));
        Template."Posting Date" = Template."Posting Date"::"Use Document Date (recognized on document)":
          SalesHeader.VALIDATE("Posting Date",SalesDocMgnt.GetDocumentDate(Document));
        ELSE
          SalesHeader.VALIDATE("Posting Date",TODAY);
      END;

      IF DocType IN [SalesHeader."Document Type"::Order,SalesHeader."Document Type"::Invoice] THEN
        SalesHeader.VALIDATE("Order Date",SalesDocMgnt.GetDocumentDate(Document))
      ELSE
        SalesHeader.VALIDATE("Document Date",SalesDocMgnt.GetDocumentDate(Document));

      SalesHeader.VALIDATE("External Document No.",SalesDocMgnt.GetDocumentNo(Document));

      IF (Alias."Translate to No." <> '') AND (SalesHeader."Salesperson Code" <> Alias."Translate to No.") THEN
        SalesHeader.VALIDATE("Salesperson Code",Alias."Translate to No.");

      CustContact := COPYSTR(SalesDocMgnt.GetCustContact(Document),1,MAXSTRLEN(SalesHeader."Sell-to Contact"));
      IF (CustContact <> '') AND (CustContact <> SalesHeader."Sell-to Contact") THEN BEGIN
        SalesHeader.VALIDATE("Sell-to Contact No.",'');
        SalesHeader.VALIDATE("Sell-to Contact",SalesDocMgnt.GetCustContact(Document));
      END;

      IF (SalesDocMgnt.GetCurrencyCode(Document) = GLSetup."LCY Code") AND (NOT DCSetup."Fill-out LCY") THEN
        CurrCode := ''
      ELSE
        CurrCode := SalesDocMgnt.GetCurrencyCode(Document);

      IF SalesHeader."Currency Code" <> CurrCode THEN
        SalesHeader.VALIDATE("Currency Code",CurrCode);

      RespCenter := SalesDocMgnt.GetResponsibillityCenter(Document);
      IF (RespCenter <> '') AND (RespCenter <> SalesHeader."Responsibility Center") THEN
        SalesHeader.VALIDATE("Responsibility Center",RespCenter);

      IF SalesHeader."Prices Including VAT" <> Template."Prices Including VAT" THEN
        SalesHeader.VALIDATE("Prices Including VAT",Template."Prices Including VAT");

      TransferSalesHeader(SalesHeader,Document);

      SalesHeader.MODIFY(TRUE);

      Field.SETRANGE("Template No.",Document."Template No.");
      Field.SETRANGE(Type,Field.Type::Header);
      Field.SETRANGE("Data Type",Field."Data Type"::Lookup);
      Field.SETRANGE("Source Table No.",DATABASE::"Dimension Value");
      IF Field.FINDSET THEN BEGIN
        OldDimId := SalesHeader."Dimension Set ID";
        REPEAT
          DimValueCode := CaptureMgnt.GetValueAsText(Document."No.",0,Field);
          IF DimValueCode <> '' THEN BEGIN
            CASE TRUE OF
              Field.Code = GLSetup."Global Dimension 1 Code":
                SalesHeader.VALIDATE("Shortcut Dimension 1 Code",DimValueCode);
              Field.Code = GLSetup."Global Dimension 2 Code":
                SalesHeader.VALIDATE("Shortcut Dimension 2 Code",DimValueCode);
              ELSE
                SalesHeader."Dimension Set ID" := UpdateDimension(SalesHeader."Dimension Set ID",Field.Code,DimValueCode);
            END;
          END;
        UNTIL Field.NEXT = 0;
      END;

      IF OldDimId <> SalesHeader."Dimension Set ID" THEN
        SalesHeader.MODIFY(TRUE);
    END;

    PROCEDURE CreateWithoutMatch@1000000000(VAR Document@1000000012 : Record 6085590;VAR SalesHeader@1000000022 : Record 36;DocType@1160040001 : Integer;Alias@1000000019 : Record 6085584);
    VAR
      Template@1000000024 : Record 6085579;
      DocumentLine@1000000023 : TEMPORARY Record 6085596;
      SalesLine@1000000021 : Record 37;
      Field@1000000020 : Record 6085580;
      TemplField2@161024016 : Record 6085580;
      LineTrans@1000000018 : Record 6085584;
      Item@161024017 : Record 27;
      ItemVendor@161024019 : Record 99;
      ItemCrossRef@161024018 : Record 5717;
      OurContact@1000000016 : Text[250];
      PostingDesc@1000000015 : Text[250];
      LineDescription@1000000014 : Text[250];
      TextLineArray@161024012 : ARRAY [50] OF Text[1024];
      IsValid@1000000006 : Boolean;
      AccountRequired@1000000005 : Boolean;
      NextLineNo@1000000003 : Integer;
      No@1000000002 : Code[50];
      NoOfTextLines@161024013 : Integer;
      Quantity@1000000007 : Decimal;
      UnitCost@1000000001 : Decimal;
      LineAmount@1000000000 : Decimal;
      LineSum@1000000025 : Decimal;
      TotalAmountExclVAT@1000000011 : Decimal;
      TotalVATAmount@1000000009 : Decimal;
      Amount@161024015 : Decimal;
      PriceDifference@1160040000 : Boolean;
      SkipStep2@1000000008 : Boolean;
      i@161024014 : Integer;
    BEGIN
      CreateSalesHeader(Document,SalesHeader,DocType,Alias);

      Template.GET(Document."Template No.");

      PostingDesc := SalesDocMgnt.GetPostingDescription(Document);

      // *********************************************************************************************************************************
      // BUILD TEMP. TABLE TO HOLD LINES. THIS IS DONE TO GET TOTAL NUMBER OF LINES
      // *********************************************************************************************************************************
      Document.BuildTempLinesTable(DocumentLine);


      // *********************************************************************************************************************************
      // IF LINES HAVE BEEN RECOGNIZED THEN START CREATING EQUIVALENT SALES LINES
      // *********************************************************************************************************************************
      IF DocumentLine.FINDSET(FALSE,FALSE) THEN BEGIN
        REPEAT
          No := SalesDocMgnt.GetLineAccountNo(Document,DocumentLine."Line No.");
          Quantity := SalesDocMgnt.GetLineQuantity(Document,DocumentLine."Line No.");
          UnitCost := SalesDocMgnt.GetLineUnitCost(Document,DocumentLine."Line No.");
          LineAmount := SalesDocMgnt.GetLineAmount(Document,DocumentLine."Line No.");
          LineSum := LineSum + LineAmount;

          // If either Quantity, Unit Cost or Line Amount has a value, then an Account No. for this line is required
          AccountRequired := (Quantity <> 0) OR (UnitCost <> 0) OR (LineAmount <> 0);

          NextLineNo := NextLineNo + 10000;
          SalesLine.INIT;
          SalesLine."Document Type" := SalesHeader."Document Type";
          SalesLine.VALIDATE("Document No.",SalesHeader."No.");
          SalesLine."Line No." := NextLineNo;
          SalesLine.INSERT(TRUE);

          IF AccountRequired THEN BEGIN
            // Filter the Line Translation table to find the appropriate account no.
            IF No <> '' THEN BEGIN
              IF NOT SalesDocMgnt.GetTranslLineInfo2(Document,DocumentLine."Line No.",LineTrans) THEN
                ERROR(Text003,LineTrans.FIELDCAPTION("Translate From"),No);
            END ELSE BEGIN
              LineTrans.RESET;
              LineTrans.SETRANGE("Template No.",Document."Template No.");
              LineTrans.SETFILTER("Translate to Type",'>%1',LineTrans."Translate to Type"::" ");
              LineTrans.SETFILTER("Translate to No.",'<>%1','');
              LineTrans.SETRANGE(Type,LineTrans.Type::"Header Field");
              LineTrans.SETRANGE("Field Code",'AMOUNTEXCLVAT');
              IF NOT LineTrans.FINDFIRST THEN BEGIN
                TemplField2.GET(Document."Template No.",Field.Type::Header,'AMOUNTEXCLVAT');
                ERROR(Text004,TemplField2."Field Name");
              END;
            END;

            SalesLine.VALIDATE(Type,LineTrans."Translate to Type");
            SalesLine.VALIDATE("No.",LineTrans."Translate to No.");
            IF LineTrans."Transl. to VAT Prod. Post.Grp." <> '' THEN
              SalesLine.VALIDATE("VAT Prod. Posting Group",LineTrans."Transl. to VAT Prod. Post.Grp.");
            IF LineTrans."Translate to Variant Code" <> '' THEN
              SalesLine.VALIDATE("Variant Code",LineTrans."Translate to Variant Code");
            SalesLine.VALIDATE("Unit of Measure Code",LineTrans."Translate to UOM Code");
          END ELSE
            LineTrans."Translate to (Text)" := '';

          LineDescription := SalesDocMgnt.GetLineDescription(Document,DocumentLine."Line No.");

          // Determine the description
          CASE TRUE OF
            LineTrans."Translate to (Text)" <> '':
              SalesLine.VALIDATE(Description,COPYSTR(LineTrans."Translate to (Text)",1,MAXSTRLEN(SalesLine.Description)));
            LineDescription <> '':
              SalesLine.VALIDATE(Description,COPYSTR(LineDescription,1,MAXSTRLEN(SalesLine.Description)));
            PostingDesc <> '':
              SalesLine.VALIDATE(Description,COPYSTR(PostingDesc,1,MAXSTRLEN(SalesLine.Description)));
          END;

          // Fill-out quantity, unit cost and line amount if present
          IF Quantity <> SalesLine.Quantity THEN
            SalesLine.VALIDATE(Quantity,Quantity);

          IF (SalesLine."Line Amount" <> LineAmount) AND (LineAmount <> 0) THEN
            PriceDifference := TRUE;

          TransferSalesLine(SalesLine,Document,DocumentLine."Line No.");

          SalesLine.MODIFY(TRUE);

          IF AccountRequired THEN
            TransferLineDim(Document,DocumentLine."Line No.",SalesLine,LineTrans);
        UNTIL DocumentLine.NEXT = 0;

        IF (LineSum <> SalesDocMgnt.GetLineTotalHeaderAmount(Document)) AND (LineSum <> 0) AND
          (SalesDocMgnt.GetLineTotalHeaderAmount(Document) <> 0)
        THEN
          CreateAmountDifferenceLine(Document,SalesHeader,SalesDocMgnt.GetLineTotalHeaderAmount(Document) - LineSum,PostingDesc);

        CreateHeaderAmounts(Document,SalesHeader,TRUE,PostingDesc);
      END ELSE BEGIN
        NoOfTextLines := CreateTextArray(PostingDesc,MAXSTRLEN(SalesLine.Description),TextLineArray);
        IF NoOfTextLines > 0 THEN
          PostingDesc := TextLineArray[1];

        CreateHeaderAmounts(Document,SalesHeader,FALSE,PostingDesc);

        FOR i := 2 TO NoOfTextLines DO
          CreateTextLine(SalesHeader,TextLineArray[i]);
      END;

      IF AmountsTransfered(SalesHeader) OR PriceDifference THEN BEGIN
        // Calculate total amounts on the newly created Invoice/Cr.Memo and check the amounts against the recognized amounts
        SalesDocMgnt.GetTotalAmounts(SalesHeader,TotalAmountExclVAT,TotalVATAmount);
        IF (SalesDocMgnt.GetAllAmountsExclVAT(Document) <> 0) OR (PriceDifference) THEN BEGIN
          IF (NOT ((SalesDocMgnt.AmountsWithinTolerance(Template,SalesDocMgnt.GetAllAmountsExclVAT(Document),TotalAmountExclVAT)) AND
            SalesDocMgnt.AmountsWithinTolerance(Template,SalesDocMgnt.GetVATAmount(Document),TotalVATAmount))) OR PriceDifference
          THEN BEGIN
            SkipStep2 := TRUE;
            IF Template."Sales Order Reg. Step 2" <> Template."Sales Order Reg. Step 2"::" " THEN
              MESSAGE(Text006)
            ELSE
              MESSAGE(Text007);
          END;
        END;
      END;

      Document."Amount Excl. VAT" := SalesDocMgnt.GetAllAmountsExclVAT(Document);
      Document."Amount Incl. VAT" := SalesDocMgnt.GetAmountInclVAT(Document);
      StoreDocReference(Document,SalesHeader);

      IF Template."Codeunit ID: After Step 1" <> 0 THEN
        CODEUNIT.RUN(Template."Codeunit ID: After Step 1",Document);

      IF NOT SkipStep2 THEN BEGIN
        PerformStep2(Template,SalesHeader);
        IF Template."Codeunit ID: After Step 2" <> 0 THEN
          CODEUNIT.RUN(Template."Codeunit ID: After Step 2",Document);
      END;
    END;

    PROCEDURE CreateAmountDifferenceLine@161024020(VAR Document@1000000002 : Record 6085590;VAR SalesHeader@1000000001 : Record 36;Amount@161024014 : Decimal;Description@1000000005 : Text[250]);
    VAR
      Field@161024012 : Record 6085580;
    BEGIN
      Field.GET(Document."Template No.",Field.Type::Header,'AMOUNTEXCLVAT');
      CreateSalesLine(Document,SalesHeader,Field,Amount,Description,FALSE);
    END;

    PROCEDURE CreateHeaderAmounts@161024015(VAR Document@1000000002 : Record 6085590;VAR SalesHeader@1000000001 : Record 36;LinesRecognised@1000000007 : Boolean;Description@1000000005 : Text[250]);
    VAR
      Template@161024015 : Record 6085579;
      DataTrans@1000000000 : Record 6085584;
      Field@161024012 : Record 6085580;
      TemplField@161024014 : Record 6085580;
      Amount@161024013 : Decimal;
    BEGIN
      Field.SETRANGE("Template No.",Document."Template No.");
      Field.SETRANGE(Type,Field.Type::Header);
      Field.SETRANGE("Data Type",Field."Data Type"::Number);
      IF LinesRecognised THEN
        Field.SETRANGE("Transfer Amount to Document",Field."Transfer Amount to Document"::Always)
      ELSE
        Field.SETFILTER("Transfer Amount to Document",'<>%1',Field."Transfer Amount to Document"::" ");

      TemplField.COPY(Field);

      Template.GET(Document."Template No.");

      IF Field.FINDSET THEN
        REPEAT
          Amount := CaptureMgnt.GetDecimal(Document,Field.Type,Field.Code,0);

          // *****************************************************************************************************************************
          // FIND POTENTIAL AMOUNTS (FROM OTHER TEMPLATE FIELDS) TO BE SUBTRACTED
          // *****************************************************************************************************************************
          TemplField.SETRANGE("Subtract from Amount Field",Field.Code);
          IF TemplField.FINDSET THEN
            REPEAT
              Amount := Amount - CaptureMgnt.GetDecimal(Document,TemplField.Type,TemplField.Code,0);
            UNTIL TemplField.NEXT = 0;

          IF Amount <> 0 THEN
            CreateSalesLine(Document,SalesHeader,Field,Amount,Description,Template."Allow Register without Amounts");
        UNTIL Field.NEXT = 0;
    END;

    PROCEDURE CreateSalesLine@1000000005(VAR Document@161024016 : Record 6085590;VAR SalesHeader@1000000001 : Record 36;Field@161024013 : Record 6085580;Amount@1000000003 : Decimal;Description@1000000005 : Text[250];CreateIfNoAccountNo@161024017 : Boolean);
    VAR
      SalesLine@1000000004 : Record 37;
      DataTransl@161024012 : Record 6085584;
      TemplField2@161024014 : Record 6085580;
      AccountNo@161024015 : Code[20];
      NextLineNo@1000000006 : Integer;
    BEGIN
      IF (TemplField2.GET(Field."Template No.",Field.Type,Field."G/L Account Field Code")) THEN BEGIN
        AccountNo := CaptureMgnt.GetValueAsText(Document."No.",0,TemplField2);
        IF AccountNo = '' THEN BEGIN
          IF NOT CreateIfNoAccountNo THEN
            ERROR(Text004,Field."Field Name")
          ELSE IF Description = '' THEN
            EXIT;
        END ELSE BEGIN
          IF NOT ((DataTransl.GET(Document."Template No.",Field.Type,Field.Code)) AND
            (DataTransl."Translate to Type" = DataTransl."Translate to Type"::"G/L Account") AND
            (DataTransl."Translate to No." = AccountNo))
          THEN BEGIN
            DataTransl.INIT;
            DataTransl."Translate to Type" := DataTransl."Translate to Type"::"G/L Account";
            DataTransl."Translate to No." := AccountNo;
          END;
        END;
      END ELSE BEGIN
        IF NOT DataTransl.GET(Document."Template No.",Field.Type,Field.Code) THEN BEGIN
          IF NOT CreateIfNoAccountNo THEN
            ERROR(Text004,Field."Field Name")
          ELSE IF Description = '' THEN
            EXIT;
        END;
      END;

      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      IF SalesLine.FINDLAST THEN
        NextLineNo := SalesLine."Line No." + 10000
      ELSE
        NextLineNo := 10000;

      // *********************************************************************************************************************************
      // CREATE THE SALES LINE
      // *********************************************************************************************************************************
      SalesLine.INIT;
      SalesLine."Document Type" := SalesHeader."Document Type";
      SalesLine.VALIDATE("Document No.",SalesHeader."No.");
      SalesLine."Line No." := NextLineNo;
      SalesLine.INSERT(TRUE);

      IF DataTransl."Translate to No." <> '' THEN BEGIN
        SalesLine.VALIDATE(Type,DataTransl."Translate to Type");
        SalesLine.VALIDATE("No.",DataTransl."Translate to No.");
        IF DataTransl."Transl. to VAT Prod. Post.Grp." <> '' THEN
          SalesLine.VALIDATE("VAT Prod. Posting Group",DataTransl."Transl. to VAT Prod. Post.Grp.");

        SalesLine.VALIDATE("Unit of Measure Code",DataTransl."Translate to UOM Code");
      END;

      CASE TRUE OF
        DataTransl."Translate to (Text)" <> '':
          SalesLine.VALIDATE(Description,COPYSTR(DataTransl."Translate to (Text)",1,MAXSTRLEN(SalesLine.Description)));
        Description <> '':
          SalesLine.VALIDATE(Description,COPYSTR(Description,1,MAXSTRLEN(SalesLine.Description)));
      END;

      IF DataTransl."Translate to No." <> '' THEN BEGIN
        SalesLine.VALIDATE(Quantity,1);
        SalesLine.VALIDATE("Unit Price",Amount);
      END;

      TransferSalesLine(SalesLine,Document,0);
      SalesLine.MODIFY(TRUE);

      TransferLineDim(Document,0,SalesLine,DataTransl);
    END;

    PROCEDURE AmountsTransfered@161024023(SalesHeader@161024013 : Record 36) : Boolean;
    VAR
      SalesLine@161024012 : Record 37;
    BEGIN
      SalesLine.RESET;
      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      SalesLine.SETFILTER("Line Amount",'<>%1',0);
      EXIT(SalesLine.FINDFIRST);
    END;

    PROCEDURE StoreDocReference@1000000008(VAR Document@1000000003 : Record 6085590;VAR SalesHeader@1000000001 : Record 36);
    BEGIN
      // *********************************************************************************************************************************
      // SAVE A REFERENCE TO THE DOCUMENT CREATE FOR LATER TRACKING ETC.
      // *********************************************************************************************************************************
      Document.Description := STRSUBSTNO('%1 %2',SalesHeader."Document Type",SalesHeader."No.");
      Document."Created Doc. Type" := DATABASE::"Sales Header";
      Document."Created Doc. Subtype" := SalesHeader."Document Type";
      Document."Created Doc. ID" := SalesHeader."No.";
      Document.MODIFY;
    END;

    PROCEDURE PerformStep2@1000000011(VAR Template@1000000000 : Record 6085579;VAR SalesHeader@1000000001 : Record 36);
    VAR
      DCApprovalsBridge@1000 : Codeunit 6085790;
    BEGIN
      SalesHeader.FIND('=');
      IF (SalesHeader."Document Type" IN [SalesHeader."Document Type"::Order,SalesHeader."Document Type"::Invoice]) THEN BEGIN
        CASE Template."Sales Order Reg. Step 2" OF
          Template."Sales Order Reg. Step 2"::"Submit for Approval":
            DCApprovalsBridge.SendSalesDocApprovalRequest(SalesHeader);

          Template."Sales Order Reg. Step 2"::Release:
            CODEUNIT.RUN(CODEUNIT::"Release Sales Document",SalesHeader);
        END;
      END ELSE IF (SalesHeader."Document Type" = SalesHeader."Document Type"::"Credit Memo") THEN BEGIN
        CASE Template."Sales Cr.Memo Reg. Step 2" OF
          Template."Sales Cr.Memo Reg. Step 2"::"Submit for Approval":
            DCApprovalsBridge.SendSalesDocApprovalRequest(SalesHeader);

          Template."Sales Cr.Memo Reg. Step 2"::Release:
            CODEUNIT.RUN(CODEUNIT::"Release Sales Document",SalesHeader);
        END;
      END;
    END;

    PROCEDURE CreateTextLine@1000000002(VAR SalesHeader@1000000000 : Record 36;Text@1000000002 : Text[1024]);
    VAR
      SalesLine@1000000003 : Record 37;
      LineNo@161024012 : Integer;
    BEGIN
      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      IF SalesLine.FINDLAST THEN
        LineNo := SalesLine."Line No." + 10000
      ELSE
        LineNo := 10000;

      SalesLine.INIT;
      SalesLine."Document Type" := SalesHeader."Document Type";
      SalesLine.VALIDATE("Document No.",SalesHeader."No.");
      SalesLine."Line No." := LineNo;
      SalesLine.INSERT(TRUE);
      SalesLine.VALIDATE(Description,COPYSTR(Text,1,MAXSTRLEN(SalesLine.Description)));
      SalesLine.MODIFY(TRUE);
    END;

    PROCEDURE TransferLineDim@161024014(VAR Document@1160040001 : Record 6085590;LineNo@1160040003 : Integer;VAR SalesLine@161024012 : Record 37;VAR LineTrans@161024013 : Record 6085584);
    VAR
      DataTranslDim@6085577 : Record 6085586;
      GLSetup@6085573 : Record 98;
      Field@6085574 : Record 6085580;
      DimValueCode@6085575 : Code[20];
      OldDimID@6085576 : Integer;
    BEGIN
      GLSetup.GET;

      DataTranslDim.SETRANGE("Template No.",LineTrans."Template No.");
      DataTranslDim.SETRANGE("Field Type",LineTrans.Type);
      DataTranslDim.SETRANGE("Field Code",LineTrans."Field Code");
      DataTranslDim.SETRANGE("Translate From",LineTrans."Translate From");
      IF DataTranslDim.FINDSET THEN BEGIN
        OldDimID := SalesLine."Dimension Set ID";
        REPEAT
          CASE TRUE OF
            DataTranslDim."Dimension Code" = GLSetup."Global Dimension 1 Code":
              SalesLine.VALIDATE("Shortcut Dimension 1 Code",DataTranslDim."Dimension Value Code");
            DataTranslDim."Dimension Code" = GLSetup."Global Dimension 2 Code":
              SalesLine.VALIDATE("Shortcut Dimension 2 Code",DataTranslDim."Dimension Value Code");
            ELSE
              SalesLine."Dimension Set ID" := UpdateDimension(SalesLine."Dimension Set ID",DataTranslDim."Dimension Code",DataTranslDim."Dimension Value Code");
          END;
        UNTIL DataTranslDim.NEXT = 0;
        IF OldDimID <> SalesLine."Dimension Set ID" THEN
          SalesLine.MODIFY(TRUE);
      END;

      IF LineNo = 0 THEN
        EXIT;

      Field.SETRANGE("Template No.",Document."Template No.");
      Field.SETRANGE(Type,Field.Type::Line);
      Field.SETRANGE("Data Type",Field."Data Type"::Lookup);
      Field.SETRANGE("Source Table No.",DATABASE::"Dimension Value");
      IF Field.FINDSET THEN BEGIN
        OldDimID := SalesLine."Dimension Set ID";
        REPEAT
          DimValueCode := CaptureMgnt.GetValueAsText(Document."No.",LineNo,Field);
          CASE TRUE OF
            Field.Code = GLSetup."Global Dimension 1 Code":
              SalesLine.VALIDATE("Shortcut Dimension 1 Code",DimValueCode);
            Field.Code = GLSetup."Global Dimension 2 Code":
              SalesLine.VALIDATE("Shortcut Dimension 2 Code",DimValueCode);
            ELSE
              SalesLine."Dimension Set ID" := UpdateDimension(SalesLine."Dimension Set ID",Field.Code,DimValueCode);
          END;
        UNTIL Field.NEXT = 0;

        IF OldDimID <> SalesLine."Dimension Set ID" THEN
          SalesLine.MODIFY(TRUE);
      END;
    END;

    PROCEDURE CreateTextArray@161024013(Text@161024012 : Text[1024];MaxLen@161024013 : Integer;VAR Arr@161024014 : ARRAY [50] OF Text[1024]) : Integer;
    VAR
      i@161024015 : Integer;
      Index@161024016 : Integer;
    BEGIN
      WHILE Text <> '' DO BEGIN
        i := i + 1;
        Text := DELCHR(Text,'<>',' ');
        IF STRLEN(Text) <= MaxLen THEN BEGIN
          Arr[i] := Text;
          EXIT(i);
        END;

        IF COPYSTR(Text,MaxLen+1,1) <> ' ' THEN
          Index := LastIndexOf(COPYSTR(Text,1,MaxLen),' ',TRUE)
        ELSE
          Index := MaxLen;

        IF Index = 0 THEN
          Index := MaxLen;

        Arr[i] := DELCHR(COPYSTR(Text,1,Index),'<>',' ');

        Text := COPYSTR(Text,STRLEN(Arr[i])+1);
      END;
      EXIT(i);
    END;

    PROCEDURE LastIndexOf@161024012(Text@161024012 : Text[1024];CharToFind@161024013 : Text[1];IgnoreCase@161024014 : Boolean) : Integer;
    VAR
      StartIndex@161024015 : Integer;
    BEGIN
      IF IgnoreCase THEN BEGIN
        Text := UPPERCASE(Text);
        CharToFind := UPPERCASE(CharToFind);
      END;

      StartIndex := STRLEN(Text);
      WHILE StartIndex > 0 DO BEGIN
        IF COPYSTR(Text,StartIndex,1) = CharToFind THEN
          EXIT(StartIndex)
        ELSE
          StartIndex -= 1;
      END;
    END;

    PROCEDURE UpdateDimension@6085573(DimSetID@6085573 : Integer;DimCode@6085574 : Code[20];DimValueCode@6085575 : Code[20]) : Integer;
    VAR
      DimSetEntry@6085576 : TEMPORARY Record 480;
      DimMgt@6085577 : Codeunit 408;
    BEGIN
      DimMgt.GetDimensionSet(DimSetEntry,DimSetID);

      IF DimSetEntry.GET(DimSetID,DimCode) THEN BEGIN
        IF DimSetEntry."Dimension Value Code" = DimValueCode THEN
          EXIT;
        DimSetEntry.VALIDATE("Dimension Value Code",DimValueCode);
        DimSetEntry.MODIFY(TRUE);
      END ELSE BEGIN
        DimSetEntry.INIT;
        DimSetEntry."Dimension Set ID" := DimSetID;
        DimSetEntry.VALIDATE("Dimension Code",DimCode);
        DimSetEntry.VALIDATE("Dimension Value Code",DimValueCode);
        DimSetEntry.INSERT(TRUE);
      END;

      EXIT(DimMgt.GetDimensionSetID(DimSetEntry));
    END;

    PROCEDURE TransferSalesHeader@1160040009(VAR SalesHeader@1160040000 : Record 36;Document@1160040004 : Record 6085590);
    VAR
      RecRef@1160040007 : RecordRef;
    BEGIN
      RecRef.GETTABLE(SalesHeader);
      CaptureMgnt.TransferTableFields(RecRef,Document,0,TRUE);
      RecRef.SETTABLE(SalesHeader);
    END;

    PROCEDURE TransferSalesLine@1160040001(VAR SalesLine@1160040000 : Record 37;Document@1160040012 : Record 6085590;DocumentLineNo@1160040010 : Integer);
    VAR
      RecRef@1160040006 : RecordRef;
    BEGIN
      RecRef.GETTABLE(SalesLine);
      CaptureMgnt.TransferTableFields(RecRef,Document,DocumentLineNo,FALSE);
      RecRef.SETTABLE(SalesLine);
    END;

    BEGIN
    END.
  }
}

