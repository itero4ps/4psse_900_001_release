OBJECT Codeunit 6085777 Sales Doc. - Reopen
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.04;
  }
  PROPERTIES
  {
    TableNo=6085590;
    OnRun=VAR
            SalesHeader@1000000001 : Record 36;
            TemplField@1000000005 : Record 6085580;
            SalesInvHeader@1000000002 : Record 112;
            SalesInvLine@1160040000 : Record 113;
            SalesCrMemoHeader@1000000003 : Record 114;
            SalesCrMemoLine@1160040001 : Record 115;
          BEGIN
            // *********************************************************************************************************************************
            // REOPEN A SALES DOCUMENT. IF THIS IS AN OPEN OR POSTED INVOICE IT CAN'T BE REOPEN.
            // *********************************************************************************************************************************

            TESTFIELD("Created Doc. Type",DATABASE::"Sales Header");
            IF NOT ("Created Doc. Subtype" IN [SalesHeader."Document Type"::Order,SalesHeader."Document Type"::"Credit Memo"]) THEN
              ERROR('');

            CASE Status OF
              Status::Open:
                ERROR(Text004,Rec.TABLECAPTION);

              Status::Registered:
                BEGIN
                  IF SalesHeader.GET("Created Doc. Subtype","Created Doc. ID") THEN BEGIN
                    IF NOT CONFIRM(Text001,TRUE,SalesHeader."Document Type","Created Doc. ID") THEN
                      ERROR('');
                    SalesHeader.DELETE(TRUE);
                  END ELSE BEGIN
                    IF "Created Doc. Subtype" = SalesHeader."Document Type"::Order THEN BEGIN
                      SalesInvHeader.SETCURRENTKEY("Order No.");
                      SalesInvHeader.SETRANGE("Order No.","Created Doc. ID");
                      IF SalesInvHeader.FINDFIRST THEN BEGIN
                        // CHECK IF THE ORIGINAL DOCUMENT HAS JUST BEEN DELETE
                        SalesInvLine.SETRANGE("Document No.",SalesInvHeader."No.");
                        SalesInvLine.SETFILTER(Type,'>0');
                        IF NOT SalesInvLine.ISEMPTY THEN
                          ERROR(Text002);
                      END;
                    END ELSE BEGIN
                      SalesCrMemoHeader.SETCURRENTKEY("Pre-Assigned No.");
                      SalesCrMemoHeader.SETRANGE("Pre-Assigned No.","Created Doc. ID");
                      IF SalesCrMemoHeader.FINDFIRST THEN BEGIN
                        // CHECK IF THE ORIGINAL DOCUMENT HAS JUST BEEN DELETE
                        SalesCrMemoLine.SETRANGE("Document No.",SalesCrMemoHeader."No.");
                        SalesCrMemoLine.SETFILTER(Type,'>0');
                        IF NOT SalesCrMemoLine.ISEMPTY THEN
                          ERROR(Text002);
                      END;
                    END;
                  END;

                  IF NOT CONFIRM(STRSUBSTNO(Text003),TRUE) THEN
                    ERROR('');
                END;

              Status::Rejected:
                IF NOT CONFIRM(STRSUBSTNO(Text003),TRUE) THEN
                  ERROR('');
            END;

            VALIDATE(Status,Status::Open);
            MODIFY(TRUE);
          END;

  }
  CODE
  {
    VAR
      Text001@1000000000 : TextConst 'ENU=This document is already linked to %1 %2.\\Do you want to delete %1 %2 so this document can be reopened?';
      Text002@1000000001 : TextConst 'ENU=This document has already been posted and cannot be reopened.';
      Text003@1000000002 : TextConst 'ENU=Do you want to reopen the document?';
      Text004@1000000003 : TextConst 'ENU=%1 is already open.';
      Text005@161024012 : TextConst 'ENU=This document is already linked to %1 %2.\\Do you want to reopen the document anyway?';

    BEGIN
    END.
  }
}

