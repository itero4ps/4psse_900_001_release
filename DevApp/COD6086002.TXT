OBJECT Codeunit 6086002 Web User Functions (WS)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.04;
  }
  PROPERTIES
  {
    Permissions=TableData 6086001=rimd,
                TableData 6086006=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE ValidateUser@161024012(Username@161024012 : Code[50]) : Boolean;
    BEGIN
      EXIT(TRUE);
    END;

    PROCEDURE LogOff@6085573();
    BEGIN
    END;

    PROCEDURE UpdateWebUserCompany@1160040000(Username@1160040005 : Code[50]);
    VAR
      WebUserCompany@1160040006 : Record 6086006;
      Company@1160040000 : Record 2000000006;
      COSetup@1160040001 : Record 6086500;
      UserSetup@1160040002 : Record 91;
      ApprEntry@1160040003 : Record 454;
      WebSubMenu@1160040007 : Record 6086011;
      DCApprovalMgnt@1160040008 : Codeunit 6085722;
      User@1160040004 : Code[50];
    BEGIN
      WebUserCompany.SETRANGE("User Id",Username);
      WebUserCompany.DELETEALL;

      IF Company.FINDSET THEN
        REPEAT
          IF COSetup.CHANGECOMPANY(Company.Name) THEN BEGIN
            IF COSetup.READPERMISSION THEN BEGIN
              IF COSetup.GET THEN BEGIN
                IF COSetup."Enable Web Approval" THEN BEGIN
                  UserSetup.CHANGECOMPANY(Company.Name);
                  IF UserSetup.READPERMISSION THEN BEGIN
                    IF UserSetup.GET(Username) THEN BEGIN
                      WebUserCompany."Company Code" := GetCompanyCode(Company.Name);
                      WebUserCompany."Company Name" := Company.Name;
                      WebUserCompany."User Id" := Username;
                      IF NOT WebUserCompany.FIND('=') THEN BEGIN
                        ApprEntry.CHANGECOMPANY(Company.Name);
                        ApprEntry.SETCURRENTKEY("Approver ID",Status);
                        ApprEntry.SETRANGE("Approver ID",Username);
                        ApprEntry.SETRANGE(Status,ApprEntry.Status::Open);
                        ApprEntry.SETFILTER("Table ID",'%1|%2',DATABASE::"Purchase Header",6086320); //6086320 = Expense Management
                        WebUserCompany."No. of Documents for Approval" := ApprEntry.COUNT;

                        ApprEntry.SETRANGE("Approver ID",DCApprovalMgnt.GetApprovalSharingToUserFilter(Username));
                        WebUserCompany."No. of Shared Documents" := ApprEntry.COUNT;
                        ApprEntry.SETRANGE("Approver ID",Username);

                        WebSubMenu.CHANGECOMPANY(Company.Name);
                        IF WebSubMenu.FINDSET THEN
                          REPEAT
                            IF WebSubMenu."Approval Code Filter" <> '' THEN
                              ApprEntry.SETFILTER("Approval Code",WebSubMenu."Approval Code Filter");
                            IF WebSubMenu."Table ID Filter" <> '' THEN
                              ApprEntry.SETFILTER("Table ID",WebSubMenu."Table ID Filter");
                            IF WebSubMenu."Document Type Filter" <> '' THEN
                              ApprEntry.SETFILTER("Document Type",WebSubMenu."Document Type Filter");

                            WebUserCompany."No. of Documents for Approval" += ApprEntry.COUNT;


                            ApprEntry.SETRANGE("Approver ID",DCApprovalMgnt.GetApprovalSharingToUserFilter(Username));
                            WebUserCompany."No. of Shared Documents" += ApprEntry.COUNT;
                            ApprEntry.SETRANGE("Approver ID",Username);
                          UNTIL WebSubMenu.NEXT = 0;
                        WebUserCompany.INSERT;
                      END;
                    END;
                  END;
                END;
              END;
            END;
          END;
        UNTIL Company.NEXT = 0;
    END;

    PROCEDURE GetDateFormat@161024014() : Integer;
    VAR
      TestDate@161024012 : Text[30];
      DayPos@161024014 : Integer;
      MonthPos@161024013 : Integer;
      YearPos@161024015 : Integer;
    BEGIN
      TestDate := FORMAT(99990213D);
      DayPos := STRPOS(TestDate,'13');
      MonthPos := STRPOS(TestDate,'2');
      YearPos := STRPOS(TestDate,'99');

      CASE TRUE OF
        // Day / Month / Year
        (DayPos < MonthPos) AND (MonthPos < YearPos):
          EXIT(1);

        // Month / Day / Year
        (MonthPos < DayPos) AND (DayPos < YearPos):
          EXIT(2);

        // Year / Day / Month
        (YearPos < DayPos) AND (DayPos < MonthPos):
          EXIT(3);

        // Year / Month / Day
        (YearPos < MonthPos) AND (MonthPos < DayPos):
          EXIT(4);

        // Day / Year / Month
        (DayPos < YearPos) AND (YearPos < MonthPos):
          EXIT(5);

        // Month / Year / Day
        (MonthPos < YearPos) AND (YearPos < DayPos):
          EXIT(6);
      END;
    END;

    PROCEDURE GetCompanyCode@1160040001(_CompanyName@1160040004 : Text[30]) : Code[10];
    VAR
      DCSetup@1160040000 : Record 6085573;
      Obj@1160040003 : Record 2000000001;
      RecRef@1160040001 : RecordRef;
      FieldRef@1160040002 : FieldRef;
    BEGIN
      DCSetup.CHANGECOMPANY(_CompanyName);
      IF DCSetup.GET THEN
        EXIT(DCSetup."Company Code");

      IF NOT Obj.GET(Obj.Type::Table,'',6086300) THEN
        EXIT;

      RecRef.OPEN(6086300,FALSE,_CompanyName);
      IF NOT RecRef.FINDFIRST THEN
        EXIT;

      FieldRef := RecRef.FIELD(2);
      EXIT(FieldRef.VALUE);
    END;

    BEGIN
    END.
  }
}

