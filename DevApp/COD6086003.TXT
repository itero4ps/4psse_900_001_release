OBJECT Codeunit 6086003 Web User List-Indent
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.3.01;
  }
  PROPERTIES
  {
    TableNo=6086004;
    OnRun=BEGIN
            IF NOT
               CONFIRM(
                 STRSUBSTNO(
                   Text000 +
                   Text001 +
                   Text003,"List Code"),TRUE)
            THEN
              EXIT;

            ListItem.SETRANGE(Type,Type);
            ListItem.SETRANGE("List Code","List Code");
            ListItem.SETRANGE("Dimension Code","Dimension Code");
            Indent;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This function updates the indentation of all the items for list %1.';
      Text001@1001 : TextConst 'ENU="All items between a Begin-Total and the matching End-Total are indented by one level. "';
      Text003@1003 : TextConst 'ENU=Do you want to indent the items?';
      Text004@1004 : TextConst 'ENU=Indenting Items @1@@@@@@@@@@@@@@@@@@';
      Text005@1005 : TextConst 'ENU=End-Total %1 is missing a matching Begin-Total.';
      ListItem@1006 : Record 6086004;
      Window@1007 : Dialog;
      i@1009 : Integer;

    PROCEDURE Indent@1();
    VAR
      NoOfItems@1000 : Integer;
      Progress@1001 : Integer;
    BEGIN
      Window.OPEN(Text004);

      NoOfItems := ListItem.COUNT;
      IF NoOfItems = 0 THEN
        NoOfItems := 1;
      WITH ListItem DO
        IF FIND('-') THEN
          REPEAT
            Progress := Progress + 1;
            Window.UPDATE(1,10000 * Progress DIV NoOfItems);

            IF "Account Type" = "Account Type"::"End-total" THEN BEGIN
              IF i < 1 THEN
                ERROR(
                  Text005,
                  "No.");
              i := i - 1;
            END;

            Indentation := i;
            MODIFY;

            IF "Account Type" = "Account Type"::"Begin-total" THEN
              i := i + 1;
          UNTIL NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

