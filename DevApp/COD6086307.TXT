OBJECT Codeunit 6086307 Expense Inbox-Process
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.1.02.00;
  }
  PROPERTIES
  {
    TableNo=6086323;
    OnRun=BEGIN
            ExpenseInbox.COPY(Rec);
            Code;
            Rec := ExpenseInbox;
          END;

  }
  CODE
  {
    VAR
      ExpenseInbox@1160040004 : Record 6086323;
      ExpInboxCheck@1160040001 : Codeunit 6086308;

    LOCAL PROCEDURE Code@1160040000();
    VAR
      Expense@1160040004 : Record 6086320;
      ExpValidate@1160040003 : Codeunit 6086321;
      ExpenseReopen@1160040002 : Codeunit 6086303;
      TempExpenseAttachment@1160040000 : TEMPORARY Record 6086326;
      ExpenseInboxAttachment@1160040001 : Record 6086328;
    BEGIN
      WITH ExpenseInbox DO BEGIN
        IF Status = Status::Accepted THEN
          EXIT;

        IF NOT ExpInboxCheck.RUN(ExpenseInbox) THEN BEGIN
          "Error Text" := COPYSTR(GETLASTERRORTEXT,1,MAXSTRLEN("Error Text"));
          Status := Status::Error;

          IF "Expense Entry No." = 0 THEN BEGIN
            Expense.SETCURRENTKEY("Expense GUID");
            Expense.SETRANGE("Expense GUID","Expense GUID");
            IF Expense.FINDFIRST THEN
              "Expense Entry No." := Expense."Entry No.";
          END;
        END ELSE BEGIN
          "Expense Entry No." := ExpInboxCheck.GetExpenseEntryNo;
          "Error Text" := '';
          Status := Status::Accepted;
        END;

        "Processed Date/Time" := CURRENTDATETIME;
        "Processed by User-ID" := USERID;
        MODIFY;

        IF Status = Status::Accepted THEN
          IF "Expense Entry No." <> 0 THEN BEGIN
            Expense.GET("Expense Entry No.");

            ExpenseReopen.ReopenWithoutDelete(Expense);
            COMMIT;

            ExpInboxCheck.DeleteFiles(ExpenseInbox);

            ExpValidate.RUN(Expense);
          END;
      END;
    END;

    BEGIN
    END.
  }
}

