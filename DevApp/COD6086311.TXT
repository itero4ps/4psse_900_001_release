OBJECT Codeunit 6086311 Expense Bank Trans. Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.1.02.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      EMSetup@1160040002 : Record 6086300;
      HaveGotEMSetup@1160040003 : Boolean;
      Sloppy@1160040001 : Boolean;
      AmountUpdatedOnExpense@1160040000 : TextConst 'ENU=The amount on the expense have been updated with the amount from the Bank Transaction.';

    PROCEDURE TryMatch@1160040000();
    VAR
      Expense@1160040000 : Record 6086320;
      Transaction@1160040001 : Record 6086330;
    BEGIN
      SetSloppy;
      Expense.SETCURRENTKEY(Posted);
      Expense.SETRANGE(Posted,FALSE);
      Expense.SETRANGE("Matched to Bank Transaction",FALSE);
      IF Expense.FINDFIRST THEN
        REPEAT
          MatchInsertTransaction(Expense,Transaction);
        UNTIL Expense.NEXT = 0;
    END;

    PROCEDURE MatchExpense@1160040001(Transaction@1160040000 : Record 6086330;VAR Expense@1160040001 : Record 6086320) : Boolean;
    BEGIN
      SetSloppy;
      IF ExactExpense(Transaction,Expense) THEN
        EXIT(TRUE);
      IF Sloppy THEN
        IF SloppyExpense(Transaction,Expense) THEN
          EXIT(TRUE);
    END;

    PROCEDURE MatchInsertExpense@1160040013(Transaction@1160040000 : Record 6086330;VAR Expense@1160040001 : Record 6086320) : Boolean;
    BEGIN
      IF MatchExpense(Transaction,Expense) THEN BEGIN
        InsertMatch(Transaction,Expense);
        EXIT(TRUE);
      END;
    END;

    PROCEDURE MatchTransaction@1160040002(Expense@1160040000 : Record 6086320;VAR Transaction@1160040002 : Record 6086330) : Boolean;
    BEGIN
      SetSloppy;
      IF ExactTransaction(Expense,Transaction) THEN
        EXIT(TRUE);
      IF Sloppy THEN
        IF SloppyTransaction(Expense,Transaction) THEN
          EXIT(TRUE);
    END;

    PROCEDURE MatchInsertTransaction@1160040010(Expense@1160040001 : Record 6086320;VAR Transaction@1160040000 : Record 6086330) : Boolean;
    BEGIN
      IF MatchTransaction(Expense,Transaction) THEN
        InsertmatchSet(Transaction,Expense);
    END;

    PROCEDURE ExactExpense@1160040003(Transaction@1160040001 : Record 6086330;VAR Expense@1160040000 : Record 6086320) : Boolean;
    BEGIN
      Expense.SETRANGE("Employee No.",Transaction."Employee No.");
      Expense.SETRANGE(Amount,Transaction.Amount);
      Expense.SETRANGE("Document Date",Transaction."Document Date");
      Expense.SETRANGE("Matched to Bank Transaction",FALSE);
      Expense.SETRANGE("Currency Code",Transaction."Currency Code");
      Expense.SETRANGE(Posted,FALSE);
      EXIT(Expense.FINDFIRST);
    END;

    PROCEDURE ExactTransaction@1160040004(Expense@1160040001 : Record 6086320;VAR Transaction@1160040000 : Record 6086330) : Boolean;
    BEGIN
      Transaction.SETCURRENTKEY("Employee No.","Matched to Expense","Currency Code","Exclude Entry");
      Transaction.SETRANGE("Employee No.",Expense."Employee No.");
      Transaction.SETRANGE("Matched to Expense",FALSE);
      Transaction.SETRANGE("Currency Code",Expense."Currency Code");
      Transaction.SETRANGE(Amount,Expense.Amount);
      Transaction.SETRANGE("Document Date",Expense."Document Date");
      Transaction.SETRANGE("Exclude Entry",FALSE);
      EXIT(Transaction.FINDFIRST);
    END;

    PROCEDURE SloppyExpense@1160040005(Transaction@1160040001 : Record 6086330;VAR Expense@1160040000 : Record 6086320) : Boolean;
    BEGIN
      IF Transaction."Document Date" = 0D THEN
        Transaction."Document Date" := TODAY;
      Expense.SETRANGE(Amount,Transaction.Amount);
      Expense.SETFILTER("Document Date",'>=%1&<=%2',
        CALCDATE('<-2D>',Transaction."Document Date"),
        CALCDATE('<+2D>',Transaction."Document Date"));
      Expense.SETRANGE("Matched to Bank Transaction",FALSE);
      Expense.SETRANGE("Currency Code",Transaction."Currency Code");
      Expense.SETRANGE(Posted,FALSE);
      IF Expense.FINDFIRST THEN
        EXIT(TRUE);

      IF Transaction.Amount > 0 THEN
        Expense.SETFILTER(Amount,'>=%1&<=%2',Transaction.Amount * Low,Transaction.Amount * High)
      ELSE
        Expense.SETFILTER(Amount,'<=%1&>=%2',Transaction.Amount * Low,Transaction.Amount * High);
      Expense.SETFILTER("Document Date",'=%1',Transaction."Document Date");
      IF Expense.FINDFIRST THEN
        EXIT(TRUE);

      Expense.SETFILTER("Document Date",'>=%1&<=%2',
        CALCDATE('<-2D>',Transaction."Document Date"),
        CALCDATE('<+2D>',Transaction."Document Date"));
      EXIT(Expense.FINDFIRST);
    END;

    PROCEDURE SloppyTransaction@1160040006(Expense@1160040001 : Record 6086320;VAR Transaction@1160040000 : Record 6086330) : Boolean;
    BEGIN
      Transaction.SETRANGE(Amount,Expense.Amount);
      IF Expense."Document Date" = 0D THEN
        Expense."Document Date" := TODAY;
      Transaction.SETFILTER("Document Date",'>=%1&<=%2',
        CALCDATE('<-2D>',Expense."Document Date"),
        CALCDATE('<+2D>',Expense."Document Date"));
      Transaction.SETRANGE("Matched to Expense",FALSE);
      Transaction.SETRANGE("Currency Code",Expense."Currency Code");
      IF Transaction.FINDFIRST THEN
        EXIT(TRUE);

      IF Expense.Amount > 0 THEN
        Transaction.SETFILTER(Amount,'>=%1&<=%2',Expense.Amount * Low,Expense.Amount * High)
      ELSE
        Transaction.SETFILTER(Amount,'<=%1&>=%2',Expense.Amount * Low,Expense.Amount * High);
      Transaction.SETFILTER("Document Date",'=%1',Expense."Document Date");
      IF Transaction.FINDFIRST THEN
        EXIT(TRUE);

      Transaction.SETFILTER("Document Date",'>=%1&<=%2',
        CALCDATE('<-2D>',Expense."Document Date"),
        CALCDATE('<+2D>',Expense."Document Date"));
      EXIT(Transaction.FINDFIRST);
    END;

    PROCEDURE SetSloppy@1160040016();
    BEGIN
      GetEMSetup;
      Sloppy := EMSetup."Maximum Matching Diff. Pct." <> 0;
    END;

    PROCEDURE InsertMatch@1160040009(Transaction@1160040001 : Record 6086330;Expense@1160040000 : Record 6086320);
    VAR
      Match@1160040002 : Record 6086349;
      ExpCmtMgt@1160040003 : Codeunit 6086323;
      ExpValidate@1160040006 : Codeunit 6086321;
      ExpComment@1160040004 : Record 6086355;
      AddCommentForAmount@1160040005 : Boolean;
    BEGIN
      IF Expense."Cash/Private Card" THEN BEGIN
        Expense."Cash/Private Card" := FALSE;
        Expense.MODIFY;
      END;

      AddCommentForAmount := FALSE;
      IF (Expense.Amount <> Transaction.Amount) OR (Expense."Amount (LCY)" <> Transaction."Amount (LCY)") THEN BEGIN
        Expense.Amount := Transaction.Amount;
        Expense."Amount (LCY)" := Transaction."Amount (LCY)";
        Expense.MODIFY(TRUE);
        ExpCmtMgt.AddComment(Expense."Entry No.",ExpComment.Importance::Information,'MATCH',AmountUpdatedOnExpense,TRUE);
      END;

      Transaction.TESTFIELD("Matched to Expense",FALSE);
      Expense.TESTFIELD("Matched to Bank Transaction",FALSE);
      Expense.TESTFIELD("Currency Code",Transaction."Currency Code");
      Expense.TESTFIELD(Amount,Transaction.Amount);
      Match."Expense Entry No." := Expense."Entry No.";
      Match."Transaction Entry No." := Transaction."Entry No.";
      Match.INSERT(TRUE);

      Expense.GET(Expense."Entry No.");
      ExpValidate.RUN(Expense);

      IF AddCommentForAmount THEN
        ExpCmtMgt.AddComment(Expense."Entry No.",ExpComment.Importance::Information,'MATCH',AmountUpdatedOnExpense,TRUE);
    END;

    PROCEDURE InsertmatchSet@1160040011(VAR TransactionsRec@1160040000 : Record 6086330;Expense@1160040001 : Record 6086320);
    BEGIN
      IF TransactionsRec.MARK THEN BEGIN
        TransactionsRec.MARKEDONLY(TRUE);
        IF TransactionsRec.FINDFIRST THEN
          REPEAT
            InsertMatch(TransactionsRec,Expense);
          UNTIL TransactionsRec.NEXT = 0;
      END ELSE
        InsertMatch(TransactionsRec,Expense);
    END;

    PROCEDURE High@1160040014() : Decimal;
    BEGIN
      GetEMSetup;
      EXIT((100 + EMSetup."Maximum Matching Diff. Pct.") / 100);
    END;

    PROCEDURE Low@1160040015() : Decimal;
    BEGIN
      GetEMSetup;
      EXIT((100 - EMSetup."Maximum Matching Diff. Pct.") / 100);
    END;

    PROCEDURE GetEMSetup@1160040019();
    BEGIN
      IF HaveGotEMSetup THEN
        EXIT;
      EMSetup.GET;
      HaveGotEMSetup := TRUE;
    END;

    PROCEDURE MarkMatchedExpense@1160040021(EntryNo@1160040000 : Integer;Matched@1160040002 : Boolean);
    VAR
      Expense@1160040001 : Record 6086320;
    BEGIN
      Expense.GET(EntryNo);
      Expense."Matched to Bank Transaction" := Matched;
      Expense.SetSkipSendToExpUser(TRUE);
      Expense.MODIFY(TRUE);
    END;

    PROCEDURE MarkMatchedBankTransaction@1160040020(EntryNo@1160040000 : Integer;Matched@1160040002 : Boolean);
    VAR
      BankTransaction@1160040001 : Record 6086330;
    BEGIN
      BankTransaction.GET(EntryNo);
      BankTransaction."Matched to Expense" := Matched;
      BankTransaction.MODIFY(TRUE);
    END;

    PROCEDURE DeleteExpenseMatch@1160040022(VAR Expense@1160040000 : Record 6086320);
    VAR
      BankTrans@1160040002 : Record 6086330;
      ExpenseMatch@1160040001 : Record 6086349;
    BEGIN
      ExpenseMatch.LOCKTABLE;
      ExpenseMatch.SETRANGE("Expense Entry No.",Expense."Entry No.");
      IF ExpenseMatch.FINDFIRST THEN
        REPEAT
          BankTrans.GET(ExpenseMatch."Transaction Entry No.");
          BankTrans."Matched to Expense" := FALSE;
          BankTrans."Exclude Entry" := TRUE;
          BankTrans.MODIFY;

          ExpenseMatch.DELETE;
        UNTIL ExpenseMatch.NEXT = 0;

      Expense."Matched to Bank Transaction" := FALSE;
    END;

    PROCEDURE DeleteBankTransMatch@1160040023(VAR BankTrans@1160040000 : Record 6086330);
    VAR
      Expense@1160040001 : Record 6086320;
      ExpenseMatch@1160040002 : Record 6086349;
    BEGIN
      ExpenseMatch.LOCKTABLE;
      ExpenseMatch.SETCURRENTKEY("Transaction Entry No.");
      ExpenseMatch.SETRANGE("Transaction Entry No.",BankTrans."Entry No.");
      IF ExpenseMatch.FINDFIRST THEN
        REPEAT
          Expense.GET(ExpenseMatch."Expense Entry No.");
          Expense."Matched to Bank Transaction" := FALSE;
          Expense.MODIFY;

          ExpenseMatch.DELETE;
        UNTIL ExpenseMatch.NEXT = 0;

      BankTrans."Matched to Expense" := FALSE;
    END;

    BEGIN
    END.
  }
}

