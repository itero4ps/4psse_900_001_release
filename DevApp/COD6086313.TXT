OBJECT Codeunit 6086313 Expense Approval E-Mail
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.1.02.00;
  }
  PROPERTIES
  {
    Permissions=TableData 454=rm,
                TableData 6085740=ri,
                TableData 6085741=ri,
                TableData 6085742=ri;
    OnRun=BEGIN
            SendApprovalEmails;
          END;

  }
  CODE
  {
    VAR
      Text001@161024012 : TextConst 'ENU=(On Hold)';
      Text004@161024013 : TextConst 'ENU="<td style=""border-width: 1px 1px 0 0; border-style: solid; border-color: #e7e8e6; font-size: 11px; font-family: Tahoma,Verdana;"">#VALUE#</td>"';
      Text005@161024016 : TextConst 'ENU="<td align=right style=""border-width: 1px 1px 0 0; border-style: solid; border-color: #e7e8e6; font-size: 11px; font-family: Tahoma,Verdana;"">#VALUE#</td>"';
      Text008@161024019 : TextConst 'ENU=Sending Follow-up E-mails...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text009@161024020 : TextConst 'ENU=%1 %2 / %3 %4';
      Text012@161024024 : TextConst 'ENU=Document Capture E-mail Error: E-mail is blank on user %1 - %2.';
      Text013@161024023 : TextConst 'ENU=Document';
      Text014@161024031 : TextConst 'ENU=Employee';
      Text015@161024030 : TextConst 'ENU=Date';
      Text017@161024028 : TextConst 'ENU=Currency';
      Text018@161024027 : TextConst 'ENU=Amount';
      Text019@161024026 : TextConst 'ENU=Amount %1';
      Text021@161024032 : TextConst 'ENU="<table style=""border-width: 0 0 1px 1px; border-style: solid; border-color: #e7e8e6;"" cellspacing=0 cellpadding=3>"';
      Text022@161024035 : TextConst 'ENU=</table>';
      Text023@161024033 : TextConst 'ENU="<p style=""font-weight: bold"">%1</p>"';
      Text024@161024034 : TextConst 'ENU=You have the following Expenses for approval:';
      Text025@161024037 : TextConst 'ENU=Shared by %1 (out of office)';
      Text026@161024036 : TextConst 'ENU=Shared by %1';
      Text027@161024039 : TextConst 'ENU="<td style=""border-width: 1px 1px 0 0; border-style: solid; border-color: #e7e8e6; font-size: 11px; font-family: Tahoma,Verdana; font-weight:bold"">#VALUE#</td>"';
      Text028@161024038 : TextConst 'ENU="<td align=right style=""border-width: 1px 1px 0 0; border-style: solid; border-color: #e7e8e6; font-size: 11px; font-family: Tahoma,Verdana; font-weight: bold"">#VALUE#</td>"';
      Text034@161024045 : TextConst 'ENU=You must import the Approval E-mail Template in the %1 table.';
      Text035@1160040000 : TextConst 'ENU=Description';
      Text037@1160040002 : TextConst 'ENU=Click here to access your expenses for approval.';

    LOCAL PROCEDURE SendApprovalEmails@161024015();
    VAR
      ApprEntry@161024013 : Record 454;
      ApprovalSharing@161024032 : Record 6085749;
      ContiniaUserSetup@161024015 : Record 6086002;
      ContiniaUserSetup2@1160040004 : Record 6086002;
      EMSetup@1160040001 : Record 6086300;
      Expense@161024024 : Record 6086320;
      DocumentHTML@161024018 : Codeunit 6085587;
      HTML@161024016 : Codeunit 6085587;
      TableRow@161024033 : Codeunit 6085587;
      SmtpMailMgt@161024012 : Codeunit 6085598;
      EMAppMgnt@161024031 : Codeunit 6086312;
      NavDiffMgt@1160040003 : Codeunit 6086319;
      Window@161024021 : Dialog;
      TempFile@161024026 : File;
      NVInStream@161024027 : InStream;
      FileData@161024028 : Text[1024];
      Filename@161024029 : Text[1024];
      FromEventEntryNo@1160040000 : Integer;
      i@161024020 : Integer;
      RecCount@161024019 : Integer;
      ToEventEntryNo@1160040002 : Integer;
      Created@161024035 : Boolean;
      SkipEmail@161024014 : Boolean;
    BEGIN
      EMSetup.GET;

      EMSetup.TESTFIELD("Error E-Mail");

      EMSetup.TESTFIELD("Approval E-Mail Subject");
      EMSetup.CALCFIELDS("Approval Template");

      IF NOT EMSetup."Approval Template".HASVALUE THEN
        ERROR(Text034,EMSetup.TABLECAPTION);

      TempFile.CREATETEMPFILE;
      Filename := TempFile.NAME;
      TempFile.CLOSE;
      TempFile.CREATE(Filename);
      EMSetup."Approval Template".CREATEINSTREAM(NVInStream);
      WHILE NVInStream.READ(FileData) <> 0 DO
        TempFile.WRITE(FileData);
      TempFile.CLOSE;

      ApprEntry.SETCURRENTKEY("Approver ID",Status);
      ApprEntry.SETRANGE("Table ID",DATABASE::Expense);
      ApprEntry.SETRANGE(Status,ApprEntry.Status::Open);

      IF ContiniaUserSetup.FINDFIRST THEN BEGIN
        IF GUIALLOWED THEN BEGIN
          Window.OPEN(Text008);
          RecCount := ContiniaUserSetup.COUNT;
        END;

        FromEventEntryNo := GetLastEventEntry + 1;
        ToEventEntryNo := FromEventEntryNo;

        REPEAT
          IF GUIALLOWED THEN BEGIN
            i := i + 1;
            Window.UPDATE(1,CalcProgress(RecCount,i));
          END;
          SkipEmail := FALSE;

          EMAppMgnt.FilterApprovalSharingFromUser(ApprovalSharing,ContiniaUserSetup."Continia User ID");
          ApprovalSharing.SETRANGE("Forward E-mails",TRUE);
          IF NOT ApprovalSharing.ISEMPTY THEN
            SkipEmail := TRUE;

          IF NOT SkipEmail THEN BEGIN
            CLEAR(DocumentHTML);
            Created := FALSE;

            ApprEntry.SETRANGE("Approver ID",ContiniaUserSetup."Continia User ID");
            IF ApprEntry.FINDFIRST THEN BEGIN
              Created := TRUE;
              DocumentHTML.Append(STRSUBSTNO(Text023,Text024));

              DocumentHTML.Append(Text021);
              CreateTableHeaderRow(TableRow);
              DocumentHTML.Append('#####');
              DocumentHTML.Replace2('#####',TableRow);

              REPEAT
                Expense.GET(EMAppMgnt.Code2Int(ApprEntry."Document No."));
                CreateTableRow(Expense,TableRow);
                DocumentHTML.Append('#####');
                DocumentHTML.Replace2('#####',TableRow);

                IF IsNewEventEntry(ApprEntry) THEN BEGIN
                  CreateEventEntry(ApprEntry);
                  ToEventEntryNo += 1;
                END;
              UNTIL ApprEntry.NEXT = 0;

              DocumentHTML.Append(Text022);
            END;

            EMAppMgnt.FilterApprovalSharingToUser(ApprovalSharing,ContiniaUserSetup."Continia User ID");
            ApprovalSharing.SETRANGE("Forward E-mails",TRUE);
            IF ApprovalSharing.FINDFIRST THEN
              REPEAT
                ApprEntry.SETRANGE("Approver ID",ApprovalSharing."Owner User ID");
                IF ApprEntry.FINDFIRST THEN BEGIN
                  Created := TRUE;

                  ContiniaUserSetup2.GET(ApprovalSharing."Owner User ID");

                  IF ApprovalSharing."Sharing Type" = ApprovalSharing."Sharing Type"::Normal THEN
                    DocumentHTML.Append(STRSUBSTNO(Text023,STRSUBSTNO(Text026,ContiniaUserSetup2.GetName)))
                  ELSE
                    DocumentHTML.Append(STRSUBSTNO(Text023,STRSUBSTNO(Text025,ContiniaUserSetup2.GetName)));

                  DocumentHTML.Append(Text021);
                  CreateTableHeaderRow(TableRow);
                  DocumentHTML.Append('#####');
                  DocumentHTML.Replace2('#####',TableRow);

                  REPEAT
                    Expense.GET(EMAppMgnt.Code2Int(ApprEntry."Document No."));
                    CreateTableRow(Expense,TableRow);
                    DocumentHTML.Append('#####');
                    DocumentHTML.Replace2('#####',TableRow);

                    IF IsNewEventEntry(ApprEntry) THEN BEGIN
                      CreateEventEntry(ApprEntry);
                      ToEventEntryNo += 1;
                    END;
                  UNTIL ApprEntry.NEXT = 0;

                  DocumentHTML.Append(Text022);
                END;
              UNTIL ApprovalSharing.NEXT = 0;

            IF Created THEN BEGIN
              HTML.LoadFromFile(Filename);

              HTML.Replace2('#DOCUMENTS#',DocumentHTML);
              HTML.Replace('#APPROVALFORMLINK#',NavDiffMgt.GetHyperlink(ContiniaUserSetup."Continia User ID"));
              HTML.Replace('#APPROVALFORMTITLE#',Text037);

              SetupMail(SmtpMailMgt,ContiniaUserSetup,EMSetup."Approval E-Mail Subject" + ' - ' + COMPANYNAME);

              SmtpMailMgt.SetBodyFromBigStringMgnt(HTML);
              SmtpMailMgt.SetStartTLS(EMSetup."SMTP Require SSL/TLS");
              IF NOT SmtpMailMgt.Send THEN
                SmtpMailMgt.DisplayLastError;

              CLEAR(HTML);
            END;
          END;
        UNTIL ContiniaUserSetup.NEXT = 0;

        CreateEventReg(FromEventEntryNo,ToEventEntryNo,1);
        COMMIT;

        IF GUIALLOWED THEN
          Window.CLOSE;

        EMSetup."Last Status E-Mail Sent" := TODAY;
        EMSetup.MODIFY;
      END;
    END;

    LOCAL PROCEDURE CreateTableHeaderRow@161024018(VAR BigString@161024012 : Codeunit 6085587);
    VAR
      GLSetup@1160040000 : Record 98;
      CaptureMgnt@161024013 : Codeunit 6085576;
      LeftAlignedTd@161024014 : Text[1024];
      RightAlignedTd@161024015 : Text[1024];
    BEGIN
      GLSetup.GET;

      CLEAR(BigString);

      LeftAlignedTd := Text027;
      RightAlignedTd := Text028;

      BigString.Append('<tr>');
      BigString.Append(CaptureMgnt.Replace(LeftAlignedTd,'#VALUE#',Text013,FALSE));
      BigString.Append(CaptureMgnt.Replace(LeftAlignedTd,'#VALUE#',Text014,FALSE));
      BigString.Append(CaptureMgnt.Replace(LeftAlignedTd,'#VALUE#',Text015,FALSE));
      BigString.Append(CaptureMgnt.Replace(LeftAlignedTd,'#VALUE#',Text035,FALSE));
      BigString.Append(CaptureMgnt.Replace(LeftAlignedTd,'#VALUE#',Text017,FALSE));
      BigString.Append(CaptureMgnt.Replace(RightAlignedTd,'#VALUE#',Text018,FALSE));
      BigString.Append(CaptureMgnt.Replace(RightAlignedTd,'#VALUE#',STRSUBSTNO(Text019,GLSetup."LCY Code"),FALSE));
      BigString.Append('</tr>');
    END;

    LOCAL PROCEDURE CreateTableRow@161024012(Expense@161024016 : Record 6086320;VAR BigString@161024012 : Codeunit 6085587);
    VAR
      Currency@161024021 : Record 4;
      CaptureMgnt@161024013 : Codeunit 6085576;
      LeftAlignedTd@161024014 : Text[1024];
      RightAlignedTd@161024015 : Text[1024];
    BEGIN
      CLEAR(BigString);

      LeftAlignedTd := Text004;
      RightAlignedTd := Text005;

      WITH Expense DO BEGIN
        IF "Currency Code" = '' THEN
          Currency.InitRoundingPrecision
        ELSE
          IF NOT Currency.GET("Currency Code") THEN
            Currency."Amount Decimal Places" := '2:2';

        CALCFIELDS("Employee Name");

        BigString.Append('<tr>');
        IF "On Hold" = '' THEN
          BigString.Append(CaptureMgnt.Replace(LeftAlignedTd,'#VALUE#',FORMAT("Entry No."),FALSE))
        ELSE
          BigString.Append(CaptureMgnt.Replace(LeftAlignedTd,'#VALUE#',FORMAT("Entry No.") + Text001,FALSE));
        BigString.Append(CaptureMgnt.Replace(LeftAlignedTd,'#VALUE#',"Employee No." + ' - ' + "Employee Name",FALSE));
        BigString.Append(CaptureMgnt.Replace(LeftAlignedTd,'#VALUE#',FORMAT("Document Date"),FALSE));
        BigString.Append(CaptureMgnt.Replace(LeftAlignedTd,'#VALUE#',Description,FALSE));
        BigString.Append(CaptureMgnt.Replace(LeftAlignedTd,'#VALUE#',"Currency Code",FALSE));
        BigString.Append(
        CaptureMgnt.Replace(RightAlignedTd,'#VALUE#',
          FORMAT(Amount,0,STRSUBSTNO('<Precision,%1><Standard Format,0>',Currency."Amount Decimal Places")),FALSE));
        BigString.Append(
        CaptureMgnt.Replace(RightAlignedTd,'#VALUE#',
          FORMAT("Amount (LCY)",0,STRSUBSTNO('<Precision,%1><Standard Format,0>',Currency."Amount Decimal Places")),FALSE));
        BigString.Append('</tr>');
      END;
    END;

    PROCEDURE SetupMail@161024013(VAR Mail@161024012 : Codeunit 6085598;ContiniaUserSetup@161024018 : Record 6086002;Subject@161024017 : Text[1024]);
    VAR
      SMTPMailSetup@161024014 : Record 409;
      EMSetup@161024013 : Record 6086300;
      ServerAddr@161024016 : Text[250];
      ServerPort@161024015 : Integer;
    BEGIN
      EMSetup.GET;
      EMSetup.TESTFIELD("Sender E-mail");
      EMSetup.TESTFIELD("Sender Name");
      EMSetup.TESTFIELD("Approval E-Mail Subject");

      SMTPMailSetup.GET;
      SMTPMailSetup.TESTFIELD("SMTP Server");

      IF STRPOS(SMTPMailSetup."SMTP Server",':') <> 0 THEN BEGIN
        ServerAddr := COPYSTR(SMTPMailSetup."SMTP Server",1,STRPOS(SMTPMailSetup."SMTP Server",':') - 1);
        EVALUATE(ServerPort,COPYSTR(SMTPMailSetup."SMTP Server",STRPOS(SMTPMailSetup."SMTP Server",':') + 1));
      END ELSE BEGIN
        ServerAddr := SMTPMailSetup."SMTP Server";
        ServerPort := 25;
      END;

      CLEAR(Mail);
      Mail.SetServerAddress := ServerAddr;
      Mail.SetServerPort := ServerPort;
      CASE SMTPMailSetup.Authentication OF
        SMTPMailSetup.Authentication::Anonymous:
          Mail.SetSmtpAuthMethod := 'NONE';
        SMTPMailSetup.Authentication::NTLM:
          Mail.SetSmtpAuthMethod := 'NTLM';
        SMTPMailSetup.Authentication::Basic:
          BEGIN
            Mail.SetSmtpAuthMethod := 'LOGIN';
            SMTPMailSetup.TESTFIELD("User ID");
            SMTPMailSetup.TESTFIELD(Password);
            Mail.SetUsername := SMTPMailSetup."User ID";
            Mail.SetPassword := SMTPMailSetup.Password;
          END;
      END;

      Mail.SetBodyIsHtml := TRUE;

      Mail.SetFromAddress := EMSetup."Sender E-mail";
      Mail.SetFromName := EMSetup."Sender Name";

      IF ContiniaUserSetup.GetEmail = '' THEN BEGIN
        Mail.AddTo(EMSetup."Error E-Mail",EMSetup."Error E-Mail");
        Mail.SetSubject := STRSUBSTNO(Text012,ContiniaUserSetup."Continia User ID",COMPANYNAME);
      END ELSE BEGIN
        Mail.SetSubject := Subject;
        Mail.AddTo(ContiniaUserSetup.GetEmail,ContiniaUserSetup.GetEmail);
      END;
    END;

    LOCAL PROCEDURE CalcProgress@161024017(VAR TotalCount@161024012 : Integer;VAR Index@161024013 : Integer) : Integer;
    BEGIN
      EXIT(ROUND(Index / TotalCount * 10000,1,'>'));
    END;

    LOCAL PROCEDURE IsNewEventEntry@1160040003(ApprEntry@1160040000 : Record 454) : Boolean;
    VAR
      EventEntryCmt@1160040001 : Record 6085742;
    BEGIN
      EventEntryCmt.LOCKTABLE;
      EventEntryCmt.SETCURRENTKEY("Document Type","Document No.","Approver ID",Date);
      EventEntryCmt.SETRANGE("Document Type",ApprEntry."Document Type");
      EventEntryCmt.SETRANGE("Document No.",ApprEntry."Document No.");
      EventEntryCmt.SETRANGE("Approver ID",ApprEntry."Approver ID");
      EventEntryCmt.SETRANGE(Date,TODAY);
      IF EventEntryCmt.ISEMPTY THEN
        EXIT(TRUE);
    END;

    LOCAL PROCEDURE CreateEventReg@1160040002(FromEntryNo@1160040001 : Integer;ToEntryNo@1160040004 : Integer;EventArea@1160040000 : 'Status,Reminder');
    VAR
      EventReg@1160040002 : Record 6085740;
    BEGIN
      EventReg.LOCKTABLE;
      EventReg.INIT;
      EventReg."From Entry No." := FromEntryNo;
      EventReg."To Entry No." := ToEntryNo;
      EventReg.Area := EventArea;
      EventReg.INSERT(TRUE);
    END;

    LOCAL PROCEDURE CreateEventEntry@1160040001(ApprEntry@1160040004 : Record 454);
    VAR
      EventEntry@1160040001 : Record 6085741;
      EventEntryCmt@1160040000 : Record 6085742;
    BEGIN
      EventEntry.INIT;
      EventEntry.Type := EventEntry.Type::Information;
      EventEntry.INSERT(TRUE);

      EventEntryCmt.INIT;
      EventEntryCmt."Event Entry No." := EventEntry."Entry No.";
      EventEntryCmt."Document Type" := ApprEntry."Document Type";
      EventEntryCmt."Document No." := ApprEntry."Document No.";
      EventEntryCmt."Approver ID" := ApprEntry."Approver ID";
      EventEntryCmt.Comment :=
        STRSUBSTNO(Text009,ApprEntry.FIELDCAPTION("Approver ID"),ApprEntry."Approver ID",
         ApprEntry."Document Type",ApprEntry."Document No.");
      EventEntryCmt.INSERT(TRUE);
    END;

    LOCAL PROCEDURE GetLastEventEntry@1160040000() : Integer;
    VAR
      EventEntry@1160040000 : Record 6085741;
    BEGIN
      EventEntry.LOCKTABLE;
      IF EventEntry.FINDLAST THEN
        EXIT(EventEntry."Entry No.");
    END;

    BEGIN
    END.
  }
}

