OBJECT Codeunit 6086318 Expense Dimension Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW17.00.00.1.02.00,4PS9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE UpdateDimension@6085573(DimSetID@6085574 : Integer;DimCode@6085575 : Code[20];DimValueCode@6085576 : Code[20]) : Integer;
    VAR
      DimSetEntry@6085573 : TEMPORARY Record 480;
      DimMgt@6085577 : Codeunit 408;
    BEGIN
      DimMgt.GetDimensionSet(DimSetEntry,DimSetID);

      IF DimSetEntry.GET(DimSetID,DimCode) THEN BEGIN
        IF DimSetEntry."Dimension Value Code" = DimValueCode THEN
          EXIT;
        DimSetEntry.VALIDATE("Dimension Value Code",DimValueCode);
        DimSetEntry.MODIFY(TRUE);
      END ELSE BEGIN
        DimSetEntry.INIT;
        DimSetEntry."Dimension Set ID" := DimSetID;
        DimSetEntry.VALIDATE("Dimension Code",DimCode);
        DimSetEntry.VALIDATE("Dimension Value Code",DimValueCode);
        DimSetEntry.INSERT(TRUE);
      END;

      EXIT(DimMgt.GetDimensionSetID(DimSetEntry));
    END;

    PROCEDURE InsertGenJnlLineDim@6(VAR GenJnlLine@1002 : Record 81;DimCode@1001 : Code[20];DimValueCode@1000 : Code[20]);
    VAR
      GLSetup@1160040006 : Record 98;
    BEGIN
      GLSetup.GET;

      CASE DimCode OF
        GLSetup."Global Dimension 1 Code":
          GenJnlLine.VALIDATE("Shortcut Dimension 1 Code",DimValueCode);
        GLSetup."Global Dimension 2 Code":
          GenJnlLine.VALIDATE("Shortcut Dimension 2 Code",DimValueCode);
        ELSE
          BEGIN
            GenJnlLine."Dimension Set ID" := UpdateDimension(GenJnlLine."Dimension Set ID",DimCode,DimValueCode);
            GenJnlLine.MODIFY;
          END;
      END;
    END;

    PROCEDURE CopyExpDimToJnlLine@5(VAR Expense@1160040001 : Record 6086320;VAR GenJnlLine@1160040002 : Record 81);
    VAR
      ExpDim@1160040000 : Record 6086353;
    BEGIN
      ExpDim.SETRANGE("Expense Entry No.",Expense."Entry No.");
      ExpDim.SETFILTER("Dimension Code",'<>%1','');
      IF ExpDim.FINDFIRST THEN
        REPEAT
          InsertGenJnlLineDim(GenJnlLine,ExpDim."Dimension Code",ExpDim."Dimension Value Code");
        UNTIL ExpDim.NEXT = 0;
    END;

    PROCEDURE CopyExpAllocDimToJnlLine@4(VAR ExpenseAllocation@1160040001 : Record 6086321;VAR GenJnlLine@1160040002 : Record 81);
    VAR
      ExpAllocationDim@1000 : Record 6086357;
    BEGIN
      ExpAllocationDim.SETRANGE("Expense Allocation Entry No.",ExpenseAllocation."Entry No.");
      IF ExpAllocationDim.FINDFIRST THEN
        REPEAT
          InsertGenJnlLineDim(GenJnlLine,ExpAllocationDim."Dimension Code",ExpAllocationDim."Dimension Value Code");
        UNTIL ExpAllocationDim.NEXT = 0;
    END;

    PROCEDURE InsertPurchLineDim@3(VAR PurchLine@1160040004 : Record 39;DimCode@1160040002 : Code[20];DimValueCode@1160040001 : Code[20]);
    VAR
      GLSetup@1160040006 : Record 98;
    BEGIN
      GLSetup.GET;

      CASE DimCode OF
        GLSetup."Global Dimension 1 Code":
          PurchLine.VALIDATE("Shortcut Dimension 1 Code",DimValueCode);
        GLSetup."Global Dimension 2 Code":
          PurchLine.VALIDATE("Shortcut Dimension 2 Code",DimValueCode);
        ELSE
          BEGIN
            PurchLine."Dimension Set ID" := UpdateDimension(PurchLine."Dimension Set ID",DimCode,DimValueCode);
            PurchLine.MODIFY;
          END;
      END;
    END;

    PROCEDURE CopyExpDimToPurchLine@1160040007(VAR Expense@1160040001 : Record 6086320;VAR PurchLine@1160040002 : Record 39);
    VAR
      ExpDim@1160040000 : Record 6086353;
    BEGIN
      ExpDim.SETRANGE("Expense Entry No.",Expense."Entry No.");
      ExpDim.SETFILTER("Dimension Code",'<>%1','');
      IF ExpDim.FINDFIRST THEN
        REPEAT
          InsertPurchLineDim(PurchLine,ExpDim."Dimension Code",ExpDim."Dimension Value Code");
        UNTIL ExpDim.NEXT = 0;
    END;

    PROCEDURE CopyExpAllocDimToPurchLine@2(VAR ExpenseAllocation@1160040001 : Record 6086321;VAR PurchLine@1160040002 : Record 39);
    VAR
      ExpAllocationDim@1000 : Record 6086357;
    BEGIN
      ExpAllocationDim.SETRANGE("Expense Allocation Entry No.",ExpenseAllocation."Entry No.");
      IF ExpAllocationDim.FINDFIRST THEN
        REPEAT
          InsertPurchLineDim(PurchLine,ExpAllocationDim."Dimension Code",ExpAllocationDim."Dimension Value Code");
        UNTIL ExpAllocationDim.NEXT = 0;
    END;

    PROCEDURE ShowExpAllocDim@1(VAR ExpenseAllocation@1160040000 : Record 6086321;ReadOnly@1000 : Boolean);
    VAR
      ExpAllocationDim@1160040001 : Record 6086357;
      ExpAllDimensions@1001 : Page 6086368;
    BEGIN
      ExpAllocationDim.SETRANGE("Expense Allocation Entry No.",ExpenseAllocation."Entry No.");
      ExpAllDimensions.SETTABLEVIEW(ExpAllocationDim);
      IF ReadOnly THEN
         ExpAllDimensions.SetReadOnly;
      ExpAllDimensions.RUN;
    END;

    PROCEDURE InsertJobJnlLineDim@9(VAR JobJnlLine@1000 : Record 11072008;DimCode@1160040002 : Code[20];DimValueCode@1160040001 : Code[20]);
    VAR
      GLSetup@1160040006 : Record 98;
    BEGIN
      GLSetup.GET;

      CASE DimCode OF
        GLSetup."Global Dimension 1 Code":
          JobJnlLine.VALIDATE("Shortcut Dimension 1 Code",DimValueCode);
        GLSetup."Global Dimension 2 Code":
          JobJnlLine.VALIDATE("Shortcut Dimension 2 Code",DimValueCode);
        ELSE
          BEGIN
            JobJnlLine."Dimension Set ID" := UpdateDimension(JobJnlLine."Dimension Set ID",DimCode,DimValueCode);
            JobJnlLine.MODIFY;
          END;
      END;
    END;

    PROCEDURE CopyExpDimToJobJnlLine@8(VAR Expense@1160040001 : Record 6086320;VAR JobJnlLine@1000 : Record 11072008);
    VAR
      ExpDim@1160040000 : Record 6086353;
    BEGIN
      ExpDim.SETRANGE("Expense Entry No.",Expense."Entry No.");
      ExpDim.SETFILTER("Dimension Code",'<>%1','');
      IF ExpDim.FINDFIRST THEN
        REPEAT
          InsertJobJnlLineDim(JobJnlLine,ExpDim."Dimension Code",ExpDim."Dimension Value Code");
        UNTIL ExpDim.NEXT = 0;
    END;

    PROCEDURE CopyExpAllocDimToJobJnlLine@7(VAR ExpenseAllocation@1160040001 : Record 6086321;VAR JobJnlLine@1001 : Record 11072008);
    VAR
      ExpAllocationDim@1000 : Record 6086357;
    BEGIN
      ExpAllocationDim.SETRANGE("Expense Allocation Entry No.",ExpenseAllocation."Entry No.");
      IF ExpAllocationDim.FINDFIRST THEN
        REPEAT
          InsertJobJnlLineDim(JobJnlLine,ExpAllocationDim."Dimension Code",ExpAllocationDim."Dimension Value Code");
        UNTIL ExpAllocationDim.NEXT = 0;
    END;

    PROCEDURE InsertDefaultDimExpense@1160040010(VendorNo@1160040000 : Code[20];VAR Expense@1160040001 : Record 6086320);
    VAR
      GLSetup@1160040004 : Record 98;
      DefaultDim@1160040002 : Record 352;
      ExpenseDim@1160040003 : Record 6086353;
    BEGIN
      GLSetup.GET;
      DefaultDim.SETRANGE("Table ID",DATABASE::Vendor);
      DefaultDim.SETRANGE("No.",VendorNo);
      DefaultDim.SETFILTER("Dimension Value Code",'<>%1','');
      IF DefaultDim.FINDSET THEN
        REPEAT
          CASE DefaultDim."Dimension Code" OF
            GLSetup."Global Dimension 1 Code":
              Expense.VALIDATE("Global Dimension 1 Code",DefaultDim."Dimension Value Code");
            GLSetup."Global Dimension 2 Code":
              Expense.VALIDATE("Global Dimension 2 Code",DefaultDim."Dimension Value Code");
            ELSE
              IF NOT ExpenseDim.GET(Expense."Entry No.",DefaultDim."Dimension Code") THEN BEGIN
                ExpenseDim."Expense Entry No." := Expense."Entry No.";
                ExpenseDim."Dimension Code" := DefaultDim."Dimension Code";
                ExpenseDim."Dimension Value Code" := DefaultDim."Dimension Value Code";
                ExpenseDim.INSERT;
              END
          END
        UNTIL DefaultDim.NEXT = 0;
    END;

    PROCEDURE DeleteDefaultDimExpense@1160040011(VendorNo@1160040000 : Code[20];VAR Expense@1160040001 : Record 6086320);
    VAR
      GLSetup@1160040004 : Record 98;
      DefaultDim@1160040002 : Record 352;
      ExpenseDim@1160040003 : Record 6086353;
    BEGIN
      GLSetup.GET;
      DefaultDim.SETRANGE("Table ID",DATABASE::Vendor);
      DefaultDim.SETRANGE("No.",VendorNo);
      DefaultDim.SETFILTER("Dimension Value Code",'<>%1','');
      IF DefaultDim.FINDSET THEN
        REPEAT
          CASE DefaultDim."Dimension Code" OF
            GLSetup."Global Dimension 1 Code":
              Expense.VALIDATE("Global Dimension 1 Code",'');
            GLSetup."Global Dimension 2 Code":
              Expense.VALIDATE("Global Dimension 2 Code",'');
            ELSE
              IF ExpenseDim.GET(Expense."Entry No.",DefaultDim."Dimension Code") THEN
                ExpenseDim.DELETE;
          END
        UNTIL DefaultDim.NEXT = 0;
    END;

    BEGIN
    {
      4PS, Objects renumbered
    }
    END.
  }
}

