OBJECT Codeunit 6086319 Expense NAV-version Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW17.00.00.1.02.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      BankFileFilters@1002 : TextConst 'ENU=All Files (*.*)|*.*';
      SelectBankTxt@1001 : TextConst 'ENU=Select Bank File';
      Text033@1000 : TextConst 'ENU="DynamicsNAV://%1:%2/%3/%4/runpage?page=%5"';

    PROCEDURE GetClientFilename@3(ServerFilename@1160040000 : Text[1024]) : Text[1024];
    VAR
      FileMgt@6085574 : Codeunit 419;
      FileSysMgt@1000 : Codeunit 6085584;
      ClientFilename@6085573 : Text[1024];
    BEGIN
      ClientFilename := FileMgt.ClientTempFileName(FileSysMgt.GetFileExtension(ServerFilename));
      ClientFilename := FileMgt.GetDirectoryName(ClientFilename) + '\' + FileMgt.GetFileName(ServerFilename);
      FileMgt.DownloadToFile(ServerFilename,ClientFilename);
      EXIT(ClientFilename);
    END;

    PROCEDURE OpenBankfile@1() : Text;
    VAR
      FileMgt@1001 : Codeunit 419;
      Filename@1000 : Text;
    BEGIN
      Filename := FileMgt.OpenFileDialog(SelectBankTxt,'',BankFileFilters);
      Filename := FileMgt.UploadFileSilent(Filename);
      EXIT(Filename);
    END;

    PROCEDURE GetHyperlink@161024016(_UserId@161024017 : Code[50]) : Text[1024];
    VAR
      Setup@161024016 : Record 6085573;
      ContiniaUserSetup@161024018 : Record 6086002;
      ActiveSession@6085573 : Record 2000000110;
      ServerInstance@6085574 : Record 2000000112;
      COSetup@1000 : Record 6086500;
      EMSetup@1001 : Record 6086300;
    BEGIN
      COSetup.GET;
      EMSetup.GET;

      IF ContiniaUserSetup.GET(_UserId) AND ContiniaUserSetup."Web Approval User" THEN BEGIN
        IF EMSetup."Company Code" <> '' THEN
          EXIT(STRSUBSTNO('%1/%2/expense/approval/e',COSetup."Web Site Url",LOWERCASE(EMSetup."Company Code")))
        ELSE
          EXIT(STRSUBSTNO('%1/expense/approval/e',COSetup."Web Site Url"))
      END ELSE BEGIN
        ActiveSession.SETRANGE("Session ID",SESSIONID);
        ActiveSession.FINDFIRST;
        ServerInstance.GET(ActiveSession."Server Instance ID");
        EXIT(STRSUBSTNO(Text033,ServerInstance."Server Computer Name",ServerInstance."Server Port",
          ServerInstance."Server Instance Name",COMPANYNAME,PAGE::"Expense Approval Entries"));
      END;
    END;

    PROCEDURE BlobImport@1160040004(VAR BlobRef@1160040000 : Record 99008535) : Text[1024];
    VAR
      FileMgt@1160040001 : Codeunit 419;
    BEGIN
      EXIT(FileMgt.BLOBImport(BlobRef,'*.htm'));
    END;

    PROCEDURE BlobExport@1160040005(VAR BlobRef@1160040000 : Record 99008535;Filetypes@1160040002 : Text[100]);
    VAR
      FileMgt@1160040001 : Codeunit 419;
    BEGIN
      FileMgt.BLOBExport(BlobRef,Filetypes,TRUE);
    END;

    PROCEDURE HasPermission@1160040008(AdminUserRoleID@1160040000 : Code[20]) : Boolean;
    VAR
      AccessControl@1002 : Record 2000000053;
      User@1000 : Record 2000000120;
    BEGIN
      User.SETCURRENTKEY("User Name");
      User.SETRANGE("User Name",USERID);
      IF NOT User.FINDFIRST THEN
        EXIT(FALSE);

      IF AccessControl.GET(User."User Security ID",AdminUserRoleID) THEN
        EXIT(TRUE);

      IF AccessControl.GET(User."User Security ID",AdminUserRoleID,COMPANYNAME) THEN
        EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

