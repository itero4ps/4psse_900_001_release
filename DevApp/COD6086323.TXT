OBJECT Codeunit 6086323 Expense Comment Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.1.02.00;
  }
  PROPERTIES
  {
    Permissions=TableData 6086355=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ValChanged@1160040000 : TextConst 'ENU=%1 changed from %2 to %3.';

    PROCEDURE AddComment@1160040000(ExpEntryNo@1160040001 : Integer;Importance@1160040002 : Integer;Area@1160040003 : Code[20];Comment@1160040004 : Text[250];ValidationComment@1160040005 : Boolean);
    VAR
      ExpComment@1160040000 : Record 6086355;
    BEGIN
      ExpComment.SETCURRENTKEY("Expense Entry No.",Importance);
      ExpComment.SETRANGE("Expense Entry No.",ExpEntryNo);
      ExpComment.SETRANGE(Importance,Importance);
      ExpComment.SETRANGE(Area,Area);
      ExpComment.SETRANGE("Validation Comment",ValidationComment);
      ExpComment.DELETEALL(TRUE);

      ExpComment.INIT;
      ExpComment."Entry No." := GetNextEntryNo;
      ExpComment."Expense Entry No." := Importance;
      ExpComment.Importance := Importance;
      ExpComment.Area := Area;
      ExpComment."Expense Entry No." := ExpEntryNo;
      ExpComment.Comment := Comment;
      ExpComment."Validation Comment" := ValidationComment;
      ExpComment.INSERT(TRUE);
    END;

    PROCEDURE DeleteComment@1160040005(ExpEntryNo@1160040001 : Integer;Importance@1160040002 : Integer;Area@1160040003 : Code[20];Comment@1160040004 : Text[250];ValidationComment@1160040005 : Boolean);
    VAR
      ExpComment@1160040000 : Record 6086355;
    BEGIN
      ExpComment.SETCURRENTKEY("Expense Entry No.",Importance);
      ExpComment.SETRANGE("Expense Entry No.",ExpEntryNo);
      ExpComment.SETRANGE(Importance,Importance);
      ExpComment.SETRANGE(Area,Area);
      ExpComment.SETRANGE("Validation Comment",ValidationComment);
      ExpComment.DELETEALL(TRUE);
    END;

    PROCEDURE AddExpFieldValueChanged@1160040003(ExpEntryNo@1160040000 : Integer;ExpFieldNo@1160040001 : Integer;FidCaption@1160040002 : Text[250];xRecValue@1160040003 : Text[1024];RecValue@1160040004 : Text[1024]);
    VAR
      ExpComment@1160040005 : Record 6086355;
      AreaCode@1160040006 : Code[20];
    BEGIN
      IF xRecValue = RecValue THEN
        EXIT;

      DelExpFieldValueChangedCmt(ExpEntryNo,ExpFieldNo);

      AreaCode := 'FIELD CHANGED - ' + FORMAT(ExpFieldNo);
      AddComment(ExpEntryNo,ExpComment.Importance::Information,AreaCode,STRSUBSTNO(ValChanged,FidCaption,xRecValue,RecValue),FALSE);
    END;

    PROCEDURE DelExpFieldValueChangedCmt@1160040004(ExpEntryNo@1160040000 : Integer;ExpFieldNo@1160040001 : Integer);
    VAR
      ExpComment@1160040005 : Record 6086355;
      AreaCode@1160040006 : Code[20];
    BEGIN
      AreaCode := 'FIELD CHANGED - ' + FORMAT(ExpFieldNo);
      ExpComment.SETRANGE("Expense Entry No.",ExpEntryNo);
      ExpComment.SETRANGE(Area,AreaCode);
      ExpComment.DELETEALL;
    END;

    LOCAL PROCEDURE GetNextEntryNo@1160040001() : Integer;
    VAR
      ExpComment@1160040000 : Record 6086355;
    BEGIN
      IF ExpComment.FINDLAST THEN
        EXIT(ExpComment."Entry No." + 1)
      ELSE
        EXIT(1);
    END;

    PROCEDURE LoadCommentsInTemp@1160040002(ExpEntryNo@1160040000 : Integer;VAR TempExpenseCmt@1160040002 : TEMPORARY Record 6086356) : Boolean;
    VAR
      ApprovalComment@1160040003 : Record 455;
      ExpenseComment@1160040001 : Record 6086355;
      NewEntryNo@1160040004 : Integer;
      TypeAdded@1160040005 : Boolean;
    BEGIN
      NewEntryNo := 0;
      TempExpenseCmt.RESET;
      TempExpenseCmt.DELETEALL;

      ExpenseComment.SETCURRENTKEY("Expense Entry No.",Importance);
      ExpenseComment.SETRANGE("Expense Entry No.",ExpEntryNo);
      IF ExpenseComment.FINDSET THEN
        REPEAT
          NewEntryNo += 1;
          TempExpenseCmt."Entry No." := NewEntryNo;
          TempExpenseCmt.Importance := ExpenseComment.Importance;
          TempExpenseCmt.Area := ExpenseComment.Area;
          TempExpenseCmt.Comment := ExpenseComment.Comment;
          IF NOT TypeAdded THEN BEGIN
            TempExpenseCmt.Source := TempExpenseCmt.Source::ExpenseComment;
            TypeAdded := TRUE;
          END ELSE
            TempExpenseCmt.Source := TempExpenseCmt.Source::" ";
          TempExpenseCmt."Expense Entry No." := ExpEntryNo;
          TempExpenseCmt."Created Date/Time" := ExpenseComment."Created Date/Time";
          TempExpenseCmt."Created by" := ExpenseComment."Created by";
          TempExpenseCmt.INSERT;
        UNTIL ExpenseComment.NEXT = 0;

      TypeAdded := FALSE;
      ApprovalComment.SETCURRENTKEY("Table ID","Document Type","Document No.");
      ApprovalComment.SETRANGE("Table ID",DATABASE::Expense);
      ApprovalComment.SETRANGE("Document Type",ApprovalComment."Document Type"::Invoice);
      ApprovalComment.SETRANGE("Document No.",FORMAT(ExpEntryNo));
      IF ApprovalComment.FINDSET THEN BEGIN
        //INSERT SEPPARATOR
        IF NewEntryNo <> 0 THEN BEGIN
          NewEntryNo += 1;
          TempExpenseCmt."Entry No." := NewEntryNo;
          TempExpenseCmt.Importance := TempExpenseCmt.Importance::Information;
          TempExpenseCmt.Area := 'SEPPARATOR';
          TempExpenseCmt.Comment := '';
          TempExpenseCmt.Source := TempExpenseCmt.Source::" ";
          TempExpenseCmt."Expense Entry No." := ExpEntryNo;
          TempExpenseCmt.INSERT;
        END;

        REPEAT
          NewEntryNo += 1;
          TempExpenseCmt."Entry No." := NewEntryNo;
          TempExpenseCmt.Importance := TempExpenseCmt.Importance::Information;
          TempExpenseCmt.Area := 'APPRCOMM';
          TempExpenseCmt.Comment := ApprovalComment.Comment;
          IF NOT TypeAdded THEN BEGIN
            TempExpenseCmt.Source := TempExpenseCmt.Source::ApprovalComment;
            TypeAdded := TRUE;
          END ELSE
            TempExpenseCmt.Source := TempExpenseCmt.Source::" ";
          TempExpenseCmt."Expense Entry No." := ExpEntryNo;
          TempExpenseCmt."Created Date/Time" := ApprovalComment."Date and Time";
          TempExpenseCmt."Created by" := ApprovalComment."User ID";
          TempExpenseCmt.INSERT;
        UNTIL ApprovalComment.NEXT = 0;
      END;

      TempExpenseCmt.RESET;
      EXIT(TempExpenseCmt.FINDFIRST);
    END;

    BEGIN
    END.
  }
}

