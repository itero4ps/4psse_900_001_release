OBJECT Codeunit 6086329 Expense - Move to Company
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.1.02.00;
  }
  PROPERTIES
  {
    TableNo=6086320;
    OnRun=VAR
            NewExpenseInbox@1160040000 : Record 6086323;
            CompanyRec@1160040001 : Record 2000000006;
            Companyview@1160040002 : Page 1;
            ExpenseDim@1160040003 : Record 6086353;
            NewExpenseInboxDim@1160040007 : Record 6086324;
            ExpenseAttachment@1160040004 : Record 6086326;
            NewExpenseInboxAttachment@1160040005 : Record 6086328;
            FileManagement@1160040006 : Codeunit 6085584;
            MoveToCompany@1160040009 : Text[30];
            EntryNo@1160040008 : Integer;
          BEGIN
            TESTFIELD("Matched to Bank Transaction",FALSE);
            TESTFIELD(Status,Status::Open);
            IF NOT SelectCompany(MoveToCompany) THEN
              EXIT;

            NewExpenseInbox.CHANGECOMPANY(MoveToCompany);
            NewExpenseInboxAttachment.CHANGECOMPANY(MoveToCompany);
            NewExpenseInboxDim.CHANGECOMPANY(MoveToCompany);

            CLEAR(NewExpenseInbox."Expense GUID");
            IF NewExpenseInbox.FINDLAST THEN
              EntryNo := NewExpenseInbox."Entry No." + 1
            ELSE
              EntryNo := 1;
            NewExpenseInbox.INIT;
            NewExpenseInbox."Entry No." := EntryNo;
            NewExpenseInbox.INSERT;

            NewExpenseInbox."Employee No." := "Employee No.";
            NewExpenseInbox.Description := Description;
            NewExpenseInbox."Description 2" := "Description 2";
            NewExpenseInbox."Document Date" := "Document Date";
            NewExpenseInbox."Country/Region Code" := "Country/Region Code";
            NewExpenseInbox."Currency Code" := "Currency Code";
            NewExpenseInbox.Amount := Amount;
            NewExpenseInbox."Date Created" := TODAY;
            NewExpenseInbox."Job No." := "Job No.";
            NewExpenseInbox."Job Task No." := "Job Task No.";
            NewExpenseInbox."Cash/Private Card" := "Cash/Private Card";
            NewExpenseInbox."Imported Date/Time" := CURRENTDATETIME;
            NewExpenseInbox."Imported by User-ID" := USERID;
            NewExpenseInbox."Expense Type" := "Expense Type";
            NewExpenseInbox.MODIFY;

            ExpenseDim.SETCURRENTKEY("Expense Entry No.");
            ExpenseDim.SETRANGE("Expense Entry No.","Entry No.");
            IF ExpenseDim.FINDFIRST THEN
              REPEAT
                NewExpenseInboxDim."Inbox Entry No." := NewExpenseInbox."Entry No.";
                NewExpenseInboxDim."Dimension Code" := ExpenseDim."Dimension Code";
                NewExpenseInboxDim."Field Code" := ExpenseDim."Field Code";
                NewExpenseInboxDim."Dimension Value Code" := ExpenseDim."Dimension Value Code";
                NewExpenseInboxDim."Field Value" := ExpenseDim."Field Value";
                NewExpenseInboxDim.INSERT;
              UNTIL ExpenseDim.NEXT = 0;

            ExpenseAttachment.SETCURRENTKEY("Expense Entry No.");
            ExpenseAttachment.SETRANGE("Expense Entry No.","Entry No.");
            IF NewExpenseInboxAttachment.FINDLAST THEN;
            IF ExpenseAttachment.FINDFIRST THEN
              REPEAT
                NewExpenseInboxAttachment."Entry No." := NewExpenseInboxAttachment."Entry No." + 1;
                NewExpenseInboxAttachment."Attachment GUID" := CREATEGUID;
                NewExpenseInboxAttachment."Expense Inbox Entry No." := NewExpenseInbox."Entry No.";
                NewExpenseInboxAttachment.Filename := ExpenseAttachment.Filename;
                FileManagement.CopyFile(
                  ExpenseAttachment.GetServerFilenameAndPath,
                  GetServerFilenameAndPath(MoveToCompany,NewExpenseInboxAttachment));
                NewExpenseInboxAttachment.INSERT;
              UNTIL ExpenseAttachment.NEXT = 0;
            COMMIT;

            Rec.DELETE(TRUE);

            MESSAGE(TextMoved,MoveToCompany);
          END;

  }
  CODE
  {
    VAR
      TextMoved@1160040001 : TextConst 'ENU=Expense has been moved to %1';

    LOCAL PROCEDURE SelectCompany@1160040000(VAR MoveToCompany@1160040000 : Text[30]) : Boolean;
    VAR
      Company@1160040001 : Record 2000000006;
    BEGIN
      Company.SETFILTER(Name,'<>%1',COMPANYNAME);
      IF PAGE.RUNMODAL(357,Company) = ACTION::LookupOK THEN BEGIN
        MoveToCompany := Company.Name;
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE GetServerFilenameAndPath@1160040005(Company@1160040000 : Text[30];ExpenseInboxAtt@1160040001 : Record 6086328) Path : Text[1024];
    VAR
      EMSetup@1160040002 : Record 6086300;
      Expense@1160040003 : Record 6086320;
      ExpenseInbound@1160040005 : Record 6086323;
      DateCreated@1160040004 : Date;
    BEGIN
      DateCreated := GetCreatedDate(Company,ExpenseInboxAtt);
      IF DateCreated = 0D THEN
        EXIT;
      EMSetup.CHANGECOMPANY(Company);
      Path := EMSetup.GetArchiveDirFromDate(DateCreated,TRUE);
      Path := Path + STRSUBSTNO('%1 - %2',ExpenseInboxAtt."Attachment GUID",ExpenseInboxAtt.Filename);
    END;

    LOCAL PROCEDURE GetClientFilenameAndPath@1160040002(GetServerFilenameAndPath@1160040001 : Text[1024]) : Text[1024];
    VAR
      EMVersionMgt@1160040000 : Codeunit 6086319;
    BEGIN
      EXIT(EMVersionMgt.GetClientFilename(GetServerFilenameAndPath));
    END;

    LOCAL PROCEDURE GetCreatedDate@1160040001(Company@1160040000 : Text[30];ExpenseInboxAtt@1160040003 : Record 6086328) : Date;
    VAR
      ExpenseInbox@1160040002 : Record 6086323;
    BEGIN
      ExpenseInbox.CHANGECOMPANY(Company);
      IF ExpenseInbox.GET(ExpenseInboxAtt."Expense Inbox Entry No.") THEN
        EXIT(ExpenseInbox."Date Created");
    END;

    BEGIN
    END.
  }
}

