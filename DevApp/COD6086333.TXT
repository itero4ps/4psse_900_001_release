OBJECT Codeunit 6086333 Expense-Check
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.1.02.00;
  }
  PROPERTIES
  {
    TableNo=6086320;
    OnRun=VAR
            ExpenseAllocation@1160040000 : Record 6086321;
            UseExpenseAllocation@1160040001 : Boolean;
            ContiniaUserSetup@1160040002 : Record 6086002;
          BEGIN
            TESTFIELD(Posted,FALSE);
            TESTFIELD("Employee No.");
            TESTFIELD("Document Date");
            TESTFIELD("Expense Type");
            IF NOT ((Status = Status::Rejected) AND "Matched to Bank Transaction") THEN
              TESTFIELD("Expense Account");
            IF NOT "Cash/Private Card" THEN
              TESTFIELD("Matched to Bank Transaction");

            ContiniaUserSetup.GET("Employee No.");
            ContiniaUserSetup.TESTFIELD("Vendor No.");

            IF NOT (Status IN [Status::Approved,Status::Rejected]) THEN
              ERROR(WrongStatusErr,FIELDCAPTION(Status),GetStatusCaption(Status::Approved),GetStatusCaption(Status::Rejected));

            CLEAR(UseExpenseAllocation);
            ExpenseAllocation.SETCURRENTKEY("Expense Entry No.");
            ExpenseAllocation.SETRANGE("Expense Entry No.","Entry No.");
            IF ExpenseAllocation.FINDFIRST THEN BEGIN
              CALCFIELDS("Allocated Amount (LCY)");
              IF "Allocated Amount (LCY)" <> "Amount (LCY)" THEN
                ERROR(WrongAllocAmtErr,FIELDCAPTION("Amount (LCY)"),TABLECAPTION,"Entry No.");
              UseExpenseAllocation := TRUE;
            END;

            IF UseExpenseAllocation THEN
              CheckAllocation(Rec);

            IF (Status = Status::Rejected) AND "Matched to Bank Transaction" AND "No Refund" AND
               ("Amount (LCY)" < 0)
            THEN
              ERROR(MatchedNegativeRefundRejected);
          END;

  }
  CODE
  {
    VAR
      WrongMatchAmtErr@1160040001 : TextConst 'ENU=Amount and Matched Amount does not match.';
      WrongAllocAmtErr@1160040000 : TextConst 'ENU=%1 on %2 %3 does not match the total allocated amount.';
      NoAllocationAllowed@1160040002 : TextConst 'ENU=No Allocation allowed for Expenses that are positive,matched and rejected.';
      NoAllocationAllowedNegativ@1160040005 : TextConst 'ENU=No Allocation allowed for Expenses that are matched and without refund.';
      WrongStatusErr@1160040003 : TextConst 'ENU=%1 must be either %2 or %3';
      MatchedNegativeRefundRejected@1160040004 : TextConst 'ENU=A negative amount that has been matched and has no refund can''t be rejected';

    PROCEDURE CheckAllocation@1160040000(VAR Rec@1160040000 : Record 6086320);
    BEGIN
      WITH Rec DO BEGIN
        IF (Status = Status::Rejected) AND "Matched to Bank Transaction" AND (NOT "No Refund") AND
         ("Amount (LCY)" > 0)
      THEN
        ERROR(NoAllocationAllowed);

      IF "Matched to Bank Transaction" AND "No Refund" THEN
        ERROR(NoAllocationAllowedNegativ);
      END;
    END;

    BEGIN
    END.
  }
}

