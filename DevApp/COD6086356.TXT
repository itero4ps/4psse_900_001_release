OBJECT Codeunit 6086356 EM Approval Functions (WS)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.1.03.00;
  }
  PROPERTIES
  {
    Permissions=TableData 8=r,
                TableData 454=m,
                TableData 455=rim,
                TableData 6085590=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      NSTLanguageId@161024015 : Integer;
      Text001@161024013 : TextConst 'ENU=You are not allowed to reject this document.';
      Text002@161024012 : TextConst 'ENU=You are not allowed to approve this document.';
      Text004@161024016 : TextConst 'ENU=You are not allowed to put this document on hold.';
      Text005@161024017 : TextConst 'ENU=You are not allowed to remove this document from on hold.';
      Text006@161024018 : TextConst 'ENU=Your approval limit does not allow you to forward this %1.';
      UserFilterType@1160040000 : 'CurrUserOnly,CurrUserAndSharing,NoUserFilter';

    PROCEDURE GetAttachmentFile@1160040003(AttachmentEntryNo@1160040005 : Integer;ToUrl@1160040001 : Text[1024]);
    VAR
      ExpenseAttachment@1160040000 : Record 6086326;
      OnlineMgt@1160040002 : Codeunit 6086500;
    BEGIN
      ExpenseAttachment.GET(AttachmentEntryNo);
      OnlineMgt.UploadFile(ExpenseAttachment.GetServerFilenameAndPath,ToUrl,FALSE);
    END;

    PROCEDURE AddAttachmentFile@1160040001(ExpenseEntryNo@1160040008 : Integer;Filename@1160040000 : Text[1024];Username@1160040003 : Code[50];FromUrl@1160040005 : Text[1024];VAR NewAttachmentEntryNo@1160040006 : Integer);
    VAR
      ExpenseAttachment@1160040007 : Record 6086326;
      OnlineMgt@1160040010 : Codeunit 6086500;
    BEGIN
      ExpenseAttachment."Expense Entry No." := ExpenseEntryNo;
      ExpenseAttachment.Filename := Filename;
      ExpenseAttachment.INSERT(TRUE);

      NewAttachmentEntryNo := ExpenseAttachment."Entry No.";

      OnlineMgt.DownloadFile(FromUrl,ExpenseAttachment.GetServerFilenameAndPath,FALSE);
    END;

    PROCEDURE DeleteAttachmentFile@1160040000(AttachmentEntryNo@1160040000 : Integer);
    VAR
      ExpenseAttachment@1160040002 : Record 6086326;
    BEGIN
      ActivateDefaultLanguage;

      ExpenseAttachment.GET(AttachmentEntryNo);
      ExpenseAttachment.DELETE(TRUE);

      GLOBALLANGUAGE := NSTLanguageId;
    END;

    PROCEDURE AddComment@161024012(ExpenseEntryNo@1160040000 : Integer;_UserID@161024014 : Code[50];Comment@161024016 : Text[1024]);
    VAR
      ApprovalCmtLine@161024017 : Record 455;
    BEGIN
      WHILE Comment <> '' DO BEGIN
        CLEAR(ApprovalCmtLine);
        ApprovalCmtLine."Table ID" := DATABASE::Expense;
        ApprovalCmtLine."Document Type" := ApprovalCmtLine."Document Type"::Invoice;
        ApprovalCmtLine."Document No." := FORMAT(ExpenseEntryNo);
        ApprovalCmtLine.Comment := COPYSTR(Comment,1,MAXSTRLEN(ApprovalCmtLine.Comment));
        Comment := COPYSTR(Comment,MAXSTRLEN(ApprovalCmtLine.Comment) + 1);

        ApprovalCmtLine.SETRANGE("Table ID",DATABASE::Expense);
        ApprovalCmtLine.SETRANGE("Document Type",ApprovalCmtLine."Document Type"::Invoice);
        ApprovalCmtLine.SETRANGE("Document No.",FORMAT(ExpenseEntryNo));

        ApprovalCmtLine.INSERT(TRUE);

        ApprovalCmtLine."Date and Time" := CURRENTDATETIME;
        ApprovalCmtLine."User ID" := _UserID;
        ApprovalCmtLine.MODIFY;
      END;
    END;

    PROCEDURE Approve@161024013(ExpenseEntryNo@1160040003 : Integer;ApproverId@161024014 : Code[50];VAR NextEntryNo@161024016 : Integer;VAR NextApprover@1160040000 : Code[250]);
    VAR
      Expense@1160040001 : Record 6086320;
      ApprovalEntry@161024017 : Record 454;
      EMApprovalMgt@1160040004 : Codeunit 6086312;
      EMApprovalsBridge@1160040002 : Codeunit 6086369;
    BEGIN
      ActivateDefaultLanguage;

      Expense.GET(ExpenseEntryNo);

      EMApprovalsBridge.SetCurrentUser(ApproverId);
      EMApprovalsBridge.Approve(Expense);

      NextApprover := EMApprovalMgt.GetApproverDisplayName(EMApprovalMgt.GetNextApprover(Expense));

      NextEntryNo := GetNextExpenseEntryNo(Expense."Entry No.",ApproverId);

      GLOBALLANGUAGE := NSTLanguageId;
    END;

    PROCEDURE Reject@161024014(ExpenseEntryNo@1160040004 : Integer;ApproverId@1160040003 : Code[50];VAR NextEntryNo@1160040002 : Integer);
    VAR
      Expense@1160040006 : Record 6086320;
      ApprovalEntry@1160040005 : Record 454;
      EMApprovalMgt@1160040000 : Codeunit 6086312;
      EMApprovalsBridge@1160040007 : Codeunit 6086369;
    BEGIN
      ActivateDefaultLanguage;

      Expense.GET(ExpenseEntryNo);
      EMApprovalMgt.SetCurrUserID(ApproverId);
      EMApprovalMgt.FilterApprovalEntry(Expense,ApprovalEntry,UserFilterType::CurrUserAndSharing);
      EMApprovalsBridge.RejectApprovalRequest(ApprovalEntry);

      Expense.GET(ExpenseEntryNo);
      EMApprovalMgt.RejectExpense(Expense);

      NextEntryNo := GetNextExpenseEntryNo(Expense."Entry No.",ApproverId);

      GLOBALLANGUAGE := NSTLanguageId;
    END;

    PROCEDURE Delegate@161024015(ExpenseEntryNo@1160040004 : Integer;ApproverId@1160040003 : Code[50];DelegateToUserId@1160040006 : Code[50];DelegateAction@1160040005 : Integer;VAR NextEntryNo@1160040002 : Integer;VAR NextApprover@1160040000 : Code[250]);
    VAR
      Expense@1160040008 : Record 6086320;
      ApprovalEntry@1160040007 : Record 454;
      EMApprovalMgt@1160040001 : Codeunit 6086312;
      DCApprovalMgt@1000 : Codeunit 6085722;
    BEGIN
      ActivateDefaultLanguage;

      Expense.GET(ExpenseEntryNo);

      ApprovalEntry.SETRANGE("Table ID",DATABASE::Expense);
      ApprovalEntry.SETRANGE("Document Type",ApprovalEntry."Document Type"::Invoice);
      ApprovalEntry.SETRANGE("Document No.",FORMAT(Expense."Entry No."));
      ApprovalEntry.SETRANGE(Status,ApprovalEntry.Status::Open);
      ApprovalEntry.FINDFIRST;

      DCApprovalMgt.SetCurrUserID(ApproverId);
      DCApprovalMgt.DelegateApprovalRequest2(ApprovalEntry,DelegateAction,DelegateToUserId);

      NextApprover := EMApprovalMgt.GetApproverDisplayName(EMApprovalMgt.GetNextApprover(Expense));

      NextEntryNo := GetNextExpenseEntryNo(Expense."Entry No.",ApproverId);

      GLOBALLANGUAGE := NSTLanguageId;
    END;

    PROCEDURE PutOnHold@161024021(ExpenseEntryNo@1160040007 : Integer;ApproverId@1160040006 : Code[50];VAR NextEntryNo@1160040005 : Integer);
    VAR
      Expense@1160040002 : Record 6086320;
      ApprovalEntry@1160040001 : Record 454;
      EMApprovalMgt@1160040000 : Codeunit 6086312;
    BEGIN
      ActivateDefaultLanguage;

      Expense.GET(ExpenseEntryNo);

      EMApprovalMgt.SetCurrUserID(ApproverId);
      EMApprovalMgt.PutOnHold(Expense);

      NextEntryNo := GetNextExpenseEntryNo(Expense."Entry No.",ApproverId);

      GLOBALLANGUAGE := NSTLanguageId;
    END;

    PROCEDURE RemoveOnHold@161024026(ExpenseEntryNo@1160040005 : Integer;ApproverId@1160040004 : Code[50];VAR NextEntryNo@1160040000 : Integer);
    VAR
      Expense@1160040003 : Record 6086320;
      ApprovalEntry@1160040002 : Record 454;
      EMApprovalMgt@1160040001 : Codeunit 6086312;
    BEGIN
      ActivateDefaultLanguage;

      Expense.GET(ExpenseEntryNo);

      EMApprovalMgt.SetCurrUserID(ApproverId);
      EMApprovalMgt.RemoveOnHold(Expense);

      NextEntryNo := GetNextExpenseEntryNo(Expense."Entry No.",ApproverId);

      GLOBALLANGUAGE := NSTLanguageId;
    END;

    LOCAL PROCEDURE XFilterApprovalEntry@161024016(VAR ApprovalEntry@161024013 : Record 454;TableID@1160040000 : Integer;DocumentType@161024015 : Integer;DocumentNo@161024014 : Code[20];ApproverFilter@161024012 : Text[1024];StatusFilter@6085573 : Integer) : Boolean;
    BEGIN
      IF ApproverFilter = '' THEN
        EXIT(FALSE);

      ApprovalEntry.RESET;
      ApprovalEntry.SETCURRENTKEY("Table ID","Document Type","Document No.");
      ApprovalEntry.SETRANGE("Table ID",TableID);
      ApprovalEntry.SETRANGE("Document Type",DocumentType);
      ApprovalEntry.SETRANGE("Document No.",DocumentNo);
      ApprovalEntry.SETFILTER("Approver ID",ApproverFilter);
      IF StatusFilter <> -1 THEN
        ApprovalEntry.SETRANGE(Status,StatusFilter);

      EXIT(ApprovalEntry.FINDFIRST);
    END;

    LOCAL PROCEDURE GetNextExpenseEntryNo@161024018(CurrExpenseEntryNo@1160040001 : Integer;ApproverID@1160040000 : Code[50]) : Integer;
    VAR
      ApprovalEntry@1160040003 : Record 454;
      Expense@1160040006 : Record 6086320;
      EMApprovalMgt@161024014 : Codeunit 6086312;
      SharingFilter@1160040005 : Text[1024];
    BEGIN
      ApprovalEntry.SETRANGE("Table ID",DATABASE::Expense);
      ApprovalEntry.SETRANGE("Document Type",ApprovalEntry."Document Type"::Invoice);
      ApprovalEntry.SETRANGE(Status,ApprovalEntry.Status::Open);

      SharingFilter := EMApprovalMgt.GetApprovalSharingToUserFilter(ApproverID);
      IF SharingFilter <> '' THEN
        ApprovalEntry.SETFILTER("Approver ID",ApproverID + '|' + SharingFilter)
      ELSE
        ApprovalEntry.SETRANGE("Approver ID",ApproverID);

      ApprovalEntry.SETFILTER("Document No.",'>%1',FORMAT(CurrExpenseEntryNo));
      IF ApprovalEntry.FINDFIRST THEN
        REPEAT
          Expense.GET(EMApprovalMgt.Code2Int(ApprovalEntry."Document No."));
          IF Expense."On Hold" = '' THEN
            EXIT(Expense."Entry No.");
        UNTIL ApprovalEntry.NEXT = 0;

      ApprovalEntry.SETFILTER("Document No.",'<%1',FORMAT(CurrExpenseEntryNo));
      IF ApprovalEntry.FINDFIRST THEN
        REPEAT
          Expense.GET(EMApprovalMgt.Code2Int(ApprovalEntry."Document No."));
          IF Expense."On Hold" = '' THEN
            EXIT(Expense."Entry No.");
        UNTIL ApprovalEntry.NEXT = 0;
    END;

    PROCEDURE GetDocumentPermissions@161024023(ExpenseEntryNo@161024014 : Integer;ApproverId@161024023 : Code[50];VAR AllowView@161024013 : Boolean;VAR AllowEditLines@161024016 : Boolean;VAR AllowAddComment@161024017 : Boolean;VAR AllowAttachFiles@161024018 : Boolean;VAR AllowApprove@161024019 : Boolean;VAR AllowReject@161024020 : Boolean;VAR AllowForward@161024021 : Boolean;VAR AllowPutOnHold@161024022 : Boolean);
    VAR
      Expense@161024026 : Record 6086320;
      ApprovalEntry@161024025 : Record 454;
      EMApprovalMgt@161024028 : Codeunit 6086312;
    BEGIN
      Expense.GET(ExpenseEntryNo);
      EMApprovalMgt.FilterApprovalEntry(Expense,ApprovalEntry,UserFilterType::CurrUserAndSharing);

      IF ApprovalEntry.FINDFIRST THEN BEGIN
        AllowView := TRUE;
        AllowEditLines := TRUE;
        AllowAddComment := TRUE;
        AllowAttachFiles := TRUE;
        AllowApprove := TRUE;
        AllowReject := TRUE;
        AllowForward := TRUE;
        AllowPutOnHold := TRUE;
      END;
    END;

    LOCAL PROCEDURE ActivateDefaultLanguage@161024022();
    VAR
      Language@161024013 : Record 8;
      COSetup@1160040000 : Record 6086500;
    BEGIN
      IF NOT COSetup.GET THEN
        EXIT;

      COSetup.TESTFIELD("Default Language Code");
      Language.GET(COSetup."Default Language Code");
      Language.TESTFIELD("Windows Language ID");
      NSTLanguageId := GLOBALLANGUAGE;
      GLOBALLANGUAGE := Language."Windows Language ID";
    END;

    BEGIN
    END.
  }
}

