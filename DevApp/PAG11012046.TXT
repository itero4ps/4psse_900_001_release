OBJECT Page 11012046 Settlement Sheet
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Settlement Sheet;
    SourceTable=Table11012021;
    PopulateAllFields=Yes;
    PageType=Document;
    OnInit=BEGIN
             SheetLineSFEditable := TRUE;
           END;

    OnAfterGetRecord=BEGIN
                       CALCFIELDS("Open (Purchase)","Received/Not Invoiced");
                       SetSubformEditable();
                     END;

    OnNewRecord=VAR
                  lvSettlRec@1210190000 : Record 11012021;
                  Commission@1100525001 : Record 11012020;
                  TmpSettlSheet@1100525002 : TEMPORARY Record 11012021;
                  lString@1100525501 : Text[500];
                  lProjNo@1100525502 : Code[20];
                  lPrincipal@1100525503 : Code[20];
                  lCommNo@1100525504 : Code[20];
                BEGIN
                  lProjNo := '';
                  lPrincipal := '';
                  lCommNo := '';

                  lString := GETFILTER("Project No.");
                  lString := DELCHR(lString, '<>');
                  IF (STRLEN(lString) <= 20) AND (STRLEN(lString) > 0) THEN BEGIN
                    lProjNo := lString;
                    lString := GETFILTER(Principal);
                    lString := DELCHR(lString, '<>');
                    IF (STRLEN(lString) <= 20) AND (STRLEN(lString) > 0) THEN BEGIN
                      lPrincipal := lString;
                      lString := GETFILTER("Commission No.");
                      lString := DELCHR(lString, '<>');
                      IF (STRLEN(lString) <= 20) AND (STRLEN(lString) > 0) THEN BEGIN
                        lCommNo := lString;
                        "Project No." := lProjNo;
                        Principal := lPrincipal;
                        "Commission No." := lCommNo;
                      END;
                    END;
                  END;
                  //* C-027523.sn
                  IF (("Project No." = '') OR (Principal = '') OR ("Commission No." = '')) AND
                     ((xRec."Project No." <> '') AND (xRec.Principal <> '') AND (xRec."Commission No." <> ''))
                  THEN BEGIN
                    //* If there is a (single) filter on the fields proj/prin/comm then these fields are filled automaticaly (fields not
                    //* on form). But if the user has modified/reset the filter then it possible that these fields are not filled autom.,
                    //* then try with xRec. Note: Combination proj/prin/comm must exist and must be within filters of Rec.
                    IF Commission.GET(xRec."Project No.", xRec.Principal, xRec."Commission No.") THEN BEGIN
                      IF "Project No." <> '' THEN
                        TmpSettlSheet."Project No." := "Project No."
                      ELSE
                        TmpSettlSheet."Project No." := xRec."Project No.";
                      IF Principal <> '' THEN
                        TmpSettlSheet.Principal := Principal
                      ELSE
                        TmpSettlSheet.Principal := xRec.Principal;
                      IF "Commission No." <> '' THEN
                        TmpSettlSheet."Commission No." := "Commission No."
                      ELSE
                        TmpSettlSheet."Commission No." := xRec."Commission No.";
                      TmpSettlSheet.INSERT;
                      TmpSettlSheet.COPYFILTERS(Rec);
                      TmpSettlSheet.SETRANGE("No.");  //* Reset filter on Settl. No.
                      IF TmpSettlSheet.FINDFIRST THEN BEGIN
                        "Project No." := TmpSettlSheet."Project No.";
                        Principal := TmpSettlSheet.Principal;
                        "Commission No." := TmpSettlSheet."Commission No.";
                      END;
                    END;
                  END;


                  lvSettlRec.SETRANGE("Project No.","Project No.");
                  lvSettlRec.SETRANGE(Principal,Principal);
                  lvSettlRec.SETRANGE("Commission No.","Commission No.");
                  IF lvSettlRec.FINDLAST AND (lvSettlRec."No." <> '') THEN
                    "No." := INCSTR(lvSettlRec."No.")
                  ELSE
                    IF "No." = '1' THEN
                      "No." := '001';
                    WHILE lvSettlRec.GET("Project No.", Principal, "Commission No.", "No.") DO
                      "No." := INCSTR(lvSettlRec."No.");
                  //* C-027523.en
                END;

    OnAfterGetCurrRecord=VAR
                           RecRef@1100528400 : RecordRef;
                         BEGIN
                           RecRef.GETTABLE(Rec);
                           CurrPage.RegisterExternalDocumentFB.PAGE.SetRecordReference(RecRef);
                           CurrPage.RegisterExternalDocumentFB.PAGE.SetByCompany(TRUE);
                           CurrPage.RegisterExternalDocumentFB.PAGE.SetDepartmentCode("Global Dimension 1 Code");
                         END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 12      ;1   ;ActionGroup;
                      CaptionML=ENU=&Settlement Sheet }
      { 1210190008;2 ;Action    ;
                      ShortCutKey=F7;
                      CaptionML=ENU=Statistics;
                      RunObject=Page 11124850;
                      RunPageView=SORTING(Project No.,Principal,Commission No.,No.);
                      RunPageLink=Project No.=FIELD(Project No.),
                                  Principal=FIELD(Principal),
                                  Commission No.=FIELD(Commission No.),
                                  No.=FIELD(No.);
                      Promoted=Yes;
                      Image=Statistics;
                      PromotedCategory=Process }
      { 1100485010;2 ;Action    ;
                      CaptionML=ENU=&Documents;
                      Image=Documents;
                      OnAction=VAR
                                 DocumentFilters@1100525001 : Codeunit 11012432;
                                 RecRef@1100525002 : RecordRef;
                               BEGIN
                                 RecRef.GETTABLE(Rec);
                                 //DocumentFilters.CreateTempFilterAndExecute2(RecRef, '', FALSE, TRUE, FORMAT(TRUE)); //C008584.o
                                 DocumentFilters.CreateTempFilterAndExecute1(RecRef, '', FALSE, TRUE); //C008584.n
                               END;
                                }
      { 1100525003;2 ;Action    ;
                      CaptionML=ENU=Project Cost Plus Surcharges;
                      Image=CostEntries;
                      OnAction=VAR
                                 ProjSurchargeForm@1100525001 : Page 11072598;
                                 ProjectCostPlusSurcharge@1100525000 : Record 11020561;
                               BEGIN
                                 ProjectCostPlusSurcharge.FILTERGROUP(10);
                                 ProjectCostPlusSurcharge.SETRANGE(Level, ProjectCostPlusSurcharge.Level::"Project Principal");
                                 ProjectCostPlusSurcharge.SETRANGE("Project No.", "Project No.");
                                 ProjectCostPlusSurcharge.SETRANGE(Principal, Principal);
                                 ProjectCostPlusSurcharge.FILTERGROUP(0);
                                 ProjectCostPlusSurcharge.Level := ProjectCostPlusSurcharge.Level::"Project Principal";
                                 ProjSurchargeForm.SETTABLEVIEW(ProjectCostPlusSurcharge);
                                 ProjSurchargeForm.SETRECORD(ProjectCostPlusSurcharge);
                                 ProjSurchargeForm.RUNMODAL;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210190000;1 ;Action    ;
                      CaptionML=ENU=&Text;
                      Promoted=Yes;
                      Image=SNInfo;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CurrPage.SheetLineSF.PAGE.EditText;
                               END;
                                }
      { 1210190001;1 ;ActionGroup;
                      CaptionML=ENU=F&unctions }
      { 1100525500;2 ;Action    ;
                      CaptionML=ENU=Get Open Purchase Order Lines;
                      Image=GetLines;
                      OnAction=VAR
                                 JobPurchFrm@1100525501 : Page 11020267;
                                 PurchaseLineRec@1100525500 : Record 39;
                               BEGIN
                                 PurchaseLineRec.SETRANGE("Job No.","Project No.");
                                 PurchaseLineRec.SETRANGE("Document Type", PurchaseLineRec."Document Type"::Order);
                                 JobPurchFrm.LOOKUPMODE := TRUE;
                                 JobPurchFrm.SetSelection(DATABASE::"Project Cost Plus Entry","Project No.",Principal,"Commission No.","No.");
                                 JobPurchFrm.SETTABLEVIEW(PurchaseLineRec);
                                 JobPurchFrm.RUNMODAL;
                                 CurrPage.UPDATE;
                               END;
                                }
      { 1100525502;2 ;Action    ;
                      CaptionML=ENU=Get Open Hour Lines;
                      Image=GetEntries;
                      OnAction=VAR
                                 HourLineRec@1100525506 : Record 11012039;
                                 HourLineFrm@1100525505 : Page 11012277;
                                 ProjSetup@1100525504 : Record 315;
                                 ProjRec@1100525503 : Record 11072003;
                                 lvCopyPerm@1100525502 : Boolean;
                               BEGIN
                                 ProjSetup.GET;
                                 ProjRec.GET("Project No.");
                                 IF ProjRec."Small Project" THEN
                                   lvCopyPerm := (ProjSetup."Small Project Cost Plus Hours" = ProjSetup."Small Project Cost Plus Hours"::"All Hours")
                                 ELSE
                                   lvCopyPerm := (ProjSetup."Project Cost Plus Hours from" = ProjSetup."Project Cost Plus Hours from"::"All Hours");

                                 HourLineRec.SETRANGE("Project No.", "Project No.");
                                 IF lvCopyPerm THEN
                                   HourLineFrm.SetSelection(DATABASE::"Project Cost Plus Entry", "Project No.", Principal, "Commission No.", "No.", FALSE);
                                 HourLineFrm.SETTABLEVIEW(HourLineRec);
                                 HourLineFrm.RUNMODAL();
                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1210190006;2 ;Action    ;
                      CaptionML=ENU=Generate Cost Plus Entries (Estimate);
                      Image=CreateLinesFromJob;
                      OnAction=BEGIN
                                 CurrPage.SheetLineSF.PAGE.CreateProjectCostPlusEntries(Rec);
                               END;
                                }
      { 1100485008;2 ;Action    ;
                      CaptionML=ENU=Set Cost Plus Entries to Processed;
                      Image=SetupList;
                      OnAction=BEGIN
                                 //SetCPEntriesProcessed(); // 34599.o
                                 CurrPage.SheetLineSF.PAGE.SetProcessed;  // 34599.n
                               END;
                                }
      { 1100525001;2 ;Action    ;
                      CaptionML=ENU=Get Project Cost Plus Sur&charge Lines;
                      Image=GetStandardJournal;
                      OnAction=BEGIN
                                 CreateSurchargeLines;
                               END;
                                }
      { 1100409000;2 ;Action    ;
                      CaptionML=ENU=Update Description Cost Plus Entries;
                      Image=RefreshText;
                      OnAction=BEGIN
                                 CurrPage.SheetLineSF.PAGE.UpdateDescriptionCPE;  //db, 12-12-12: C002998
                               END;
                                }
      { 1100525009;2 ;ActionGroup;
                      CaptionML=ENU=Calculate;
                      Image=CostAccounting }
      { 1100485015;3 ;Action    ;
                      ShortCutKey=Shift+Ctrl+S;
                      CaptionML=ENU=Calculate &Surcharge;
                      Image=Calculate;
                      OnAction=VAR
                                 CostPlusRec@1100485000 : Record 11012019;
                               BEGIN
                                 CostPlusRec.CalcSurcharge("Project No.", Principal, "Commission No.", "No.");
                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1100485005;3 ;Action    ;
                      ShortCutKey=Shift+Ctrl+R;
                      CaptionML=ENU=Calculate Removal Contribution;
                      Image=RemoveLine;
                      OnAction=VAR
                                 CostPlusRec@1100485000 : Record 11012019;
                               BEGIN
                                 CostPlusRec."Project No." := "Project No.";
                                 CostPlusRec.Principal := Principal;
                                 CostPlusRec."Commission No." := "Commission No.";
                                 CostPlusRec."Settl.Sheet No." := "No.";
                                 CostPlusRec.DetermineRemovalContribution;
                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1100525006;3 ;Action    ;
                      ShortCutKey=Shift+Ctrl+T;
                      CaptionML=ENU=Calculate &Travel Cost;
                      Image=Delivery;
                      OnAction=VAR
                                 CostPlusRec@1100525000 : Record 11012019;
                               BEGIN
                                 CostPlusRec."Project No." := "Project No.";
                                 CostPlusRec.Principal := Principal;
                                 CostPlusRec."Commission No." := "Commission No.";
                                 CostPlusRec."Settl.Sheet No." := "No.";
                                 CostPlusRec.CheckTravelData;
                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1100525000;2 ;Action    ;
                      CaptionML=ENU=Release Cost Plus Entries;
                      Promoted=Yes;
                      Image=Approve;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CurrPage.SheetLineSF.PAGE.SetChargeable;  // 34599.n
                               END;
                                }
      { 1100485009;2 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=Create Invoice;
                      Promoted=Yes;
                      Image=CreateJobSalesInvoice;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Job@1210190002 : Record 11072003;
                                 lvCommissionRec@1100485000 : Record 11012020;
                                 lvProjPrincipRec@1100485001 : Record 11012005;
                                 CreateProjectInvoices@1210190001 : Report 11012014;
                               BEGIN
                                 lvCommissionRec.GET("Project No.", Principal, "Commission No.");
                                 lvCommissionRec.TESTFIELD("Collect Cost Plus Lines By", lvCommissionRec."Collect Cost Plus Lines By"::"Settlement Sheet");

                                 // jhoek.270611.begin: Call 16372
                                 Job.GET("Project No.");
                                 Job.SETRECFILTER;
                                 lvProjPrincipRec.SETRANGE(Principal,Principal);
                                 // REPORT.RUNMODAL(REPORT::"Create Project Invoices",TRUE,FALSE,lvProjPrincipRec);
                                 CreateProjectInvoices.SETTABLEVIEW(Job);
                                 CreateProjectInvoices.SETTABLEVIEW(lvProjPrincipRec);
                                 CreateProjectInvoices.RUNMODAL;
                                 // jhoek.270611.end

                                 CurrPage.SheetLineSF.PAGE.UpdateForm();
                               END;
                                }
      { 1100528202;2 ;Action    ;
                      CaptionML=ENU=Create Collective List;
                      Image=ApplyEntries;
                      OnAction=BEGIN
                                 CreateCollectiveListPerSettlem();
                               END;
                                }
      { 16      ;1   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=&Print;
                      Promoted=Yes;
                      Image=Print;
                      PromotedCategory=Report;
                      OnAction=VAR
                                 PrintDoc@1210190001 : Codeunit 229;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(SettlRec);
                                 PrintDoc.PrintProjCostPlus(SettlRec);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=ENU=General;
                GroupType=Group }

    { 2   ;2   ;Field     ;
                SourceExpr="No." }

    { 4   ;2   ;Field     ;
                SourceExpr=Description;
                OnValidate=BEGIN
                             DescriptionOnAfterValidate;
                           END;
                            }

    { 6   ;2   ;Field     ;
                SourceExpr=Status }

    { 1100485001;2;Field  ;
                SourceExpr="Order No. Customer" }

    { 1100485011;2;Field  ;
                SourceExpr="Commision Date Principal" }

    { 1100528200;2;Field  ;
                SourceExpr="Collective List No." }

    { 8   ;2   ;Field     ;
                SourceExpr=Text;
                Importance=Additional;
                MultiLine=Yes }

    { 1210190003;2;Field  ;
                SourceExpr="Reference Date";
                OnValidate=BEGIN
                             ReferenceDateOnAfterValidate;
                           END;
                            }

    { 1100485006;2;Field  ;
                SourceExpr=["Hash Total" ] }

    { 1100525004;2;Field  ;
                SourceExpr="Date Last Status Change" }

    { 10  ;1   ;Part      ;
                Name=SheetLineSF;
                SubPageLink=Project No.=FIELD(Project No.),
                            Principal=FIELD(Principal),
                            Commission No.=FIELD(Commission No.),
                            Settl.Sheet No.=FIELD(No.);
                PagePartID=Page11012048;
                Editable=SheetLineSFEditable }

    { 1210190002;0;Container;
                ContainerType=FactBoxArea }

    { 1100529900;1;Part   ;
                Name=RegisterExternalDocumentFB;
                PagePartID=Page11125053;
                PartType=Page }

    { 1100525002;1;Part   ;
                SubPageLink=Project No.=FIELD(Project No.),
                            Principal=FIELD(Principal),
                            Commission No.=FIELD(Commission No.);
                PagePartID=Page11130000;
                PartType=Page }

    { 1210190004;1;Part   ;
                SubPageLink=Project No.=FIELD(Project No.),
                            Principal=FIELD(Principal),
                            Commission No.=FIELD(Commission No.),
                            No.=FIELD(No.);
                PagePartID=Page11130119;
                PartType=Page }

    { 1210190005;1;Part   ;
                SubPageLink=Project No.=FIELD(Project No.),
                            Principal=FIELD(Principal),
                            Commission No.=FIELD(Commission No.),
                            Settl.Sheet No.=FIELD(Settl.Sheet No.),
                            Line No.=FIELD(Line No.);
                PagePartID=Page11130116;
                ProviderID=10;
                PartType=Page }

    { 1100525008;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1100525007;1;Part   ;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      SettlRec@1100485000 : Record 11012021;
      CostPlusRec@1100525000 : Record 11012019;
      SheetLineSFEditable@19032568 : Boolean INDATASET;

    PROCEDURE SetSubformEditable@1100525001();
    VAR
      lvCostPlusRec@1100525000 : Record 11012019;
    BEGIN
      lvCostPlusRec."Project No." := "Project No.";
      SheetLineSFEditable := lvCostPlusRec.CheckModifyAllowed(FALSE);
    END;

    LOCAL PROCEDURE DescriptionOnAfterValidate@19030973();
    BEGIN
      IF ("Project No." <> '') AND (Principal <> '') AND ("Commission No." <> '') AND ("No." <> '') THEN BEGIN
        IF NOT SettlRec.GET("Project No.", Principal, "Commission No.") THEN
          CurrPage.UPDATE;
      END;
    END;

    LOCAL PROCEDURE ReferenceDateOnAfterValidate@19033250();
    BEGIN
      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

