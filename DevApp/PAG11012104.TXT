OBJECT Page 11012104 Advice Order Defaults
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Advice Order Defaults;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    PageType=NavigatePage;
    OnOpenPage=BEGIN
                 GetDefaults;
               END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 3       ;1   ;Action    ;
                      CaptionML=ENU=&Process;
                      InFooterBar=Yes;
                      Image=Evaluate;
                      OnAction=BEGIN
                                 StoreDefaults;
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 6   ;1   ;Field     ;
                CaptionML=ENU=Deliver;
                OptionCaptionML=ENU=" ,Location,Project Location";
                SourceExpr=defDeliver;
                OnValidate=BEGIN
                             ValidateDefaults(1);
                           END;
                            }

    { 1   ;1   ;Field     ;
                CaptionML=ENU=Ship to Location;
                SourceExpr=defLocation;
                OnValidate=BEGIN
                             ValidateDefaults(2);
                           END;

                OnLookup=BEGIN
                           IF defDeliver = defDeliver::"Project Location" THEN BEGIN
                             ProjLocRec.SETRANGE("Project No.", defProject);
                             IF PAGE.RUNMODAL(PAGE::"Project Location", ProjLocRec, ProjLocRec.Code) = ACTION::LookupOK THEN
                               defLocation := ProjLocRec.Code;
                           END;
                           IF defDeliver = defDeliver::Location THEN
                             IF PAGE.RUNMODAL(PAGE::"Location List", LocRec, LocRec.Code) = ACTION::LookupOK THEN
                               defLocation := LocRec.Code;
                         END;
                          }

    { 4   ;1   ;Field     ;
                CaptionML=ENU=Shipment Date;
                SourceExpr=defShippingDate;
                OnValidate=BEGIN
                             ValidateDefaults(3);
                           END;
                            }

    { 1100529800;1;Field  ;
                CaptionML=ENU=Transfer Comment Lines;
                SourceExpr=TransferCommentLines }

    { 1210190004;1;Field  ;
                CaptionML=ENU=Ignore Elements;
                SourceExpr=CompressByElement }

    { 1210190002;1;Field  ;
                CaptionML=ENU=Ignore Variation Contracts;
                SourceExpr=CompressByVariContract }

  }
  CODE
  {
    VAR
      ImexRec@11012001 : Record 11012056;
      ProjRec@1100485000 : Record 11072003;
      LocRec@11012002 : Record 14;
      ProjLocRec@11012003 : Record 11012011;
      defDeliver@11012004 : ' ,Location,Project Location';
      defLocation@11012005 : Code[10];
      defShippingDate@11012006 : Date;
      defProject@11012007 : Code[20];
      defPeriod@1100525001 : Text[30];
      TransferCommentLines@1100529800 : Boolean;
      CompressByElement@1210190001 : Boolean;
      CompressByVariContract@1210190002 : Boolean;

    PROCEDURE GetDefaults@1000000001();
    BEGIN
      defProject := '';
      ImexRec.SETRANGE("User ID", USERID);
      ImexRec.SETFILTER("Process ID", 'F51011*');
      ImexRec.SETRANGE(Type, ImexRec.Type::Import);
      IF ImexRec.FIND('-') THEN BEGIN
        REPEAT
          IF ImexRec.Description = 'Project' THEN
            defProject := ImexRec.Condition;
          ImexRec.DELETE;
        UNTIL ImexRec.NEXT = 0;
      END;
      COMMIT;

      IF defProject <> '' THEN BEGIN
        ProjRec.GET(defProject);
        CASE ProjRec.Deliver OF
          ProjRec.Deliver::Location:
            BEGIN
              defDeliver := defDeliver::Location;
              defLocation := ProjRec."Ship To Location";
            END;
          ProjRec.Deliver::"Project Location":
            BEGIN
              defDeliver := defDeliver::"Project Location";
              IF ProjRec."Ship To Location" <> '' THEN
                defLocation := ProjRec."Ship To Location"
              ELSE BEGIN
                ProjLocRec.RESET;
                ProjLocRec.SETRANGE("Project No.", defProject);
                IF ProjLocRec.FINDFIRST THEN
                  IF ProjLocRec.NEXT = 0 THEN
                    defLocation := ProjLocRec.Code;
              END;
            END;
        END;
      END;

      defShippingDate := TODAY;
      TransferCommentLines := TRUE; // **4PS MVOS M28906
    END;

    PROCEDURE ValidateDefaults@1(lAction@11012000 : Integer);
    BEGIN
      CASE lAction OF
        1: BEGIN
             IF defDeliver = defDeliver::" " THEN BEGIN
               defLocation := ''
             END ELSE BEGIN
               IF defDeliver = defDeliver::"Project Location" THEN
                 IF NOT ProjLocRec.GET(defProject, defLocation) THEN
                   defLocation := '';
               IF defDeliver = defDeliver::Location THEN
                 IF NOT LocRec.GET(defLocation) THEN
                   defLocation := '';
             END;
           END;
        2: BEGIN
             IF defDeliver = defDeliver::"Project Location" THEN
               ProjLocRec.GET(defProject, defLocation);
             IF defDeliver = defDeliver::Location THEN
               LocRec.GET(defLocation);
             IF defDeliver = defDeliver::" " THEN
               defLocation := '';
           END;
        3: BEGIN
             IF defShippingDate < TODAY THEN
               IF defShippingDate <> 0D THEN  //db, 27-07-10: M20091
                 defShippingDate := TODAY;
           END;
      END;
    END;

    PROCEDURE StoreDefaults@1000000000();
    BEGIN
      ValidateDefaults(2);

      ImexRec.INIT;
      ImexRec."User ID" := USERID;
      ImexRec.Type := ImexRec.Type::Export;
      ImexRec."Process ID" := 'F51011.1';
      ImexRec.Description := 'Location';
      ImexRec.Condition := defLocation;
      ImexRec.INSERT;

      ImexRec."Process ID" := 'F51011.2';
      ImexRec.Description := 'Shipm.Date';
      ImexRec.Condition := STRSUBSTNO('%1', defShippingDate);
      ImexRec.INSERT;

      ImexRec."Process ID" := 'F51011.3';
      ImexRec.Description := 'Shipm.Method';
      IF defDeliver = defDeliver::" " THEN
        ImexRec.Condition := 'PR';
      IF defDeliver = defDeliver::Location THEN
        ImexRec.Condition := 'IL';
      IF defDeliver = defDeliver::"Project Location" THEN
        ImexRec.Condition := 'PL';
      ImexRec.INSERT;
      COMMIT;
    END;

    PROCEDURE GetVars@1100529801(VAR vTransferCommentLines@1100529800 : Boolean;VAR vCompressByElement@1210190000 : Boolean;VAR vCompressByVariContract@1210190001 : Boolean);
    BEGIN
      vTransferCommentLines := TransferCommentLines;
      vCompressByElement := CompressByElement;  // 14586.n
      vCompressByVariContract := CompressByVariContract;  // 14586.n
      //db, 29-01-13: C004646 (form-option removed; use ProjRec."Use Starting Date ProjectElem")
    END;

    BEGIN
    END.
  }
}

