OBJECT Page 11012212 Copy Part
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Estimate Part;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    PageType=StandardDialog;
    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK,ACTION::LookupOK];
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100525000;1;Group  ;
                CaptionML=ENU=Copy from;
                GroupType=Group }

    { 7   ;2   ;Field     ;
                CaptionML=ENU=Estimate;
                NotBlank=Yes;
                SourceExpr=EstFrom;
                OnValidate=BEGIN
                             EstRec."No." := EstFrom;
                             EstRec.DPA_FindRecWithDPAFilterAppld;
                             EstFromTxt := EstRec.Description;

                             CheckDefaultNormGroupFrom;  //db, 10-02-15: C009772
                           END;

                OnLookup=BEGIN
                           EstRec.RESET;
                           IF PAGE.RUNMODAL(PAGE::"Estimate List",EstRec) = ACTION::LookupOK THEN BEGIN
                              EstFrom := EstRec."No.";
                              EstFromTxt := EstRec.Description;
                              CheckDefaultNormGroupFrom;  //db, 10-02-15: C009772
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 10  ;2   ;Field     ;
                CaptionML=ENU=Estimate Description;
                SourceExpr=EstFromTxt;
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                CaptionML=ENU=Estimate Part;
                NotBlank=Yes;
                SourceExpr=SubEstFrom;
                OnValidate=VAR
                             Chapter@1100485003 : Code[20];
                             Paragraph@1100485002 : Code[20];
                             Level@1100485001 : Integer;
                           BEGIN
                             FormatMgt.FormatSubEstimate(SubEstFrom,Chapter,Paragraph,Level,EstFrom,FALSE);
                             SubEstRec.GET(EstFrom,SubEstFrom);
                             SubEstFromTxt := SubEstRec.Description;
                             PartGrpFrom := SubEstRec."Part Group";

                             CheckDefaultNormGroupFrom;  //db, 10-02-15: C009772
                           END;

                OnLookup=BEGIN
                           SubEstRec.RESET;
                           SubEstRec.SETRANGE("Estimate No.",EstFrom);
                           IF PAGE.RUNMODAL(PAGE::"Sub-Estimate List",SubEstRec) = ACTION::LookupOK THEN BEGIN
                              EstRec.GET(SubEstRec."Estimate No.");
                              EstFrom := EstRec."No.";
                              EstFromTxt := EstRec.Description;
                              SubEstFrom := SubEstRec."Sub-Estimate No.";
                              SubEstFromTxt := SubEstRec.Description;
                              PartGrpFrom := SubEstRec."Part Group";
                              CheckDefaultNormGroupFrom;  //db, 10-02-15: C009772
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 11  ;2   ;Field     ;
                CaptionML=ENU=Estimate Part Description;
                SourceExpr=SubEstFromTxt;
                Editable=FALSE }

    { 9   ;2   ;Field     ;
                CaptionML=ENU=Part;
                NotBlank=Yes;
                SourceExpr=EstPartFrom;
                OnValidate=BEGIN
                             FormatMgt.FormatElem(EstPartFrom,1,'');
                             EstPartRec.GET(EstFrom,SubEstFrom,PartGrpFrom,EstPartFrom);
                             PartGrpFrom := EstPartRec."Part Group";
                             //DP00686.sn
                             EstPartFromTxt := EstPartRec.Description;
                             EstPartFromTxt2 := EstPartRec."Description 2";
                             EstPartToTxt := EstPartFromTxt;
                             EstPartToTxt2 := EstPartFromTxt2;
                             //DP00686.en

                             CheckDefaultNormGroupFrom;  //db, 10-02-15: C009772
                           END;

                OnLookup=BEGIN
                           EstPartRec.RESET;
                           EstPartRec.SETRANGE("Estimate No.",EstFrom);
                           EstPartRec.SETRANGE("Sub-Estimate No.",SubEstFrom);
                           IF PAGE.RUNMODAL(PAGE::"Estimate Part List",EstPartRec) = ACTION::LookupOK THEN BEGIN
                             EstPartRec.CALCFIELDS("Estimate Description", "Sub-Estimate Description");
                             EstFrom := EstPartRec."Estimate No.";
                             EstFromTxt := EstPartRec."Estimate Description";
                             SubEstFrom := EstPartRec."Sub-Estimate No.";
                             SubEstFromTxt := EstPartRec."Sub-Estimate Description";
                             PartGrpFrom := EstPartRec."Part Group";
                             EstPartFrom := EstPartRec.Part;
                             //DP00686.sn
                             EstPartFromTxt := EstPartRec.Description;
                             EstPartFromTxt2 := EstPartRec."Description 2";
                             EstPartToTxt := EstPartFromTxt;
                             EstPartToTxt2 := EstPartFromTxt2;
                             //DP00686.en
                             CheckDefaultNormGroupFrom;  //db, 10-02-15: C009772
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 12  ;2   ;Field     ;
                CaptionML=ENU=Part Description;
                SourceExpr=EstPartFromTxt;
                Editable=FALSE }

    { 1100409000;2;Field  ;
                CaptionML=ENU=Part Description;
                SourceExpr=EstPartFromTxt2;
                Editable=FALSE }

    { 1100525001;1;Group  ;
                CaptionML=ENU=Copy to;
                GroupType=Group }

    { 16  ;2   ;Field     ;
                CaptionML=ENU=Estimate;
                NotBlank=Yes;
                SourceExpr=EstTo;
                OnValidate=BEGIN
                             EstRec."No." := EstTo;
                             EstRec.DPA_FindRecWithDPAFilterAppld;
                             EstToTxt := EstRec.Description;

                             CheckDefaultNormGroupTo;  //db, 10-02-15: C009772
                           END;

                OnLookup=BEGIN
                           EstRec.RESET;
                           IF PAGE.RUNMODAL(PAGE::"Estimate List",EstRec) = ACTION::LookupOK THEN BEGIN
                              EstTo := EstRec."No.";
                              EstToTxt := EstRec.Description;
                              CheckDefaultNormGroupTo;  //db, 10-02-15: C009772
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 19  ;2   ;Field     ;
                CaptionML=ENU=Estimate Description;
                SourceExpr=EstToTxt;
                Editable=FALSE }

    { 17  ;2   ;Field     ;
                CaptionML=ENU=Estimate Part;
                NotBlank=Yes;
                SourceExpr=SubEstTo;
                OnValidate=VAR
                             Chapter@1100485003 : Code[20];
                             Paragraph@1100485002 : Code[20];
                             Level@1100485001 : Integer;
                           BEGIN
                             FormatMgt.FormatSubEstimate(SubEstTo,Chapter,Paragraph,Level,EstTo,FALSE);
                             SubEstRec.GET(EstTo,SubEstTo);
                             SubEstToTxt := SubEstRec.Description;
                             PartGrpTo := SubEstRec."Part Group";

                             CheckDefaultNormGroupTo;  //db, 10-02-15: C009772
                           END;

                OnLookup=BEGIN
                           SubEstRec.RESET;
                           SubEstRec.SETRANGE("Estimate No.",EstTo);
                           IF PAGE.RUNMODAL(PAGE::"Sub-Estimate List",SubEstRec) = ACTION::LookupOK THEN BEGIN
                              EstRec.GET(SubEstRec."Estimate No.");
                              EstTo := EstRec."No.";
                              EstToTxt := EstRec.Description;
                              SubEstTo := SubEstRec."Sub-Estimate No.";
                              SubEstToTxt := SubEstRec.Description;
                              PartGrpTo := SubEstRec."Part Group";
                              CheckDefaultNormGroupTo;  //db, 10-02-15: C009772
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 20  ;2   ;Field     ;
                CaptionML=ENU=Estimate Part Description;
                SourceExpr=SubEstToTxt;
                Editable=FALSE }

    { 18  ;2   ;Field     ;
                CaptionML=ENU=Part;
                NotBlank=Yes;
                SourceExpr=EstPartTo;
                OnValidate=BEGIN
                             FormatMgt.FormatElem(EstPartTo,1,'');
                             CheckLevel;  //db, 17-01-08

                             IF EstPartRec.GET(EstTo, SubEstTo, PartGrpTo, EstPartTo) THEN BEGIN
                               EstPartToTxt := EstPartRec.Description;
                               EstPartToTxt2 := EstPartRec."Description 2";  //DP00686
                             END ELSE BEGIN
                               EstPartToTxt := EstPartFromTxt;  //db, 11-08-05
                               EstPartToTxt2 := EstPartFromTxt2;  //DP00686
                             END;

                             CheckDefaultNormGroupTo;  //db, 10-02-15: C009772
                           END;

                OnLookup=BEGIN
                           EstPartRec.RESET;
                           EstPartRec.SETRANGE("Estimate No.",EstTo);
                           EstPartRec.SETRANGE("Sub-Estimate No.",SubEstTo);
                           IF PAGE.RUNMODAL(PAGE::"Estimate Part List",EstPartRec) = ACTION::LookupOK THEN BEGIN
                              PartGrpTo := EstPartRec."Part Group";
                              EstPartTo := EstPartRec.Part;
                              EstPartToTxt := EstPartRec.Description;
                              EstPartToTxt2 := EstPartRec."Description 2";  //DP00686
                              FormatMgt.FormatElem(EstPartTo,1,'');  //db, 12-05-09: M15319
                              CheckLevel;  //db, 17-01-08
                              CheckDefaultNormGroupTo;  //db, 10-02-15: C009772
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 21  ;2   ;Field     ;
                CaptionML=ENU=Part Description;
                SourceExpr=EstPartToTxt }

    { 1100409001;2;Field  ;
                CaptionML=ENU=Part Description;
                SourceExpr=EstPartToTxt2 }

    { 1100525003;1;Group  ;
                CaptionML=ENU=Apply;
                GroupType=Group }

    { 1100485000;2;Field  ;
                CaptionML=ENU=Skip Lines;
                SourceExpr=SkipLines }

    { 1210190000;2;Field  ;
                CaptionML=ENU=Copy Price Agreement;
                SourceExpr=CopyPriceAgreement }

    { 1210190003;2;Field  ;
                CaptionML=ENU=Transfer Comment Lines;
                SourceExpr=TransferCommentLines }

  }
  CODE
  {
    VAR
      EstFrom@11012050 : Code[20];
      EstTo@11012049 : Code[20];
      SubEstFrom@11012048 : Code[20];
      SubEstTo@11012047 : Code[20];
      PartGrpFrom@11012046 : Code[20];
      PartGrpTo@11012045 : Code[20];
      EstPartFrom@11012044 : Code[20];
      EstPartTo@11012043 : Code[20];
      EstFromTxt@11012009 : Text[50];
      EstToTxt@11012008 : Text[50];
      SubEstFromTxt@11012007 : Text[50];
      SubEstToTxt@11012006 : Text[50];
      EstPartFromTxt@11012005 : Text[50];
      EstPartFromTxt2@1100409001 : Text[50];
      EstPartToTxt@11012004 : Text[50];
      EstPartToTxt2@1100409000 : Text[50];
      EstRec@11012016 : Record 11012151;
      SubEstRec@11012017 : Record 11012152;
      EstPartRec@11012000 : Record 11012153;
      FormatMgt@11012019 : Codeunit 11012022;
      SkipLines@1100485000 : Boolean;
      TransferCommentLines@1210190001 : Boolean;
      CopyPriceAgreement@1100525007 : Boolean;
      CloseActionOK@1100528400 : Boolean;
      NormGroupFrom@1100409006 : Code[20];
      NormGroupFromTxt@1100409005 : Text[50];
      NormGroupTo@1100409004 : Code[20];
      NormGroupToTxt@1100409003 : Text[50];
      NormGroupRec@1100409002 : Record 11012164;

    PROCEDURE FillDefaults@1(lEstFrom@11012000 : Code[20];lSubEstFrom@11012001 : Code[20];lPartGrpFrom@11012002 : Code[20];lEstPartFrom@11012003 : Code[20]);
    BEGIN
      EstFrom  := lEstFrom;
      SubEstFrom := lSubEstFrom;
      PartGrpFrom := lPartGrpFrom;
      EstPartFrom := lEstPartFrom;

      EstTo := EstFrom;
      SubEstTo := SubEstFrom;
      PartGrpTo:= PartGrpFrom;
      EstPartTo := EstPartFrom;

      IF EstRec.GET(EstFrom) THEN BEGIN
        EstFromTxt := EstRec.Description;
        EstToTxt := EstFromTxt;
      END;

      IF SubEstRec.GET(EstFrom,SubEstFrom) THEN BEGIN
        SubEstFromTxt := SubEstRec.Description;
        SubEstToTxt := SubEstFromTxt;
      END;

      IF EstPartRec.GET(EstFrom,SubEstFrom,PartGrpFrom,EstPartFrom) THEN BEGIN
        EstPartFromTxt := EstPartRec.Description;
        EstPartFromTxt2 := EstPartRec."Description 2";  //DP00686
        EstPartToTxt := EstPartFromTxt;
        EstPartToTxt2 := EstPartFromTxt2;  //DP00686
      END;

      CheckDefaultNormGroupFrom;  //db, 10-02-15: C009772
      CheckDefaultNormGroupTo;  //db, 10-02-15: C009772
    END;

    PROCEDURE ValidateEstimate@1210190001();
    VAR
      EstLineRec@1210190000 : Record 11012154;
      lvText000@1210190001 : TextConst 'ENU=Estimate Lines present in Destination.';
      lvChapter@1210190003 : Code[20];
      lvParagraph@1210190004 : Code[20];
      lvLevel@1210190005 : Integer;
      LineExist@1100525000 : Boolean;
      lvText001@1100525001 : TextConst 'ENU="Estimate Lines present in Destination; Continue ?"';
    BEGIN
      //db, 08-10-10: M23043
      FormatMgt.FormatElemExtended(1,EstPartTo,lvChapter,lvParagraph,lvLevel);

      EstLineRec.RESET;
      EstLineRec.SETRANGE("Estimate No.", EstTo);
      EstLineRec.SETRANGE("Sub-Estimate No.", SubEstTo);
      EstLineRec.SETRANGE("Part Group", PartGrpTo);
      CASE lvLevel OF
        1: EstLineRec.SETRANGE(Chapter, lvChapter);
        2: EstLineRec.SETRANGE(Paragraph, lvParagraph);
        3: EstLineRec.SETRANGE(Part, EstPartTo);
      END;
      IF EstLineRec.FINDFIRST THEN BEGIN
        LineExist := TRUE;
        IF NOT SkipLines THEN
          ERROR(lvText000);
      END;

      IF LineExist THEN BEGIN
        IF NOT CONFIRM(lvText001) THEN
          ERROR('');
      END;
    END;

    PROCEDURE StartUpdate@1210190000();
    VAR
      CopyCu@1210190000 : Codeunit 11012151;
    BEGIN
      EstRec.GET(EstTo);
      EstRec.TESTFIELD(Fixed, FALSE);
      ValidateEstimate;
      CopyCu.SetSelectionSubLevel(NormGroupFrom, NormGroupTo);  //db, 10-02-15: C009772
      CopyCu.SetTransferComments(TransferCommentLines);
      CopyCu.CopyEstPart(EstFrom,SubEstFrom,EstTo,SubEstTo,PartGrpFrom,EstPartFrom,EstPartTo,
        EstPartToTxt,EstPartToTxt2,SkipLines,CopyPriceAgreement);
    END;

    PROCEDURE CheckLevel@1100485000();
    VAR
      lvText000@1100485000 : TextConst 'ENU=Source and Target Part should be of the same level';
      lvPartRec@1100485001 : TEMPORARY Record 11012153;
    BEGIN
      WITH lvPartRec DO BEGIN
        FormatMgt.DetermineLevel(EstPartTo,Chapter,Paragraph,Level);
        EstPartRec.GET(EstFrom, SubEstFrom, PartGrpFrom, EstPartFrom);
        IF Level <> EstPartRec.Level THEN BEGIN
          EstPartTo := EstPartFrom;
          ERROR(lvText000);
        END;
      END;
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528400() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      StartUpdate;
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    PROCEDURE CheckDefaultNormGroupFrom@1100525017();
    VAR
      EstRecFrom@1100525000 : Record 11012151;
      SubEstRecFrom@1100525001 : Record 11012152;
      EstPartRecFrom@1100525002 : Record 11012153;
    BEGIN
      //db, 10-02-15: C009772
      IF NOT EstRecFrom.GET(EstFrom) THEN EstRecFrom.INIT;
      IF NOT SubEstRecFrom.GET(EstFrom,SubEstFrom) THEN SubEstRecFrom.INIT;
      IF NOT EstPartRecFrom.GET(EstFrom,SubEstFrom,PartGrpFrom,EstPartFrom) THEN EstPartRecFrom.INIT;

      IF EstPartRecFrom."Norm Group" <> '' THEN
        NormGroupFrom := EstPartRecFrom."Norm Group"
      ELSE
        IF SubEstRecFrom."Norm Group" <> '' THEN
          NormGroupFrom := SubEstRecFrom."Norm Group"
        ELSE
          NormGroupFrom := EstRecFrom."Norm Group";

      IF NOT NormGroupRec.GET(NormGroupFrom) THEN NormGroupRec.INIT;
      NormGroupFromTxt := COPYSTR(NormGroupRec.Description, 1, 50);
    END;

    PROCEDURE CheckDefaultNormGroupTo@1100525014();
    VAR
      EstRecTo@1100525001 : Record 11012151;
      SubEstRecTo@1100525000 : Record 11012152;
    BEGIN
      //db, 10-02-15: C009772
      IF NOT EstRecTo.GET(EstTo) THEN EstRecTo.INIT;
      IF NOT SubEstRecTo.GET(EstTo,SubEstTo) THEN SubEstRecTo.INIT;

      IF SubEstRecTo."Norm Group" <> '' THEN
        NormGroupTo := SubEstRecTo."Norm Group"
      ELSE
        NormGroupTo := EstRecTo."Norm Group";

      OnValidateNormGroupTo;
    END;

    PROCEDURE OnValidateNormGroupTo@1100525016();
    BEGIN
      //db, 10-02-15: C009772
      IF NOT NormGroupRec.GET(NormGroupTo) THEN BEGIN
        NormGroupTo := NormGroupFrom;
        NormGroupToTxt := NormGroupFromTxt;
      END ELSE BEGIN
        NormGroupToTxt := COPYSTR(NormGroupRec.Description, 1, 50);
      END;
    END;

    BEGIN
    END.
  }
}

