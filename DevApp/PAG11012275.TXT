OBJECT Page 11012275 Deblock Purch Inv./cred.memo
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    Permissions=TableData 25=rm,
                TableData 122=rm,
                TableData 124=rm,
                TableData 11012031=r;
    CaptionML=ENU=Deblock Purchase Invoices/Credit memo's;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table25;
    DataCaptionExpr=Caption;
    SourceTableView=SORTING(Document No.)
                    WHERE(Document Type=FILTER(Invoice|Credit Memo));
    PageType=List;
    OnOpenPage=BEGIN
                 //**4PS Generated Code. Do Not Change.sn01
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS Generated Code.en01
                 FILTERGROUP(2);
                 IF UserRec.GET(USERID) THEN BEGIN
                   UserRec.TESTFIELD("Approvement Initials");
                   SETRANGE("Approvement Initials",UserRec."Approvement Initials");
                   SETFILTER("On Hold",'<>%1','');
                 END ELSE
                   ERROR(STRSUBSTNO(Text000,USERID));
                 FILTERGROUP(0);
               END;

    OnNewRecord=BEGIN
                  //**4PS Generated Code. Do Not Change.sn02
                  DPA_SetDefaultDepartment;
                  //**4PS Generated Code.en02
                END;

    OnInsertRecord=VAR
                     xRecRef@1100000999 : RecordRef;
                   BEGIN
                     //**4PS Generated Code. Do Not Change.sn05
                     xRecRef.GETTABLE(xRec);
                     DPA_SetDefaultDpmtFromNoSeries(xRecRef);
                     //**4PS Generated Code.en05
                   END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210190020;1 ;Action    ;
                      CaptionML=ENU=&Deblock;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Approve;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 DeBlock;
                               END;
                                }
      { 1100528700;1 ;Action    ;
                      ShortCutKey=Shift+Ctrl+S;
                      CaptionML=ENU=Scanned Document;
                      Image=Document;
                      OnAction=VAR
                                 PurchSetup@1100528701 : Record 312;
                               BEGIN
                                 PurchSetup.GET;
                                 PurchSetup.ShowScannedDocumentWithDocNo("Document No.");
                               END;
                                }
      { 1100528801;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1100528800;1 ;Action    ;
                      CaptionML=ENU=Co&mments;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ViewComments;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 PurchCommentLine@1100528801 : Record 43;
                                 PurchCommentSheetPAGE@1100528800 : Page 66;
                               BEGIN
                                 CASE "Document Type" OF
                                   "Document Type"::Invoice:
                                     PurchCommentLine.SETRANGE("Document Type", PurchCommentLine."Document Type"::"Posted Invoice");
                                   "Document Type"::"Credit Memo":
                                     PurchCommentLine.SETRANGE("Document Type", PurchCommentLine."Document Type"::"Posted Credit Memo");
                                 END;
                                 PurchCommentLine.SETRANGE("No.", "Document No.");
                                 PurchCommentLine.SETRANGE("Document Line No.", 0);

                                 PurchCommentSheetPAGE.SETTABLEVIEW(PurchCommentLine);
                                 PurchCommentSheetPAGE.RUNMODAL;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210190000;1;Group  ;
                GroupType=Repeater }

    { 1210190001;2;Field  ;
                SourceExpr="Document No.";
                Editable=FALSE;
                OnAssistEdit=VAR
                               InvoiceHeaderRec@1210190000 : Record 122;
                               CreditMemoRec@1210190001 : Record 124;
                               InvoiceHeaderFrm@1210190002 : Page 138;
                               CreditMemoFrm@1210190003 : Page 140;
                             BEGIN
                               CASE "Document Type" OF
                                 "Document Type"::Invoice:
                                   BEGIN
                                     InvoiceHeaderRec.SETRANGE("No.","Document No.");
                                     IF InvoiceHeaderRec.FIND('-') THEN BEGIN
                                       InvoiceHeaderFrm.SETRECORD(InvoiceHeaderRec);
                                       InvoiceHeaderFrm.RUNMODAL;
                                     END;
                                   END;
                                 "Document Type"::"Credit Memo":
                                   BEGIN
                                     CreditMemoRec.SETRANGE("No.","Document No.");
                                     IF CreditMemoRec.FIND('-') THEN BEGIN
                                       CreditMemoFrm.SETRECORD(CreditMemoRec);
                                       CreditMemoFrm.RUNMODAL;
                                     END;
                                   END;
                               END;
                               CurrPage.UPDATE;
                             END;
                              }

    { 1210190021;2;Field  ;
                SourceExpr="Comment Line Present";
                Editable=FALSE }

    { 1210190003;2;Field  ;
                SourceExpr="Document Type";
                Editable=FALSE }

    { 1210190005;2;Field  ;
                SourceExpr="Vendor No.";
                Editable=FALSE }

    { 1100525000;2;Field  ;
                SourceExpr="Vendor Name";
                Visible=FALSE }

    { 1210190007;2;Field  ;
                SourceExpr=Description;
                Editable=FALSE }

    { 1210190009;2;Field  ;
                SourceExpr=Amount;
                Editable=FALSE }

    { 1100485002;2;Field  ;
                SourceExpr="Project No." }

    { 1210190011;2;Field  ;
                SourceExpr="Document Date";
                Editable=FALSE }

    { 1210190013;2;Field  ;
                SourceExpr="External Document No.";
                Editable=FALSE }

    { 1210190015;2;Field  ;
                SourceExpr="Subcontracting Invoice";
                Editable=FALSE }

    { 1210190025;2;Field  ;
                SourceExpr="On Hold";
                Editable=FALSE }

    { 1100485000;2;Field  ;
                SourceExpr="Approvement Initials";
                OnValidate=BEGIN
                             ApprovementInitialsOnAfterVali;
                           END;
                            }

    { 1100409000;2;Field  ;
                SourceExpr="User ID";
                Visible=FALSE;
                Editable=FALSE }

    { 1100525001;2;Field  ;
                SourceExpr="Wage Charge (to B Account)";
                Visible=FALSE;
                Editable=FALSE }

    { 1100528400;2;Field  ;
                SourceExpr="Due Date";
                Visible=FALSE;
                Editable=FALSE }

    { 1100528401;2;Field  ;
                SourceExpr="WKA approval";
                Visible=FALSE;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Text000@1210190000 : TextConst 'ENU=User Setup not present for user %1';
      Text001@1210190001 : TextConst 'ENU=First select records.';
      Text002@1210190002 : TextConst 'ENU=Deblock selected invoices ?';
      UserRec@1210190003 : Record 91;
      DPA_IsHardFilter@1100000999 : Boolean;

    PROCEDURE DeBlock@1210190000();
    VAR
      lvOnHoldRec@1210190000 : Record 11012031;
      lvPurchInvoiceHeadRec@1210190001 : Record 122;
      lvPurchCrMemoHeadRec@1210190002 : Record 124;
      VendorLedgerEntry@1100525000 : Record 25;
      NewOnHoldCode@1100525001 : Code[3];
    BEGIN
      CurrPage.SETSELECTIONFILTER(VendorLedgerEntry);
      IF NOT VendorLedgerEntry.FIND('-') THEN
        MESSAGE(Text001)
      ELSE BEGIN
        IF CONFIRM(Text002,TRUE) THEN BEGIN
          REPEAT;
            // dp00206.sn
            //VendorLedgerEntry.CheckIfOnHoldCodeCanBeRemoved; //C008247.n
            NewOnHoldCode := VendorLedgerEntry.CheckIfOnHoldCodeCanBeRemoved; //C008247.n //C010724.modify
            //When Onholdcodecanberemoved then NewOnHoldcode = ''                         //C010724
            // dp00206.en
            IF NOT lvOnHoldRec.GET(VendorLedgerEntry."On Hold") THEN
              lvOnHoldRec.INIT;
            // ChangeLog is not necessary. This is automatically handled via codeunit 1.
            VendorLedgerEntry."On Hold" := NewOnHoldCode;    //C010724.modify
            VendorLedgerEntry.MODIFY;
            IF NOT lvOnHoldRec."Cause by Vendor" THEN BEGIN
              //* When cause of blocking is by vendor, then do not clear On Hold code at invoice.
              //* This field is i.a. used at statistic of vendor assessment.
              CASE VendorLedgerEntry."Document Type" OF
                VendorLedgerEntry."Document Type"::Invoice:
                  BEGIN
                    IF lvPurchInvoiceHeadRec.GET(VendorLedgerEntry."Document No.") THEN BEGIN
                      lvPurchInvoiceHeadRec."On Hold" := NewOnHoldCode;    //C010724.modify
                      lvPurchInvoiceHeadRec.MODIFY;
                    END;
                  END;
                VendorLedgerEntry."Document Type"::"Credit Memo":
                  BEGIN
                    IF lvPurchCrMemoHeadRec.GET(VendorLedgerEntry."Document No.") THEN BEGIN
                      lvPurchCrMemoHeadRec."On Hold" := NewOnHoldCode;    //C010724.modify
                      lvPurchCrMemoHeadRec.MODIFY;
                    END;
                  END;
              END;
            END;
          UNTIL VendorLedgerEntry.NEXT = 0;
        END;
      END;
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE Caption@1210190001() Text : Text[200];
    VAR
      EmplRec@1210190000 : Record 5200;
    BEGIN
      IF NOT EmplRec.GET(UserRec."Employee No.") THEN
        EXIT(USERID)
      ELSE
        EXIT(EmplRec."Full Name");
    END;

    LOCAL PROCEDURE ApprovementInitialsOnAfterVali@19023298();
    BEGIN
      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

