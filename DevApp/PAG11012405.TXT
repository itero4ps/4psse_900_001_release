OBJECT Page 11012405 Edit Word Documents
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    Editable=Yes;
    CaptionML=ENU=Edit Word Documents;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=Yes;
    SourceTable=Table11012401;
    PageType=List;
    OnInit=BEGIN
             SecureEditable := TRUE;
           END;

    OnAfterGetRecord=BEGIN
                       GetDocumentData(Rec);
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  OnAfterGetCurrRecord;
                END;

    ActionList=ACTIONS
    {
      { 1900000003;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210190002;1 ;ActionGroup;
                      Name=Document;
                      CaptionML=ENU=D&ocument }
      { 1210190004;2 ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=ENU=Card;
                      Image=EditLines;
                      OnAction=VAR
                                 WordDocFrm@1210190001 : Page 11012406;
                               BEGIN
                                 GetDocumentData(Rec);
                                 IF DocumentProperties."No." <> '' THEN BEGIN
                                   WordDocFrm.SETRECORD(DocumentProperties);
                                   WordDocFrm.RUN;
                                 END ELSE
                                   EXIT;
                               END;
                                }
      { 1210190015;2 ;Action    ;
                      ShortCutKey=F7;
                      CaptionML=ENU=Edit;
                      Promoted=Yes;
                      Image=Edit;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 EditDoc;
                               END;
                                }
      { 1100525000;2 ;Action    ;
                      CaptionML=ENU=Open DMS Card;
                      Image=OpenWorksheet;
                      OnAction=BEGIN
                                 GetDocumentData(Rec);
                                 IF DocumentProperties.IsConfidentialOwner() THEN
                                   DocumentProperties.OpenDocumentD3;
                               END;
                                }
      { 1210190019;2 ;Action    ;
                      CaptionML=ENU=Create;
                      Promoted=Yes;
                      Image=AddAction;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocumentTemplate@1100528701 : Record 11012401;
                                 UpdateDocument@1199198001 : Boolean;
                                 CopyAndUpdateDocument@1100528702 : Boolean;
                               BEGIN
                                 IF NOT ChooseCreateAction(UpdateDocument, CopyAndUpdateDocument, Rec) THEN
                                   EXIT;

                                 DocumentTemplate.COPY(Rec);
                                 CreateDoc(DocumentTemplate, FALSE, UpdateDocument, CopyAndUpdateDocument);
                               END;
                                }
      { 1210190020;2 ;Action    ;
                      CaptionML=ENU=Multiple Create;
                      Promoted=Yes;
                      Image=Documents;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocumentTemplate@1100525001 : Record 11012401;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(DocumentTemplate);
                                 IF DocumentTemplate.FINDSET(FALSE, FALSE) THEN BEGIN
                                   REPEAT
                                     CreateDoc(DocumentTemplate, FALSE, FALSE, FALSE);
                                   UNTIL DocumentTemplate.NEXT = 0;
                                 END ELSE
                                   ERROR(Text001);
                               END;
                                }
      { 1210190021;2 ;Action    ;
                      CaptionML=ENU=Create and Edit;
                      Promoted=Yes;
                      Image=AddAction;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocumentTemplate@1100528701 : Record 11012401;
                                 UpdateDocument@1100528702 : Boolean;
                                 CopyAndUpdateDocument@1199198001 : Boolean;
                               BEGIN
                                 IF NOT ChooseCreateAction(UpdateDocument, CopyAndUpdateDocument, Rec) THEN
                                   EXIT;

                                 DocumentTemplate.COPY(Rec);
                                 CreateDoc(DocumentTemplate, TRUE, UpdateDocument, CopyAndUpdateDocument);
                               END;
                                }
      { 1210190022;2 ;Action    ;
                      CaptionML=ENU=Multiple C&reate and Edit;
                      Promoted=Yes;
                      Image=Documents;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocumentTemplate@1100525001 : Record 11012401;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(DocumentTemplate);
                                 IF DocumentTemplate.FINDSET(FALSE, FALSE) THEN BEGIN
                                   REPEAT
                                     CreateDoc(DocumentTemplate, TRUE, FALSE, FALSE);
                                   UNTIL DocumentTemplate.NEXT = 0;
                                 END ELSE BEGIN
                                   ERROR(Text001);
                                 END;
                               END;
                                }
      { 1210190016;2 ;Action    ;
                      CaptionML=ENU=Print;
                      Promoted=Yes;
                      Image=Print;
                      PromotedCategory=Report;
                      OnAction=VAR
                                 DocumentTemplate@1100528701 : Record 11012401;
                               BEGIN
                                 DocumentTemplate.COPY(Rec);
                                 PrintDoc(DocumentTemplate);
                               END;
                                }
      { 1210190017;2 ;Action    ;
                      CaptionML=ENU=M&ultiple Print;
                      Promoted=Yes;
                      Image=Print;
                      PromotedCategory=Report;
                      OnAction=VAR
                                 DocumentTemplate@1100525001 : Record 11012401;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(DocumentTemplate);
                                 IF DocumentTemplate.FINDSET(FALSE, FALSE) THEN BEGIN
                                   REPEAT
                                     PrintDoc(DocumentTemplate);
                                   UNTIL DocumentTemplate.NEXT = 0;
                                 END ELSE BEGIN
                                   ERROR(Text001);
                                 END;
                               END;
                                }
      { 1210190003;1 ;ActionGroup;
                      CaptionML=ENU=F&unctions }
      { 1210190013;2 ;Action    ;
                      CaptionML=ENU=Create Document and Send by E-Mail;
                      Promoted=Yes;
                      Image=SendTo;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF NOT MailMode THEN
                                   ERROR(Text006);
                                 CreateDocumentAndSendMail;
                               END;
                                }
      { 1100525500;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1100525501;1 ;ActionGroup;
                      CaptionML=ENU=Documents }
      { 1210190011;2 ;Action    ;
                      CaptionML=ENU=Previous Versions;
                      Image=ListPage;
                      OnAction=VAR
                                 DocumentList@11012001 : Page 11012407;
                                 DocumentPropeties2@11012002 : Record 11012746;
                                 DocumentLink@1100525001 : Record 11012747;
                                 RID@1100525002 : RecordID;
                               BEGIN
                                 GetDocumentData(Rec);
                                 DocumentPropeties2.SETRANGE("Document Group", "Document Group");
                                 DocumentPropeties2.SETRANGE("Document Template", "Template Code");
                                 DocumentPropeties2.SETRANGE("Contact No.", gCodContact);
                                 DocumentPropeties2.SETRANGE("Internal Company", COMPANYNAME); //**4PS01.n
                                 IF gCodAct <> '' THEN
                                   DocumentPropeties2.SETRANGE("Doc. Action", gCodAct);

                                 DocumentLink.SETRANGE("Document No.", DocumentProperties."No.");
                                 DocumentLink.SETRANGE(Source, TRUE);
                                 IF DocumentLink.FINDFIRST THEN BEGIN
                                   RID := DocumentLink."Record ID";

                                   DocumentLink.RESET;
                                   DocumentLink.SETCURRENTKEY("Record ID"); //mg, 05-07-11: M27801
                                   DocumentLink.SETFILTER("Record ID", STRSUBSTNO('''%1''', RID));
                                   DocumentLink.SETRANGE(Source, TRUE);
                                   DocumentLink.SETRANGE("Internal Company", COMPANYNAME);
                                   IF DocumentLink.FINDSET THEN BEGIN
                                     REPEAT
                                       IF DocumentPropeties2.GET(DocumentLink."Document No.") THEN
                                         DocumentPropeties2.MARK(TRUE);
                                     UNTIL DocumentLink.NEXT = 0;
                                   END;
                                 END;

                                 DocumentPropeties2.MARKEDONLY(TRUE);

                                 DocumentList.SETTABLEVIEW(DocumentPropeties2);
                                 DocumentList.SETRECORD(DocumentPropeties2);
                                 DocumentList.RUNMODAL;

                                 DocumentPropeties2.CLEARMARKS;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Template Code";
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                SourceExpr=Description;
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                Name=Version;
                CaptionML=ENU=Version;
                SourceExpr=DocumentProperties.Version;
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                Name=Creation Date;
                CaptionML=ENU=Subject;
                SourceExpr=DocumentProperties.Subject;
                Editable=TRUE }

    { 1100528100;2;Field  ;
                SourceExpr="Publish on Service Portal";
                Visible=FALSE }

    { 1100485001;2;Field  ;
                Name=Secure;
                CaptionML=ENU=Secure;
                SourceExpr=DocumentProperties.Secure;
                Editable=SecureEditable }

    { 8   ;2   ;Field     ;
                Name=User;
                CaptionML=ENU=User;
                SourceExpr=DocumentProperties."User ID";
                Editable=FALSE }

    { 1210190000;2;Field  ;
                CaptionML=ENU=Creation Date;
                SourceExpr=DocumentProperties."Creation Date" }

    { 12  ;2   ;Field     ;
                Name=Creation Time;
                CaptionML=ENU=Creation Time;
                SourceExpr=DocumentProperties."Creation Time";
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                Name=File;
                CaptionML=ENU=File;
                SourceExpr=DocumentProperties.File;
                Editable=FALSE }

    { 1100409000;2;Field  ;
                CaptionML=ENU=Web Link;
                SourceExpr=DocumentProperties."Web link";
                Editable=false }

  }
  CODE
  {
    VAR
      Text000@11012005 : TextConst 'ENU=Creating documents not possible from here';
      DocumentProperties@11012000 : Record 11012746;
      gCodNo@11012002 : Code[20];
      gCodContact@11012003 : Code[20];
      gCodAct@11012004 : Code[20];
      gCodProject@1210190001 : Code[20];
      gCodEmployee@1210190000 : Code[20];
      gCodPerson@1210190003 : Code[20];
      gCodcode@1210190002 : Code[20];
      gCodRespCenter@1210190004 : Code[20];
      gIntTryOutQuoteVersion@1210190005 : Integer;
      Text001@1100525001 : TextConst 'ENU=First select records';
      DocLinkRID@1199198001 : ARRAY [5] OF RecordID;
      Text002@1199198000 : TextConst 'ENU=Do you want to create a new version? (Choose no to update the existing document.)';
      DepartmentCode@1100528700 : Code[20];
      Text004@1100528702 : TextConst 'ENU=New version based on the previous version and update the bookmarks,New version based on the document template (%1),Update the bookmarks in the current version';
      Text005@1100528701 : TextConst 'ENU=Select how to create a Word Document.';
      Recipient@1100528703 : Text[120];
      MailMode@1100528704 : Boolean;
      Text006@1210190006 : TextConst 'ENU=No recipient selected to send an e-mail.';
      Text007@1210190007 : TextConst 'ENU=This %1 cannot be used for e-mail.';
      SecureEditable@19010427 : Boolean INDATASET;
      Text008@1100528705 : TextConst 'ENU=E-Mail could not be found in sent items.';
      Text009@1100525000 : TextConst 'ENU=Update the bookmarks in the current version';

    PROCEDURE InitTypeNo@1(iOptType@1199198002 : Option;iDocLinkRID@1199198000 : ARRAY [5] OF RecordID;NewDepartmentCode@1100528700 : Code[20];iCodContact@1199198001 : Code[20];iCodPerson@1199198003 : Code[20];iCodAct@1199198004 : Code[20]);
    VAR
      i@1199198005 : Integer;
    BEGIN
      // This function should not be used anymore. Use SetVars instead.
      // When there are no references to this function anymore this function can be deleted.
      FOR i := 1 TO ARRAYLEN(iDocLinkRID) DO
        DocLinkRID[i] := iDocLinkRID[i];

      DepartmentCode := NewDepartmentCode;
      gCodContact := iCodContact;
      gCodPerson := iCodPerson;
      gCodAct := iCodAct;
    END;

    PROCEDURE SetVars@1210190001(DocumementLinkIDs@1210190000 : ARRAY [5] OF RecordID;NewDepartmentCode@1210190003 : Code[20];NewContactNo@1210190001 : Code[20];NewContactPersonNo@1210190002 : Code[20];NewDocumentAction@1100528700 : Code[20]);
    VAR
      I@1210190004 : Integer;
    BEGIN
      FOR I := 1 TO ARRAYLEN(DocumementLinkIDs) DO
        DocLinkRID[I] := DocumementLinkIDs[I];

      DepartmentCode := NewDepartmentCode;
      gCodContact := NewContactNo;
      gCodPerson := NewContactPersonNo;
      gCodAct := NewDocumentAction;
    END;

    PROCEDURE SetMailVars@1100528705(NewRecipient@1100528700 : Text[120]);
    BEGIN
      MailMode := TRUE;
      Recipient := NewRecipient;
    END;

    PROCEDURE GetDocumentData@1210190000(DocumentTemplate@1210190000 : Record 11012401) : Boolean;
    VAR
      DocumentLink@1199198000 : Record 11012747;
    BEGIN
      //Note: Function is also called extern

      DocumentProperties.SETCURRENTKEY("Document Group","Document Template","Contact No.","Doc. Action",Version);
      DocumentProperties.SETRANGE("Document Group", DocumentTemplate."Document Group");
      DocumentProperties.SETRANGE("Document Template", DocumentTemplate."Template Code");
      DocumentProperties.SETRANGE("Internal Company", COMPANYNAME);
      DocumentProperties.SETRANGE("Contact No.", gCodContact);

      IF gCodAct <> '' THEN
        DocumentProperties.SETRANGE("Doc. Action", gCodAct)
      ELSE
        DocumentProperties.SETRANGE("Doc. Action");

      // Mark records with a document link to the source table. (First array in DocLinkRID)
      DocumentLink.SETCURRENTKEY("Record ID"); //mg, 05-07-11: M27801
      DocumentLink.SETFILTER("Record ID", STRSUBSTNO('''%1''', DocLinkRID[1]));
      DocumentLink.SETRANGE(Source, TRUE);
      DocumentLink.SETRANGE("Internal Company", COMPANYNAME);
      IF DocumentLink.FINDSET THEN BEGIN
        REPEAT
          IF DocumentProperties.GET(DocumentLink."Document No.") THEN
            DocumentProperties.MARK(TRUE);
        UNTIL DocumentLink.NEXT = 0;
      END;

      // Only Marked records in filter
      DocumentProperties.MARKEDONLY(TRUE);

      IF NOT DocumentProperties.FINDLAST THEN BEGIN
        DocumentProperties.INIT;
        IF DocumentTemplate."Secured Document" = DocumentTemplate."Secured Document"::Always THEN
          DocumentProperties.Secure := TRUE;

        DocumentProperties.CLEARMARKS();
        EXIT(FALSE);
      END;

      CASE DocumentTemplate."Secured Document" OF
        DocumentTemplate."Secured Document"::"Not":
          DocumentProperties.Secure := FALSE;
        DocumentTemplate."Secured Document"::Always:
          DocumentProperties.Secure := TRUE;
      END;

      DocumentProperties.CLEARMARKS();
      EXIT(TRUE);
    END;

    PROCEDURE CreateWordDoc@1210190002(DocumentTemplate@1210190001 : Record 11012401;UpdateDocument@1199198000 : Boolean;CopyAndUpdateDocument@1100528700 : Boolean;lvEdit@1100525000 : Boolean);
    VAR
      lCduWordInterface@1210190000 : Codeunit 11012406;
      TryOutQuoteRec@1210190002 : Record 11012758;
    BEGIN
      //Note: Function is also called extern
      DocumentTemplate.Subject := DocumentProperties.Subject;

      IF DocumentProperties.Secure THEN
        DocumentTemplate."Secured Document" := DocumentTemplate."Secured Document"::Always
      ELSE
        DocumentTemplate."Secured Document" := DocumentTemplate."Secured Document"::"Not";

      lCduWordInterface.GenerateDocFromTemplate2(DocumentTemplate,
                                                 DocLinkRID,
                                                 DepartmentCode,
                                                 gCodContact,
                                                 gCodPerson,
                                                 gCodAct,
                                                 UpdateDocument,
                                                 CopyAndUpdateDocument,
                                                 lvEdit);
    END;

    PROCEDURE CreateDoc@1100485001(DocumentTemplate@1100525000 : Record 11012401;lvEdit@1100485000 : Boolean;UpdateDocument@1199198000 : Boolean;CopyAndUpdateDocument@1100528700 : Boolean);
    VAR
      lCduWord@1100485001 : Codeunit 11012400;
    BEGIN
      IF FORMAT(DocLinkRID[1]) = '' THEN
        ERROR(Text000);

      GetDocumentData(DocumentTemplate);
      CreateWordDoc(DocumentTemplate, UpdateDocument, CopyAndUpdateDocument, lvEdit);
      CurrPage.UPDATE;

      IF lvEdit = TRUE THEN BEGIN
        IF DocumentTemplate.FIND THEN BEGIN
          GetDocumentData(DocumentTemplate);
          IF DocumentProperties."D3 Document ID" = '' THEN
            DocumentProperties.OpenDocument();
        END;
      END;
    END;

    PROCEDURE CreateDocumentAndSendMail@1100528701();
    VAR
      DocumentMailing@1100528700 : Codeunit 11012278;
      DocumentTemplate@1100528703 : Record 11012401;
      UpdateDocument@1100528701 : Boolean;
      CopyAndUpdateDocument@1100528702 : Boolean;
    BEGIN
      IF NOT ("Use for" IN ["Use for"::"E-Mail", "Use for"::"Document and E-Mail"]) THEN
        ERROR(Text007, TABLECAPTION);

      IF NOT ChooseCreateAction(UpdateDocument, CopyAndUpdateDocument, Rec) THEN
        EXIT;

      // Create Word document
      DocumentTemplate.COPY(Rec);
      CreateDoc(DocumentTemplate, FALSE, UpdateDocument, CopyAndUpdateDocument);
      DocumentTemplate.FIND;
      GetDocumentData(DocumentTemplate);

      // Commit after generating the document because sending the mail may take some time and you do not want tables remain locked
      COMMIT;

      IF NOT DocumentMailing.MailDocument(
        Recipient,
        DocumentProperties,
        (DocumentTemplate."Attach to E-Mail as" = DocumentTemplate."Attach to E-Mail as"::Attachment))
      THEN BEGIN
        DocumentProperties.DELETE(TRUE); // Rollback
        MESSAGE(Text008);
      END;
    END;

    PROCEDURE EditDoc@1100485002();
    VAR
      lCduWord@1100485000 : Codeunit 11012400;
    BEGIN
      GetDocumentData(Rec);
      DocumentProperties.OpenDocument();
    END;

    PROCEDURE PrintDoc@1100485003(DocumentsRec@1100525000 : Record 11012401);
    VAR
      lCduWord@1100485000 : Codeunit 11012400;
      DocPrintFrm@1100485001 : Page 11012792;
      DocumentTemplate@1199198000 : Record 11012401;
      FilePath@1100525002 : Text[500];
      DocumentMgt@1100525001 : Codeunit 11012406;
    BEGIN
      GetDocumentData(DocumentsRec);
      IF (DocumentProperties.File <> '') OR (DocumentProperties."D3 Document Type" <> '') THEN BEGIN

        IF (DocumentProperties."D3 Document Type" <> '' ) AND (DocumentProperties."D3 Document ID" <> '') THEN BEGIN
          FilePath := DocumentMgt.D3DocumentURL(DocumentProperties);
        END ELSE BEGIN
          FilePath := DocumentProperties.File;
        END;

        lCduWord.Initialise;
        lCduWord.Open(FALSE, FilePath, FALSE);
        lCduWord.Print;
        IF DocumentProperties."D3 Document ID" <> '' THEN
          lCduWord.Close2(FALSE)
        ELSE
          lCduWord.Close;
        lCduWord.Terminate;
      END;

      DocumentTemplate.GET("Document Group", "Template Code");
      IF (DocumentTemplate.Type = Type::"Purchase Quote") OR  (DocumentTemplate.Type = Type::"Purchase Order") THEN BEGIN
      //db, 30-03-06
        DocPrintFrm.SetSelection(DocumentTemplate.Type, DocLinkRID[1]);
        DocPrintFrm.RUNMODAL;
      END;
    END;

    PROCEDURE ChooseCreateAction@1100528700(VAR UpdateDocument@1100528700 : Boolean;VAR CopyAndUpdateDocument@1100528707 : Boolean;DocumentTemplate@1100528701 : Record 11012401) : Boolean;
    VAR
      Choice@1100525002 : Integer;
    BEGIN
      UpdateDocument := FALSE;
      CopyAndUpdateDocument := FALSE;

      IF DocumentProperties.Version <> 0 THEN BEGIN
        Choice := STRMENU(STRSUBSTNO(Text004, DocumentTemplate."Template Code"), 1, Text005);
        CASE Choice OF
          0: EXIT(FALSE); // Cancel
          1: // New version based on the previous version and update the bookmarks
            BEGIN
              UpdateDocument := TRUE;
              CopyAndUpdateDocument := TRUE;
              EXIT(TRUE);
            END;
          2: // New version based on the document template
            BEGIN
              UpdateDocument := FALSE;
              EXIT(TRUE);
            END;
          3: // Update the bookmarks in the current version
            BEGIN
              DocumentProperties.CheckFileIsInUse;
              UpdateDocument := TRUE;
              EXIT(TRUE);
            END;
        END;
      END;

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    VAR
      lDocRec@1100485000 : Record 11012401;
    BEGIN
      xRec := Rec;
      GetDocumentData(Rec);

      SecureEditable := FALSE;
      IF lDocRec.GET("Document Group","Template Code") THEN
        IF lDocRec."Secured Document" = lDocRec."Secured Document"::Both THEN
          SecureEditable := TRUE;
    END;

    BEGIN
    END.
  }
}

