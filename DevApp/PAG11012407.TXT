OBJECT Page 11012407 Document List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    Permissions=TableData 91=rm;
    CaptionML=ENU=Document List;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table11012746;
    PageType=List;
    OnInit=BEGIN
             MyDocuVisible := TRUE;
           END;

    OnOpenPage=VAR
                 DocumentFilters@1100528700 : Codeunit 11012432;
               BEGIN
                 //**4PS Generated Code. Do Not Change.sn01
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS Generated Code.en01

                 LinkField1Visible := FALSE;
                 LinkField2Visible := FALSE;
                 LinkField3Visible := FALSE;
                 LinkField4Visible := FALSE;
                 LinkField5Visible := FALSE;

                 IF EVALUATE(ShowingResultDocumentFilter, GETFILTER("Showing Result Document Filter")) THEN;
                 IF EVALUATE(FromFilterForm, GETFILTER("From Filter Form")) THEN;

                 IF ShowingResultDocumentFilter THEN BEGIN
                   DocumentFilters.GetVarsDocumentList(RID, DepartmentCode, CopyDoc, ByCompany);
                   DocumentFilters.GetContact(ContactNo, ContactPersonNo);
                   DocumentFilters.GetDocumentRelationRecRef(DocumentRelationRecRef);
                   SetLinkFieldsVisible;
                 END;

                 MyDocuVisible := (NOT CopyDoc);
                 CopyButtonVisible := (CopyDoc);

                 IF NOT ShowingResultDocumentFilter THEN
                   SetShowOnlyOwnDocuments; // jhoek.050511
               END;

    OnNewRecord=BEGIN
                  //**4PS Generated Code. Do Not Change.sn02
                  DPA_SetDefaultDepartment;
                  //**4PS Generated Code.en02
                END;

    OnInsertRecord=VAR
                     xRecRef@1100000999 : RecordRef;
                   BEGIN
                     //**4PS Generated Code. Do Not Change.sn05
                     xRecRef.GETTABLE(xRec);
                     DPA_SetDefaultDpmtFromNoSeries(xRecRef);
                     //**4PS Generated Code.en05
                   END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1210190049;1 ;ActionGroup;
                      Name=MyDocu;
                      CaptionML=ENU=&My documents;
                      Visible=MyDocuVisible }
      { 1210190048;2 ;Action    ;
                      CaptionML=ENU=Sender;
                      Image=SendMail;
                      OnAction=VAR
                                 UserSetup@1100528701 : Record 91;
                               BEGIN
                                 UserSetup.GET(USERID);
                                 UserSetup.TESTFIELD("Employee No.");
                                 SETRANGE(Editor);
                                 SETRANGE(Sender, UserSetup."Employee No.");
                                 SETRANGE("Final Printed", FALSE);
                               END;
                                }
      { 1210190050;2 ;Action    ;
                      CaptionML=ENU=Editor;
                      Image=DocumentEdit;
                      OnAction=VAR
                                 UserSetup@1100528701 : Record 91;
                               BEGIN
                                 UserSetup.GET(USERID);
                                 UserSetup.TESTFIELD("Employee No.");
                                 SETRANGE(Sender);
                                 SETRANGE(Editor, UserSetup."Employee No.");
                                 SETRANGE("Final Printed", FALSE);
                               END;
                                }
      { 37      ;1   ;ActionGroup;
                      CaptionML=ENU=&Document }
      { 38      ;2   ;Action    ;
                      ShortCutKey=Return;
                      CaptionML=ENU=Card;
                      Promoted=Yes;
                      PromotedIsBig=No;
                      Image=EditLines;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Choice@1100528704 : Integer;
                                 WordDocument@1210190002 : Page 11012406;
                                 ExternalDocument@1210190001 : Page 11012417;
                                 DocumentFilters@1100528701 : Codeunit 11012432;
                                 DocumentFilter@1100528702 : Record 11012073;
                               BEGIN
                                 IF ("No." = '') THEN BEGIN
                                   Choice := STRMENU(Text009, 1, Text010);
                                   IF Choice = 0 THEN
                                     EXIT;
                                 END ELSE BEGIN
                                   IF "External Document" THEN
                                     Choice := 2
                                   ELSE
                                     Choice := 1;
                                 END;

                                 FILTERGROUP(2);
                                 SETRANGE("RID Filter", FORMAT(RID));
                                 SETRANGE("Department Code Filter", DepartmentCode);
                                 FILTERGROUP(0);

                                 IF Choice = 1 THEN BEGIN
                                   CLEAR(WordDocument);
                                   WordDocument.SETTABLEVIEW(Rec);
                                   WordDocument.SETRECORD(Rec);
                                   WordDocument.RUNMODAL;
                                 END ELSE BEGIN
                                   CLEAR(ExternalDocument);
                                   ExternalDocument.SETTABLEVIEW(Rec);
                                   ExternalDocument.SETRECORD(Rec);
                                   ExternalDocument.RUNMODAL;
                                 END;

                                 IF ShowingResultDocumentFilter THEN BEGIN
                                   DocumentFilters.GetCurrentDocumentFilter(DocumentFilter);
                                   DocumentFilters.RefreshDocumentFilterResult(DocumentFilter);
                                 END;
                               END;
                                }
      { 1210190012;2 ;Action    ;
                      CaptionML=ENU=Open;
                      Promoted=Yes;
                      Image=ViewPage;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF IsConfidentialOwner() THEN
                                   OpenDocument;
                               END;
                                }
      { 1100525000;2 ;Action    ;
                      ShortCutKey=Return;
                      CaptionML=ENU=Open DMS Card;
                      Promoted=Yes;
                      Image=ViewPage;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF IsConfidentialOwner() THEN
                                   OpenDocumentD3;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525005;1 ;Action    ;
                      CaptionML=ENU=Copy;
                      Promoted=Yes;
                      Visible=CopyButtonVisible;
                      Image=Copy;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CopyLines;
                               END;
                                }
      { 1100528711;1 ;Action    ;
                      CaptionML=ENU=Filter;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=UseFilters;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocumentFilters@1100528701 : Codeunit 11012432;
                                 DocumentFilterCard@1100528702 : Page 11012415;
                                 DocumentFilter@1100528703 : Record 11012073;
                               BEGIN
                                 IF ShowingResultDocumentFilter THEN BEGIN
                                   IF FromFilterForm THEN
                                     CurrPage.CLOSE
                                   ELSE BEGIN
                                     DocumentFilters.GetCurrentDocumentFilter(DocumentFilter);
                                     DocumentFilter.FILTERGROUP(10);
                                     DocumentFilter.SETRECFILTER;
                                     DocumentFilter.FILTERGROUP(0);
                                     DocumentFilterCard.SetShowTempFilters(TRUE);
                                     DocumentFilterCard.SETRECORD(DocumentFilter);
                                     DocumentFilterCard.SETTABLEVIEW(DocumentFilter);
                                     DocumentFilterCard.RUNMODAL;
                                     DocumentFilters.GetCurrentDocumentFilter(DocumentFilter);
                                     DocumentFilters.ApplyDocumentFilterFields(DocumentFilter, Rec);
                                     SetLinkFieldsVisible;
                                   END;
                                 END ELSE
                                   MESSAGE(Text008);
                               END;
                                }
      { 1100525004;1 ;Action    ;
                      CaptionML=ENU=Register;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=AddAction;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ExternalDocumentRegistration@1100525000 : Page 11125843;
                                 DocumentFilters@1100528702 : Codeunit 11012432;
                                 DocumentFilter@1100528701 : Record 11012073;
                               BEGIN
                                 // jhoek.260111
                                 ExternalDocumentRegistration.SetContact(ContactNo, ContactPersonNo);
                                 ExternalDocumentRegistration.SetRecordID(RID);
                                 ExternalDocumentRegistration.SetDepartmentCode(DepartmentCode);
                                 ExternalDocumentRegistration.RUNMODAL;

                                 IF ShowingResultDocumentFilter THEN BEGIN
                                   DocumentFilters.GetCurrentDocumentFilter(DocumentFilter);
                                   DocumentFilters.RefreshDocumentFilterResult(DocumentFilter);
                                 END;
                               END;
                                }
      { 1100528702;1 ;Action    ;
                      CaptionML=ENU=New Word Document;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Word;
                      OnAction=VAR
                                 WordDocument@1100528703 : Page 11012406;
                                 DocumentProperties@1100528702 : Record 11012746;
                                 Contact@1100528705 : Record 5050;
                                 DocumentFilters@1100528701 : Codeunit 11012432;
                                 DocumentFilter@1100528700 : Record 11012073;
                               BEGIN
                                 DocumentProperties.COPY(Rec);
                                 DocumentProperties.INIT;
                                 DocumentProperties."No." := '';
                                 DocumentProperties."External Document" := FALSE;

                                 DocumentProperties.FILTERGROUP(2);
                                 DocumentProperties.SETRANGE("RID Filter", FORMAT(RID));
                                 DocumentProperties.SETRANGE("Department Code Filter", DepartmentCode);
                                 DocumentProperties.FILTERGROUP(0);

                                 IF Contact.GET(ContactNo) THEN
                                   DocumentProperties.VALIDATE("Contact No.", Contact."No.");
                                 IF Contact.GET(ContactPersonNo) THEN
                                   DocumentProperties.VALIDATE("Contact Person No.", Contact."No.");

                                 DocumentProperties.INSERT(TRUE);
                                 COMMIT;

                                 CLEAR(WordDocument);
                                 WordDocument.SETTABLEVIEW(DocumentProperties);
                                 WordDocument.SETRECORD(DocumentProperties);
                                 WordDocument.RUNMODAL;

                                 IF ShowingResultDocumentFilter THEN BEGIN
                                   DocumentFilters.GetCurrentDocumentFilter(DocumentFilter);
                                   DocumentFilters.RefreshDocumentFilterResult(DocumentFilter);
                                 END;
                               END;
                                }
      { 1100528600;1 ;Action    ;
                      CaptionML=ENU=Send to Dispatch Woon;
                      Image=ExportElectronicDocument;
                      OnAction=VAR
                                 DispatchInterface@1100528600 : Codeunit 11071685;
                                 ErrorMessage@1100528601 : Text;
                               BEGIN
                                 IF IsDocumentPicture THEN
                                   DispatchInterface.AddPhotoToFile(Rec, ErrorMessage);
                                   IF ErrorMessage <> '' THEN
                                     ERROR(ErrorMessage)
                                 ELSE
                                   DispatchInterface.AddAttachmentToFile(Rec, ErrorMessage);
                                   IF ErrorMessage <> '' THEN
                                     ERROR(ErrorMessage);
                               END;
                                }
      { 1100525006;1 ;ActionGroup;
                      CaptionML=ENU=Document Portal }
      { 1100525011;2 ;Action    ;
                      CaptionML=ENU=Add all Documents to Project Site;
                      Image=PostDocument;
                      OnAction=BEGIN
                                 AddAllProjectDocsToPPT(RID);
                               END;
                                }
      { 1100525003;0 ;ActionContainer;
                      ActionContainerType=Reports }
      { 1100528700;1 ;Action    ;
                      CaptionML=ENU=Print;
                      Promoted=Yes;
                      Image=Print;
                      PromotedCategory=Report;
                      OnAction=BEGIN
                                 PrintDocuments(FALSE);
                               END;
                                }
      { 1100525001;1 ;Action    ;
                      CaptionML=ENU=Print Final;
                      Promoted=Yes;
                      Image=Print;
                      PromotedCategory=Report;
                      OnAction=BEGIN
                                 PrintDocuments(TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 1210190000;2;Field  ;
                SourceExpr="No.";
                Editable=FALSE }

    { 1100485014;2;Field  ;
                SourceExpr="React Before";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190008;2;Field  ;
                SourceExpr="Contact No.";
                Editable=FALSE }

    { 18  ;2   ;Field     ;
                SourceExpr="Contact Name";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190036;2;Field  ;
                SourceExpr="Contact Person No.";
                Editable=FALSE }

    { 1210190010;2;Field  ;
                SourceExpr="Contact Person Name";
                Visible=FALSE;
                Editable=FALSE }

    { 1100525008;2;Field  ;
                SourceExpr="Project Portal Subsite";
                Editable=FALSE }

    { 1210190004;2;Field  ;
                SourceExpr="Document Type";
                Editable=FALSE }

    { 1100528300;2;Field  ;
                SourceExpr="Private Company Document";
                Editable=FALSE }

    { 1100525007;2;Field  ;
                SourceExpr="Document Category";
                Visible=FALSE;
                Editable=FALSE }

    { 1100525009;2;Field  ;
                SourceExpr="Project Element";
                Editable=FALSE }

    { 1210190006;2;Field  ;
                SourceExpr=Description;
                Editable=FALSE }

    { 24  ;2   ;Field     ;
                SourceExpr=Subject;
                Editable=FALSE }

    { 1100528701;2;Field  ;
                SourceExpr="Link Field 1";
                CaptionClass=GetCaptionClass(1);
                Visible=LinkField1Visible;
                Editable=FALSE }

    { 1100528703;2;Field  ;
                SourceExpr="Link Field 2";
                CaptionClass=GetCaptionClass(2);
                Visible=LinkField2Visible;
                Editable=FALSE }

    { 1100528705;2;Field  ;
                SourceExpr="Link Field 3";
                CaptionClass=GetCaptionClass(3);
                Visible=LinkField3Visible;
                Editable=FALSE }

    { 1100528707;2;Field  ;
                SourceExpr="Link Field 4";
                CaptionClass=GetCaptionClass(4);
                Visible=LinkField4Visible;
                Editable=FALSE }

    { 1100528709;2;Field  ;
                SourceExpr="Link Field 5";
                CaptionClass=GetCaptionClass(5);
                Visible=LinkField5Visible;
                Editable=FALSE }

    { 1210190014;2;Field  ;
                SourceExpr="User ID";
                Editable=FALSE }

    { 1210190053;2;Field  ;
                SourceExpr="E-Mail Sent On";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190055;2;Field  ;
                SourceExpr="E-Mail Sent By";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190057;2;Field  ;
                SourceExpr="E-Mail Sent To";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190002;2;Field  ;
                SourceExpr="External Document";
                Editable=FALSE }

    { 1210190051;2;Field  ;
                SourceExpr=File;
                Visible=FALSE;
                Editable=FALSE }

    { 1100485016;2;Field  ;
                SourceExpr="Web link";
                Visible=FALSE;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               TESTFIELD("Web link");
                               HYPERLINK("Web link");
                             END;
                              }

    { 30  ;2   ;Field     ;
                SourceExpr="Creation Date";
                Visible=FALSE;
                Editable=FALSE }

    { 32  ;2   ;Field     ;
                SourceExpr="Creation Time";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190016;2;Field  ;
                SourceExpr=Sender;
                Visible=FALSE;
                Editable=FALSE }

    { 1210190020;2;Field  ;
                SourceExpr="Final Printed Date";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190018;2;Field  ;
                SourceExpr="Final Printed";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190024;2;Field  ;
                SourceExpr="Document Template";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190022;2;Field  ;
                SourceExpr="Document Date";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190026;2;Field  ;
                SourceExpr="Document No.";
                Visible=FALSE;
                Editable=FALSE }

    { 1100525002;2;Field  ;
                SourceExpr=Version;
                Visible=FALSE;
                Editable=FALSE }

    { 1210190028;2;Field  ;
                SourceExpr="Description Document Template";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190030;2;Field  ;
                SourceExpr="Internal Company";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190032;2;Field  ;
                SourceExpr="Addressed To";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190034;2;Field  ;
                SourceExpr="Addressed To Name";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190038;2;Field  ;
                SourceExpr=City;
                Visible=FALSE;
                Editable=FALSE }

    { 1210190040;2;Field  ;
                CaptionML=ENU=Type;
                SourceExpr=GetType();
                Visible=FALSE;
                Editable=FALSE }

    { 1210190042;2;Field  ;
                SourceExpr=Status;
                Visible=FALSE;
                Editable=FALSE }

    { 1210190044;2;Field  ;
                SourceExpr=Editor;
                Visible=FALSE;
                Editable=FALSE }

    { 1100485012;2;Field  ;
                SourceExpr="Handled By";
                Visible=FALSE;
                Editable=FALSE }

    { 1210190071;2;Field  ;
                SourceExpr=Reference;
                Visible=FALSE;
                Editable=FALSE }

    { 1100525104;2;Field  ;
                SourceExpr=Modified;
                Visible=FALSE;
                Editable=FALSE }

    { 1210190046;2;Field  ;
                SourceExpr="Modification Date";
                Visible=FALSE;
                Editable=FALSE }

    { 1100528704;2;Field  ;
                SourceExpr="Publish on Service Portal";
                Visible=FALSE;
                Editable=FALSE }

    { 1100528706;2;Field  ;
                SourceExpr="Publish on Subcont.Portal";
                Visible=FALSE;
                Editable=FALSE }

    { 1100409000;2;Field  ;
                SourceExpr="Created by Field Service";
                Visible=FALSE;
                Editable=FALSE }

    { 1100525010;2;Field  ;
                SourceExpr="Available for Field Service";
                OnValidate=VAR
                             DocumentProperties@1100528700 : Record 11012746;
                           BEGIN
                             IF ShowingResultDocumentFilter THEN BEGIN
                               IF DocumentProperties.GET("No.") THEN BEGIN
                                 DocumentProperties."Available for Field Service" := "Available for Field Service";
                                 DocumentProperties.MODIFY;
                               END;
                             END;
                           END;
                            }

    { 1100528301;2;Field  ;
                SourceExpr="Project Portal Publish Date";
                Editable=FALSE }

    { 1100528602;2;Field  ;
                SourceExpr="Send to Dispatch" }

    { 1100528601;2;Field  ;
                SourceExpr="Sent to Dispatch on" }

    { 1100528603;2;Field  ;
                SourceExpr="Dispatch Attachment Type" }

  }
  CODE
  {
    VAR
      Text000@1210190000 : TextConst 'ENU=First select records';
      Text001@1100485010 : TextConst 'ENU=No';
      Text002@1100525009 : TextConst 'ENU=Document(s) Send Succesfully To Printer';
      Text003@1100525008 : TextConst 'ENU=Document(s) Not Sended to Printer';
      Text007@1100525006 : TextConst 'ENU=Transaction Aborted';
      DPA_IsHardFilter@1100000999 : Boolean;
      RID@1100525010 : RecordID;
      DocumentRelationRecRef@1100528706 : RecordRef;
      ProjectRID@1100525011 : RecordID;
      CopyDoc@1199198001 : Boolean;
      ByCompany@1199198002 : Boolean;
      ContactNo@1100525014 : Code[20];
      ContactPersonNo@1100528703 : Code[20];
      DepartmentCode@1100528700 : Code[20];
      ShowingResultDocumentFilter@1100528702 : Boolean;
      FromFilterForm@1100528704 : Boolean;
      Text008@1100528705 : TextConst 'ENU=There is no filter active.';
      LinkField1Visible@1100525016 : Boolean INDATASET;
      LinkField2Visible@1100525018 : Boolean INDATASET;
      LinkField3Visible@1100525019 : Boolean INDATASET;
      LinkField4Visible@1100525020 : Boolean INDATASET;
      LinkField5Visible@1100525021 : Boolean INDATASET;
      MyDocuVisible@19067208 : Boolean INDATASET;
      CopyButtonVisible@19037030 : Boolean INDATASET;
      Text009@1100528707 : TextConst 'ENU=Word Document,External Document';
      Text010@1100528708 : TextConst 'ENU=Select type of document';

    PROCEDURE CopyLines@1210190000();
    VAR
      PurchActionDocumentRel@1210190001 : Record 11012071;
      DocumentRelation@1100525001 : Record 11012407;
      DocumentRelation2@1100525000 : Record 11012407;
      ProjectNo@1100525002 : Code[20];
      PurchaseAction@1100528700 : Code[10];
      DocumentProperties@1100525004 : Record 11012746;
    BEGIN
      CurrPage.SETSELECTIONFILTER(DocumentProperties);
      IF NOT DocumentProperties.FINDSET THEN BEGIN
        MESSAGE(Text000);
        EXIT;
      END;

      CASE DocumentRelationRecRef.NUMBER OF
        DATABASE::"Document Relation":
          BEGIN
            DocumentRelationRecRef.SETTABLE(DocumentRelation);
            REPEAT
              IF NOT DocumentRelation2.GET(DocumentRelation."Document Type", DocumentRelation."No.", DocumentProperties."No.") THEN BEGIN
                DocumentRelation2.INIT;
                DocumentRelation2."Document Type" := DocumentRelation."Document Type";
                DocumentRelation2."No." := DocumentRelation."No.";
                DocumentRelation2.VALIDATE("Related Document No.", DocumentProperties."No.");
                DocumentRelation2."Document No." := DocumentProperties."Document No.";
                DocumentRelation2."Send by E-Mail" := TRUE;
                IF DocumentProperties.FileCanBeConvertedToPDF THEN
                  DocumentRelation2.Print := TRUE;
                DocumentRelation2.INSERT;
              END;
            UNTIL DocumentProperties.NEXT = 0;
          END;
        DATABASE::"Purch.Action Document Rel.":
          BEGIN
            DocumentRelationRecRef.SETTABLE(PurchActionDocumentRel);
            ProjectNo := PurchActionDocumentRel."Project No.";
            PurchaseAction := PurchActionDocumentRel."Purchase Action";
            REPEAT
              IF NOT PurchActionDocumentRel.GET(PurchActionDocumentRel.Source::Project, '',
                                                ProjectNo, PurchaseAction, DocumentProperties."No.") THEN BEGIN
                PurchActionDocumentRel.INIT;
                PurchActionDocumentRel.Source := PurchActionDocumentRel.Source::Project;
                PurchActionDocumentRel."Project No." := ProjectNo;
                PurchActionDocumentRel."Purchase Action" := PurchaseAction;
                PurchActionDocumentRel.VALIDATE("Related Document No.", DocumentProperties."No.");
                PurchActionDocumentRel.INSERT;
              END;
            UNTIL DocumentProperties.NEXT = 0;
          END;
      END; // END CASE

      COMMIT;
      CurrPage.CLOSE;
    END;

    PROCEDURE PrintDocuments@1100528709(Final@1100528701 : Boolean);
    VAR
      UserSetup@1100528712 : Record 91;
      DocumentProperties@1100528700 : Record 11012746;
      Printer@1100528706 : Record 2000000039;
      Printers@1100528705 : Page 356;
      WordAutomation@1100528702 : Codeunit 11012400;
      ExternalDocumentMgt@1100528704 : Codeunit 11012403;
      FileManagement@1100409000 : Codeunit 419;
      FileExists@1100528703 : Boolean;
      PrinterSelected@1100528709 : Boolean;
      Extension@1100528711 : Text[30];
      FilePath@1100528710 : Text[1024];
      SuccCnt@1100528707 : Integer;
      FlseCnt@1100528708 : Integer;
      DocumentMgt@1100525000 : Codeunit 11012406;
    BEGIN
      UserSetup.GET(USERID);

      CurrPage.SETSELECTIONFILTER(DocumentProperties);
      DocumentProperties.ASCENDING(TRUE);
      IF NOT DocumentProperties.FINDSET THEN BEGIN
        MESSAGE(Text000);
        EXIT;
      END;

      WordAutomation.Initialise;

      REPEAT
        FileExists := FALSE;
        Extension := '';
        IF DocumentProperties.IsConfidentialOwner AND
           ((DocumentProperties.File <> '') OR (DocumentProperties."D3 Document ID" <> '')) AND
           NOT (DocumentProperties."Final Printed" AND Final)
        THEN BEGIN
          IF NOT DocumentProperties."External Document" THEN BEGIN

            IF DocumentProperties."D3 Document ID" = '' THEN BEGIN
              IF WordAutomation.ExistingFileName(DocumentProperties.File, FALSE, TRUE, Extension) THEN
                FileExists := TRUE
              ELSE IF WordAutomation.ExistingFileName(DocumentProperties.File, TRUE, FALSE, Extension) THEN
               FileExists := TRUE;

              IF FileExists THEN BEGIN
                FilePath := DocumentProperties.File + Extension;
                IF NOT FileManagement.ClientFileExists(FilePath) THEN
                  FileExists := FALSE;
              END;

            END ELSE BEGIN
               FilePath := DocumentMgt.D3DocumentURL(DocumentProperties);
               IF FilePath <> '' THEN
                 FileExists := TRUE;

            END;

           IF FileExists THEN BEGIN
               IF NOT PrinterSelected THEN BEGIN
                 Printers.LOOKUPMODE(TRUE);
                 IF Printers.RUNMODAL = ACTION::LookupOK THEN BEGIN
                   Printers.GETRECORD(Printer);
                 END ELSE
                   ERROR(Text007);
                 PrinterSelected := TRUE;
               END;

               WordAutomation.Open(FALSE, FilePath, DocumentProperties."Final Printed");
               WordAutomation.PrintToSelectedPrinter(Printer.ID);
               WordAutomation.WaitForPrinter;
               WordAutomation.Close2(FALSE);
               SuccCnt := SuccCnt + 1;
               IF Final THEN BEGIN
                 DocumentProperties.VALIDATE("Final Printed", TRUE);
                 DocumentProperties."Final Printed Date" := TODAY;
                 DocumentProperties.MODIFY;
                 COMMIT;
               END;
            END ELSE
              FlseCnt := FlseCnt + 1;
          END ELSE BEGIN
            IF ExternalDocumentMgt.PrintExternalDocument(
              DocumentProperties."Document Type",
              DocumentProperties.File,
              DocumentProperties."Internal Company")
            THEN
              SuccCnt := SuccCnt + 1
            ELSE
              FlseCnt := FlseCnt + 1;
          END;
        END ELSE
          FlseCnt := FlseCnt + 1;

      UNTIL DocumentProperties.NEXT = 0;

      WordAutomation.Terminate;

      IF FlseCnt > 0 THEN
        MESSAGE('%1 ' + Text002 + '\%2 ' + Text003, SuccCnt, FlseCnt)
      ELSE
        MESSAGE('%1 ' + Text002, SuccCnt);
    END;

    PROCEDURE SetVars@1199198000(RecID@1199198000 : RecordID;NewDepartmentCode@1100528700 : Code[20];SetCopy@1199198001 : Boolean;SetByCompany@1199198002 : Boolean);
    BEGIN
      RID := RecID;
      DepartmentCode := NewDepartmentCode;
      CopyDoc := SetCopy;
      ByCompany := SetByCompany; // Obsoleted
    END;

    PROCEDURE SetContact@1100525000(NewContactNo@1100525000 : Code[20];NewContactPersonNo@1100525001 : Code[20]);
    BEGIN
      ContactNo := NewContactNo;
      ContactPersonNo := NewContactPersonNo;
    END;

    PROCEDURE SetDocumentRelationRecRef@1100528700(RecRef@1100528700 : RecordRef);
    BEGIN
      DocumentRelationRecRef := RecRef;
    END;

    LOCAL PROCEDURE SetShowOnlyOwnDocuments@1210190004();
    VAR
      RelationSetup@1100528700 : Record 5079;
    BEGIN
      RelationSetup.GET;
      // jhoek.050511; moved from OnOpenForm
      FILTERGROUP(2);
      IF RelationSetup."Show only own documents" THEN
        SETRANGE("Internal Company", COMPANYNAME);
      FILTERGROUP(0);
    END;

    PROCEDURE SetLinkFieldsVisible@1100528701();
    VAR
      DocumentFilters@1100528700 : Codeunit 11012432;
    BEGIN
      IF ShowingResultDocumentFilter THEN BEGIN
        LinkField1Visible := DocumentFilters.LinkFieldUsed(1);
        LinkField2Visible := DocumentFilters.LinkFieldUsed(2);
        LinkField3Visible := DocumentFilters.LinkFieldUsed(3);
        LinkField4Visible := DocumentFilters.LinkFieldUsed(4);
        LinkField5Visible := DocumentFilters.LinkFieldUsed(5);
      END;
    END;

    BEGIN
    END.
  }
}

