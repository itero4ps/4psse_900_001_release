OBJECT Page 11012508 Option List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=Option List;
    SourceTable=Table11012502;
    DataCaptionFields=Project No.,Plot No.;
    PageType=List;
    CardPageID=Option;
    OnInit=BEGIN
             CopyBtnEnable := TRUE;
           END;

    OnOpenPage=BEGIN
                 CopyBtnEnable := (gCopyProcessBln OR gCopyToCrossList);
               END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 23      ;1   ;ActionGroup;
                      CaptionML=ENU=&Option }
      { 24      ;2   ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=ENU=Card;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=EditLines;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 OptionCard();
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210190004;1 ;Action    ;
                      Name=CopyBtn;
                      CaptionML=ENU=&Copy;
                      Promoted=Yes;
                      Enabled=CopyBtnEnable;
                      Image=Copy;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 lvOptionRec@1210190003 : Record 11012502;
                                 lvPlotRec@1100525001 : Record 11012500;
                                 lvTmpPlotRec@1100525002 : TEMPORARY Record 11012500;
                                 lvPlotNoFrm@1100525003 : Page 11012506;
                                 lvBuyerMgtCU@1210190002 : Codeunit 11012500;
                                 lvLevelFrom@1210190001 : 'Standard,Project,Plot';
                                 OptionCode@1100528000 : Code[20];
                               BEGIN
                                 WITH lvOptionRec DO BEGIN
                                   CurrPage.SETSELECTIONFILTER(lvOptionRec);
                                   IF NOT FINDSET(FALSE, FALSE) THEN
                                     ERROR(Text000);

                                   IF NOT gCopyToCrossList THEN BEGIN
                                     CASE gCopyTypeTo OF
                                       gCopyTypeTo::Option:
                                         BEGIN
                                           IF gCopyLevelTo = gCopyLevelTo::Plot THEN BEGIN
                                             IF (NOT gMultiPlotToSelection) THEN BEGIN
                                               IF NOT CONFIRM(Text001, TRUE, gProjectTo, gPlotTo, gHouseTypeTo) THEN
                                                 EXIT;
                                               IF lvPlotRec.GET(gProjectTo, gPlotTo) THEN BEGIN
                                                 lvTmpPlotRec := lvPlotRec;
                                                 lvTmpPlotRec.INSERT;
                                               END;
                                             END ELSE BEGIN
                                               lvPlotRec.FILTERGROUP(9);
                                               lvPlotRec.SETRANGE("Project No.", gProjectTo);
                                               lvPlotRec.SETRANGE("House Model", gHouseTypeTo);
                                               lvPlotRec.FILTERGROUP(0);
                                               lvPlotNoFrm.SETTABLEVIEW(lvPlotRec);
                                               lvPlotNoFrm.InitSelectionCopyPlotOptions();
                                               lvPlotNoFrm.RUNMODAL;
                                               IF lvPlotNoFrm.GetMarkedRecords(lvPlotRec) THEN BEGIN
                                                 IF lvPlotRec.FINDSET(FALSE,FALSE) THEN BEGIN
                                                   REPEAT
                                                     lvTmpPlotRec := lvPlotRec;
                                                     lvTmpPlotRec.INSERT;
                                                   UNTIL lvPlotRec.NEXT = 0;
                                                 END;
                                               END;
                                             END;
                                             IF NOT lvTmpPlotRec.FINDFIRST THEN
                                               EXIT;
                                           END;

                                           REPEAT;
                                             IF ("Project No." = '') THEN
                                               lvLevelFrom := lvLevelFrom::Standard
                                             ELSE BEGIN
                                               IF ("Plot No." = '') THEN
                                                 lvLevelFrom := lvLevelFrom::Project
                                               ELSE
                                                 lvLevelFrom := lvLevelFrom::Plot;
                                             END;

                                             //Problem with '(' and ')' in Option handled by conversion to '?'   CALL T005792 Maintenance and Renovation
                                             OptionCode := CONVERTSTR(Option, '(', '?');
                                             OptionCode := CONVERTSTR(OptionCode, ')', '?');
                                             //

                                             CASE gCopyLevelTo OF
                                               gCopyLevelTo::Standard:
                                                 lvBuyerMgtCU.BmCopyOptions(
                                                   lvLevelFrom, gCopyLevelTo,
                                                   "Project No.", '',
                                                   "Plot No.", '',
                                                   "House Model", gHouseTypeTo,
                                                   "Main Group", '',
                                                   Group, '',
                                                   "Sub Group", '',
                                                   OptionCode, '');
                                               gCopyLevelTo::Project:
                                                 lvBuyerMgtCU.BmCopyOptions(
                                                   lvLevelFrom, gCopyLevelTo,
                                                   "Project No.", gProjectTo,
                                                   "Plot No.", '',
                                                   "House Model", gHouseTypeTo,
                                                   "Main Group", '',
                                                   Group, '',
                                                   "Sub Group", '',
                                                   OptionCode, '');
                                               gCopyLevelTo::Plot:
                                                 BEGIN
                                                   IF lvTmpPlotRec.FINDSET(FALSE,FALSE) THEN BEGIN
                                                     REPEAT
                                                       lvBuyerMgtCU.BmCopyOptions(
                                                         lvLevelFrom, lvLevelFrom::Plot,
                                                         "Project No.", gProjectTo,
                                                         //"Plot No.", gPlotTo,
                                                         //"House Model", gHouseTypeTo,
                                                         "Plot No.", lvTmpPlotRec."Plot No.",
                                                         "House Model", lvTmpPlotRec."House Model",
                                                         "Main Group", '',
                                                         Group, '',
                                                         "Sub Group", '',
                                                         OptionCode, '');
                                                     UNTIL lvTmpPlotRec.NEXT = 0;
                                                   END;
                                                 END;
                                             END;
                                           UNTIL NEXT = 0;
                                         END;
                                       gCopyTypeTo::Quote:
                                         BEGIN
                                           IF NOT CONFIRM(Text002, TRUE, gQuoteTo, gProjectTo, gPlotTo) THEN
                                             EXIT;

                                           REPEAT;
                                             IF NOT lvBuyerMgtCU.BmCheckOptionOnQuote(gProjectTo, gPlotTo, gHouseTypeTo, gQuoteTo, lvOptionRec, -1) THEN
                                               MARK := FALSE;
                                           UNTIL NEXT = 0;
                                           IF FINDSET(FALSE, FALSE) THEN BEGIN
                                             REPEAT;
                                               lvBuyerMgtCU.BmCopyOptionsToQuote(gProjectTo, gPlotTo, gHouseTypeTo, gQuoteTo, lvOptionRec);
                                             UNTIL NEXT = 0;
                                           END;
                                         END;
                                     END;

                                   END ELSE BEGIN
                                     IF NOT CONFIRM(Text003, TRUE, gProjectTo) THEN
                                       EXIT;

                                     REPEAT;
                                       lvBuyerMgtCU.BmCopyOptionsToCrossList(gProjectTo, "House Model", "Main Group", Group, "Sub Group", Option);
                                     UNTIL NEXT = 0;
                                   END;

                                 END;

                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 1210190005;2;Field  ;
                SourceExpr="Project No." }

    { 1210190007;2;Field  ;
                SourceExpr="Plot No." }

    { 1210190002;2;Field  ;
                SourceExpr="House Model";
                OnLookup=VAR
                           lvHouseModelRec@1210190000 : Record 11012501;
                         BEGIN
                           lvHouseModelRec.SETRANGE("Project No.", "Project No.");
                           lvHouseModelRec."Project No." := "Project No.";
                           lvHouseModelRec.Model := "House Model";
                           IF "Project No." <> '' THEN
                             PAGE.RUNMODAL(PAGE::"House Models by Project List", lvHouseModelRec)
                           ELSE
                             PAGE.RUNMODAL(PAGE::"House Model List", lvHouseModelRec);
                         END;
                          }

    { 1210190000;2;Field  ;
                SourceExpr="Main Group";
                OnLookup=VAR
                           lvMainGroupRec@1210190000 : Record 11012509;
                         BEGIN
                           lvMainGroupRec.SETRANGE("Project No.", "Project No.");
                           lvMainGroupRec."Project No." := "Project No.";
                           lvMainGroupRec.Code := "Main Group";
                           IF "Project No." <> '' THEN
                             PAGE.RUNMODAL(PAGE::"Project Option Main Group List", lvMainGroupRec)
                           ELSE
                             PAGE.RUNMODAL(PAGE::"Option Main Group List", lvMainGroupRec);
                         END;
                          }

    { 2   ;2   ;Field     ;
                SourceExpr=Group;
                OnLookup=VAR
                           lvGroupRec@1210190000 : Record 11012504;
                         BEGIN
                           lvGroupRec.SETRANGE("Project No.", "Project No.");
                           lvGroupRec."Project No." := "Project No.";
                           lvGroupRec.Code := Group;
                           IF "Project No." <> '' THEN
                             PAGE.RUNMODAL(PAGE::"Project Option Groups", lvGroupRec)
                           ELSE
                             PAGE.RUNMODAL(PAGE::"Option Groups", lvGroupRec);
                         END;
                          }

    { 4   ;2   ;Field     ;
                SourceExpr="Sub Group";
                OnLookup=VAR
                           lvSubGroupRec@1210190000 : Record 11012505;
                         BEGIN
                           lvSubGroupRec.SETRANGE("Project No.", "Project No.");
                           lvSubGroupRec."Project No." := "Project No.";
                           lvSubGroupRec.Code := "Sub Group";
                           IF "Project No." <> '' THEN
                             PAGE.RUNMODAL(PAGE::"Project Option Sub Groups", lvSubGroupRec)
                           ELSE
                             PAGE.RUNMODAL(PAGE::"Option Sub Groups", lvSubGroupRec);
                         END;
                          }

    { 6   ;2   ;Field     ;
                SourceExpr=Option }

    { 8   ;2   ;Field     ;
                SourceExpr=Description }

    { 1210190021;2;Field  ;
                SourceExpr="Option No. External";
                Visible=FALSE }

    { 12  ;2   ;Field     ;
                SourceExpr=Quantities }

    { 1210190015;2;Field  ;
                SourceExpr=Quantity }

    { 1210190011;2;Field  ;
                SourceExpr="Nett Price" }

    { 1210190013;2;Field  ;
                SourceExpr=Surcharges }

    { 14  ;2   ;Field     ;
                SourceExpr="Computed Sales Price Incl. VAT" }

    { 1210190017;2;Field  ;
                SourceExpr="Fixed Sales Price" }

    { 1100485000;2;Field  ;
                SourceExpr="Sales Price excl. VAT";
                Visible=FALSE }

    { 1210190009;2;Field  ;
                SourceExpr="Sales Price incl. VAT" }

    { 1100485006;2;Field  ;
                SourceExpr=Status }

    { 1100485008;2;Field  ;
                SourceExpr="Order Date" }

    { 16  ;2   ;Field     ;
                SourceExpr="Valid from" }

    { 18  ;2   ;Field     ;
                SourceExpr="Valid Until" }

    { 1100485002;2;Field  ;
                SourceExpr="Contract Amount";
                Visible=FALSE }

    { 1210190019;2;Field  ;
                SourceExpr=Mandatory }

    { 1100485004;2;Field  ;
                SourceExpr="Technical Text";
                Visible=FALSE }

  }
  CODE
  {
    VAR
      gCopyTypeTo@1210190010 : 'Option,Quote';
      gCopyLevelTo@1210190008 : 'Standard,Project,Plot';
      gCopyProcessBln@1210190009 : Boolean;
      gCopyToCrossList@1210190006 : Boolean;
      gMultiPlotToSelection@1100525000 : Boolean;
      gProjectTo@1210190001 : Code[20];
      gPlotTo@1210190005 : Code[10];
      gHouseTypeTo@1210190000 : Code[20];
      Text000@1210190002 : TextConst 'ENU=First select records.';
      Text001@1210190003 : TextConst 'ENU=Copy selected options to Project ''%1'', Plot ''%2'', House Model ''%3'' ?';
      gQuoteTo@1210190007 : Code[20];
      Text002@1210190004 : TextConst 'ENU=Copy selected options to Quote ''%1'', Project ''%2'', Plot ''%3'''' ?';
      Text003@1210190011 : TextConst 'ENU=Copy selected options to the ''Option Cross List'' of Project ''%1'''' ?';
      CopyBtnEnable@19067989 : Boolean INDATASET;

    PROCEDURE SetDefault@1210190002(ICopyTypeTo@1210190003 : 'Option,Quote';ICopyLevelTo@1210190005 : 'Standard,Project,Plot';IProjectTo@1210190001 : Code[20];IPlotTo@1210190002 : Code[10];IHouseTypeTo@1210190000 : Code[20];IQuoteTo@1210190004 : Code[20]);
    BEGIN
      gCopyTypeTo := ICopyTypeTo;
      gCopyProcessBln := TRUE;
      gProjectTo := IProjectTo;
      gHouseTypeTo := IHouseTypeTo;
      gPlotTo := IPlotTo;
      gCopyLevelTo := ICopyLevelTo;
      gQuoteTo := IQuoteTo;
    END;

    PROCEDURE SetDefaultCrossList@1210190000(IProjectTo@1210190001 : Code[20]);
    BEGIN
      gCopyToCrossList := TRUE;
      gProjectTo := IProjectTo;
    END;

    PROCEDURE SetMultiPlotToSelection@1100525000();
    BEGIN
      gMultiPlotToSelection := TRUE;
    END;

    PROCEDURE OptionCard@1210190001();
    VAR
      lvOptionRec@1210190000 : Record 11012502;
    BEGIN
      lvOptionRec.FILTERGROUP(9);
      lvOptionRec.SETRANGE("Project No.", "Project No.");
      lvOptionRec.SETRANGE("Plot No.", "Plot No.");
      lvOptionRec.FILTERGROUP(0);
      lvOptionRec.SETRANGE("House Model", "House Model");
      lvOptionRec.SETRANGE("Main Group", "Main Group");
      lvOptionRec.SETRANGE("Sub Group", "Sub Group");
      lvOptionRec.SETRANGE(Group, Group);
      lvOptionRec.SETRANGE(Option, Option);

      IF "Project No." <> '' THEN BEGIN
        IF "Plot No." <> '' THEN
          PAGE.RUNMODAL(PAGE::"Plot No. Option", lvOptionRec)
        ELSE
          PAGE.RUNMODAL(PAGE::"Project Option", lvOptionRec);
      END ELSE
        PAGE.RUNMODAL(PAGE::Option, lvOptionRec);
    END;

    BEGIN
    END.
  }
}

