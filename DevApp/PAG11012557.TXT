OBJECT Page 11012557 Plant Location
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Plant Location;
    SourceTable=Table11012554;
    PageType=Card;
    PromotedActionCategoriesML=ENU=New,Process,Report,Mail Documents;
    OnInit=BEGIN
             DepartmentCodeVisible := TRUE;
             VATLocationCodeVisible := TRUE;
             EmployeeNoEditable := TRUE;
             ProjectAddressCodeEditable := TRUE;
             AlternativeBilltoAddressEditab := TRUE;
             BilltoCustomerNoEditable := TRUE;
             CompanyNameEditable := TRUE;
             ServiceOrderNoEditable := TRUE;
             ElementEditable := TRUE;
             ProjectNoEditable := TRUE;
             CustomerNoEditable := TRUE;
           END;

    OnOpenPage=VAR
                 RecordOfGranuleSOPOSep@1100485000 : Record 11012787;
               BEGIN
                 //**4PS Generated Code. Do Not Change.sn01
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS Generated Code.en01
                 VATLocationCodeVisible := RecordOfGranuleSOPOSep.READPERMISSION;

                 PlantSetup.GET;
                 GLSetup.GET;
                 DepartmentCodeVisible := GLSetup."Department Authoriz. Active";

                 ReturnDepotsByDepotBtnVisible := (PlantSetup."Deviating Return Depot base on" = PlantSetup."Deviating Return Depot base on"::"Multiple Depots");
                 PlantTypeNoVisible := (PlantSetup."Cost Component Internal Charge" <> '');
                 PlantTypeNoEditable := PlantTypeNoVisible;
               END;

    OnAfterGetRecord=BEGIN
                       OnInsertNew := FALSE;
                       SETRANGE(Code);
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  //**4PS Generated Code. Do Not Change.sn02
                  DPA_SetDefaultDepartment;
                  //**4PS Generated Code.en02
                  OnInsertNew := TRUE;
                  InitRecord;
                  OnAfterGetCurrRecord;
                END;

    OnInsertRecord=BEGIN
                     IF Code <> '' THEN
                       OnInsertNew := FALSE;
                   END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 54      ;1   ;ActionGroup;
                      CaptionML=ENU=Location }
      { 1100485002;2 ;Action    ;
                      CaptionML=ENU=Contacts;
                      RunObject=Page 11012629;
                      RunPageLink=Location=FIELD(Code);
                      Image=CustomerContact }
      { 1100485000;2 ;Action    ;
                      CaptionML=ENU=Addresses;
                      RunObject=Page 11012625;
                      RunPageLink=Location=FIELD(Code);
                      Image=IndustryGroups }
      { 1100409002;2 ;Action    ;
                      AccessByPermission=Page 11125925=X;
                      CaptionML=ENU=Return Depots by Depot;
                      Visible=ReturnDepotsByDepotBtnVisible;
                      Image=Return;
                      OnAction=VAR
                                 ReturnDepots@1100409000 : Record 11012668;
                               BEGIN
                                 IF Depot THEN BEGIN
                                   ReturnDepots.SETRANGE(Depot, Code);
                                   ReturnDepots.Depot := Code;
                                 END;
                                 PAGE.RUNMODAL(PAGE::"Return Depots by Depot", ReturnDepots);
                               END;
                                }
      { 1100529900;2 ;ActionGroup;
                      CaptionML=ENU=Rates;
                      Image=AdjustExchangeRates }
      { 1210190009;3 ;Action    ;
                      CaptionML=ENU=Rental Rates by Location;
                      RunObject=Page 11012616;
                      RunPageView=WHERE(Rental Type=FILTER(Rental));
                      RunPageLink=Location=FIELD(Code),
                                  Rental Type=FILTER(Rental);
                      Image=Payment }
      { 1100485038;3 ;Action    ;
                      CaptionML=ENU=Rental Discount by Location;
                      Image=Discount;
                      OnAction=VAR
                                 lvRentDistRec@1100485001 : Record 11012544;
                                 lvRentDiscFrm@1100485000 : Page 11020599;
                               BEGIN
                                 TESTFIELD(Code);
                                 lvRentDistRec.FILTERGROUP(9);
                                 lvRentDistRec.SETRANGE("Customer Discount Group", '');
                                 lvRentDistRec.SETRANGE(Location, Code);
                                 lvRentDistRec.SETRANGE(Level, lvRentDistRec.Level::Location, lvRentDistRec.Level::LocationPlantNo);
                                 lvRentDistRec.FILTERGROUP(0);
                                 lvRentDiscFrm.SETTABLEVIEW(lvRentDistRec);
                                 lvRentDistRec.Location := Code;
                                 lvRentDiscFrm.SETRECORD(lvRentDistRec);
                                 lvRentDiscFrm.SetCalledFrom(4);
                                 lvRentDiscFrm.RUNMODAL;
                               END;
                                }
      { 1100528800;3 ;Action    ;
                      CaptionML=ENU=Rental Quantity Discount per Location;
                      Image=Discount;
                      OnAction=VAR
                                 RentalQuantityDiscount@1100528800 : Record 11072669;
                                 RentalQuantityDiscounts@1100528801 : Page 11125935;
                               BEGIN
                                 TESTFIELD(Code);
                                 RentalQuantityDiscount.FILTERGROUP(9);
                                 RentalQuantityDiscount.SETRANGE("Customer Discount Group", '');
                                 RentalQuantityDiscount.SETRANGE(Location, Code);
                                 RentalQuantityDiscount.SETRANGE(Level, RentalQuantityDiscount.Level::Location, RentalQuantityDiscount.Level::LocationPlantNo);
                                 RentalQuantityDiscount.FILTERGROUP(0);
                                 RentalQuantityDiscounts.SETTABLEVIEW(RentalQuantityDiscount);
                                 RentalQuantityDiscount.Location := Code;
                                 RentalQuantityDiscounts.SETRECORD(RentalQuantityDiscount);
                                 RentalQuantityDiscounts.SetCalledFrom(4);
                                 RentalQuantityDiscounts.RUNMODAL;
                               END;
                                }
      { 1100485022;3 ;Action    ;
                      CaptionML=ENU=Hour Rates by Location;
                      RunObject=Page 11020587;
                      RunPageView=WHERE(Rental Type=FILTER(Plant Hours));
                      RunPageLink=Location=FIELD(Code),
                                  Rental Type=FILTER(Plant Hours);
                      Image=Payment }
      { 1100525008;3 ;Action    ;
                      CaptionML=ENU=Sales / Buy Back Rates by Location;
                      RunObject=Page 11229860;
                      RunPageLink=Location=FIELD(Code);
                      Image=Payment }
      { 1100525009;3 ;Action    ;
                      AccessByPermission=Page 11020531=X;
                      CaptionML=ENU=Rate Codes by Location;
                      Image=LedgerBudget;
                      OnAction=VAR
                                 PlantLocRateCodeRec@1100525000 : Record 11020551;
                               BEGIN
                                 TESTFIELD(Code);
                                 PlantLocRateCodeRec.FILTERGROUP(9);
                                 PlantLocRateCodeRec.SETRANGE(Location, Code);
                                 PlantLocRateCodeRec.FILTERGROUP(0);
                                 PAGE.RUNMODAL(PAGE::"Plant Location Rate Codes", PlantLocRateCodeRec);
                               END;
                                }
      { 1100409003;3 ;Action    ;
                      CaptionML=ENU=Standard C&ost Lines by Location;
                      Image=Costs;
                      OnAction=VAR
                                 PlantCostLine@1100409000 : Record 11012591;
                               BEGIN
                                 //DP00671.n
                                 PlantCostLine.FILTERGROUP(4);
                                 PlantCostLine.SETRANGE("Cost Level", PlantCostLine."Cost Level"::Location);
                                 PlantCostLine.FILTERGROUP(0);
                                 PlantCostLine.SETRANGE(Code, Code);
                                 PAGE.RUNMODAL(PAGE::"Plant Cost Lines", PlantCostLine);
                               END;
                                }
      { 1100529901;2 ;ActionGroup;
                      CaptionML=ENU=Inventory;
                      Image=CreateInventoryPickup }
      { 56      ;3   ;Action    ;
                      CaptionML=ENU=Plant Inventory;
                      RunObject=Page 11012559;
                      RunPageLink=Removed=CONST(No),
                                  Location=FIELD(Code);
                      Promoted=Yes;
                      Image=InventoryJournal;
                      PromotedCategory=Process }
      { 57      ;3   ;Action    ;
                      CaptionML=ENU=Plant Inventory Course;
                      RunObject=Page 11012560;
                      RunPageView=SORTING(Location,Plant Type,Plant No.,Present from);
                      RunPageLink=Location=FIELD(Code);
                      Promoted=Yes;
                      Image=ItemTrackingLines;
                      PromotedCategory=Process }
      { 1100525012;3 ;Action    ;
                      AccessByPermission=Page 11071772=X;
                      CaptionML=ENU=Item  Inventory Plant Location;
                      Image=Item;
                      OnAction=VAR
                                 PlantLocationItemInventory@1100525000 : Page 11071772;
                               BEGIN
                                 TESTFIELD(Code);
                                 TESTFIELD(Depot, FALSE);
                                 PlantLocationItemInventory.SetSelections(Code, 0D);
                                 PlantLocationItemInventory.RUNMODAL;
                               END;
                                }
      { 1100485055;3 ;Action    ;
                      AccessByPermission=Page 11071766=X;
                      CaptionML=ENU=Item Inventory Course Plant Location;
                      Image=ItemTrackingLines;
                      OnAction=VAR
                                 lvPlantOrderLineRec@1100485000 : Record 11012557;
                               BEGIN
                                 TESTFIELD(Code);
                                 TESTFIELD(Depot, FALSE);
                                 lvPlantOrderLineRec.FILTERGROUP(9);
                                 lvPlantOrderLineRec.SETRANGE("Plant Location (Posted Item)", Code);
                                 lvPlantOrderLineRec.FILTERGROUP(0);
                                 PAGE.RUNMODAL(PAGE::"Items by Plant Location", lvPlantOrderLineRec);
                               END;
                                }
      { 1100485039;2 ;ActionGroup;
                      CaptionML=ENU=Plant Request;
                      Image=Resource }
      { 1100485040;3 ;Action    ;
                      CaptionML=ENU=Plant Requests - To Location;
                      RunObject=Page 11020526;
                      RunPageView=SORTING(To Location);
                      RunPageLink=To Location=FIELD(Code);
                      Image=Resource }
      { 1100485041;3 ;Action    ;
                      CaptionML=ENU=Plant Requests - From Location;
                      RunObject=Page 11020526;
                      RunPageView=SORTING(From Location);
                      RunPageLink=From Location=FIELD(Code);
                      Image=Resource }
      { 1100409010;3 ;Action    ;
                      CaptionML=ENU=Posted Plant Requests - To Location;
                      RunObject=Page 11130335;
                      RunPageView=SORTING(To Location);
                      RunPageLink=To Location=FIELD(Code);
                      Image=Resource }
      { 1100409009;3 ;Action    ;
                      CaptionML=ENU=Posted Plant Requests - From Location;
                      RunObject=Page 11130335;
                      RunPageView=SORTING(From Location);
                      RunPageLink=From Location=FIELD(Code);
                      Image=Resource }
      { 1100485008;2 ;ActionGroup;
                      CaptionML=ENU=Plant Orders;
                      Image=Order }
      { 1100485003;3 ;Action    ;
                      CaptionML=ENU=Plant Orders - To Location;
                      RunObject=Page 11012563;
                      RunPageView=SORTING(To Location);
                      RunPageLink=To Location=FIELD(Code);
                      Image=MakeOrder }
      { 1100485004;3 ;Action    ;
                      CaptionML=ENU=Plant Orders - From Location;
                      RunObject=Page 11012563;
                      RunPageView=SORTING(From Location);
                      RunPageLink=From Location=FIELD(Code);
                      Image=MakeOrder }
      { 1100409006;3 ;Action    ;
                      CaptionML=ENU=Posted Plant Orders - To Location;
                      RunObject=Page 11130058;
                      RunPageView=SORTING(To Location);
                      RunPageLink=To Location=FIELD(Code);
                      Image=MakeOrder }
      { 1100409005;3 ;Action    ;
                      CaptionML=ENU=Posted Plant Orders - From Location;
                      RunObject=Page 11130058;
                      RunPageView=SORTING(From Location);
                      RunPageLink=From Location=FIELD(Code);
                      Image=MakeOrder }
      { 1100409004;2 ;ActionGroup;
                      CaptionML=ENU=Rental Free Orders;
                      Image=Order }
      { 1100525010;3 ;Action    ;
                      CaptionML=ENU=Rental Free Orders;
                      RunObject=Page 11071769;
                      RunPageView=SORTING(Plant Location);
                      RunPageLink=Plant Location=FIELD(Code);
                      Image=Payment }
      { 1100409007;3 ;Action    ;
                      CaptionML=ENU=Posted Rental Free Orders;
                      RunObject=Page 11130337;
                      RunPageView=SORTING(Plant Location);
                      RunPageLink=Plant Location=FIELD(Code);
                      Image=Payment }
      { 1100485026;2 ;ActionGroup;
                      CaptionML=ENU=Transport Orders;
                      Image=Order }
      { 1100485027;3 ;Action    ;
                      CaptionML=ENU=Transport Orders  - To Location;
                      RunObject=Page 11020511;
                      RunPageLink=To Location=FIELD(Code);
                      Image=SpecialOrder }
      { 1100485028;3 ;Action    ;
                      CaptionML=ENU=Transport Orders - From Location;
                      RunObject=Page 11020511;
                      RunPageLink=From Location=FIELD(Code);
                      Image=SpecialOrder }
      { 1100485029;3 ;Action    ;
                      CaptionML=ENU=Posted Transport Order - To Location;
                      RunObject=Page 11020514;
                      RunPageView=SORTING(To Location);
                      RunPageLink=To Location=FIELD(Code);
                      Image=SpecialOrder }
      { 1100485030;3 ;Action    ;
                      CaptionML=ENU=Posted Transport Orders - From Location;
                      RunObject=Page 11020514;
                      RunPageView=SORTING(From Location);
                      RunPageLink=From Location=FIELD(Code);
                      Image=SpecialOrder }
      { 1100525005;2 ;ActionGroup;
                      CaptionML=ENU=Exit Orders }
      { 1100485023;3 ;Action    ;
                      CaptionML=ENU=Exit Orders;
                      RunObject=Page 11012567;
                      RunPageView=SORTING(Location);
                      RunPageLink=Location=FIELD(Code);
                      Image=MakeOrder }
      { 1100409008;3 ;Action    ;
                      CaptionML=ENU=Posted Exit Orders;
                      RunObject=Page 11130059;
                      RunPageView=SORTING(Location);
                      RunPageLink=Location=FIELD(Code);
                      Image=MakeOrder }
      { 1100485025;3 ;Action    ;
                      CaptionML=ENU=Exit Order Lines;
                      RunObject=Page 11020592;
                      RunPageLink=Exit Location=FIELD(Code);
                      Image=MakeOrder }
      { 1100485024;2 ;Action    ;
                      AccessByPermission=Page 11012149=X;
                      CaptionML=ENU=Job Orders (Small Projects);
                      Image=Order;
                      OnAction=VAR
                                 lvProjRec@1100485001 : Record 11072003;
                                 lvSmallProjectFrm@1100485000 : Page 11012149;
                                 lvLicensePlantExtendedCU@1100485003 : Codeunit 11012579;
                                 lvText11012000@1100485004 : TextConst 'ENU=You do not have license (or permission) for the Granule ''Plant Extended''.';
                               BEGIN
                                 //* Test op licentie van een willekeurig tabel uit de granule 'Materieel uitgebreid' (voor mat. werkorders geen tabellen)
                                 //* Gekozen voor Plant-Items (T11020519), voor gebruikers die hier mee moeten werken moet de leespermissie van deze tabel
                                 //* minimaal 'Indirect' zijn bij de toegangsrechten.
                                 IF NOT lvLicensePlantExtendedCU.LicenseTablePlantItem(FALSE,0) THEN
                                   ERROR(lvText11012000);
                                 TESTFIELD(Code);

                                 lvProjRec.FILTERGROUP(9);
                                 lvProjRec.SETRANGE("Plant Location", Code);
                                 lvProjRec.FILTERGROUP(0);
                                 lvSmallProjectFrm.SETTABLEVIEW(lvProjRec);
                                 lvSmallProjectFrm.SetSelectionCreateFromPlantLoc(Rec);
                                 lvSmallProjectFrm.RUNMODAL;
                               END;
                                }
      { 1100485033;2 ;ActionGroup;
                      CaptionML=ENU=Complaint Management;
                      Image=EditReminder }
      { 1100485034;3 ;Action    ;
                      AccessByPermission=TableData 11012450=I;
                      Ellipsis=Yes;
                      CaptionML=ENU=Create New Complaint;
                      Image=EditReminder;
                      OnAction=VAR
                                 lvComplRec@1100485001 : Record 11012450;
                                 lvComplMgtCU@1100485000 : Codeunit 11012418;
                               BEGIN
                                 TESTFIELD(Code);
                                 lvComplMgtCU.CreateNewComplaintOnSource(lvComplRec."Source Type"::PlantLoc, Code, '');
                               END;
                                }
      { 1100485035;3 ;Action    ;
                      CaptionML=ENU=Open Complaints;
                      RunObject=Page 11012468;
                      RunPageView=SORTING(Source Type,Source No.,Source No. 2)
                                  WHERE(Source Type=CONST(PlantLoc),
                                        Status=FILTER(<>Finished));
                      RunPageLink=Source No.=FIELD(Code);
                      Image=EditReminder }
      { 1100485036;3 ;Action    ;
                      CaptionML=ENU=Complaints;
                      RunObject=Page 11012468;
                      RunPageView=SORTING(Source Type,Source No.,Source No. 2)
                                  WHERE(Source Type=CONST(PlantLoc));
                      RunPageLink=Source No.=FIELD(Code);
                      Image=EditReminder }
      { 1100485007;2 ;ActionGroup;
                      CaptionML=ENU=Invoices;
                      Image=Invoice }
      { 1100485011;3 ;Action    ;
                      CaptionML=ENU=Invoice Proposals;
                      RunObject=Page 45;
                      RunPageView=WHERE(Document Type=CONST(Invoice Proposal));
                      RunPageLink=Plant Invoice=CONST(Yes),
                                  Plant Location Filter=FIELD(Code),
                                  Plant Loc. Filter On Invoice=CONST(Yes);
                      Promoted=No;
                      Image=MaintenanceRegistrations;
                      PromotedCategory=New }
      { 1100485009;3 ;Action    ;
                      CaptionML=ENU=Invoices;
                      RunObject=Page 45;
                      RunPageView=WHERE(Document Type=CONST(Invoice));
                      RunPageLink=Plant Invoice=CONST(Yes),
                                  Plant Location Filter=FIELD(Code),
                                  Plant Loc. Filter On Invoice=CONST(Yes);
                      Image=Invoice }
      { 1100485010;3 ;Action    ;
                      CaptionML=ENU=Credit Memos;
                      RunObject=Page 45;
                      RunPageView=WHERE(Document Type=CONST(Credit Memo));
                      RunPageLink=Plant Invoice=CONST(Yes),
                                  Plant Location Filter=FIELD(Code),
                                  Plant Loc. Filter On Invoice=CONST(Yes);
                      Image=CreditMemo }
      { 1100485005;3 ;Action    ;
                      AccessByPermission=Page 143=X;
                      CaptionML=ENU=Posted Invoices;
                      Image=PrepaymentInvoice;
                      OnAction=BEGIN
                                 RunPostedSalesInvoices();
                               END;
                                }
      { 1100485006;3 ;Action    ;
                      AccessByPermission=Page 144=X;
                      CaptionML=ENU=Posted Credit &Memos;
                      Image=PostedOrder;
                      OnAction=BEGIN
                                 RunPostedSalesCredMemos();
                               END;
                                }
      { 59      ;2   ;Action    ;
                      CaptionML=ENU=Revenues;
                      RunObject=Page 11012588;
                      RunPageOnRec=Yes;
                      Image=Cost }
      { 1210190004;2 ;Action    ;
                      CaptionML=ENU=Documents;
                      Visible=FALSE;
                      Image=Documents;
                      OnAction=BEGIN
                                 ShowDocuments();  //DP00183.c
                               END;
                                }
      { 1100529001;2 ;Action    ;
                      CaptionML=ENU=Comments;
                      Image=ViewComments;
                      OnAction=BEGIN
                                 CommentSheet(TRUE);  //DP00183
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100485042;1 ;ActionGroup;
                      CaptionML=ENU=Functions }
      { 1100485047;2 ;Action    ;
                      AccessByPermission=TableData 11012011=R;
                      CaptionML=ENU=Copy &Project Location Addresses;
                      Image=Copy;
                      OnAction=VAR
                                 lvNoCopied@1100485000 : Integer;
                               BEGIN
                                 TESTFIELD(Code);
                                 TESTFIELD("Project No.");

                                 IF NOT CONFIRM(Text001) THEN
                                   EXIT;

                                 lvNoCopied := Rec.CopyProjLocsToPlantLocAddress('');
                                 MESSAGE(Text002, lvNoCopied);
                               END;
                                }
      { 1100485049;2 ;Action    ;
                      CaptionML=ENU=Modify Plant Responsible Employee;
                      RunObject=Page 11072375;
                      RunPageLink=Location=FIELD(Code);
                      Image=Employee }
      { 1100525014;2 ;Action    ;
                      ShortCutKey=Ctrl+K;
                      CaptionML=ENU=Completeness Check;
                      Image=CheckList;
                      OnAction=VAR
                                 CheckRecordCompletion@1100525000 : Codeunit 11012252;
                                 RecRef@1100525001 : RecordRef;
                               BEGIN
                                 RecRef.GETTABLE(Rec);
                                 CheckRecordCompletion.ShowIncompleteFields(RecRef);
                               END;
                                }
      { 1100409000;1 ;ActionGroup;
                      CaptionML=ENU=Mail Documents;
                      ActionContainerType=NewDocumentItems }
      { 1100409001;2 ;Action    ;
                      CaptionML=ENU=Plant Location;
                      Promoted=Yes;
                      Image=MailAttachment;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 Contact@1100409000 : Record 5050;
                               BEGIN
                                 IF GetContact(Contact, TRUE) THEN BEGIN
                                   IF Contact.IsContactPerson THEN
                                     EditDocuments(Contact."Company No.", Contact."No.", TRUE, "E-Mail")
                                   ELSE
                                     EditDocuments(Contact."No.", '', TRUE, "E-Mail");
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=ENU=General;
                GroupType=Group }

    { 2   ;2   ;Field     ;
                SourceExpr=Code;
                Importance=Promoted;
                OnAssistEdit=BEGIN
                               IF OnInsertNew THEN BEGIN
                                 AssistEdit(xRec);
                                 IF Code <> '' THEN
                                   CurrPage.SAVERECORD;  //C025605.n
                               END;
                             END;
                              }

    { 31  ;2   ;Field     ;
                SourceExpr="Company Name";
                Importance=Promoted;
                Editable=CompanyNameEditable;
                OnValidate=BEGIN
                             CurrPage.UPDATE(TRUE);
                           END;
                            }

    { 10  ;2   ;Field     ;
                SourceExpr="Project No.";
                TableRelation=Job.No.;
                Editable=ProjectNoEditable;
                OnValidate=BEGIN
                             CurrPage.UPDATE(TRUE);
                           END;

                OnLookup=VAR
                           lvProjRec@1210190000 : Record 11072003;
                           PageNo@1100525001 : Integer;
                         BEGIN
                           IF "Company Name" <> '' THEN
                             lvProjRec.CHANGECOMPANY("Company Name");
                           lvProjRec."No." := "Project No.";
                           IF RefersToOtherCompany THEN
                             PageNo := PAGE::"Job List IC";
                           IF PAGE.RUNMODAL(PageNo, lvProjRec) = ACTION::LookupOK THEN
                             VALIDATE("Project No.",lvProjRec."No.");
                           CurrPage.UPDATE;
                         END;
                          }

    { 1210190003;2;Field  ;
                SourceExpr=Element;
                Importance=Additional;
                Editable=ElementEditable }

    { 1100525000;2;Field  ;
                SourceExpr="Service Order No.";
                Editable=ServiceOrderNoEditable;
                OnValidate=BEGIN
                             CurrPage.UPDATE(TRUE);
                           END;

                OnLookup=VAR
                           lvServOrderRec@1100525000 : Record 11012823;
                         BEGIN
                           IF "Company Name" <> '' THEN
                             lvServOrderRec.CHANGECOMPANY("Company Name");
                           lvServOrderRec."No." := "Service Order No.";
                           IF PAGE.RUNMODAL(0, lvServOrderRec) = ACTION::LookupOK THEN
                             VALIDATE("Service Order No.",lvServOrderRec."No.");
                           CurrPage.UPDATE;
                         END;
                          }

    { 1100485046;2;Field  ;
                SourceExpr="Employee No.";
                Editable=EmployeeNoEditable;
                OnValidate=BEGIN
                             CurrPage.UPDATE(TRUE);
                           END;

                OnLookup=BEGIN
                           LookupEmployee(EmployeeNoEditable);
                           CurrPage.UPDATE;
                         END;
                          }

    { 1100529003;2;Field  ;
                SourceExpr="Plant Type";
                Visible=PlantTypeNoVisible;
                Editable=PlantTypeNoEditable;
                OnValidate=BEGIN
                             //CurrPage.UPDATE(TRUE);
                             OnAfterGetCurrRecord;
                           END;
                            }

    { 1100529004;2;Field  ;
                SourceExpr="Plant No.";
                Visible=PlantTypeNoVisible;
                Editable=PlantTypeNoEditable;
                ShowMandatory=PlantNoShowMandatory }

    { 35  ;2   ;Field     ;
                SourceExpr="Customer No.";
                Editable=CustomerNoEditable;
                OnValidate=BEGIN
                             CurrPage.UPDATE(TRUE);
                           END;
                            }

    { 1100485043;2;Field  ;
                SourceExpr="Project Address Code";
                Editable=ProjectAddressCodeEditable }

    { 1100485045;2;Field  ;
                CaptionML=ENU=Number of Job Addresses;
                SourceExpr=GetNoOfCustJobAddressesTxt();
                Importance=Additional;
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                SourceExpr=Description }

    { 6   ;2   ;Field     ;
                SourceExpr="Description 2";
                Importance=Additional }

    { 12  ;2   ;Field     ;
                SourceExpr=Address }

    { 14  ;2   ;Field     ;
                SourceExpr="Address 2";
                Importance=Additional }

    { 19  ;2   ;Field     ;
                SourceExpr="Post Code" }

    { 16  ;2   ;Field     ;
                SourceExpr=City }

    { 29  ;2   ;Field     ;
                SourceExpr="Country/Region Code" }

    { 17  ;2   ;Field     ;
                SourceExpr=Contact }

    { 1100525004;2;Field  ;
                SourceExpr="Contact Phone No." }

    { 8   ;2   ;Field     ;
                SourceExpr="Search Name" }

    { 24  ;2   ;Field     ;
                SourceExpr="Territory Code" }

    { 20  ;2   ;Field     ;
                SourceExpr="Phone No." }

    { 22  ;2   ;Field     ;
                SourceExpr="Fax No." }

    { 1100485052;2;Field  ;
                ExtendedDatatype=E-Mail;
                SourceExpr="E-Mail" }

    { 1100529002;2;Field  ;
                SourceExpr="E-Mail Status PO Modified" }

    { 1100485050;2;Field  ;
                SourceExpr="Employee Location" }

    { 27  ;2   ;Field     ;
                SourceExpr=Depot }

    { 37  ;2   ;Field     ;
                SourceExpr=Blocked }

    { 1100485922;2;Field  ;
                SourceExpr=Status }

    { 1100525002;2;Field  ;
                CaptionML=ENU=Estimate No.;
                SourceExpr=EstimateNoOnFormat();
                Importance=Additional;
                OnDrillDown=VAR
                              Estimate@1100409000 : Record 11012151;
                            BEGIN
                              Estimate.SETCURRENTKEY("Plant Location");
                              Estimate.SETRANGE("Plant Location", Code);
                              PAGE.RUNMODAL(0, Estimate);
                            END;
                             }

    { 1100526000;2;Field  ;
                SourceExpr="Department Code";
                Visible=DepartmentCodeVisible;
                OnValidate=BEGIN
                             //**4PS Generated Code. Do Not Change.sn03
                             DPA_ChkDepartmentCodeAllowed(DPA_IsHardFilter);
                             //**4PS Generated Code.en03
                           END;
                            }

    { 1100529000;2;Field  ;
                SourceExpr="Location Code (Items)" }

    { 1100529005;2;Field  ;
                SourceExpr="Internal Charge Plant Entry as";
                Visible=PlantTypeNoVisible;
                Editable=PlantTypeNoEditable }

    { 1100529304;2;Field  ;
                SourceExpr="Travel Distance" }

    { 1100529303;2;Field  ;
                CaptionML=ENU=Travel Distance Unit;
                SourceExpr=TravelDistanceUnit;
                Importance=Additional }

    { 1100529302;2;Field  ;
                SourceExpr="Travel Time" }

    { 1100529301;2;Field  ;
                CaptionML=ENU=Travel Time Unit;
                SourceExpr=TravelTimeUnit;
                Importance=Additional }

    { 1100529300;2;Field  ;
                SourceExpr="Travel Cost Code" }

    { 1905885101;1;Group  ;
                CaptionML=ENU=Invoicing }

    { 1100485016;2;Field  ;
                SourceExpr="Bill-to Customer No.";
                Importance=Promoted;
                Editable=BilltoCustomerNoEditable }

    { 1100485018;2;Field  ;
                SourceExpr="Alternative Bill-to Address";
                Editable=AlternativeBilltoAddressEditab }

    { 1210190014;2;Field  ;
                SourceExpr="Gen. Bus. Posting Group" }

    { 43  ;2   ;Field     ;
                SourceExpr="VAT Bus. Posting Group" }

    { 39  ;2   ;Field     ;
                SourceExpr="Discount %" }

    { 1100525011;2;Field  ;
                CaptionML=ENU=Discount % for;
                SourceExpr=FillDiscountComment();
                Importance=Additional }

    { 41  ;2   ;Field     ;
                SourceExpr=Calendar }

    { 1100485020;2;Field  ;
                SourceExpr="Rate Type" }

    { 1210190010;2;Field  ;
                SourceExpr="Kind of Rate" }

    { 1100525007;2;Field  ;
                SourceExpr="Rate Codes by Location" }

    { 1100485012;2;Field  ;
                SourceExpr="Invoice Method" }

    { 1100485014;2;Field  ;
                SourceExpr="Invoice Copies" }

    { 1210190001;2;Field  ;
                SourceExpr=Distance }

    { 1210190012;2;Field  ;
                SourceExpr="Damage Settlement";
                Importance=Additional }

    { 1100485031;2;Field  ;
                SourceExpr="VAT Location Code";
                Importance=Additional;
                Visible=VATLocationCodeVisible }

    { 1100525006;2;Field  ;
                SourceExpr="BB with Sales Rate (L to L.)" }

    { 1905223301;1;Group  ;
                CaptionML=ENU=Dates }

    { 51  ;2   ;Field     ;
                SourceExpr="Starting Date";
                Importance=Promoted }

    { 49  ;2   ;Field     ;
                SourceExpr="Ending Date";
                Importance=Promoted }

    { 47  ;2   ;Field     ;
                SourceExpr="Creation Date" }

    { 45  ;2   ;Field     ;
                SourceExpr="Last Date Modified" }

    { 1100529902;0;Container;
                ContainerType=FactBoxArea }

    { 1100529903;1;Part   ;
                Name=RegisterExternalDocumentFB;
                PagePartID=Page11125053;
                PartType=Page }

    { 1100409011;1;Part   ;
                SubPageLink=Code=FIELD(Code);
                PagePartID=Page11130064;
                PartType=Page }

    { 1100525003;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1100525001;1;Part   ;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text001@1100485000 : TextConst 'ENU=Copy Project Location Addresses to Plant Locatie Addresses?';
      Text002@1100485001 : TextConst 'ENU=%1 Addresses copied (created/updated)';
      PlantSetup@1100525001 : Record 11012550;
      GLSetup@1100525002 : Record 98;
      OnInsertNew@1100525000 : Boolean;
      DPA_IsHardFilter@1100000999 : Boolean;
      Text003a@1100525003 : TextConst 'ENU=Plant Hours';
      Text003b@1100525004 : TextConst 'ENU=S/BB Plant';
      Text003c@1100525005 : TextConst 'ENU=Items';
      CustomerNoEditable@19062884 : Boolean INDATASET;
      ProjectNoEditable@19028701 : Boolean INDATASET;
      ElementEditable@19042443 : Boolean INDATASET;
      ServiceOrderNoEditable@19020415 : Boolean INDATASET;
      CompanyNameEditable@19027413 : Boolean INDATASET;
      BilltoCustomerNoEditable@19000288 : Boolean INDATASET;
      AlternativeBilltoAddressEditab@19051052 : Boolean INDATASET;
      ProjectAddressCodeEditable@19074756 : Boolean INDATASET;
      EmployeeNoEditable@19049966 : Boolean INDATASET;
      PlantTypeNoEditable@1100529001 : Boolean INDATASET;
      VATLocationCodeVisible@19032591 : Boolean INDATASET;
      DepartmentCodeVisible@19030186 : Boolean INDATASET;
      ReturnDepotsByDepotBtnVisible@1100409000 : Boolean INDATASET;
      PlantTypeNoVisible@1100529000 : Boolean INDATASET;
      PlantNoShowMandatory@1100529002 : Boolean INDATASET;

    PROCEDURE RunPostedSalesInvoices@1100525000();
    VAR
      lvInvoiceHeadRec@1100525001 : Record 112;
      lvInvoiceLineRec@1100525000 : Record 113;
    BEGIN
      TESTFIELD(Code);
      lvInvoiceLineRec.SETCURRENTKEY("Plant Location", "Document No.");
      lvInvoiceLineRec.SETRANGE("Plant Location", Code);
      IF lvInvoiceLineRec.FINDFIRST THEN BEGIN
        REPEAT
          IF lvInvoiceHeadRec.GET(lvInvoiceLineRec."Document No.") THEN
            lvInvoiceHeadRec.MARK(TRUE);

          //* Set pointer on last line of the invoice, so 'Next' will be another invoice
          lvInvoiceLineRec.SETRANGE("Document No.", lvInvoiceLineRec."Document No.");
          lvInvoiceLineRec.FINDLAST;
          lvInvoiceLineRec.SETRANGE("Document No.");  //* Reset filter on invoice

        UNTIL lvInvoiceLineRec.NEXT = 0;
      END;

      lvInvoiceHeadRec.MARKEDONLY(TRUE);
      PAGE.RUNMODAL(PAGE::"Posted Sales Invoices", lvInvoiceHeadRec);
    END;

    PROCEDURE RunPostedSalesCredMemos@1100525001();
    VAR
      lvCredMemHeadRec@1100525001 : Record 114;
      lvCredMemLineRec@1100525000 : Record 115;
    BEGIN
      TESTFIELD(Code);
      lvCredMemLineRec.SETCURRENTKEY("Plant Location", "Document No.");
      lvCredMemLineRec.SETRANGE("Plant Location", Code);
      IF lvCredMemLineRec.FINDFIRST THEN BEGIN
        REPEAT
          IF lvCredMemHeadRec.GET(lvCredMemLineRec."Document No.") THEN
            lvCredMemHeadRec.MARK(TRUE);

          //* Set pointer on last line of the invoice, so 'Next' will be another invoice
          lvCredMemLineRec.SETRANGE("Document No.", lvCredMemLineRec."Document No.");
          lvCredMemLineRec.FINDLAST;
          lvCredMemLineRec.SETRANGE("Document No.");  //* Reset filter on invoice

        UNTIL lvCredMemLineRec.NEXT = 0;
      END;

      lvCredMemHeadRec.MARKEDONLY(TRUE);
      PAGE.RUNMODAL(PAGE::"Posted Sales Credit Memos", lvCredMemHeadRec);
    END;

    PROCEDURE FillDiscountComment@1100525003() DiscountComment : Text[100];
    BEGIN
      DiscountComment := '';
      IF "Discount %" = 0 THEN
        EXIT;
      IF PlantSetup."Discount Plant Hours" THEN
        DiscountComment := Text003a;
      IF PlantSetup."Discount Sales/Buy Back" THEN BEGIN
        IF DiscountComment <> '' THEN
          DiscountComment := DiscountComment + ', ';
        DiscountComment := DiscountComment + Text003b;
      END;
      IF PlantSetup."Discount Items" THEN BEGIN
        IF DiscountComment <> '' THEN
          DiscountComment := DiscountComment + ', ';
        DiscountComment := DiscountComment + Text003c;
      END;
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    VAR
      RecRef@1100529900 : RecordRef;
      PlantType@1100529000 : Record 11012551;
    BEGIN
      xRec := Rec;
      CustomerNoEditable := (
        ("Project No." = '') AND ("Service Order No." = '') AND ("Employee No." = '') AND ("Company Name" ='') AND ("Plant Type" = ''));
      ProjectNoEditable := (
        ("Employee No." = '') AND ("Service Order No." = '') AND ("Plant Type" = '') AND
        (("Customer No." = '') OR (NOT (("Project No." = '') AND ("Company Name" = '')))) );
      ElementEditable := (
        ("Employee No." = '') AND ("Service Order No." = '') AND
        (("Customer No." = '') OR (NOT (("Project No." = '') AND ("Company Name" = '')))) );
      ServiceOrderNoEditable := (
        ("Employee No." = '') AND ("Project No." = '') AND ("Plant Type" = '') AND
        (("Customer No." = '') OR (NOT (("Service Order No." = '') AND ("Company Name" = '')))) );
      CompanyNameEditable := (
        ("Customer No." = '') AND ("Plant Type" = '') OR
        (NOT (("Project No." = '') AND ("Service Order No." = '') AND ("Employee No." = '') AND ("Company Name" =''))) );
      BilltoCustomerNoEditable := ("Customer No." <> '');
      AlternativeBilltoAddressEditab := ("Customer No." <> '');
      ProjectAddressCodeEditable := (
        ("Customer No." <> '') AND
        ("Project No." = '') AND ("Service Order No." = '') AND
        ("Employee No." = '') AND ("Company Name" = ''));
      EmployeeNoEditable := (
        ("Project No." = '') AND ("Service Order No." = '') AND ("Plant Type" = '') AND
        (("Customer No." = '') OR (NOT (("Employee No." = '') AND ("Company Name" = '')))) );
      PlantTypeNoVisible := (PlantSetup."Cost Component Internal Charge" <> '');
      PlantTypeNoEditable := (PlantTypeNoVisible AND
        ("Project No." = '') AND ("Service Order No." = '') AND ("Employee No." = '') AND ("Company Name" ='') AND ("Customer No." =''));
      PlantNoShowMandatory := FALSE;
      IF PlantTypeNoEditable AND ("Plant Type" <> '') THEN BEGIN
        IF PlantType.GET("Plant Type") THEN
          PlantNoShowMandatory := (NOT PlantType.Bulk);
      END;

      RecRef.GETTABLE(Rec);
      CurrPage.RegisterExternalDocumentFB.PAGE.SetRecordReference(RecRef);
      CurrPage.RegisterExternalDocumentFB.PAGE.SetByCompany(TRUE);
      CurrPage.RegisterExternalDocumentFB.PAGE.SetDepartmentCode("Department Code");
    END;

    LOCAL PROCEDURE EstimateNoOnFormat@19057408() EstimateNoTxt : Text[30];
    VAR
      EstRec@1100525000 : Record 11012151;
    BEGIN
      EstimateNoTxt := "Estimate No.";
      IF EstRec.READPERMISSION THEN BEGIN  //db, 26-06-14: C016159
        CALCFIELDS("No. of Linked Estimates");
        IF "No. of Linked Estimates" > 1 THEN
          EstimateNoTxt := '[' + FORMAT("No. of Linked Estimates") + ']';
      END;
    END;

    BEGIN
    END.
  }
}

