OBJECT Page 11012665 Inv. Proposal Template Subform
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Lines;
    SourceTable=Table11012917;
    PageType=ListPart;
    OnOpenPage=BEGIN
                 SetFieldEditable;
               END;

    OnAfterGetRecord=BEGIN
                       DescriptionIndent := 0;
                       SetFieldEditable;
                     END;

    ActionList=ACTIONS
    {
      { 1100409004;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100409003;1 ;ActionGroup;
                      CaptionML=ENU=F&unctions }
      { 1100409002;2 ;Action    ;
                      ShortCutKey=Ctrl+I;
                      CaptionML=ENU=Increase Indentation;
                      Image=IndentChartOfAccounts;
                      OnAction=BEGIN
                                 AdjustIndentation(1);
                               END;
                                }
      { 1100409001;2 ;Action    ;
                      ShortCutKey=Ctrl+D;
                      CaptionML=ENU=Decrease Indentation;
                      Image=IndentChartOfAccounts;
                      OnAction=BEGIN
                                 AdjustIndentation(-1);
                               END;
                                }
      { 1100409000;2 ;Action    ;
                      ShortCutKey=Ctrl+B;
                      CaptionML=ENU=Swith &Bold (On/Off);
                      Image=BreakRulesOn;
                      OnAction=BEGIN
                                 SwitchBold();
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210190000;1;Group  ;
                IndentationColumnName=Indentation;
                IndentationControls=Description;
                GroupType=Repeater }

    { 1210190001;2;Field  ;
                SourceExpr="Rubric Code" }

    { 1210190003;2;Field  ;
                SourceExpr=Description;
                Editable=DescriptionEditable }

    { 1210190005;2;Field  ;
                SourceExpr=Type;
                OnValidate=BEGIN
                             TypeOnAfterValidate;
                             SetFieldEditable;
                           END;
                            }

    { 1100409006;2;Field  ;
                SourceExpr=Bold }

    { 1100485000;2;Field  ;
                SourceExpr="Budget Distribution";
                Editable=BudgetDistributionEditable }

    { 1210190007;2;Field  ;
                SourceExpr=Totaling;
                Editable=TotalingEditable }

    { 1210190009;2;Field  ;
                SourceExpr="Cost Object Filter";
                Editable=CostObjectFilterEditable }

    { 1210190011;2;Field  ;
                BlankZero=Yes;
                SourceExpr=Percentage;
                Editable=PercentageEditable }

    { 1210190013;2;Field  ;
                SourceExpr="Percentage Filter";
                Editable=PercentageFilterEditable }

    { 1100525000;2;Field  ;
                SourceExpr="Tot.Type for Perc. (Sub)Totals";
                Visible=FALSE;
                Editable=TotTypeForPercSubTotalsEdita }

  }
  CODE
  {
    VAR
      "Rubric CodeEmphasize"@19058023 : Boolean INDATASET;
      DescriptionEmphasize@19010547 : Boolean INDATASET;
      DescriptionIndent@19057867 : Integer INDATASET;
      TypeEmphasize@19012868 : Boolean INDATASET;
      "Budget DistributionEmphasize"@19016142 : Boolean INDATASET;
      TotalingEmphasize@19055023 : Boolean INDATASET;
      "Cost Object FilterEmphasize"@19032841 : Boolean INDATASET;
      PercentageEmphasize@19011354 : Boolean INDATASET;
      "Percentage FilterEmphasize"@19018200 : Boolean INDATASET;
      DescriptionEditable@1100409000 : Boolean INDATASET;
      BudgetDistributionEditable@1100409001 : Boolean INDATASET;
      TotalingEditable@1100409002 : Boolean INDATASET;
      CostObjectFilterEditable@1100409003 : Boolean INDATASET;
      PercentageEditable@1100409004 : Boolean INDATASET;
      PercentageFilterEditable@1100409005 : Boolean INDATASET;
      TotTypeForPercSubTotalsEdita@1100409006 : Boolean INDATASET;

    PROCEDURE AdjustIndentation@1210190005(IndentStep@1100529000 : Integer);
    VAR
      InvPropTemplLine@1210190000 : Record 11012917;
    BEGIN
      CurrPage.SETSELECTIONFILTER(InvPropTemplLine);
      CollAdjustIndentation(InvPropTemplLine, IndentStep);
    END;

    PROCEDURE SwitchBold@1210190006();
    VAR
      InvPropTemplLine@1210190000 : Record 11012917;
    BEGIN
      CurrPage.SETSELECTIONFILTER(InvPropTemplLine);
      CollSwitchBold(InvPropTemplLine);
    END;

    LOCAL PROCEDURE TypeOnAfterValidate@19069045();
    VAR
      lvInvPropTemplLineRec@1210190000 : Record 11012917;
    BEGIN
      IF (Totaling = '') AND ("Rubric Code" <> '') THEN BEGIN
        CASE Type OF
          Type::Subtotal:
            BEGIN
              lvInvPropTemplLineRec.SETRANGE(Template, Template);
              lvInvPropTemplLineRec.SETFILTER("Rubric Code", '<%1', "Rubric Code");
              lvInvPropTemplLineRec.SETRANGE(Type, Type::Title);
              IF lvInvPropTemplLineRec.FIND('+') THEN
                Totaling := lvInvPropTemplLineRec."Rubric Code" + '..' + "Rubric Code";
            END;
          Type::Balance:
            Totaling := '..' + "Rubric Code";
        END;
      END;
    END;

    PROCEDURE SetFieldEditable@1100409000();
    BEGIN
      DescriptionEditable := (Type <> Type::Line);
      BudgetDistributionEditable := (Type IN [Type::Costs, Type::Revenues, Type::"Costs to Distr. as Perc. Rev."]);
      TotalingEditable := (Type IN [Type::Subtotal, Type::Balance, Type::"Perc. (Sub)Totals"]);  //*15625.c
      CostObjectFilterEditable := (Type IN [Type::Costs, Type::Revenues, Type::"Percentage Costs", Type::"Percentage Revenues",
                  Type::"Costs as Perc. Revenues", Type::"Revenues as Perc. Costs",
                  Type::"Costs to Distr. as Perc. Rev."]);

      PercentageEditable :=  (Type IN [Type::"Percentage Costs", Type::"Percentage Revenues",
         Type::"Costs as Perc. Revenues", Type::"Revenues as Perc. Costs",
         Type::"Costs to Distr. as Perc. Rev."]);

      PercentageFilterEditable := (Type IN [Type::"Percentage Costs", Type::"Percentage Revenues",
         Type::"Costs as Perc. Revenues", Type::"Revenues as Perc. Costs",
         Type::"Costs to Distr. as Perc. Rev.",
         Type::"Perc. (Sub)Totals"]);  //*15625.n

      TotTypeForPercSubTotalsEdita := (Type = Type::"Perc. (Sub)Totals");  //*15625.n
    END;

    BEGIN
    END.
  }
}

