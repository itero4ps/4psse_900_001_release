OBJECT Page 11020241 Hour Accounting Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    Permissions=TableData 11012600=r;
    CaptionML=ENU=Hour Accounting Project;
    SourceTable=Table11020224;
    PageType=Document;
    OnInit=BEGIN
             WeekEditable := TRUE;
             YearEditable := TRUE;
             ProjectNoEditable := TRUE;
             DocumentNoEditable := TRUE;
           END;

    OnOpenPage=VAR
                 lvUarHrsSetupRec@1100485000 : Record 11012600;
               BEGIN
                 //**4PS Spoor.sn
                 IF lvUarHrsSetupRec.READPERMISSION THEN BEGIN
                   IF lvUarHrsSetupRec.GET THEN BEGIN
                     IF lvUarHrsSetupRec."Hour Acc. Rail CLA" THEN
                       ERROR(Text002);
                   END;
                 END;
                 //**4PS Spoor.en

                 ProjectSetup.GET;

                 FILTERGROUP(2);
                 IF EVALUATE(gCalledFromSiteMgtForm, GETFILTER("Called From Site Mgt Form")) THEN;
                 FILTERGROUP(0);

                 //**4PS //kzwerver, 110609, sn, #RfC UKC
                 IF gCalledFromSiteMgtForm THEN BEGIN
                   SETRANGE(Year);
                   SETRANGE(Week);
                   SETRANGE("Project No.");
                   YearEditable := FALSE;
                   WeekEditable := FALSE;
                   ProjectNoEditable := FALSE;
                 END;
                 //**4PS //kzwerver, 110609, en, #RfC UKC
                 DocumentNoEditable := ProjectSetup."Soft Norm Check" AND
                   (ProjectSetup."Hour Document No." = ProjectSetup."Hour Document No."::Month);
               END;

    OnNewRecord=BEGIN

                  Year := xRec.Year;
                  IF Year = 0 THEN
                    Year := DATE2DMY(TODAY,3);
                  IF (xRec.Week > 0) THEN //kzwerver, 110609, n, #RfC ULC
                    Week := xRec.Week;
                END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210190000;1 ;ActionGroup;
                      CaptionML=ENU=F&unctions }
      { 1210190001;2 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=Explode Hour Set;
                      Image=ExplodeBOM;
                      OnAction=VAR
                                 lHourProjLine@11012001 : Record 11020225;
                                 lHourProjLine2@1210190001 : Record 11020225;
                                 lHourSetLine@1210190000 : Record 11020228;
                                 LineSpacing@11012004 : Integer;
                                 NextLineNo@11012005 : Integer;
                               BEGIN
                                 IF NOT CONFIRM(Text003, TRUE, Year, Week, "Project No.") THEN
                                   EXIT;

                                 lHourProjLine.RESET;
                                 lHourProjLine.SETRANGE(Year,Year);
                                 lHourProjLine.SETRANGE(Week,Week);
                                 lHourProjLine.SETRANGE("Header Project No.","Project No.");
                                 lHourProjLine.SETFILTER("Hour Set",'<>%1', '');

                                 IF lHourProjLine.FIND('-') THEN
                                   REPEAT
                                     lHourSetLine.SETRANGE("Hour Set", lHourProjLine."Hour Set");
                                     IF lHourSetLine.FIND('-') THEN BEGIN

                                       //Determine Line Spacing
                                       lHourProjLine2.COPY(lHourProjLine);
                                       lHourProjLine2.SETRANGE("Hour Set");
                                       IF lHourProjLine2.FIND('>') THEN BEGIN
                                         LineSpacing := (lHourProjLine2."Line No." - lHourProjLine."Line No.") DIV (lHourSetLine.COUNT);
                                         IF LineSpacing = 0 THEN
                                           ERROR(Text001);
                                       END ELSE
                                         LineSpacing := 10000;

                                       //Explode;
                                       NextLineNo := lHourProjLine."Line No.";
                                       REPEAT
                                         lHourProjLine2 := lHourProjLine;
                                         lHourProjLine2."Line No." := NextLineNo;
                                         lHourProjLine2."Hour Set":= '';
                                         lHourProjLine2.VALIDATE("Supplying Company", lHourSetLine."Supplying Company");
                                         lHourProjLine2.VALIDATE("Employee No.", lHourSetLine."Employee No.");
                                         IF lHourSetLine.Element <> '' THEN
                                           lHourProjLine2.Element := lHourSetLine.Element;
                                         IF lHourSetLine."Wage Component" <> '' THEN
                                           lHourProjLine2.VALIDATE("Wage Component",lHourSetLine."Wage Component");
                                         IF lHourSetLine."Cost Object" <> '' THEN
                                           lHourProjLine2.VALIDATE("Cost Object",lHourSetLine."Cost Object");
                                         IF lHourSetLine.Comment <> '' THEN
                                           lHourProjLine2.Comment := lHourSetLine.Comment;
                                         IF lHourSetLine."Cost Component" <> '' THEN
                                           lHourProjLine2."Cost Component" := lHourSetLine."Cost Component";
                                         lHourProjLine2."Unit Cost (LCY)" := 0;
                                         lHourProjLine2."Amount (LCY)" := 0;
                                         lHourProjLine2.VALIDATE("Employee No.");
                                         IF NextLineNo = lHourProjLine."Line No." THEN
                                           lHourProjLine2.MODIFY(TRUE)
                                         ELSE
                                           lHourProjLine2.INSERT(TRUE);
                                         NextLineNo := NextLineNo + LineSpacing;
                                       UNTIL lHourSetLine.NEXT = 0;
                                     END;
                                   UNTIL lHourProjLine.NEXT = 0;
                               END;
                                }
      { 1100485000;2 ;Action    ;
                      CaptionML=ENU=Copy hours from &planning;
                      Image=Copy;
                      OnAction=BEGIN
                                 CurrPage.HoursForm.PAGE.GetHoursFromPlanning(Rec);
                               END;
                                }
      { 1100485001;2 ;Action    ;
                      CaptionML=ENU=Copy hours from posted &hours;
                      Promoted=Yes;
                      Image=CopyFromBOM;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CurrPage.HoursForm.PAGE.GetHoursFromPostedProjectHours(Rec);
                               END;
                                }
      { 1210190004;  ;ActionContainer;
                      ActionContainerType=Reports }
      { 1210190003;1 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=&Print;
                      Promoted=Yes;
                      Image=Print;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 lHourAccProj@1210190000 : Record 11020224;
                                 PrintDoc@1100485001 : Codeunit 229;
                               BEGIN
                                 lHourAccProj.COPY(Rec);
                                 lHourAccProj.SETRECFILTER;
                                 PrintDoc.PrintHoursProject(lHourAccProj);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 11  ;1   ;Group     ;
                CaptionML=ENU=General }

    { 16  ;2   ;Field     ;
                SourceExpr=Year;
                Editable=YearEditable }

    { 12  ;2   ;Field     ;
                SourceExpr=Week;
                Editable=WeekEditable;
                OnValidate=BEGIN
                             WeekOnAfterValidate;
                           END;
                            }

    { 14  ;2   ;Field     ;
                SourceExpr="Project No.";
                Editable=ProjectNoEditable;
                OnValidate=BEGIN
                             ProjectNoOnAfterValidate;
                           END;
                            }

    { 1210190002;2;Field  ;
                SourceExpr="Project Description" }

    { 22  ;2   ;Field     ;
                SourceExpr="Document No.";
                Editable=DocumentNoEditable;
                OnValidate=BEGIN
                             DocumentNoOnAfterValidate;
                           END;
                            }

    { 20  ;2   ;Field     ;
                SourceExpr="Posting Date";
                Editable=FALSE }

    { 10  ;1   ;Part      ;
                Name=HoursForm;
                SubPageLink=Year=FIELD(Year),
                            Week=FIELD(Week),
                            Header Project No.=FIELD(Project No.);
                PagePartID=Page11020242 }

    { 1210190008;0;Container;
                ContainerType=FactBoxArea }

    { 1100525002;1;Part   ;
                SubPageLink=Year=FIELD(Year),
                            Week=FIELD(Week),
                            Header Project No.=FIELD(Project No.);
                PagePartID=Page11130234;
                PartType=Page }

    { 1100525003;1;Part   ;
                SubPageLink=Year=FIELD(Year),
                            Week=FIELD(Week),
                            Header Project No.=FIELD(Project No.);
                PagePartID=Page11130231;
                PartType=Page }

    { 1100525004;1;Part   ;
                SubPageLink=Year=FIELD(Year),
                            Week=FIELD(Week),
                            Employee No.=FIELD(Employee No.);
                PagePartID=Page11130232;
                ProviderID=10;
                PartType=Page }

    { 1100525005;1;Part   ;
                SubPageLink=Year=FIELD(Year),
                            Week=FIELD(Week),
                            Employee No.=FIELD(Employee No.);
                PagePartID=Page11130233;
                ProviderID=10;
                PartType=Page }

    { 1100525001;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1100525000;1;Part   ;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text001@1210190000 : TextConst 'ENU=There is not enough space to explode the Hour Sets.';
      Text002@1100485000 : TextConst 'ENU=Not relevant for Rail CLA.';
      ProjectSetup@1100485001 : Record 315;
      Text003@1100485002 : TextConst 'ENU=Explode Hour Set(s) of Week %1-%2, Project ''%3''?';
      gCalledFromSiteMgtForm@1210190001 : Boolean;
      YearEditable@19059786 : Boolean INDATASET;
      WeekEditable@19075403 : Boolean INDATASET;
      DocumentNoEditable@19064841 : Boolean INDATASET;
      ProjectNoEditable@19028701 : Boolean INDATASET;

    PROCEDURE Setfilter@1100485000(lProjCde@1210190000 : Code[20]);
    BEGIN
      FILTERGROUP(2);
      SETRANGE("Project No.",lProjCde);
      ProjectNoEditable := FALSE;
      //gProjCde := lProjCde;
      FILTERGROUP(0);
    END;

    LOCAL PROCEDURE WeekOnAfterValidate@19014554();
    BEGIN
      IF "Project No." <> '' THEN
        CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE ProjectNoOnAfterValidate@19041008();
    BEGIN
      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE DocumentNoOnAfterValidate@19031352();
    BEGIN
      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

