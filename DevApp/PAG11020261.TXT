OBJECT Page 11020261 Generate documents
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Generate Documents;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table11012068;
    PageType=List;
    OnOpenPage=VAR
                 lTempDocRel@1210190000 : Record 11020230;
               BEGIN
                 FILTERGROUP(2);
                 IF gProjCde <> '' THEN BEGIN
                   SETRANGE(Source, Source::Project);
                   SETRANGE("Project No.",gProjCde);
                 END ELSE BEGIN
                   SETRANGE(Source, Source::Estimate);
                   SETRANGE("Estimate No.",gEstCde);
                 END;
                 FILTERGROUP(0);

                 SETRANGE("Purchase Action",gPurchActCde);
                 lTempDocRel.SETRANGE("User ID",USERID);
                 lTempDocRel.DELETEALL;

                 IF DocGroupRec.COUNT = 1 THEN BEGIN
                   DocGroupRec.FINDFIRST;
                   DocGroup := DocGroupRec.Code;
                 END;
               END;

    OnAfterGetRecord=BEGIN
                       CalcFieldsVendor();
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  OnAfterGetCurrRecord;
                END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1210190038;1 ;ActionGroup;
                      CaptionML=ENU=D&ocument }
      { 1210190039;2 ;Action    ;
                      CaptionML=ENU=Document Relations;
                      Image=Relationship;
                      OnAction=VAR
                                 DocRelFrm@1210190000 : Page 11020262;
                               BEGIN
                                 DocRelFrm.SetUseridFilter(USERID,"Project No.");
                                 DocRelFrm.RUNMODAL;
                               END;
                                }
      { 1210190018;2 ;Action    ;
                      CaptionML=ENU=Open Last Generated document;
                      Image=ViewDocumentLine;
                      OnAction=VAR
                                 lDocumentRec@1210190000 : Record 11012746;
                               BEGIN
                                 CurrPage.UPDATE;
                                 IF "Last Generated Document No." <> '' THEN
                                   IF lDocumentRec.GET("Last Generated Document No.") THEN
                                     lDocumentRec.OpenDocument;
                               END;
                                }
      { 1210190017;2 ;Action    ;
                      CaptionML=ENU=Existing Documents;
                      Image=Documents;
                      OnAction=VAR
                                 ProjectPurchaseAction@1100525003 : Record 11012026;
                                 RecRef@1100525001 : RecordRef;
                                 DocumentFilters@1100525002 : Codeunit 11012432;
                               BEGIN
                                 ProjectPurchaseAction.GET("Project No.", "Purchase Action");
                                 RecRef.GETTABLE(ProjectPurchaseAction);
                                 DocumentFilters.CreateTempFilterAndExecute1(RecRef, '', FALSE, FALSE);
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210190036;1 ;ActionGroup;
                      CaptionML=ENU=F&unctions }
      { 1100485000;2 ;Action    ;
                      CaptionML=ENU=Create Temporay Template;
                      Image=Template;
                      OnAction=VAR
                                 lNewTemplate@1100485001 : Boolean;
                               BEGIN
                                 IF NOT gDocTemplateRec.GET(DocGroup,DocNo) THEN
                                   ERROR(Text001)
                                 ELSE BEGIN
                                   gDocGroupRec.GET(DocGroup);
                                   IF NOT FileMgt.ClientFileExists(gDocGroupRec."Template Directory" + gDocTemplateRec."Word Template") THEN
                                     ERROR(Text002);
                                 END;

                                 lNewTemplate := TRUE;
                                 IF TempProjPurActRec.FINDFIRST THEN BEGIN
                                   IF (TempDocTemplateRec."Document Group" = gDocTemplateRec."Document Group") AND
                                      (TempDocTemplateRec."Template Code" = gDocTemplateRec."Template Code") THEN BEGIN
                                     TempProjPurActRec.CALCFIELDS("Document Fysical Template");
                                     IF TempProjPurActRec."Document Fysical Template".HASVALUE THEN
                                       lNewTemplate := FALSE
                                     ELSE
                                       TempProjPurActRec.DELETE;
                                   END ELSE BEGIN
                                     TempProjPurActRec.DELETE;
                                   END;
                                 END;

                                 IF lNewTemplate THEN BEGIN
                                   TempDocTemplateRec := gDocTemplateRec;
                                   TempProjPurActRec."Document Template" := TempDocTemplateRec."Template Code";
                                   TempProjPurActRec."Project No." := "Project No.";
                                   TempProjPurActRec.Code := "Purchase Action";
                                   TempProjPurActRec.INSERT;
                                   TempProjPurActRec.ImportDocTemplate(gDocGroupRec."Template Directory" + TempDocTemplateRec."Word Template");
                                   TempProjPurActRec.MODIFY;
                                 END;

                                 TempProjPurActRec.OpenDocTemplate(gDocGroupRec."Template Directory")
                               END;
                                }
      { 1100485001;2 ;Action    ;
                      CaptionML=ENU=Remove Temporary Template;
                      Image=Delete;
                      OnAction=BEGIN
                                 IF TempProjPurActRec.FINDFIRST THEN
                                   TempProjPurActRec.DELETE;
                               END;
                                }
      { 1210190037;2 ;Action    ;
                      CaptionML=ENU=Generate Documents;
                      Image=Word;
                      OnAction=VAR
                                 VendorsbyPurchaseAction@1100409001 : Record 11012068;
                                 Job@1100528300 : Record 11072003;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(VendorsbyPurchaseAction);
                                 IF NOT VendorsbyPurchaseAction.FINDSET THEN BEGIN
                                   MESSAGE(Text005);
                                 END ELSE BEGIN
                                   IF DocNo = '' THEN BEGIN
                                     ERROR(Text006);
                                   END;

                                   Job.GET(VendorsbyPurchaseAction."Project No.");
                                   IF (Job."Project Portal Project") THEN
                                     gvOptionNo := STRMENU(Text003, 2)
                                   ELSE
                                     gvOptionNo := STRMENU(Text003, 3);
                                   IF (gvOptionNo < 1) THEN BEGIN
                                     ERROR('');
                                   END;

                                   REPEAT
                                     IF (VendorsbyPurchaseAction.Type <> VendorsbyPurchaseAction.Type::Contact) THEN BEGIN
                                       ContactCde := GetContact(VendorsbyPurchaseAction."Vendor No.");
                                       IF ContactCde = '' THEN
                                         MESSAGE(Text007,VendorsbyPurchaseAction."Vendor No.");
                                     END ELSE BEGIN
                                       ContactCde := VendorsbyPurchaseAction."Vendor No.";
                                       IF ContactCde = '' THEN
                                         MESSAGE(Text010);
                                     END;
                                     IF ContactCde <> '' THEN
                                       CreateWordDoc(VendorsbyPurchaseAction);
                                   UNTIL VendorsbyPurchaseAction.NEXT = 0;
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210190031;1;Group  ;
                CaptionML=ENU=Document fields }

    { 1210190034;2;Field  ;
                CaptionML=ENU=Document Group;
                SourceExpr=DocGroup;
                OnValidate=BEGIN
                             ValidateDocumentGroup;
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           DocGroupFrm@1210190000 : Page 11012400;
                         BEGIN
                           CLEAR(DocGroupFrm);
                           DocGroupFrm.LOOKUPMODE(TRUE);
                           IF DocGroupFrm.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             DocGroupFrm.GETRECORD(DocGroupRec);
                             DocGroup := DocGroupRec.Code;
                             ValidateDocumentGroup;
                             CurrPage.UPDATE(FALSE);
                           END;
                         END;
                          }

    { 1210190026;2;Field  ;
                CaptionML=ENU=Document Template;
                SourceExpr=DocNo;
                OnValidate=VAR
                             lvDocPermissRec@1100485001 : Record 11012404;
                           BEGIN
                             IF DocNo = '' THEN
                               EXIT;

                             lvDocPermissRec.RESET;
                             lvDocPermissRec.SETRANGE("Document Group",DocGroup);
                             lvDocPermissRec.SETRANGE("Template Code",DocNo);
                             lvDocPermissRec.SETRANGE("User ID");
                             IF lvDocPermissRec.FIND('-') THEN BEGIN
                               lvDocPermissRec.SETRANGE("User ID",USERID);
                               IF NOT lvDocPermissRec.FIND('-') THEN
                                 ERROR(Text009);
                             END;

                             IF NOT TemplateExists THEN
                               ERROR(Text011);

                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           lvTemplateRec@1100485002 : Record 11012401;
                           TempTemplateRec@1100485005 : TEMPORARY Record 11012401;
                           lvAddTemplate@1100485003 : Boolean;
                           lvDocPermissRec@1100485004 : Record 11012404;
                         BEGIN
                           //CALL 11242: Lookup aangepast code gecopieerd vanuit tabel 11012403

                           lvTemplateRec.SETRANGE("Document Group",DocGroup);
                           lvTemplateRec.SETRANGE(Type,lvTemplateRec.Type::Miscellaneous);

                           IF lvTemplateRec.FINDFIRST THEN BEGIN
                             REPEAT
                               lvAddTemplate := FALSE;
                               lvDocPermissRec.SETRANGE("Document Group", lvTemplateRec."Document Group");
                               lvDocPermissRec.SETRANGE("Template Code", lvTemplateRec."Template Code");
                               lvDocPermissRec.SETRANGE("User ID");
                               IF NOT lvDocPermissRec.FINDFIRST THEN BEGIN
                                 //no permissions defined for any user, this means all have permissions
                                 lvAddTemplate := TRUE;
                               END ELSE BEGIN
                                 //there are permissions defined, check whether they are defined for the current user
                                 lvDocPermissRec.SETRANGE("User ID",USERID);
                                 IF lvDocPermissRec.FINDFIRST THEN
                                   lvAddTemplate := TRUE;
                               END;
                               IF lvAddTemplate THEN BEGIN
                                 TempTemplateRec.TRANSFERFIELDS(lvTemplateRec, TRUE);
                                 TempTemplateRec.INSERT;
                               END;
                             UNTIL lvTemplateRec.NEXT = 0;
                           END;

                           IF PAGE.RUNMODAL(PAGE::"Document Template List", TempTemplateRec) = ACTION::LookupOK THEN BEGIN
                             DocNo := TempTemplateRec."Template Code";
                             CurrPage.UPDATE(FALSE);
                           END;
                         END;
                          }

    { 1210190027;2;Field  ;
                CaptionML=ENU=Description;
                SourceExpr=DescTxt;
                OnValidate=BEGIN
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1210190028;2;Field  ;
                CaptionML=ENU=Subject;
                SourceExpr=SubjectTxt;
                OnValidate=BEGIN
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1210190029;2;Field  ;
                CaptionML=ENU=Sender;
                SourceExpr=SenderCde;
                OnValidate=VAR
                             EmplRec@1210190000 : Record 5200;
                             DocRec@1100485001 : Record 11012746;
                           BEGIN
                             EmplRec.GET(SenderCde);
                             NameTxt := EmplRec."Full Name";
                             DocRec.VALIDATE(Sender,EmplRec."No.");
                             DocRec.FillReference;
                             ReferenceTxt := DocRec.Reference;
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           EmplFrm@1210190000 : Page 5201;
                           EmplRec@1210190001 : Record 5200;
                           DocRec@1100485002 : Record 11012746;
                         BEGIN
                           CLEAR(EmplFrm);
                           EmplFrm.LOOKUPMODE(TRUE);
                           IF EmplFrm.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             EmplFrm.GETRECORD(EmplRec);
                             SenderCde := EmplRec."No.";
                             NameTxt := EmplRec."Full Name";
                             DocRec.VALIDATE(Sender,EmplRec."No.");
                             DocRec.FillReference;
                             ReferenceTxt := DocRec.Reference;
                             CurrPage.UPDATE(FALSE);
                           END;
                         END;
                          }

    { 1210190035;2;Field  ;
                CaptionClass=FORMAT (NameTxt);
                Editable=FALSE }

    { 1210190030;2;Field  ;
                CaptionML=ENU=Reference;
                SourceExpr=ReferenceTxt;
                OnValidate=BEGIN
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1210190000;1;Group  ;
                GroupType=Repeater }

    { 1210190001;2;Field  ;
                SourceExpr="Purchase Action";
                Editable=FALSE }

    { 1100485002;2;Field  ;
                SourceExpr=Type }

    { 1210190003;2;Field  ;
                SourceExpr="Vendor No.";
                Editable=FALSE }

    { 1210190005;2;Field  ;
                DrillDown=No;
                SourceExpr="Vendor Name";
                Editable=FALSE }

    { 1210190007;2;Field  ;
                DrillDown=No;
                SourceExpr=Address;
                Editable=FALSE }

    { 1210190009;2;Field  ;
                DrillDown=No;
                SourceExpr=City;
                Editable=FALSE }

    { 1210190011;2;Field  ;
                DrillDown=No;
                SourceExpr="Phone No.";
                Editable=FALSE }

    { 1210190013;2;Field  ;
                DrillDown=No;
                SourceExpr="Post Code";
                Editable=FALSE }

    { 1210190015;2;Field  ;
                SourceExpr=Contact;
                Editable=FALSE }

    { 1100528300;2;Field  ;
                SourceExpr="Last Generated Document No." }

  }
  CODE
  {
    VAR
      gDocGroupRec@1100485011 : Record 11012400;
      gDocTemplateRec@1100485005 : Record 11012401;
      TempDocTemplateRec@1100485004 : TEMPORARY Record 11012401;
      TempProjPurActRec@1100485010 : TEMPORARY Record 11012026;
      DocGroupRec@1100485012 : Record 11012400;
      FileMgt@1100525001 : Codeunit 419;
      gProjCde@1100485007 : Code[20];
      gEstCde@1210190002 : Code[20];
      gPurchActCde@1210190001 : Code[20];
      DocNo@1210190007 : Code[20];
      SenderCde@1100485001 : Code[20];
      ContactCde@1100485003 : Code[20];
      DocGroup@1100485002 : Code[20];
      DescTxt@1210190006 : Text[50];
      SubjectTxt@1210190005 : Text[50];
      ReferenceTxt@1210190003 : Text[30];
      NameTxt@1210190013 : Text[100];
      gvOptionNo@1210190017 : Integer;
      Text001@1100528710 : TextConst 'ENU=Select first document group en doument template.';
      Text002@1100528709 : TextConst 'ENU=No worddocument linked to the selected template';
      Text003@1100528708 : TextConst 'ENU=Documents,Documents + Word documents,Documents + Print Word Documents,Documents + Print Word Documents with printer selection';
      Text005@1100528707 : TextConst 'ENU="First Select Records "';
      Text006@1100528706 : TextConst 'ENU=First enter document template.';
      Text007@1100528705 : TextConst 'ENU=Vendor %1 is not related to Contact';
      Text008@1100528704 : TextConst 'ENU="Printing Word document. Note: Possible the printer selection form is in the background. Then enter ''Alt+Tab. "';
      Text009@1100528703 : TextConst 'ENU=No permission for document template.';
      Text010@1100528702 : TextConst 'ENU=Contact must be filled.';
      Text011@1100528701 : TextConst 'ENU=Non existing document template.';
      Text012@1100528700 : TextConst 'ENU=Contact %1 - %2 is blocked. No Document is made.';

    PROCEDURE SetFilter@1210190000(lProjCde@1210190000 : Code[20];lEstCde@1100485000 : Code[20];lPurchActCde@1210190001 : Code[20]);
    BEGIN
      gProjCde := lProjCde;
      gEstCde := lEstCde;
      gPurchActCde := lPurchActCde;
    END;

    PROCEDURE GetContact@1210190002(lVendorCde@1210190001 : Code[20]) RelationCde : Code[20];
    VAR
      ContBusRelRec@1210190000 : Record 5054;
    BEGIN
      ContBusRelRec.SETCURRENTKEY("Link to Table","No.");
      ContBusRelRec.SETRANGE("No.",lVendorCde);
      ContBusRelRec.SETRANGE("Link to Table",ContBusRelRec."Link to Table"::Vendor);
      IF ContBusRelRec.FIND('-') THEN
        EXIT(ContBusRelRec."Contact No.")
      ELSE
        EXIT('');
    END;

    PROCEDURE CreateWordDoc@1210190007(VendorsbyPurchaseAction@1100409000 : Record 11012068);
    VAR
      lrecdocument@1210190000 : Record 11012746;
      DocumentLink@1100525000 : Record 11012747;
      lPurchAct@1210190001 : Record 11012026;
      lDocGrouprec@1100485001 : Record 11012400;
      lRecDocTemplate@1210190003 : Record 11012401;
      lTempDocRel@1210190004 : Record 11020230;
      lDocRelation@1210190005 : Record 11012407;
      Job@1100525001 : Record 11072003;
      DocumentLinkMgt@1100525004 : Codeunit 11012401;
      lCduWordInterface@1100485000 : Codeunit 11012406;
      lFileName@1100485002 : Text[250];
      ShortFileName@1100525005 : Text[250];
      RecRef@1100525002 : RecordRef;
      DocLinkRID@1100525003 : ARRAY [5] OF RecordID;
    BEGIN
      //**4PS.sn
      IF ContactBlocked(ContactCde) THEN BEGIN
        MESSAGE(Text012, ContactCde);
        EXIT;
      END;
      //**4PS.en

      IF lPurchAct.GET(VendorsbyPurchaseAction."Project No.",
        VendorsbyPurchaseAction."Purchase Action") THEN
      BEGIN
        RecRef.GETTABLE(lPurchAct);
        DocLinkRID[1] := RecRef.RECORDID;
      END;

      IF Job.GET(VendorsbyPurchaseAction."Project No.") THEN BEGIN
        RecRef.GETTABLE(Job);
        DocLinkRID[2] := RecRef.RECORDID;
      END;

      lrecdocument.INIT;
      lrecdocument.INSERT(TRUE);
      lrecdocument.VALIDATE("Contact No.", ContactCde);
      lrecdocument.VALIDATE("Contact Person No.", VendorsbyPurchaseAction."Contact Person No.");
      lrecdocument.VALIDATE("Document Group", DocGroup);
      lrecdocument.VALIDATE("Document Template", DocNo);
      lrecdocument.Description := DescTxt;
      lrecdocument.VALIDATE(Sender, SenderCde);
      lrecdocument.Subject := SubjectTxt;
      IF ReferenceTxt <> '' THEN
        lrecdocument.Reference := ReferenceTxt;
      lPurchAct.GET(VendorsbyPurchaseAction."Project No.",VendorsbyPurchaseAction."Purchase Action");
      lrecdocument.MODIFY;

      // Create Document Links
      DocumentLinkMgt.CreateDocumentLink(lrecdocument, DocLinkRID);

      VendorsbyPurchaseAction."Last Generated Document No." := lrecdocument."No.";
      VendorsbyPurchaseAction.MODIFY;

      lTempDocRel.SETRANGE("User ID",USERID);
      lTempDocRel.SETRANGE("Project No.",VendorsbyPurchaseAction."Project No.");
      IF lTempDocRel.FIND('-') THEN BEGIN
        REPEAT
          lDocRelation.INIT;
          lDocRelation."Document Type" := lDocRelation."Document Type"::Document;
          lDocRelation."No." := lrecdocument."No.";
          lDocRelation."Related Document No."  :=  lTempDocRel."Related Document No.";
          lDocRelation.Description := lTempDocRel.Description;
          lDocRelation."Description 2" := lTempDocRel."Description 2";
          lDocRelation."Send by E-Mail" := FALSE;
          lDocRelation.Print := FALSE;
          lDocRelation.INSERT;
        UNTIL lTempDocRel.NEXT = 0;
      END;

      COMMIT;  //Call C-009699 - Nummerreeks-tabel blijft anders te lang gelockt.

      CLEAR(lCduWordInterface);
      CLEAR(lRecDocTemplate);
      lFileName := '';
      IF lRecDocTemplate.GET(DocGroup,DocNo) THEN BEGIN
        //lRecDocTemplate."No." := lrecdocument."No.";
        //lRecDocTemplate.Prospect := lrecdocument."Contact No.";
        lRecDocTemplate.Type := lRecDocTemplate.Type::Miscellaneous;
        lRecDocTemplate."Secured Document" := lRecDocTemplate."Secured Document"::"Not";
        IF TempProjPurActRec.FINDFIRST THEN BEGIN
          lDocGrouprec.GET(DocGroup);
          TempProjPurActRec.CALCFIELDS("Document Fysical Template");
          IF TempProjPurActRec."Document Fysical Template".HASVALUE THEN BEGIN
            TempProjPurActRec.GetUniqueFilename(
              lDocGrouprec."Template Directory",
              lFileName,
              ShortFileName);
            TempProjPurActRec.ExportDocTemplate(lFileName);

            IF NOT FileMgt.ClientFileExists(lFileName) THEN
              lFileName := '';
          END;
        END;
        IF lFileName <> '' THEN BEGIN
          lRecDocTemplate."Word Template" := ShortFileName;
          lCduWordInterface.GenerateDoc(
                  lRecDocTemplate,
                  DocLinkRID,
                  '',
                  lrecdocument."No.",
                  lrecdocument."Contact No.",
                  lrecdocument."Contact Person No.",
                  '',
                  FALSE,
                  FALSE);

          TempProjPurActRec.DeleteFile(lFileName);
        END ELSE
          lCduWordInterface.GenerateDoc(lRecDocTemplate,
                                        DocLinkRID,
                                        '',
                                        lrecdocument."No.",
                                        lrecdocument."Contact No.",
                                        lrecdocument."Contact Person No.",
                                        '',
                                        FALSE, FALSE);

      END;
      lrecdocument.GET(lrecdocument."No.");
      IF lFileName <> '' THEN BEGIN
        PrintWordDoc(lrecdocument);
        TempProjPurActRec.DeleteFile(lFileName);
      END;
    END;

    PROCEDURE PrintWordDoc@1210190009(lrecdocument@1210190000 : Record 11012746);
    VAR
      lCduWord@1210190002 : Codeunit 11012400;
      Window@1210190001 : Dialog;
      lrecSavedDocument@1210190003 : Record 11012746;
    BEGIN
      lrecSavedDocument.GET(lrecdocument."No.");
      IF gvOptionNo >= 2 THEN BEGIN
        lCduWord.Initialise();
        lCduWord.Open(FALSE,lrecSavedDocument.File, FALSE);
        lCduWord.Save();
        CASE gvOptionNo OF
          2:BEGIN
              lCduWord.Close();
            END;
          3:BEGIN
              lCduWord.Print();
              lCduWord.Save();
              lCduWord.Close();
            END;
          4:BEGIN
              lCduWord.PrintWithPrinterSelection();
              Window.OPEN(Text008);
              lCduWord.Save();
              lCduWord.Close();
              SLEEP(5000);
              Window.CLOSE;
            END;
         END;
      END;
    END;

    PROCEDURE ContactBlocked@1100525000(ContactNo@1100525000 : Code[20]) : Boolean;
    VAR
      Contact@1100525001 : Record 5050;
    BEGIN
      //**4PS.n
      Contact.GET(ContactNo);
      EXIT(Contact.Blocked);
    END;

    PROCEDURE ValidateDocumentGroup@1100528701();
    BEGIN
      DocGroupRec.GET(DocGroup);

      IF NOT TemplateExists THEN
        DocNo := '';
    END;

    PROCEDURE TemplateExists@1100528700() : Boolean;
    VAR
      DocumentTemplate@1100528700 : Record 11012401;
    BEGIN
      DocumentTemplate.SETRANGE("Document Group", DocGroup);
      DocumentTemplate.SETRANGE("Template Code", DocNo);
      DocumentTemplate.SETRANGE(Type, DocumentTemplate.Type::Miscellaneous);
      EXIT(DocumentTemplate.FINDFIRST);
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      CalcFieldsVendor();
    END;

    BEGIN
    END.
  }
}

