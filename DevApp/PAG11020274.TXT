OBJECT Page 11020274 Project Financial Status
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Project Financial Status;
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table11072003;
    SourceTableView=SORTING(Main Project)
                    ORDER(Ascending)
                    WHERE(Single/Main/Sub Project=FILTER(Single|Main Project));
    PageType=Card;
    PromotedActionCategoriesML=ENU=New,Process,Report,Projects;
    OnOpenPage=BEGIN
                 //**4PS Generated Code. Do Not Change.sn01
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS Generated Code.en01

                 //Set range based on permission employee
                 Permissions.CostControlReports(ProjectRec);
                 Rec.COPYFILTERS(ProjectRec);

                 SETFILTER("Single/Main/Sub Project", '%1|%2',
                           "Single/Main/Sub Project"::Single, "Single/Main/Sub Project"::"Main Project" );

                 SearchPeriodCu.FindPeriod(Rec,PeriodType,'');
               END;

    OnAfterGetRecord=BEGIN
                       DetermineMainProjectAmounts;
                     END;

    OnNewRecord=BEGIN
                  //**4PS Generated Code. Do Not Change.sn02
                  DPA_SetDefaultDepartment;
                  //**4PS Generated Code.en02
                END;

    OnInsertRecord=VAR
                     xRecRef@1100000999 : RecordRef;
                   BEGIN
                     //**4PS Generated Code. Do Not Change.sn05
                     xRecRef.GETTABLE(xRec);
                     DPA_SetDefaultDpmtFromNoSeries(xRecRef);
                     //**4PS Generated Code.en05
                   END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1100485043;1 ;ActionGroup;
                      CaptionML=ENU=&Project }
      { 1100485045;2 ;Action    ;
                      CaptionML=ENU=Co&mments;
                      RunObject=Page 124;
                      RunPageLink=Table Name=CONST(Job),
                                  No.=FIELD(No.);
                      Image=ViewComments }
      { 1100485003;1 ;ActionGroup;
                      CaptionML=ENU=&Control }
      { 1100485004;2 ;Action    ;
                      CaptionML=ENU=Sub Projects;
                      Promoted=Yes;
                      Image=Splitlines;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 ProjRec@1100485000 : Record 11072003;
                                 ProjControlSmallPFrm@1100485002 : Page 11020275;
                                 ProjControlSmallPPage@1210190001 : Page 11020275;
                               BEGIN
                                 ProjRec.SETCURRENTKEY("Main Project");
                                 ProjRec.SETRANGE("Main Project","No.");
                                 ProjRec.SETFILTER("Period Filter",GETFILTER("Period Filter"));
                                 ProjRec.SETFILTER("Cumulation Filter",GETFILTER("Cumulation Filter"));
                                 ProjRec.SETFILTER("Period Type Filter",GETFILTER("Period Type Filter"));
                                 IF SubProjectStatus <> 0 THEN
                                   ProjRec.SETRANGE("Project Status",SubProjectStatus - 1)
                                 ELSE
                                   ProjRec.SETRANGE("Project Status");
                                 IF SubSmallProjectStatus <> 0 THEN
                                   ProjRec.SETRANGE("Small Project Status",SubSmallProjectStatus - 1)
                                 ELSE
                                   ProjRec.SETRANGE("Small Project Status");

                                 //ProjControlSmallPFrm.SETTABLEVIEW(ProjRec);
                                 //ProjControlSmallPFrm.RUNMODAL;

                                 ProjControlSmallPPage.SETTABLEVIEW(ProjRec);
                                 ProjControlSmallPPage.RUNMODAL;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100485007;1 ;Action    ;
                      CaptionML=ENU=Previous Period;
                      ToolTipML=ENU=Previous Period;
                      Promoted=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 SearchPeriodCu.FindPeriod(Rec,PeriodType,'<=');
                               END;
                                }
      { 1100485006;1 ;Action    ;
                      CaptionML=ENU=Next Period;
                      ToolTipML=ENU=Next Period;
                      Promoted=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 SearchPeriodCu.FindPeriod(Rec,PeriodType,'>=');
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1902923501;1;Group  ;
                CaptionML=ENU=Filter;
                GroupType=Group }

    { 1100485018;2;Field  ;
                CaptionML=ENU=View by;
                ToolTipML=ENU=Day;
                OptionCaptionML=ENU=Day,Week,Month,Quarter,Year,Accounting Period;
                SourceExpr=PeriodType;
                OnValidate=BEGIN
                             PeriodTypeOnValidate;
                           END;
                            }

    { 1100485019;2;Field  ;
                CaptionML=ENU=Cumulation Filter;
                SourceExpr=GETFILTER("Cumulation Filter");
                OnValidate=BEGIN
                             IF STRLEN(PeriodFilter) > 3 THEN
                               PeriodFilter := '';
                             SearchPeriodCu.FindPeriod(Rec,PeriodType,PeriodFilter);
                           END;
                            }

    { 1100485000;1;Group  ;
                CaptionML=ENU=General;
                GroupType=Group }

    { 1100485001;2;Field  ;
                SourceExpr="No.";
                Editable=FALSE }

    { 1100485005;2;Field  ;
                SourceExpr=Description;
                Editable=FALSE }

    { 1100485009;2;Field  ;
                SourceExpr="Bill-to Customer No.";
                Editable=FALSE }

    { 1100485021;2;Field  ;
                SourceExpr="Principal Name";
                Editable=FALSE }

    { 1100485046;2;Field  ;
                SourceExpr="Single/Main/Sub Project";
                Editable=FALSE }

    { 1100485011;2;Field  ;
                SourceExpr="Project Status";
                Editable=FALSE }

    { 1100485022;2;Field  ;
                CaptionML=ENU=Subproject Status Filter;
                OptionCaptionML=ENU=" ,Estimation,Preparation,Production,Technical Finished,Administrative Finished,Finished,Archive,Archived";
                SourceExpr=SubProjectStatus;
                OnValidate=BEGIN
                             SubProjectStatusOnAfterValidat;
                           END;
                            }

    { 1100485049;2;Field  ;
                CaptionML=ENU=Subproject small project status filter;
                OptionCaptionML=ENU=" ,Not Printed,Printed,Finished,Concept,Invoice,Cancelled";
                SourceExpr=SubSmallProjectStatus;
                OnValidate=BEGIN
                             SubSmallProjectStatusOnAfterVa;
                           END;
                            }

    { 1210190001;1;Group  ;
                CaptionML=ENU=Financial Status;
                GroupType=Group }

    { 1100485025;2;Group  ;
                CaptionML=ENU=Posted;
                Editable=FALSE }

    { 1100485023;3;Field  ;
                CaptionML=ENU=Actual Cost;
                SourceExpr=gActualCosts }

    { 1100485026;3;Field  ;
                CaptionML=ENU=Revenues;
                SourceExpr=gRevenues }

    { 1100485028;3;Field  ;
                CaptionML=ENU=WIP Position;
                SourceExpr=gWIPPosition }

    { 1100485032;3;Field  ;
                CaptionML=ENU=Actual Hours;
                SourceExpr=gActualHours;
                Editable=FALSE }

    { 1100485042;2;Group  ;
                CaptionML=ENU=Commitments;
                Editable=FALSE }

    { 1100485030;3;Field  ;
                CaptionML=ENU=Outstanding (Purch.);
                SourceExpr=gOpenPurch;
                Editable=FALSE }

    { 1100485040;3;Field  ;
                CaptionML=ENU=Open Amount (Hours);
                SourceExpr=gOpenAmountHours;
                Editable=FALSE }

    { 1100485036;3;Field  ;
                CaptionML=ENU=Received/Not invoiced;
                SourceExpr=gReceivedNotInv;
                Editable=FALSE }

    { 1100485038;3;Field  ;
                CaptionML=ENU=Open Hours;
                SourceExpr=gOpenHours;
                Editable=FALSE }

    { 1100485034;3;Field  ;
                CaptionML=ENU=Received/Not invoiced hours;
                SourceExpr=gReceivedNotInvHours;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      ProjectRec@1100485002 : Record 11072003;
      SearchPeriodCu@1100485000 : Codeunit 11012013;
      Permissions@1100485014 : Codeunit 11012026;
      PeriodType@1100485001 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      gActualCosts@1100485003 : Decimal;
      gActualHours@1100485004 : Decimal;
      gRevenues@1100485011 : Decimal;
      gWIPPosition@1100485005 : Decimal;
      gOpenPurch@1100485006 : Decimal;
      gReceivedNotInv@1100485007 : Decimal;
      gReceivedNotInvHours@1100485008 : Decimal;
      gOpenAmountHours@1100485009 : Decimal;
      gOpenHours@1100485010 : Decimal;
      SubProjectStatus@1100485012 : ' ,Estimation,Preparation,Production,Technical Finished,Administrative Finished,Finished,Archive,Archived';
      SubSmallProjectStatus@1100485013 : ' ,Not Printed,Printed,Finished,Concept,Invoice,Cancelled';
      PeriodFilter@1100485017 : Text[50];
      DPA_IsHardFilter@1100000999 : Boolean;

    PROCEDURE DetermineMainProjectAmounts@1100485000() rAmount : Decimal;
    BEGIN
      gActualCosts := 0;
      gActualHours := 0;
      gRevenues := 0;
      gWIPPosition := 0;
      gOpenPurch := 0;
      gReceivedNotInv := 0;
      gReceivedNotInvHours := 0;
      gOpenAmountHours := 0;
      gOpenHours := 0;

      WITH ProjectRec DO BEGIN
        RESET;
        SETCURRENTKEY("Main Project");
        //COPYFILTERS(Rec); Geeft verkeerde cijfers op deelprojecten indien een gebruiker een filter op een hoofdproject zet.
        SETRANGE("Main Project",Rec."No.");
        SETFILTER("Period Filter",Rec.GETFILTER("Period Filter"));
        SETFILTER("Cumulation Filter",Rec.GETFILTER("Cumulation Filter"));
        SETFILTER("Period Type Filter",Rec.GETFILTER("Period Type Filter"));
        IF SubProjectStatus <> 0 THEN
          SETRANGE("Project Status",SubProjectStatus - 1)
        ELSE
          SETRANGE("Project Status");
        IF SubSmallProjectStatus <> 0 THEN
          SETRANGE("Small Project Status",SubSmallProjectStatus - 1)
        ELSE
          SETRANGE("Small Project Status");
        FILTERGROUP(2);
        SETRANGE("Single/Main/Sub Project");
        FILTERGROUP(0);
        IF FINDSET THEN
          REPEAT
            IF NOT "Cost Control Up to Date" THEN
              //Function BuildCostControl can not be used becasue transaction is not started here
              CODEUNIT.RUN(CODEUNIT::CreateControlLinesProjCostObj,ProjectRec);

            SETRANGE("Project Filter","No.");
            CALCFIELDS("Actual Costs","Actual Hours",Revenues,"Open (Purchase)","Received/Not Invoiced",
                       "Received/Not Inv. Hours","Open Amount (Hours)","Open Hours");
            gActualCosts += "Actual Costs";
            gActualHours += "Actual Hours";
            gRevenues += Revenues;
            gOpenPurch += "Open (Purchase)";
            gReceivedNotInv += "Received/Not Invoiced";
            gReceivedNotInvHours += "Received/Not Inv. Hours";
            gOpenAmountHours += "Open Amount (Hours)";
            gOpenHours += "Open Hours";
          UNTIL NEXT = 0;
        gWIPPosition := gActualCosts - gRevenues;
      END;
    END;

    LOCAL PROCEDURE SubProjectStatusOnAfterValidat@19051943();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE SubSmallProjectStatusOnAfterVa@19017655();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE PeriodTypeOnValidate@1210190000();
    BEGIN
      SearchPeriodCu.FindPeriod(Rec,PeriodType,'');
    END;

    BEGIN
    END.
  }
}

