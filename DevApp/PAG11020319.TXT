OBJECT Page 11020319 Hour Accounting on Time
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Hour Accounting on time;
    SourceTable=Table11012038;
    PageType=Document;
    OnInit=BEGIN
             IAmAControllerEditable := TRUE;
             EmployeeNoEditable := TRUE;
             IAmAControllerVisible := TRUE;
             PostingEnabled := TRUE;
             DocumentNoEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 //**4PS Generated Code. Do Not Change.sn01
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS Generated Code.en01

                 ProjectSetup.GET;
                 IF UserSetup.GET(USERID) THEN BEGIN
                   IF UserSetup."Access own Hours only" THEN BEGIN
                     UserSetup.TESTFIELD("Employee No.");
                     FILTERGROUP(2);
                     SETRANGE("Employee No.",UserSetup."Employee No.");
                     FILTERGROUP(0);
                     EmployeeNoEditable := FALSE;
                     PostingEnabled := (UserSetup."Posting Hours allowed");
                   END ELSE
                     IF UserSetup."Employee No." <> '' THEN BEGIN
                       HourControllerRec.SETCURRENTKEY("Controller No.");
                       HourControllerRec.SETRANGE("Controller No.", UserSetup."Employee No.");
                       IamController := NOT HourControllerRec.ISEMPTY;
                     END;
                 END;

                 IAmAControllerVisible := (IamController);
                 IAmAControllerEditable := (IamController);
                 IF (IamController) THEN
                   ShowMyEmployeesOnly := TRUE;

                 DocumentNoEditable :=
                   ProjectSetup."Soft Norm Check" AND
                   (ProjectSetup."Hour Document No." = ProjectSetup."Hour Document No."::Month);
               END;

    OnFindRecord=BEGIN
                   EXIT(FindRec(ShowMyEmployeesOnly,Rec,Which,UserSetup."Employee No."));
                 END;

    OnNextRecord=BEGIN
                   EXIT(GetRec(ShowMyEmployeesOnly,Rec,Steps,UserSetup."Employee No."));
                 END;

    OnAfterGetRecord=BEGIN

                       CALCFIELDS("Full Name");
                       EmplDescrOtherCompany;
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  //**4PS Generated Code. Do Not Change.sn02
                  DPA_SetDefaultDepartment;
                  //**4PS Generated Code.en02
                  Year := xRec.Year;
                  IF Year = 0 THEN
                    Year := DATE2DMY(TODAY,3);

                  IF UserSetup."Access own Hours only" THEN BEGIN
                    "Employee No." := UserSetup."Employee No.";
                    CALCFIELDS("First Name","Last Name","Full Name");
                  END ELSE
                    Week := xRec.Week;
                  OnAfterGetCurrRecord;
                END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 24      ;1   ;ActionGroup;
                      Name=ButtonHours;
                      CaptionML=ENU=&Hours Accounting }
      { 1100485003;2 ;Action    ;
                      CaptionML=ENU=Leave Overview;
                      Image=AbsenceCalendar;
                      OnAction=VAR
                                 lEmplRec@1100485001 : Record 5200;
                               BEGIN
                                 IF UserSetup.GET(USERID) THEN
                                   IF UserSetup."Access own Hours only" THEN BEGIN
                                     lEmplRec.FILTERGROUP(10);
                                     lEmplRec.SETRANGE("No.", "Employee No.");
                                     lEmplRec.FILTERGROUP(0);
                                   END;

                                 lEmplRec.SETRANGE("No.", "Employee No.");
                                 lEmplRec."No." := "Employee No.";
                                 PAGE.RUNMODAL(PAGE::"Leave Overview",lEmplRec);
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100485007;1 ;ActionGroup;
                      CaptionML=ENU=F&unctions }
      { 1100485006;2 ;Action    ;
                      CaptionML=ENU=Copy hours from posted &hours;
                      Image=Copy;
                      OnAction=BEGIN
                                 CurrPage.HoursForm.PAGE.GetHoursFromPostedHours(Rec);
                               END;
                                }
      { 1100485004;2 ;Action    ;
                      CaptionML=ENU=Import &Site Management Hours;
                      RunObject=XMLport 11012425;
                      Image=Import }
      { 1100485015;2 ;Action    ;
                      CaptionML=ENU=&Log Modification;
                      Image=Log;
                      OnAction=VAR
                                 HoursLogEntrRec@1100485000 : Record 11072024;
                               BEGIN
                                 HoursLogEntrRec.SETRANGE(Year, Year);
                                 HoursLogEntrRec.SETRANGE(Week, Week);
                                 HoursLogEntrRec.SETRANGE("Employee No.","Employee No.");
                                 IF HoursLogEntrRec.FINDFIRST THEN;
                                 PAGE.RUNMODAL(PAGE::"Hours Log Entries",HoursLogEntrRec);
                               END;
                                }
      { 3       ;1   ;ActionGroup;
                      Name=ButtonPost;
                      CaptionML=ENU=P&osting }
      { 4       ;2   ;Action    ;
                      CaptionML=ENU=Test Report;
                      Enabled=PostingEnabled;
                      Image=TestReport;
                      OnAction=VAR
                                 PrintDoc@1210190001 : Codeunit 229;
                               BEGIN
                                 HourRec.COPY(Rec);
                                 HourRec.SETFILTER("Document No.", "Document No.");
                                 PrintDoc.PrintHours(HourRec,1,TRUE);
                               END;
                                }
      { 5       ;2   ;Action    ;
                      ShortCutKey=Ctrl+F11;
                      CaptionML=ENU=Reconcile;
                      Enabled=PostingEnabled;
                      Image=Reconcile;
                      OnAction=BEGIN
                                 CheckHourAccounting.RUN(Rec);
                               END;
                                }
      { 1100485008;2 ;Action    ;
                      ShortCutKey=Ctrl+F9;
                      CaptionML=ENU=Re&lease;
                      Promoted=Yes;
                      Enabled=PostingEnabled;
                      Image=ReleaseDoc;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CurrPage.UPDATE(TRUE);
                                 SetStatus(TRUE, UserSetup."Employee No.");
                               END;
                                }
      { 1100485009;2 ;Action    ;
                      ShortCutKey=Shift+Ctrl+F9;
                      CaptionML=ENU=Re&open;
                      Enabled=PostingEnabled;
                      Image=ReOpen;
                      OnAction=BEGIN
                                 SetStatus(FALSE, UserSetup."Employee No.");
                               END;
                                }
      { 1210190000;2 ;Action    ;
                      ShortCutKey=F9;
                      CaptionML=ENU=Post;
                      Promoted=Yes;
                      Enabled=PostingEnabled;
                      PromotedIsBig=Yes;
                      Image=Post;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PostHours(FALSE, FALSE);
                               END;
                                }
      { 1210190001;2 ;Action    ;
                      ShortCutKey=Shift+F9;
                      CaptionML=ENU=Post and Print;
                      Promoted=Yes;
                      Enabled=PostingEnabled;
                      PromotedIsBig=Yes;
                      Image=PostPrint;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PostHours(TRUE, FALSE);
                               END;
                                }
      { 7       ;2   ;Action    ;
                      CaptionML=ENU=Post Batch;
                      Enabled=PostingEnabled;
                      Image=PostBatch;
                      OnAction=BEGIN
                                 PostHours(FALSE, TRUE);
                               END;
                                }
      { 8       ;2   ;Action    ;
                      CaptionML=ENU=Post Batch and Print;
                      Enabled=PostingEnabled;
                      Image=PostBatch;
                      OnAction=BEGIN
                                 PostHours(TRUE, TRUE);
                               END;
                                }
      { 27      ;1   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=&Print;
                      Promoted=Yes;
                      Image=Print;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 PrintDoc@1210190001 : Codeunit 229;
                               BEGIN
                                 HourRec.COPY(Rec);
                                 HourRec.SETRECFILTER;
                                 PrintDoc.PrintHours(HourRec,0,TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 11  ;1   ;Group     ;
                CaptionML=ENU=Main }

    { 16  ;2   ;Field     ;
                SourceExpr=Year }

    { 12  ;2   ;Field     ;
                SourceExpr=Week;
                OnValidate=BEGIN
                             WeekOnAfterValidate;
                           END;
                            }

    { 14  ;2   ;Field     ;
                SourceExpr="Employee No.";
                Editable=EmployeeNoEditable;
                OnValidate=BEGIN
                             EmployeeNoOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           Rec.LookUpEmployee(EmployeeNoEditable);
                           IF EmployeeNoEditable THEN
                             CurrPage.UPDATE(TRUE);
                           CALCFIELDS("Full Name");
                         END;
                          }

    { 1210190002;2;Field  ;
                DrillDown=No;
                CaptionML=ENU=Name;
                SourceExpr="Full Name" }

    { 22  ;2   ;Field     ;
                SourceExpr="Document No.";
                Editable=DocumentNoEditable;
                OnValidate=BEGIN
                             DocumentNoOnAfterValidate;
                           END;
                            }

    { 20  ;2   ;Field     ;
                SourceExpr="Posting Date";
                Editable=FALSE }

    { 1100485001;2;Field  ;
                SourceExpr=Status;
                Editable=FALSE }

    { 1100485012;2;Field  ;
                SourceExpr="Released by" }

    { 1100485016;2;Field  ;
                Name=IAmAController;
                CaptionML=ENU=Show My Employees Only;
                SourceExpr=ShowMyEmployeesOnly;
                Visible=IAmAControllerVisible;
                Editable=IAmAControllerEditable;
                OnValidate=BEGIN
                             ShowMyEmployeesOnlyOnAfterVali;
                           END;
                            }

    { 10  ;1   ;Part      ;
                Name=HoursForm;
                SubPageLink=Year=FIELD(Year),
                            Week=FIELD(Week),
                            Employee No.=FIELD(Employee No.),
                            Document No.=FIELD(Document No.);
                PagePartID=Page11020320 }

    { 1210190007;0;Container;
                ContainerType=FactBoxArea }

    { 1210190005;1;Part   ;
                SubPageLink=Line No.=FIELD(Line No.),
                            Year=FIELD(Year),
                            Week=FIELD(Week),
                            Employee No.=FIELD(Employee No.);
                PagePartID=Page11130125;
                ProviderID=10;
                PartType=Page }

    { 1210190006;1;Part   ;
                SubPageLink=Line No.=FIELD(Line No.),
                            Year=FIELD(Year),
                            Week=FIELD(Week),
                            Employee No.=FIELD(Employee No.);
                PagePartID=Page11130103;
                ProviderID=10;
                PartType=Page }

    { 1210190004;1;Part   ;
                SubPageLink=Line No.=FIELD(Line No.),
                            Year=FIELD(Year),
                            Week=FIELD(Week),
                            Employee No.=FIELD(Employee No.);
                PagePartID=Page11130123;
                ProviderID=10;
                PartType=Page }

    { 1100525001;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1100525000;1;Part   ;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      ProjectSetup@11012000 : Record 315;
      UserSetup@11012001 : Record 91;
      HourRec@11012002 : Record 11012038;
      HourControllerRec@1100485003 : Record 11012008;
      CheckHourAccounting@11012003 : Codeunit 11012002;
      PostHoursBatch@1100485000 : Report 11020222;
      IamController@1100485002 : Boolean;
      ShowMyEmployeesOnly@1100485001 : Boolean;
      DPA_IsHardFilter@1100000999 : Boolean;
      PostingEnabled@1210190001 : Boolean INDATASET;
      IAmAControllerVisible@1210190000 : Boolean INDATASET;
      DocumentNoEditable@19064841 : Boolean INDATASET;
      EmployeeNoEditable@19049966 : Boolean INDATASET;
      IAmAControllerEditable@19060515 : Boolean INDATASET;

    PROCEDURE PostHours@1100485000(Print@1100485000 : Boolean;Batch@1100485001 : Boolean);
    BEGIN
      //call C-009959
      CLEAR(PostHoursBatch);
      PostHoursBatch.SetPostOptions(Print, NOT Batch);
      HourRec.RESET;
      HourRec.SETRANGE("Document No.", "Document No.");
      IF NOT Batch THEN BEGIN
        HourRec.SETRANGE(Year, Year);
        HourRec.SETRANGE(Week, Week);
        HourRec.SETRANGE("Employee No.", "Employee No.");
      END ELSE
        HourRec.DPA_SetFilters('');
      PostHoursBatch.SETTABLEVIEW(HourRec);
      PostHoursBatch.USEREQUESTPAGE := Batch;
      PostHoursBatch.RUNMODAL;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE WeekOnAfterValidate@19014554();
    BEGIN
      IF "Employee No." <> '' THEN
        CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE EmployeeNoOnAfterValidate@19040646();
    BEGIN
      CurrPage.UPDATE(TRUE);
      CALCFIELDS("Full Name");
    END;

    LOCAL PROCEDURE DocumentNoOnAfterValidate@19031352();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE ShowMyEmployeesOnlyOnAfterVali@19015268();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      EmplDescrOtherCompany;
    END;

    LOCAL PROCEDURE DocumentNoOnBeforeInput@19029888();
    BEGIN
      //db, 19-06-06
    END;

    BEGIN
    END.
  }
}

