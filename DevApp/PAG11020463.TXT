OBJECT Page 11020463 Estimate Quantity Sheet Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Estimate Quantity Sheet Lines;
    SourceTable=Table11020464;
    PageType=ListPart;
    AutoSplitKey=Yes;
    OnInit=BEGIN
             FormulaEditable := TRUE;
             HoogteEditable := TRUE;
             BreedteEditable := TRUE;
             LengteEditable := TRUE;
             AantalEditable := TRUE;
           END;

    OnAfterGetRecord=BEGIN
                       GetResult;
                       GetRelation; //DP00686
                     END;

    OnNewRecord=BEGIN
                  QuantDec := 0;
                  LengthDec := 0;
                  WidthDec := 0;
                  HeightDec := 0;
                  ResultDec := 0;
                  TotLengthDec := 0;
                  TotWidthDec := 0;
                  TotHeightDec := 0;
                END;

    OnAfterGetCurrRecord=BEGIN
                           AantalEditable := (Total <> Total::Subtotal);
                           LengteEditable := (Total <> Total::Subtotal);
                           BreedteEditable := (Total <> Total::Subtotal);
                           HoogteEditable := (Total <> Total::Subtotal);
                           FormulaEditable := (Total <> Total::Subtotal);
                         END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1906587504;1 ;ActionGroup;
                      CaptionML=ENU=F&unctions }
      { 1905582204;2 ;Action    ;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=ENU=Remove Relation;
                      Image=Delete;
                      OnAction=BEGIN
                                 DeleteRelation;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100525000;1;Group  ;
                GroupType=Repeater }

    { 1100525001;2;Field  ;
                CaptionML=ENU=Row No.;
                SourceExpr="Row No." }

    { 1100525013;2;Field  ;
                SourceExpr=Description;
                OnValidate=BEGIN
                             CurrPage.SAVERECORD;
                           END;
                            }

    { 1100525003;2;Field  ;
                Name=Aantal;
                CaptionML=ENU=Quantity - A;
                BlankZero=Yes;
                SourceExpr=QuantDec;
                Editable=AantalEditable;
                OnValidate=BEGIN
                             VALIDATE(Quantity,QuantDec);
                             UpdateSheet;
                           END;

                OnAssistEdit=BEGIN
                               IF QuantDec <> 0 THEN
                                 SetRelation('A',QuantDec)
                               ELSE
                                 ERROR(Text001);
                             END;
                              }

    { 1100525005;2;Field  ;
                Name=Lengte;
                CaptionML=ENU=Length - B;
                BlankZero=Yes;
                SourceExpr=LengthDec;
                Editable=LengteEditable;
                OnValidate=BEGIN
                             VALIDATE(Length,LengthDec);
                             UpdateSheet;
                           END;

                OnAssistEdit=BEGIN
                               IF LengthDec <> 0 THEN
                                 SetRelation('B',LengthDec)
                               ELSE
                                 ERROR(Text001);
                             END;
                              }

    { 1100525021;2;Field  ;
                Name=TotLengte;
                CaptionML=ENU=Total Length - C;
                BlankZero=Yes;
                SourceExpr=TotLengthDec;
                Visible=FALSE;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               SetRelation('C',TotLengthDec);
                             END;
                              }

    { 1100525007;2;Field  ;
                Name=Breedte;
                CaptionML=ENU=Width - D;
                BlankZero=Yes;
                SourceExpr=WidthDec;
                Editable=BreedteEditable;
                OnValidate=BEGIN
                             VALIDATE(Width,WidthDec);
                             UpdateSheet;
                           END;

                OnAssistEdit=BEGIN
                               IF WidthDec <> 0 THEN
                                 SetRelation('D',WidthDec)
                               ELSE
                                 ERROR(Text001);
                             END;
                              }

    { 1100525023;2;Field  ;
                Name=TotBreedte;
                CaptionML=ENU=Total Width - E;
                BlankZero=Yes;
                SourceExpr=TotWidthDec;
                Visible=FALSE;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               SetRelation('E',TotWidthDec);
                             END;
                              }

    { 1100525009;2;Field  ;
                Name=Hoogte;
                CaptionML=ENU=Height - F;
                BlankZero=Yes;
                SourceExpr=HeightDec;
                Editable=HoogteEditable;
                OnValidate=BEGIN
                             VALIDATE(Height,HeightDec);
                             UpdateSheet;
                           END;

                OnAssistEdit=BEGIN
                               IF HeightDec <> 0 THEN
                                 SetRelation('F',HeightDec)
                               ELSE
                                 ERROR(Text001);
                             END;
                              }

    { 1100525025;2;Field  ;
                Name=TotHoogte;
                CaptionML=ENU=Total Height - G;
                BlankZero=Yes;
                SourceExpr=TotHeightDec;
                Visible=FALSE;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               SetRelation('G',TotHeightDec);
                             END;
                              }

    { 1100525011;2;Field  ;
                Name=Resultaat;
                CaptionML=ENU=Result;
                BlankZero=Yes;
                SourceExpr=ResultDec;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               SetRelation('R',ResultDec);
                             END;
                              }

    { 1100525015;2;Field  ;
                SourceExpr=Formula;
                Editable=FormulaEditable;
                OnValidate=BEGIN
                             CurrPage.UPDATE;
                           END;
                            }

    { 1100525017;2;Field  ;
                SourceExpr=Total;
                OnValidate=BEGIN
                             CurrPage.UPDATE;
                           END;
                            }

    { 1100525019;2;Field  ;
                SourceExpr=Totaling;
                OnValidate=BEGIN
                             CurrPage.UPDATE;
                           END;
                            }

    { 1100409001;2;Field  ;
                SourceExpr=Feature }

    { 1100409004;2;Field  ;
                SourceExpr="Feature Description";
                Importance=Additional }

    { 1100409002;2;Field  ;
                SourceExpr="Related to Column" }

    { 1100409003;2;Field  ;
                SourceExpr="Related to Part Quantity" }

    { 1100409000;2;Field  ;
                Name=RelationTxt;
                CaptionML=ENU=Relation;
                SourceExpr=RelationTxt;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Text001@1100525011 : TextConst 'ENU=No value in concerning field';
      QuantDec@1100528915 : Decimal;
      LengthDec@1100528914 : Decimal;
      WidthDec@1100528913 : Decimal;
      HeightDec@1100528912 : Decimal;
      ResultDec@1100528911 : Decimal;
      TotLengthDec@1100528910 : Decimal;
      TotWidthDec@1100528909 : Decimal;
      TotHeightDec@1100528908 : Decimal;
      gvFromLineNo@1100528907 : Integer;
      gvFromFieldNo@1100528906 : Code[1];
      gvEstimate@1100528905 : Code[20];
      gvSubEstimate@1100528904 : Code[20];
      gvPartGroup@1100528903 : Code[20];
      gvPart@1100528902 : Code[20];
      gvEstLine@1100528901 : Integer;
      gvRecipeGroup@1100409003 : Code[20];
      gvRecipe@1100409002 : Code[20];
      gvRecipeLine@1100409001 : Integer;
      gvConverted@1100528900 : Boolean;
      AantalEditable@1100528932 : Boolean INDATASET;
      LengteEditable@1100528931 : Boolean INDATASET;
      BreedteEditable@1100528930 : Boolean INDATASET;
      HoogteEditable@1100528929 : Boolean INDATASET;
      FormulaEditable@1100528928 : Boolean INDATASET;
      RelationTxt@1100409000 : Code[10];
      Selection@1100409004 : Integer;
      Text002@1100409007 : TextConst 'ENU=Do you want to create Relation with Source: \%1 \%2 \%3 \%4 \%5';
      Text002a@1100409006 : TextConst 'ENU=Select Source';
      Text002b@1100409005 : TextConst 'ENU=Estimate Line,Recipe Line';

    PROCEDURE UpdateSheet@1100525000();
    VAR
      SheetHeader@1100409000 : Record 11020462;
    BEGIN
      IF "Row No." = 0 THEN EXIT; //db, 17-11-14: C019160 (due to call function from OnValidate instead of OnAfterValidate in classic version)

      CurrPage.SAVERECORD;
      SheetHeader.ProcessSheet("Estimate No.","Quantity Sheet");  //db, 20-01-15
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE SetDefaults@1100525001(lvEstimate@1100528907 : Code[20];lvSubEstimate@1100528906 : Code[20];lvPartGroup@1100528905 : Code[20];lvPart@1100528904 : Code[20];lvEstLine@1100528903 : Integer;lvRecipeGroup@1100409002 : Code[20];lvRecipe@1100409001 : Code[20];lvRecipeLine@1100409000 : Integer;lvConverted@1100528902 : Boolean;lvFromLineNo@1100528901 : Integer;lvFromFieldNo@1100528900 : Code[1]);
    BEGIN
      //DP00686
      gvEstimate := lvEstimate;
      gvSubEstimate := lvSubEstimate;
      gvPartGroup := lvPartGroup;
      gvPart := lvPart;
      gvEstLine := lvEstLine;
      gvRecipeGroup := lvRecipeGroup;
      gvRecipe := lvRecipe;
      gvRecipeLine := lvRecipeLine;
      gvConverted := lvConverted;
      gvFromLineNo := lvFromLineNo;
      gvFromFieldNo := lvFromFieldNo;
    END;

    PROCEDURE SetRelation@1100525002(FieldCde@1100525000 : Code[1];ValueDec@1100525002 : Decimal);
    VAR
      QuantitySheetRelation@1100525001 : Record 11020463;
      EstimateLineOLD@1100528901 : Record 11012154;
      EstimateLineNEW@1100528900 : Record 11072072;
      RecipeLineOLD@1100409000 : Record 11012156;
      RecipeLineNEW@1100409001 : Record 11072075;
    BEGIN
      //DP00686
      IF NOT SourceSelected THEN EXIT;
      IF (gvEstimate = '') THEN EXIT;

      QuantitySheetRelation.EditRelation(
        gvEstimate, gvSubEstimate, gvPartGroup, gvPart, gvEstLine, gvRecipeGroup, gvRecipe, gvRecipeLine,
        "Quantity Sheet", "Row No.", FieldCde);

      IF Selection < 0 THEN BEGIN
        IF gvConverted THEN BEGIN
          IF gvRecipe <> '' THEN BEGIN
            RecipeLineNEW.GET(gvEstimate, gvSubEstimate, gvPartGroup, gvPart, gvEstLine, gvRecipeGroup, gvRecipe, gvRecipeLine);
            RecipeLineNEW.VALIDATE(Quantity,ValueDec);
            RecipeLineNEW.MODIFY;
          END ELSE BEGIN
            EstimateLineNEW.GET(gvEstimate, gvSubEstimate, gvPartGroup, gvPart, gvEstLine);
            EstimateLineNEW.VALIDATE(Quantity,ValueDec);
            EstimateLineNEW.MODIFY;
          END;
        END ELSE BEGIN
          IF gvRecipe <> '' THEN BEGIN
            RecipeLineOLD.GET(gvEstimate, gvSubEstimate, gvPartGroup, gvPart, gvEstLine, gvRecipeGroup, gvRecipe, gvRecipeLine);
            RecipeLineOLD.VALIDATE(Quantity,ValueDec);
            RecipeLineOLD.MODIFY;
          END ELSE BEGIN
            EstimateLineOLD.GET(gvEstimate, gvSubEstimate, gvPartGroup, gvPart, gvEstLine);
            EstimateLineOLD.VALIDATE(Quantity,ValueDec);
            EstimateLineOLD.MODIFY;
          END;
        END;
      END ELSE BEGIN
        gvEstimate := '';
        gvSubEstimate := '';
        gvPartGroup := '';
        gvPart := '';
        gvEstLine := 0;
        gvRecipeGroup := '';
        gvRecipe := '';
        gvRecipeLine := 0;
      END;

      QuantitySheetRelation.CLEARMARKS;
      gvFromLineNo := "Row No.";
      gvFromFieldNo := FieldCde;
      CurrPage.UPDATE;
    END;

    PROCEDURE DeleteRelation@1100525003();
    BEGIN
      DeleteLineRelation;  //DP00686
      gvFromLineNo := 0;
      gvFromFieldNo := '';
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE GetResult@1100409000();
    VAR
      EstRec@1100409000 : Record 11012151;
    BEGIN
      IF Total = Total::Subtotal THEN BEGIN
        CALCFIELDS("Sum Quantity","Sum Length","Sum Width","Sum Height","Sum Result","Sum Total Length","Sum Total Width",
                   "Sum Total Height");
        QuantDec := "Sum Quantity";
        LengthDec := "Sum Length";
        WidthDec := "Sum Width";
        HeightDec := "Sum Height";
        ResultDec := "Sum Result";
        TotLengthDec := "Sum Total Length";
        TotWidthDec := "Sum Total Width";
        TotHeightDec := "Sum Total Height";
      END ELSE BEGIN
        QuantDec := Quantity;
        LengthDec := Length;
        WidthDec := Width;
        HeightDec := Height;
        ResultDec := Result;
        TotLengthDec := "Total Length";
        TotWidthDec := "Total Width";
        TotHeightDec := "Total Height";
      END;

      IF gvEstimate = '' THEN BEGIN  //DP00686
        EstRec.GET("Estimate No.");
        gvConverted := EstRec.Converted;
      END;
    END;

    PROCEDURE GetRelation@1100409002();
    VAR
      QuanRelation@1100409000 : Record 11020463;
    BEGIN
      //DP00686
      RelationTxt := '';
      QuanRelation.SETRANGE("Estimate No.", "Estimate No.");
      QuanRelation.SETRANGE("Quantity Sheet", "Quantity Sheet");
      QuanRelation.SETRANGE("Quantity Sheet Line", "Row No.");
      IF QuanRelation.FINDFIRST THEN
        RelationTxt := QuanRelation."Quantity Sheet Field";
    END;

    PROCEDURE SourceSelected@1100409001() : Boolean;
    VAR
      EstimateLineOLD@1100409005 : Record 11012154;
      EstimateLineNEW@1100409004 : Record 11072072;
      RecipeLineOLD@1100409003 : Record 11012156;
      RecipeLineNEW@1100409002 : Record 11072075;
      LineDesc@1100409001 : Text[250];
      SheetHeader@1100409000 : Record 11020462;
    BEGIN
      //DP00686
      Selection := 0;
      IF gvEstimate <> '' THEN BEGIN
        IF gvConverted THEN BEGIN
          IF gvRecipe <> '' THEN BEGIN
            IF NOT RecipeLineNEW.GET(gvEstimate, gvSubEstimate, gvPartGroup, gvPart, gvEstLine, gvRecipeGroup, gvRecipe, gvRecipeLine) THEN EstimateLineNEW.INIT;
            LineDesc := RecipeLineNEW.Description;
          END ELSE BEGIN
            IF NOT EstimateLineNEW.GET(gvEstimate, gvSubEstimate, gvPartGroup, gvPart, gvEstLine) THEN EstimateLineNEW.INIT;
            LineDesc := EstimateLineNEW.Description;
          END;
        END ELSE BEGIN
          IF gvRecipe <> '' THEN BEGIN
            IF NOT RecipeLineOLD.GET(gvEstimate, gvSubEstimate, gvPartGroup, gvPart, gvEstLine, gvRecipeGroup, gvRecipe, gvRecipeLine) THEN EstimateLineNEW.INIT;
            LineDesc := RecipeLineOLD.Description;
          END ELSE BEGIN
            IF NOT EstimateLineOLD.GET(gvEstimate, gvSubEstimate, gvPartGroup, gvPart, gvEstLine) THEN EstimateLineOLD.INIT;
            LineDesc := EstimateLineOLD.Description;
          END;
        END;
        IF CONFIRM(Text002, TRUE, gvEstimate, gvSubEstimate, gvPartGroup, gvPart, LineDesc) THEN
          Selection := -1;  //use predefined global variables
      END;

      IF Selection = 0 THEN
        Selection := DIALOG.STRMENU(Text002b, 1, Text002a);  //Select Source: 1=EstimateLine; 2=RecipeLine
      IF Selection = 0 THEN EXIT(FALSE);

      IF Selection > 0 THEN BEGIN
        SheetHeader.GET("Estimate No.", "Quantity Sheet");
        IF gvConverted THEN BEGIN
          EstimateLineNEW.SETRANGE("Estimate No.", "Estimate No.");
          IF SheetHeader."Sub-Estimate No." <> '' THEN
            EstimateLineNEW.SETRANGE("Sub-Estimate No.", SheetHeader."Sub-Estimate No.");
          IF SheetHeader."Part Group" <> '' THEN
            EstimateLineNEW.SETRANGE("Part Group", SheetHeader."Part Group");
          IF SheetHeader.Part <> '' THEN
            EstimateLineNEW.SETRANGE(Part, SheetHeader.Part);
          IF Selection = 1 THEN
            EstimateLineNEW.SETFILTER(Recipe, '%1', '');
          IF Selection = 2 THEN
            EstimateLineNEW.SETFILTER(Recipe, '<>%1', '');
          IF PAGE.RUNMODAL(PAGE::"Estimate Line Cost NEW", EstimateLineNEW) = ACTION::LookupOK THEN BEGIN
            gvEstimate := EstimateLineNEW."Estimate No.";
            gvSubEstimate := EstimateLineNEW."Sub-Estimate No.";
            gvPartGroup := EstimateLineNEW."Part Group";
            gvPart := EstimateLineNEW.Part;
            gvEstLine := EstimateLineNEW."Line No.";
          END ELSE BEGIN
            EXIT(FALSE);
          END;
          IF Selection = 2 THEN BEGIN
            RecipeLineNEW.SETRANGE("Estimate No.", EstimateLineNEW."Estimate No.");
            RecipeLineNEW.SETRANGE("Sub-Estimate No.", EstimateLineNEW."Sub-Estimate No.");
            RecipeLineNEW.SETRANGE("Part Group", EstimateLineNEW."Part Group");
            RecipeLineNEW.SETRANGE(Part, EstimateLineNEW.Part);
            RecipeLineNEW.SETRANGE("Estimate Line", EstimateLineNEW."Line No.");
            RecipeLineNEW.SETRANGE("Recipe Group", EstimateLineNEW."Recipe Group");
            RecipeLineNEW.SETRANGE(Recipe, EstimateLineNEW.Recipe);
            IF PAGE.RUNMODAL(PAGE::"Recipe Lines Cost", RecipeLineNEW) = ACTION::LookupOK THEN BEGIN
              gvRecipeGroup := RecipeLineNEW."Recipe Group";
              gvRecipe := RecipeLineNEW.Recipe;
              gvRecipeLine := RecipeLineNEW."Line No.";
            END ELSE BEGIN
              EXIT(FALSE);
            END;
          END;
        END ELSE BEGIN
          EstimateLineOLD.SETRANGE("Estimate No.", "Estimate No.");
          EstimateLineOLD.SETRANGE("Sub-Estimate No.", SheetHeader."Sub-Estimate No.");
          EstimateLineOLD.SETRANGE("Part Group", SheetHeader."Part Group");
          EstimateLineOLD.SETRANGE(Part, SheetHeader.Part);
          IF Selection = 1 THEN
            EstimateLineOLD.SETFILTER(Recipe, '%1', '');
          IF Selection = 2 THEN
            EstimateLineOLD.SETFILTER(Recipe, '<>%1', '');
          IF PAGE.RUNMODAL(PAGE::"Estimate Line List", EstimateLineOLD) = ACTION::LookupOK THEN BEGIN
            gvEstimate := EstimateLineOLD."Estimate No.";
            gvSubEstimate := EstimateLineOLD."Sub-Estimate No.";
            gvPartGroup := EstimateLineOLD."Part Group";
            gvPart := EstimateLineOLD.Part;
            gvEstLine := EstimateLineOLD."Line No.";
          END ELSE BEGIN
            EXIT(FALSE);
          END;
          IF Selection = 2 THEN BEGIN
            RecipeLineOLD.SETRANGE("Recipe Group", EstimateLineOLD."Recipe Group");
            RecipeLineOLD.SETRANGE(Recipe, EstimateLineOLD.Recipe);
            IF PAGE.RUNMODAL(PAGE::"Recipe Lines List", RecipeLineOLD) = ACTION::LookupOK THEN BEGIN
              gvRecipeGroup := RecipeLineOLD."Recipe Group";
              gvRecipe := RecipeLineOLD.Recipe;
              gvRecipeLine := RecipeLineOLD."Line No.";
            END ELSE BEGIN
              EXIT(FALSE);
            END;
          END;
        END;
      END;

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

