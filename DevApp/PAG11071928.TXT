OBJECT Page 11071928 Add Item Relation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Add Item Vendor;
    DeleteAllowed=No;
    ModifyAllowed=No;
    DelayedInsert=Yes;
    PageType=NavigatePage;
    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525005;1 ;Action    ;
                      CaptionML=ENU=&Add;
                      Promoted=Yes;
                      InFooterBar=Yes;
                      PromotedIsBig=Yes;
                      Image=AddAction;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 InsertItemRelation;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100525006;1;Field  ;
                CaptionML=ENU=Item No.;
                SourceExpr=ItemNo;
                Editable=FALSE }

    { 1100525000;1;Field  ;
                CaptionML=ENU=Trade Item Vendor;
                SourceExpr=VendorCde;
                OnValidate=VAR
                             lvVendRec@1100525000 : Record 11012304;
                           BEGIN
                             IF VendorCde <>'' THEN BEGIN
                               lvVendRec.GET(VendorCde);
                               VendorCde := lvVendRec.Code;
                               NameVendorTxt := lvVendRec.Name;
                               lvVendRec.CALCFIELDS("Vendor (Purchase)");
                               VendorPurchCde := lvVendRec."Vendor (Purchase)";
                             END ELSE BEGIN
                               VendorCde := '';
                               NameVendorTxt := '';
                               VendorPurchCde := '';
                             END;
                           END;

                OnLookup=VAR
                           lvVendRec@1100525000 : Record 11012304;
                         BEGIN
                           IF lvVendRec.FINDSET THEN
                             IF PAGE.RUNMODAL(0,lvVendRec) = ACTION::LookupOK THEN BEGIN
                               VendorCde := lvVendRec.Code;
                               NameVendorTxt := lvVendRec.Name;
                               lvVendRec.CALCFIELDS("Vendor (Purchase)");
                               VendorPurchCde := lvVendRec."Vendor (Purchase)";
                             END;
                         END;
                          }

    { 1100525002;1;Field  ;
                CaptionML=ENU=Name Vendor;
                SourceExpr=NameVendorTxt;
                Editable=FALSE }

    { 1100525001;1;Field  ;
                CaptionML=ENU=Vendor (Purchase);
                SourceExpr=VendorPurchCde;
                Editable=FALSE }

    { 8   ;1   ;Field     ;
                CaptionML=ENU=Vendor Item No.;
                SourceExpr=VendorItemNo;
                OnLookup=BEGIN
                           LookupItem;
                         END;
                          }

  }
  CODE
  {
    VAR
      VendorCde@1100525000 : Code[20];
      NameVendorTxt@1100525001 : Text[50];
      VendorPurchCde@1100525002 : Code[20];
      Vend@1100525003 : Record 23;
      VendorItemNo@1100525004 : Code[20];
      ItemNo@1100525005 : Code[20];
      Text001@1100525006 : TextConst 'ENU=Itemrelation not Allowed By deviant Base Unit of Measure';

    PROCEDURE LookupItem@1100525000();
    VAR
      TradeItemRec@1100525000 : Record 11012317;
    BEGIN
      Vend.GET(VendorPurchCde);
      IF Vend."Vendor (Trade Item)" = '' THEN
        EXIT;
      TradeItemRec.SETRANGE(Vendor,VendorCde);
      TradeItemRec.SETRANGE(Expired,FALSE);
      IF PAGE.RUNMODAL(0, TradeItemRec) = ACTION::LookupOK THEN
        VendorItemNo := TradeItemRec."Item Code";
    END;

    PROCEDURE GetItemNo@1100525001(ItemNoCde@1100525000 : Code[20]);
    BEGIN
      ItemNo := ItemNoCde;
    END;

    PROCEDURE InsertItemRelation@1100525002();
    VAR
      lvItemRelRec@1100525000 : Record 99;
      lvItemRelRec2@1100525002 : Record 99;
      lvTradeItemRec@1100525001 : Record 11012317;
    BEGIN
      lvItemRelRec.INIT;
      lvItemRelRec."Vendor No." := VendorPurchCde;
      lvItemRelRec.CALCFIELDS("Vendor (Trade Item)");
      lvItemRelRec."Item No." := ItemNo;
      lvItemRelRec."Source Type" := lvItemRelRec."Source Type"::"Trade Item";
      lvItemRelRec.VALIDATE("Vendor Item No.",VendorItemNo);
      IF lvTradeItemRec.GET(VendorCde,VendorItemNo) THEN
        lvItemRelRec."GTIN Code" := lvTradeItemRec."GTIN Code (Item)";
      lvItemRelRec2.SETRANGE("Vendor No.",VendorPurchCde);
      lvItemRelRec2.SETRANGE("Item No.",ItemNo);
      IF lvItemRelRec2.FINDLAST THEN
        lvItemRelRec."Serial No." := lvItemRelRec2."Serial No." + 1
      ELSE
        lvItemRelRec."Serial No." := 1;
      IF lvItemRelRec.INSERT(TRUE) THEN
        CurrPage.CLOSE
      ELSE
        ERROR(Text001);
    END;

    BEGIN
    END.
  }
}

