OBJECT Page 11072287 Copy Standard Purchase Action
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Standard Purchase Action;
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    PageType=NavigatePage;
    OnOpenPage=BEGIN
                 EstimateRec.GET(EstimateCde);
               END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 7       ;1   ;Action    ;
                      CaptionML=ENU=&Copy;
                      InFooterBar=Yes;
                      Image=Copy;
                      OnAction=BEGIN
                                 CopyPurchaseActions;

                                 MESSAGE(Text000 + Text003, PurchActionCounterOK, PurchActionCounter);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 8   ;1   ;Field     ;
                CaptionML=ENU=Estimate;
                SourceExpr=EstimateRec."No.";
                Editable=FALSE }

    { 10  ;1   ;Field     ;
                CaptionML=ENU=Description;
                SourceExpr=EstimateRec.Description;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Text000@11012017 : TextConst 'ENU=The following data has been copied:\\';
      Text003@11012020 : TextConst 'ENU=%5 of the %6 standard Purchase Actions\';
      EstimateRec@11012000 : Record 11012151;
      StandPurchActionRec@11012005 : Record 11012025;
      EstPurchActionRec@11012006 : Record 11012366;
      FeatByPurchActionRec@1100485008 : Record 11012069;
      FeatByPurchActionRec2@1100485009 : Record 11012069;
      PurchaseTextRecOrig@1100485002 : Record 11012067;
      PurchaseTextRecDest@1100485001 : Record 11012067;
      EstimateCde@11012007 : Code[20];
      PurchActionCounter@11012015 : Integer;
      PurchActionCounterOK@11012016 : Integer;

    PROCEDURE SetEstimate@4(lEstimateCde@11012000 : Code[20]);
    BEGIN
      EstimateCde := lEstimateCde;
    END;

    PROCEDURE CopyPurchaseActions@3();
    BEGIN
      PurchActionCounter := StandPurchActionRec.COUNT;
      PurchActionCounterOK := 0;

      IF StandPurchActionRec.FIND('-') THEN
        REPEAT
          IF NOT EstPurchActionRec.GET(EstimateCde,StandPurchActionRec.Code) THEN BEGIN
            EstPurchActionRec.INIT;
            EstPurchActionRec."Estimate No." := EstimateCde;
            EstPurchActionRec.TRANSFERFIELDS(StandPurchActionRec);
            EstPurchActionRec."Changed by" := USERID;
            EstPurchActionRec."Changed on" := TODAY;
            EstPurchActionRec."Requested Quote Receipt Date" := EstimateRec."Requested Quote Receipt Date";
            EstPurchActionRec."Vendors React before" := EstimateRec."Vendors React before";
            EstPurchActionRec.INSERT;


            FeatByPurchActionRec.RESET;
            FeatByPurchActionRec.SETRANGE("Estimate No.", '');
            FeatByPurchActionRec.SETRANGE("Project No.",'');
            FeatByPurchActionRec.SETRANGE("Purchase Action",StandPurchActionRec.Code);
            IF FeatByPurchActionRec.FIND('-') THEN
              REPEAT
                FeatByPurchActionRec2.INIT;
                FeatByPurchActionRec2.Source := FeatByPurchActionRec2.Source::Estimate;
                FeatByPurchActionRec2."Estimate No." := EstimateCde;
                FeatByPurchActionRec2."Purchase Action" := FeatByPurchActionRec."Purchase Action";
                FeatByPurchActionRec2."Contact Feature Group" := FeatByPurchActionRec."Contact Feature Group";
                FeatByPurchActionRec2."Contact Feature" := FeatByPurchActionRec."Contact Feature";
                FeatByPurchActionRec2."Contact Feature Description" := FeatByPurchActionRec."Contact Feature Description";
                FeatByPurchActionRec2.INSERT;
              UNTIL FeatByPurchActionRec.NEXT = 0;

            PurchaseTextRecOrig.SETRANGE(PurchaseTextRecOrig."Line Type", PurchaseTextRecOrig."Line Type"::"Purchase Action");
            PurchaseTextRecOrig.SETRANGE("Purchase Action", StandPurchActionRec.Code);
            IF PurchaseTextRecOrig.FIND('-') THEN
              REPEAT
                PurchaseTextRecDest.TRANSFERFIELDS(PurchaseTextRecOrig);
                PurchaseTextRecDest."Line Type" := PurchaseTextRecDest."Line Type"::"Estimate Purchase Action";
                PurchaseTextRecDest."No." := EstimateCde;
                PurchaseTextRecDest.INSERT;
              UNTIL PurchaseTextRecOrig.NEXT = 0;
            PurchActionCounterOK := PurchActionCounterOK + 1;
          END;
        UNTIL StandPurchActionRec.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

