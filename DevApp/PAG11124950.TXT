OBJECT Page 11124950 Project Forecast in Period
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Project Forecast in Period;
    SourceTable=Table11020632;
    DelayedInsert=Yes;
    DataCaptionFields=Forecast Date,Cost Type,Cost Object,Element;
    PageType=List;
    OnOpenPage=VAR
                 ProjectForecastInPeriod@1100528700 : Record 11020632;
               BEGIN
                 ProjectForecastInPeriod.SETRANGE("Cost Type", "Cost Type"::Labor);
                 CostTypeIsLabor := (GETFILTER("Cost Type") = ProjectForecastInPeriod.GETFILTER("Cost Type"));

                 QuantityVisible := CostTypeIsLabor;
                 RateCodeVisible := CostTypeIsLabor;
                 RateVisible := CostTypeIsLabor;
               END;

    OnAfterGetRecord=BEGIN
                       SetFieldsEditable;
                     END;

    OnNewRecord=BEGIN
                  IF GETFILTER("Forecast Date") <> '' THEN
                    "Forecast Date" := GETRANGEMIN("Forecast Date");

                  Date := GetFirstDate(GETFILTER(Date));

                  InitLaborRate;
                END;

    OnDeleteRecord=BEGIN
                     TESTFIELD(Source, Source::Forecast);
                   END;

    OnAfterGetCurrRecord=BEGIN
                           SetFieldsEditable;
                         END;

  }
  CONTROLS
  {
    { 1100528700;0;Container;
                ContainerType=ContentArea }

    { 1100528701;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100528710;2;Field  ;
                SourceExpr=Date;
                Editable=DateEditable }

    { 1100528713;2;Field  ;
                SourceExpr=Quantity;
                Visible=QuantityVisible;
                Editable=QuantityEditable }

    { 1100528715;2;Field  ;
                SourceExpr="Rate Code";
                Visible=RateCodeVisible;
                Editable=RateCodeEditable;
                OnValidate=BEGIN
                             SetFieldsEditable;
                           END;
                            }

    { 1100528716;2;Field  ;
                SourceExpr=Rate;
                Visible=RateVisible;
                Editable=RateEditable }

    { 1100528714;2;Field  ;
                SourceExpr=Amount;
                Editable=AmountEditable }

    { 1100528717;2;Field  ;
                SourceExpr=Comment }

    { 1100528718;2;Field  ;
                SourceExpr=Source }

    { 1100528702;2;Field  ;
                SourceExpr="Document No.";
                OnLookup=BEGIN
                           LookupDocumentNo;
                         END;
                          }

  }
  CODE
  {
    VAR
      CostTypeIsLabor@1100528706 : Boolean;
      DateEditable@1100528707 : Boolean INDATASET;
      QuantityEditable@1100528700 : Boolean INDATASET;
      RateCodeEditable@1100528702 : Boolean INDATASET;
      RateEditable@1100528701 : Boolean INDATASET;
      AmountEditable@1100528708 : Boolean INDATASET;
      QuantityVisible@1100528705 : Boolean INDATASET;
      RateCodeVisible@1100528704 : Boolean INDATASET;
      RateVisible@1100528703 : Boolean INDATASET;

    LOCAL PROCEDURE GetFirstDate@1(DateFilter@1001 : Text[250]) : Date;
    VAR
      Period@1002 : Record 2000000007;
    BEGIN
      IF DateFilter = '' THEN
        EXIT(0D);
      WITH Period DO BEGIN
        SETRANGE("Period Type", "Period Type"::Date);
        SETFILTER("Period Start", DateFilter);
        IF FINDFIRST THEN BEGIN
          IF "Period Start" <= "Forecast Date" THEN
            EXIT(CALCDATE('<1D>', "Forecast Date"))
          ELSE
            EXIT("Period Start")
        END;
      END;
      EXIT(0D);
    END;

    PROCEDURE SetFieldsEditable@1100528710();
    BEGIN
      DateEditable := (Source = Source::Forecast);
      QuantityEditable := (Source = Source::Forecast) AND CostTypeIsLabor;
      RateCodeEditable := (Source = Source::Forecast) AND CostTypeIsLabor;
      RateEditable := (Source = Source::Forecast) AND CostTypeIsLabor AND ("Rate Code" = '');
      AmountEditable := (Source = Source::Forecast);
    END;

    BEGIN
    END.
  }
}

