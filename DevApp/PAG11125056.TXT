OBJECT Page 11125056 Assembly Objects SM
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Assembly Objects SM;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table11020596;
    PageType=List;
    OnOpenPage=VAR
                 Project@1100528300 : Record 11072003;
               BEGIN
                 Project.GET("Project No.");
                 SetCustomerFilter;
                 SetProjectAddressData;

                 OldFrmCode := FrmCode;
                 OldFrmType := FrmType;
               END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528317;1 ;Action    ;
                      CaptionML=ENU=Process;
                      Promoted=Yes;
                      Image=Post;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Process;
                               END;
                                }
      { 1100525005;1 ;Action    ;
                      CaptionML=ENU=Fill Object with default Value;
                      Image=Action;
                      OnAction=BEGIN
                                 FillDefaultObject;
                                 CurrPage.UPDATE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100528316;1;Group  ;
                CaptionML=ENU=Options }

    { 1100409000;2;Group  ;
                GroupType=GridLayout;
                Layout=Rows }

    { 1100409001;3;Group  ;
                GroupType=Group }

    { 1100528318;4;Field  ;
                CaptionML=ENU=Type;
                OptionCaptionML=ENU=Customer,Principal;
                SourceExpr=FrmType;
                OnValidate=BEGIN
                             FrmCode := '';
                             SetProjectAddressData;
                           END;

                ShowCaption=No }

    { 1100528319;4;Field  ;
                SourceExpr=FrmCode;
                OnValidate=BEGIN
                             IF (ValidateFrmCode) THEN BEGIN
                               SetNewAddressData;
                               ResetAddressData;
                             END;
                           END;

                OnLookup=VAR
                           Customer@1100528300 : Record 18;
                           ProjectPrincipal@1100528301 : Record 11012005;
                         BEGIN
                           CASE FrmType OF
                             FrmType::Customer: BEGIN
                               Customer.SETFILTER("No.", CustomerFilter);
                               IF (PAGE.RUNMODAL(0, Customer) = ACTION::LookupOK) THEN
                                 FrmCode := Customer."No.";
                             END;
                             FrmType::Principal: BEGIN
                               ProjectPrincipal.FILTERGROUP(9);
                               ProjectPrincipal.SETRANGE("Project No.", "Project No.");
                               ProjectPrincipal.FILTERGROUP(0);
                               IF (PAGE.RUNMODAL(0, ProjectPrincipal) = ACTION::LookupOK) THEN
                                 FrmCode := ProjectPrincipal.Principal;
                             END;
                           END;
                           IF (ValidateFrmCode) THEN BEGIN
                             SetNewAddressData;
                             ResetAddressData;
                           END;
                         END;

                ShowCaption=No }

    { 1100528324;2;Field  ;
                CaptionML=ENU=Name;
                SourceExpr=GetFrmName }

    { 1100528330;2;Field  ;
                CaptionML=ENU=Location;
                SourceExpr=FrmLocation;
                TableRelation="Service Location";
                OnValidate=BEGIN
                             SetLocationAddressData;
                             ValidateFrmLocation;
                           END;

                OnLookup=VAR
                           ServiceLocation@1100525000 : Record 11012801;
                         BEGIN
                           IF (FrmType = FrmType::Customer) THEN
                             ServiceLocation.SETRANGE("Customer No.", FrmCode);
                           IF (PAGE.RUNMODAL(0, ServiceLocation) = ACTION::LookupOK) THEN
                             FrmLocation := ServiceLocation."No.";
                           SetLocationAddressData;
                           ValidateFrmLocation;
                         END;
                          }

    { 1100528320;2;Field  ;
                CaptionML=ENU=Address;
                SourceExpr=FrmAddress;
                OnValidate=VAR
                             Address2@1100525004 : Text[50];
                             CountryRegionCode@1100525003 : Code[10];
                             PhoneNo@1100525002 : Text[30];
                             FaxNo@1100525001 : Text[30];
                           BEGIN
                           END;
                            }

    { 1100528321;2;Field  ;
                CaptionML=ENU=Post Code;
                SourceExpr=FrmPostCode;
                TableRelation="Post Code".Code;
                OnValidate=VAR
                             PostCode@1100525000 : Record 225;
                             CountryDesc@1100525001 : Text[30];
                             CountryRegionCode@1100525002 : Code[10];
                           BEGIN
                             PostCode.ValidatePostCode(FrmCity, FrmPostCode, CountryDesc, CountryRegionCode, TRUE);
                           END;
                            }

    { 1100528329;2;Field  ;
                CaptionML=ENU=City;
                SourceExpr=FrmCity;
                TableRelation="Post Code".City }

    { 1100528332;2;Field  ;
                CaptionML=ENU=Start Date;
                SourceExpr=FrmStartDate }

    { 1100409002;2;Group  ;
                GroupType=GridLayout;
                Layout=Rows }

    { 1100409003;3;Group  ;
                GroupType=Group }

    { 1100525000;4;Field  ;
                CaptionML=ENU=Type;
                OptionCaptionML=ENU=Contract Template,Contract;
                SourceExpr=FrmContractType;
                OnValidate=BEGIN
                             FrmContractTypeCode := '';
                           END;

                ShowCaption=No }

    { 1100525001;4;Field  ;
                SourceExpr=FrmContractTypeCode;
                OnValidate=VAR
                             ServiceContract@1100525001 : Record 11012812;
                             ServiceContractTemplate@1100525000 : Record 11071754;
                           BEGIN
                             IF (FrmContractTypeCode <> '') THEN BEGIN
                               CASE FrmContractType OF
                                 FrmContractType::"Contract Template": ServiceContractTemplate.GET(FrmContractTypeCode);
                                 FrmContractType::Contract: ServiceContract.GET(FrmContractTypeCode);
                               END;
                             END;
                           END;

                OnLookup=VAR
                           ServiceContract@1100528300 : Record 11012812;
                           ServiceContractTemplate@1100528301 : Record 11071754;
                         BEGIN
                           CASE FrmContractType OF
                             FrmContractType::"Contract Template": BEGIN
                               IF (PAGE.RUNMODAL(0, ServiceContractTemplate) = ACTION::LookupOK) THEN
                                 FrmContractTypeCode := ServiceContractTemplate.Code;
                             END;
                             FrmContractType::Contract: BEGIN
                               ServiceContract.FILTERGROUP(9);
                               ServiceContract.SETRANGE(Blocked, FALSE);
                               ServiceContract.SETFILTER(Status, '<%1', ServiceContract.Status::Expired);
                               ServiceContract.FILTERGROUP(0);
                               IF (FrmType = FrmType::Customer) AND (FrmCode <> '') THEN
                                 ServiceContract.SETRANGE("Customer No.", FrmCode);
                               IF (PAGE.RUNMODAL(0, ServiceContract) = ACTION::LookupOK) THEN
                                 FrmContractTypeCode := ServiceContract."No.";
                             END;
                           END;

                           IF (FrmContractTypeCode <> '') THEN BEGIN
                             CASE FrmContractType OF
                               FrmContractType::"Contract Template": ServiceContractTemplate.GET(FrmContractTypeCode);
                               FrmContractType::Contract: ServiceContract.GET(FrmContractTypeCode);
                             END;
                           END;
                         END;

                ShowCaption=No }

    { 1100525002;2;Field  ;
                CaptionML=ENU=Description;
                SourceExpr=GetFrmContractTypeName }

    { 1100528300;1;Group  ;
                GroupType=Repeater }

    { 1100528301;2;Field  ;
                SourceExpr="Project No.";
                Editable=FALSE }

    { 1100528303;2;Field  ;
                SourceExpr="Yard No.";
                Editable=FALSE }

    { 1100528305;2;Field  ;
                SourceExpr="Entity Type";
                Editable=FALSE }

    { 1100528307;2;Field  ;
                SourceExpr="System No.";
                Editable=FALSE }

    { 1100528309;2;Field  ;
                SourceExpr="Entity No.";
                Editable=FALSE }

    { 1100528311;2;Field  ;
                SourceExpr=Description;
                Editable=FALSE }

    { 1100528322;2;Field  ;
                SourceExpr=Quantity;
                Editable=FALSE }

    { 1210190000;2;Field  ;
                SourceExpr="Qty. Service Objects" }

    { 1100525003;2;Field  ;
                SourceExpr="Standard Object No." }

    { 1100525004;2;Field  ;
                SourceExpr="Object No." }

  }
  CODE
  {
    VAR
      FrmType@1100528300 : 'Customer,Principal';
      FrmCode@1100528302 : Code[20];
      FrmContractType@1100525000 : 'Contract Template,Contract';
      FrmContractTypeCode@1100528306 : Code[20];
      FrmLocation@1100528308 : Code[20];
      Text000@1100528301 : TextConst 'ENU=%1 must be specified.';
      FrmStartDate@1100528311 : Date;
      Text001@1100528312 : TextConst 'ENU=Start Date';
      Text002@1100525001 : TextConst 'ENU="Do you want to change Address Data: \\Old: \%1 \%2  \\New: \%3 \%4 "';
      Text003@1210190000 : TextConst 'ENU=%1 ''%2'' does not exist.';
      FrmAddress@1100525010 : Text[50];
      FrmPostCode@1100525009 : Code[20];
      FrmCity@1100525008 : Text[30];
      CustomerFilter@1100525007 : Code[1024];
      OldFrmCode@1100525006 : Code[20];
      OldFrmType@1100525005 : Option;
      NewFrmAddress@1100525004 : Text[50];
      NewFrmPostCode@1100525003 : Code[20];
      NewFrmCity@1100525002 : Text[30];

    PROCEDURE Process@1100528302();
    VAR
      ServiceObjectMgt@1100528300 : Codeunit 11012808;
    BEGIN
      IF NOT ((ValidateFrmCode) AND (ValidateFrmLocation)) THEN
        EXIT;
      IF (FrmStartDate = 0D) THEN
        ERROR(Text000, Text001);

      ServiceObjectMgt.CreateFromAYNO(Rec, FrmCode, FrmLocation, FrmContractType, FrmContractTypeCode,
        FrmStartDate, FrmAddress, FrmPostCode, FrmCity);
    END;

    PROCEDURE FillDefaultObject@1100525001();
    VAR
      ServiceObjectMgt@1100525000 : Codeunit 11012808;
    BEGIN
      ServiceObjectMgt.FillAYNOObjectDefaults(Rec);
    END;

    PROCEDURE GetFrmName@1100528300() : Text[50];
    VAR
      Customer@1100528300 : Record 18;
      ProjectPrincipal@1100528301 : Record 11012005;
    BEGIN
      CASE FrmType OF
        FrmType::Customer: BEGIN
          IF (Customer.GET(FrmCode)) THEN
            EXIT(Customer.Name);
        END;
        FrmType::Principal: BEGIN
          IF (ProjectPrincipal.GET("Project No.", FrmCode)) THEN BEGIN
            ProjectPrincipal.CALCFIELDS("Principal Name");
            EXIT(ProjectPrincipal."Principal Name");
          END;
        END;
      END;

      EXIT('');
    END;

    PROCEDURE GetFrmContractTypeName@1100525000() : Text[50];
    VAR
      ServiceContract@1100528300 : Record 11012812;
      ServiceContractTemplate@1100528301 : Record 11071754;
    BEGIN
      CASE FrmContractType OF
        FrmContractType::"Contract Template": BEGIN
          IF (ServiceContractTemplate.GET(FrmContractTypeCode)) THEN
            EXIT(ServiceContractTemplate.Description);
        END;
        FrmContractType::Contract: BEGIN
          IF (ServiceContract.GET(FrmContractTypeCode)) THEN BEGIN
            EXIT(ServiceContract.Description);
          END;
        END;
      END;

      EXIT('');
    END;

    PROCEDURE ValidateFrmCode@1210190000() : Boolean;
    VAR
      Customer@1210190002 : Record 18;
      ProjectPrincipal@1210190001 : Record 11012005;
    BEGIN
      IF (FrmCode = '') THEN
        EXIT(TRUE);

      CASE FrmType OF
        FrmType::Customer: BEGIN
          IF (NOT Customer.GET(FrmCode)) THEN BEGIN
            MESSAGE(Text003, Customer.TABLECAPTION, FrmCode);
            EXIT(FALSE);
          END;
        END;
        FrmType::Principal: BEGIN
          IF (NOT ProjectPrincipal.GET("Project No.", FrmCode)) THEN BEGIN
            MESSAGE(Text003, ProjectPrincipal.TABLECAPTION, FrmCode);
            EXIT(FALSE);
          END;
        END;
      END;

      EXIT(TRUE);
    END;

    PROCEDURE ValidateFrmLocation@1100525002() : Boolean;
    VAR
      ServiceLocation@1100525000 : Record 11012801;
    BEGIN
      IF (FrmLocation = '') THEN
        EXIT(TRUE);

      IF (NOT ServiceLocation.GET(FrmLocation)) THEN BEGIN
        MESSAGE(Text003, ServiceLocation.TABLECAPTION, FrmCode);
        EXIT(FALSE);
      END;

      EXIT(TRUE);
    END;

    PROCEDURE SetLocationAddressData@1100525003();
    VAR
      ServiceLocation@1100525000 : Record 11012801;
    BEGIN
      FrmPostCode := '';
      FrmPostCode := '';
      FrmCity := '';

      IF (NOT ServiceLocation.GET(FrmLocation)) THEN
        EXIT;

      FrmAddress := ServiceLocation.Address;
      FrmPostCode := ServiceLocation."Post Code";
      FrmCity := ServiceLocation.City;
    END;

    PROCEDURE SetProjectAddressData@1100525004();
    VAR
      Project@1100525000 : Record 11072003;
    BEGIN
      FrmAddress := '';
      FrmPostCode := '';
      FrmCity := '';

      IF (NOT Project.GET("Project No.")) THEN
        EXIT;

      FrmAddress := Project.Address;
      FrmPostCode := Project."Post Code";
      FrmCity := Project.City;
    END;

    PROCEDURE SetCustomerFilter@1100525005();
    VAR
      ProjectPrincipal@1100525000 : Record 11012005;
      Customer@1100525001 : Record 18;
    BEGIN
      ProjectPrincipal.SETRANGE("Project No.", "Project No.");
      IF (NOT ProjectPrincipal.FINDSET) THEN
        EXIT;

      REPEAT
        IF (STRLEN(CustomerFilter) + STRLEN(ProjectPrincipal.Principal) + 1 <= MAXSTRLEN(CustomerFilter)) THEN BEGIN
          IF (CustomerFilter = '') THEN
            CustomerFilter := ProjectPrincipal.Principal
          ELSE
            CustomerFilter := CustomerFilter + '|' + ProjectPrincipal.Principal;
        END;
      UNTIL (ProjectPrincipal.NEXT = 0);
    END;

    PROCEDURE SetNewAddressData@1100525006();
    VAR
      Customer@1100525000 : Record 18;
    BEGIN
      NewFrmAddress := FrmAddress;
      NewFrmPostCode := FrmPostCode;
      NewFrmCity := FrmCity;

      IF (FrmCode = '') THEN
        EXIT;

      Customer.GET(FrmCode);
      NewFrmAddress := Customer.Address;
      NewFrmPostCode := Customer."Post Code";
      NewFrmCity := Customer.City;
    END;

    PROCEDURE ResetAddressData@1100525007();
    BEGIN
      IF ((OldFrmType <> FrmType) OR (OldFrmCode <> FrmCode)) AND
         ((NewFrmAddress <> FrmAddress) OR (NewFrmPostCode <> FrmPostCode) OR (NewFrmCity <> FrmCity))
      THEN BEGIN
        IF (CONFIRM(Text002, TRUE,
             FrmAddress, (FrmPostCode + '  ' + FrmCity),
             NewFrmAddress, (NewFrmPostCode + '  ' + NewFrmCity)))
        THEN BEGIN
          FrmAddress := NewFrmAddress;
          FrmPostCode := NewFrmPostCode;
          FrmCity := NewFrmCity;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

