OBJECT Page 11125618 Generic Item Forecast
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Generic Item Forecast;
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    DataCaptionExpr=ProductionForecastName;
    PageType=ListPlus;
    OnOpenPage=BEGIN
                 IF (NewProductionForecastName <> '') AND (NewProductionForecastName <> ProductionForecastName) THEN
                   ProductionForecastName := NewProductionForecastName;
                 SetColumns(SetWanted::First);
               END;

    ActionList=ACTIONS
    {
      { 1100528513;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528512;1 ;ActionGroup;
                      CaptionML=ENU=F&unctions;
                      Image=Action }
      { 1100528511;2 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=Copy Generic Item Forecast;
                      RunObject=Report 11125615;
                      Image=CopyForecast }
      { 1100528514;2 ;Action    ;
                      CaptionML=ENU=Calculate Production Forecast per Item;
                      RunObject=Report 11125616;
                      Image=Calculate }
      { 1100528510;1 ;Action    ;
                      CaptionML=ENU=Previous Set;
                      ToolTipML=ENU=Previous Set;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousSet;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 SetColumns(SetWanted::Previous);
                               END;
                                }
      { 1100528509;1 ;Action    ;
                      CaptionML=ENU=Previous Column;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 SetColumns(SetWanted::PreviousColumn);
                               END;
                                }
      { 1100528508;1 ;Action    ;
                      CaptionML=ENU=Next Column;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 SetColumns(SetWanted::NextColumn);
                               END;
                                }
      { 1100528507;1 ;Action    ;
                      CaptionML=ENU=Next Set;
                      ToolTipML=ENU=Next Set;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextSet;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 SetColumns(SetWanted::Next);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100528506;0;Container;
                ContainerType=ContentArea }

    { 1100528505;1;Group  ;
                CaptionML=ENU=General }

    { 1100528504;2;Field  ;
                CaptionML=ENU=Production Forecast Name;
                SourceExpr=ProductionForecastName;
                TableRelation="Production Forecast Name".Name;
                OnValidate=BEGIN
                             SetMatrix;
                           END;
                            }

    { 1100528503;2;Field  ;
                CaptionML=ENU=Location Filter;
                SourceExpr=LocationFilter;
                OnValidate=VAR
                             Location@1000 : Record 14;
                           BEGIN
                             Location.SETFILTER(Code,LocationFilter);
                             LocationFilter := Location.GETFILTER(Code);
                             SetMatrix;
                           END;

                OnLookup=VAR
                           LocList@1000 : Page 15;
                           Loc@1001 : Record 14;
                         BEGIN
                           LocList.LOOKUPMODE(TRUE);
                           Loc.SETRANGE("Use As In-Transit",FALSE);
                           LocList.SETTABLEVIEW(Loc);
                           IF NOT (LocList.RUNMODAL = ACTION::LookupOK) THEN
                             EXIT(FALSE);

                           Text := LocList.GetSelectionFilter;

                           EXIT(TRUE);
                         END;
                          }

    { 1100528502;2;Field  ;
                CaptionML=ENU=View by;
                OptionCaptionML=ENU=Day,Week,Month,Quarter,Year,Accounting Period;
                SourceExpr=PeriodType;
                OnValidate=BEGIN
                             SetColumns(SetWanted::First);
                           END;
                            }

    { 1100528501;2;Field  ;
                CaptionML=ENU=Date Filter;
                SourceExpr=DateFilter;
                OnValidate=VAR
                             ApplicationManagement@1001 : Codeunit 1;
                           BEGIN
                             IF ApplicationManagement.MakeDateFilter(DateFilter) = 0 THEN;
                             SetColumns(SetWanted::First);
                           END;
                            }

    { 1100528500;1;Part   ;
                Name=Matrix;
                PagePartID=Page11125619 }

  }
  CODE
  {
    VAR
      MatrixRecords@1100528510 : ARRAY [32] OF Record 2000000007;
      PeriodType@1100528509 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      ProductionForecastName@1100528508 : Text[30];
      NewProductionForecastName@1100528507 : Text[30];
      LocationFilter@1100528506 : Text;
      DateFilter@1100528505 : Text[1024];
      MatrixColumnCaptions@1100528504 : ARRAY [32] OF Text[1024];
      ColumnSet@1100528503 : Text[1024];
      SetWanted@1100528502 : 'First,Previous,Same,Next,PreviousColumn,NextColumn';
      PKFirstRecInCurrSet@1100528501 : Text[100];
      CurrSetLength@1100528500 : Integer;

    PROCEDURE SetColumns@6(SetWanted@1001 : 'Initial,Previous,Same,Next,PreviousSet,NextSet');
    VAR
      MatrixMgt@1000 : Codeunit 9200;
    BEGIN
      MatrixMgt.GeneratePeriodMatrixData(SetWanted,ARRAYLEN(MatrixRecords),FALSE,PeriodType,DateFilter,PKFirstRecInCurrSet,
        MatrixColumnCaptions,ColumnSet,CurrSetLength,MatrixRecords);
      SetMatrix;
    END;

    PROCEDURE SetProductionForecastName@2(NextProductionForecastName@1000 : Text[30]);
    BEGIN
      NewProductionForecastName := NextProductionForecastName;
    END;

    PROCEDURE SetMatrix@1102601000();
    BEGIN
      CurrPage.Matrix.PAGE.Load(
        MatrixColumnCaptions,MatrixRecords,ProductionForecastName,DateFilter,LocationFilter,CurrSetLength);
      CurrPage.UPDATE(FALSE);
    END;

    BEGIN
    END.
  }
}

