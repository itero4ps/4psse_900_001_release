OBJECT Page 11125697 Copy Estimate (RAW)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Estimate;
    SourceTable=Table11012151;
    SourceTableView=WHERE(RAW Estimate=CONST(Yes));
    PageType=StandardDialog;
    OnOpenPage=VAR
                 lvEstRec@1100525000 : Record 11012151;
               BEGIN
                 //**4PS Generated Code. Do Not Change.sn01
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS Generated Code.en01
                 IF NOT GET(EstFrom) THEN Rec.INIT;  //db, 26-10-04: fill formcaption due to used sourcetable
               END;

    OnNewRecord=BEGIN
                  //**4PS Generated Code. Do Not Change.sn02
                  DPA_SetDefaultDepartment;
                  //**4PS Generated Code.en02
                END;

    OnInsertRecord=VAR
                     xRecRef@1100525000 : RecordRef;
                   BEGIN
                     //**4PS Generated Code. Do Not Change.sn05
                     xRecRef.GETTABLE(xRec);
                     DPA_SetDefaultDpmtFromNoSeries(xRecRef);
                     //**4PS Generated Code.en05
                   END;

    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK,ACTION::LookupOK];
                     END;

  }
  CONTROLS
  {
    { 1100525000;0;Container;
                ContainerType=ContentArea }

    { 1100525001;1;Group  ;
                CaptionML=ENU=Copy from;
                GroupType=Group }

    { 1100525002;2;Field  ;
                CaptionML=ENU=Estimate;
                SourceExpr=EstFrom;
                OnValidate=BEGIN
                             //dbeekman.sn: 28837
                             EstRec."No." := EstFrom;
                             EstRec.DPA_FindRecWithDPAFilterAppld;
                             //dbeekman.en: 28837

                             EstFromTxt := EstRec.Description;
                             EstToTxt := EstRec.Description;
                             FillDefaults(EstFrom);  //db, 12-04-16
                           END;

                OnLookup=BEGIN
                           EstRec.RESET;
                           EstRec.SETRANGE("RAW Estimate", TRUE);  //db, 12-04-16
                           IF PAGE.RUNMODAL(PAGE::"Estimate List", EstRec) = ACTION::LookupOK THEN BEGIN
                              EstFrom := EstRec."No.";
                              EstFromTxt := EstRec.Description;
                              EstToTxt := EstRec.Description;
                              FillDefaults(EstFrom);  //db, 12-04-16
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 1100525003;2;Field  ;
                CaptionML=ENU=Estimate Description;
                SourceExpr=EstFromTxt;
                Editable=FALSE }

    { 1100525004;2;Field  ;
                CaptionML=ENU=Norm Group;
                SourceExpr=NormGroupFrom;
                Editable=FALSE }

    { 1100525007;2;Field  ;
                CaptionML=ENU=Norm Group Description;
                SourceExpr=NormGroupFromTxt;
                Editable=FALSE }

    { 1100525008;2;Field  ;
                CaptionML=ENU=Rate Group;
                SourceExpr=RateGroupFrom;
                Editable=FALSE }

    { 1100525009;2;Field  ;
                CaptionML=ENU=Rate Group Description;
                SourceExpr=RateGroupFromTxt;
                Editable=FALSE }

    { 1100525006;1;Group  ;
                CaptionML=ENU=Copy to;
                GroupType=Group }

    { 1100525014;2;Field  ;
                Name=SelectExistingEstimate;
                CaptionML=ENU=Select Existing Estimate;
                SourceExpr=SelectExistingEstimate;
                OnValidate=BEGIN
                             //db, 09-05-08: M11303
                             IF SelectExistingEstimate = FALSE THEN BEGIN
                               EstTo := NewEst;
                               EstToTxt := EstFromTxt;
                             END ELSE BEGIN
                               EstTo := '';
                               LookupEstTo;
                             END;
                           END;
                            }

    { 1100525016;2;Field  ;
                Name=EstTo;
                CaptionML=ENU=Estimate;
                SourceExpr=EstTo;
                OnValidate=BEGIN
                             //dbeekman.sn: 28837
                             EstToTxt := EstFromTxt;
                             IF SelectExistingEstimate THEN BEGIN
                               EstRec."No." := EstTo;
                               EstRec.DPA_FindRecWithDPAFilterAppld;
                               EstToTxt := EstRec.Description
                             END ELSE
                               IF EstRec.GET(EstTo) THEN
                                 EstRec.INSERT; //show error
                             //dbeekman.en: 28837

                             IF EstTo <>'' THEN BEGIN
                               NoSeriesMgt.TestManual(EstNos);  //db, 26-08-08
                             //"No. Series" := '';  //db, 26-08-08: No. Series of new (not old) estimate become empty.
                             END;
                           END;

                OnLookup=BEGIN
                           LookupEstTo;  //db, 09-05-08: M11303
                         END;
                          }

    { 1100525017;2;Field  ;
                Name=EstToTxt;
                CaptionML=ENU=Estimate Description;
                SourceExpr=EstToTxt;
                Editable=FALSE }

    { 1100525018;2;Field  ;
                Name=CustTo;
                CaptionML=ENU=Customer No.;
                SourceExpr=CustTo;
                OnValidate=VAR
                             lvRateGrp@1100525000 : Text[30];
                           BEGIN
                             IF NOT CustRec.GET(CustTo) THEN BEGIN
                               CustTo := '';
                               CustToTxt := '';
                             END ELSE BEGIN
                               CustToTxt := CustRec.Name;
                               IF EstTypeToOpt = EstTypeToOpt::Nett THEN
                                 lvRateGrp := CustRec."Rate Group (Nett)";
                               IF EstTypeToOpt = EstTypeToOpt::Gross THEN
                                 lvRateGrp := CustRec."Rate Group (Gross)";
                               IF RateGroupRec.GET(lvRateGrp) THEN BEGIN
                                 RateGroupTo := RateGroupRec."No.";
                                 RateGroupToTxt := RateGroupRec.Description;
                               END;
                             END;
                             CheckCustContact(0);
                           END;
                            }

    { 1100525019;2;Field  ;
                Name=CustToTxt;
                CaptionML=ENU=Customer Name;
                SourceExpr=CustToTxt;
                Editable=FALSE }

    { 1100525020;2;Field  ;
                Name=ContactTo;
                CaptionML=ENU=Contact;
                SourceExpr=ContactTo;
                OnValidate=BEGIN
                             IF NOT ContactRec.GET(ContactTo) THEN BEGIN
                               ContactTo := '';
                               ContactToTxt := '';
                             END ELSE BEGIN
                               ContactToTxt := ContactRec.Name;
                             END;
                             CheckCustContact(1);
                           END;
                            }

    { 1100525021;2;Field  ;
                Name=ContactToTxt;
                CaptionML=ENU=Contact Name;
                SourceExpr=ContactToTxt;
                Editable=FALSE }

    { 1100525022;2;Field  ;
                Name=ContactPersonTo;
                CaptionML=ENU=Contact Person;
                SourceExpr=ContactPersonTo;
                OnValidate=BEGIN
                             IF NOT ContactRec.GET(ContactPersonTo) THEN BEGIN
                               ContactPersonTo := '';
                               ContactPersonToTxt := '';
                             END ELSE BEGIN
                               ContactPersonToTxt := ContactRec.Name;
                             END;
                           END;

                OnLookup=BEGIN
                           ContactRec.RESET;
                           ContactRec.SETRANGE("Company No.", ContactTo);
                           ContactRec.SETRANGE(Type, ContactRec.Type::Person);
                           PAGE.RUNMODAL(PAGE::"Contact List", ContactRec);
                         END;
                          }

    { 1100525010;2;Field  ;
                Name=ContactPersonToTxt;
                CaptionML=ENU=Contact Person Name;
                SourceExpr=ContactPersonToTxt }

    { 1100525026;2;Field  ;
                Name=NormGroupTo;
                CaptionML=ENU=Norm Group;
                SourceExpr=NormGroupTo;
                OnValidate=BEGIN
                             IF NOT NormGroupRec.GET(NormGroupTo) THEN BEGIN
                               NormGroupTo := NormGroupFrom;
                               NormGroupToTxt := NormGroupFromTxt;
                             END ELSE BEGIN
                               NormGroupToTxt := COPYSTR(NormGroupRec.Description, 1, 50);
                             END;
                           END;
                            }

    { 1100525023;2;Field  ;
                Name=NormGroupToTxt;
                CaptionML=ENU=Norm Group Description;
                SourceExpr=NormGroupToTxt;
                Editable=FALSE }

    { 1100525025;2;Field  ;
                Name=RateGroupTo;
                CaptionML=ENU=Rate Group;
                SourceExpr=RateGroupTo;
                OnValidate=BEGIN
                             IF NOT RateGroupRec.GET(RateGroupTo) THEN BEGIN
                               RateGroupTo := RateGroupFrom;
                               RateGroupToTxt := RateGroupFromTxt;
                             END ELSE BEGIN
                               RateGroupToTxt := RateGroupRec.Description;
                             END;
                           END;
                            }

    { 1100525024;2;Field  ;
                Name=RateGroupToTxt;
                CaptionML=ENU=Rate Group Description;
                SourceExpr=RateGroupToTxt;
                Editable=FALSE }

    { 1100525012;2;Field  ;
                Name=RefDateTo;
                CaptionML=ENU=Reference Date Item Prices;
                SourceExpr=RefDateTo }

    { 1100525027;2;Field  ;
                Name=DiscRef1To;
                CaptionML=ENU=Discount Term Group (Customer);
                SourceExpr=DiscRef1To;
                OnValidate=BEGIN
                             IF NOT DiscRefRec.GET(DiscRefRec.Level::Customer, DiscRef1To) THEN BEGIN
                               DiscRef1To := DiscRef1From;
                               DiscRef1ToTxt := DiscRef1FromTxt;
                             END ELSE BEGIN
                               DiscRef1ToTxt := DiscRefRec.Description;
                             END;
                           END;
                            }

    { 1100525028;2;Field  ;
                Name=DiscRef1ToTxt;
                CaptionML=ENU=Discount Term Group Description (Customer);
                SourceExpr=DiscRef1ToTxt;
                Editable=FALSE }

    { 1100525029;2;Field  ;
                Name=DiscRef2To;
                CaptionML=ENU=Discount Term Group (Job);
                SourceExpr=DiscRef2To;
                OnValidate=BEGIN
                             IF NOT DiscRefRec.GET(DiscRefRec.Level::Job, DiscRef2To) THEN BEGIN
                               DiscRef2To := DiscRef2From;
                               DiscRef2ToTxt := DiscRef2FromTxt;
                             END ELSE BEGIN
                               DiscRef2ToTxt := DiscRefRec.Description;
                             END;
                           END;
                            }

    { 1100525030;2;Field  ;
                Name=DiscRef2ToTxt;
                CaptionML=ENU=Discount Term Group Description (Job);
                SourceExpr=DiscRef2ToTxt;
                Editable=FALSE }

    { 1100525031;1;Group  ;
                CaptionML=ENU=Apply;
                GroupType=Group }

    { 1100525032;2;Field  ;
                Name=CopyTryOut;
                CaptionML=ENU=Copy Try-Out Successor;
                SourceExpr=CopyTryOut }

    { 1100525033;2;Field  ;
                Name=SkipLines;
                CaptionML=ENU=Skip Lines;
                SourceExpr=SkipLines }

    { 1100525034;2;Field  ;
                Name=CopyPriceAgreement;
                CaptionML=ENU=Copy Price Agreement;
                SourceExpr=CopyPriceAgreement }

  }
  CODE
  {
    VAR
      EstFrom@1100525052 : Code[20];
      EstTo@1100525051 : Code[20];
      NewEst@1100525050 : Code[20];
      EstFromTxt@1100525049 : Text[50];
      EstToTxt@1100525048 : Text[50];
      EstNos@1100525047 : Code[20];
      SubEstFrom@1100525046 : Code[20];
      SubEstFromTxt@1100525045 : Text[50];
      EstTypeFromOpt@1100525044 : 'Nett,Gross';
      EstTypeToOpt@1100525043 : 'Nett,Gross';
      RateGroupFrom@1100525042 : Code[20];
      NormGroupFrom@1100525041 : Code[20];
      RateGroupFromTxt@1100525040 : Text[50];
      NormGroupFromTxt@1100525039 : Text[50];
      RateGroupTo@1100525038 : Code[20];
      NormGroupTo@1100525037 : Code[20];
      RateGroupToTxt@1100525036 : Text[50];
      NormGroupToTxt@1100525035 : Text[50];
      ContactTo@1100525034 : Code[20];
      ContactToTxt@1100525033 : Text[50];
      ContactPersonTo@1100525032 : Code[20];
      ContactPersonToTxt@1100525031 : Text[50];
      CustTo@1100525030 : Code[20];
      CustToTxt@1100525029 : Text[50];
      CustRec@1100525028 : Record 18;
      ContactRec@1100525027 : Record 5050;
      RateGroupRec@1100525026 : Record 11012161;
      NormGroupRec@1100525025 : Record 11012164;
      EstRec@1100525024 : Record 11012151;
      EstSetupRec@1100525023 : Record 11012150;
      SerLineRec@1100525022 : Record 309;
      SerRelRec@1100525021 : Record 310;
      NoSeriesMgt@1100525020 : Codeunit 396;
      UpdateEst@1100525019 : Codeunit 11125671;
      FormatMgt@1100525018 : Codeunit 11012022;
      CopyTryOut@1100525017 : Boolean;
      SkipLines@1100525016 : Boolean;
      SelectExistingEstimate@1100525015 : Boolean;
      RefDateTo@1100525014 : Date;
      CopySumSheet@1100525013 : 'EstFrom,EstSource,Empty';
      CopiedEstTo@1100525012 : Code[20];
      DiscRef1From@1100525011 : Code[20];
      DiscRef2From@1100525010 : Code[20];
      DiscRef1To@1100525009 : Code[20];
      DiscRef2To@1100525008 : Code[20];
      DiscRef1FromTxt@1100525007 : Text[50];
      DiscRef2FromTxt@1100525006 : Text[50];
      DiscRef1ToTxt@1100525005 : Text[50];
      DiscRef2ToTxt@1100525004 : Text[50];
      DiscRefRec@1100525003 : Record 11012338;
      RawSetup@1100525002 : Record 11125673;
      DPA_IsHardFilter@1100525001 : Boolean;
      CopyPriceAgreement@1100525000 : Boolean;
      Text000@1100525054 : TextConst 'ENU=Estimate does not exist';
      Text001@1100525053 : TextConst 'ENU=Estimate already exists';
      CloseActionOK@1100528400 : Boolean;

    PROCEDURE FillDefaults@1(lEstFrom@11012000 : Code[20]);
    BEGIN
      EstFrom := lEstFrom;
      IF EstRec.GET(EstFrom) THEN BEGIN
        EstRec.TESTFIELD("RAW Estimate", TRUE);  //db, 12-04-16
        EstFromTxt  := EstRec.Description;
        //EstTypeFromOpt := EstRec."Estimate Type";   //ISS

        NormGroupFrom := EstRec."Norm Group";
        IF NOT NormGroupRec.GET(NormGroupFrom) THEN NormGroupRec.INIT;
        NormGroupFromTxt := COPYSTR(NormGroupRec.Description, 1, 50);

        RateGroupFrom := EstRec."Rate Group";
        IF NOT RateGroupRec.GET(RateGroupFrom) THEN RateGroupRec.INIT;
        RateGroupFromTxt := RateGroupRec.Description;

        IF EstTypeFromOpt = EstTypeFromOpt::Nett THEN
          DiscRef1From := EstRec."Purch Discount Term Group 1"
        ELSE
          DiscRef1From := EstRec."Sales Discount Term Group 1";
        IF NOT DiscRefRec.GET(DiscRefRec.Level::Customer, DiscRef1From) THEN DiscRefRec.INIT;
        DiscRef1FromTxt := DiscRefRec.Description;

        IF EstTypeFromOpt = EstTypeFromOpt::Nett THEN
          DiscRef2From := EstRec."Purch Discount Term Group 2"
        ELSE
          DiscRef2From := EstRec."Sales Discount Term Group 2";
        IF NOT DiscRefRec.GET(DiscRefRec.Level::Job, DiscRef2From) THEN DiscRefRec.INIT;
        DiscRef2FromTxt := DiscRefRec.Description;

        CustTo := EstRec."Customer No.";
        IF NOT CustRec.GET(CustTo) THEN CustRec.INIT;
        CustToTxt := CustRec.Name;

        EstRec.CALCFIELDS("Contact Name", "Contact Person Name");
        ContactTo := EstRec."Contact No.";
        ContactToTxt := EstRec."Contact Name";
        ContactPersonTo := EstRec."Contact Person No.";
        ContactPersonToTxt := EstRec."Contact Person Name";
        RefDateTo := EstRec."Reference Date (Item)";

        DetermineNewEstimate;
        EstTo    := NewEst;
        EstToTxt := EstFromTxt;
        EstTypeToOpt := EstTypeFromOpt;
        NormGroupTo := NormGroupFrom;
        RateGroupTo := RateGroupFrom;
        DiscRef1To := DiscRef1From;
        DiscRef2To := DiscRef2From;
        NormGroupToTxt := NormGroupFromTxt;
        RateGroupToTxt := RateGroupFromTxt;
        DiscRef1ToTxt := DiscRef1FromTxt;
        DiscRef2ToTxt := DiscRef2FromTxt;
      END;
    END;

    PROCEDURE FillDefaultRentalContract@1100485002(ICustomerNo@1100485000 : Code[20];IContactNo@1100485001 : Code[20];IDescription@1100485002 : Text[50]);
    BEGIN
      CustTo := ICustomerNo;
      IF NOT CustRec.GET(CustTo) THEN CustRec.INIT;
      CustToTxt := CustRec.Name;
      ContactTo := IContactNo;
      IF NOT ContactRec.GET(ContactTo) THEN ContactRec.INIT;
      ContactToTxt := ContactRec.Name;
      EstToTxt := IDescription;
    END;

    PROCEDURE DetermineNewEstimate@1000000000();
    BEGIN
      CLEAR(NoSeriesMgt);  //db, 09-05-08: M11303

      IF EstRec."No. Series" <> '' THEN BEGIN
        EstNos := EstRec."No. Series";
      END ELSE BEGIN
        //ISS.sn
        RawSetup.GET;
        EstNos := RawSetup."Estimate Nos.";
        EstSetupRec.GET;
        //IF EstRec."Service Estimate" THEN  //db, 26-08-08
        //  EstNos := EstSetupRec."Service Estimate Nos."
        //ELSE
        //  EstNos := EstSetupRec."Estimate Nos.";
        //ISS.en
      END;

      SerRelRec.SETRANGE(Code, EstNos);
      IF SerRelRec.FINDSET THEN BEGIN
        REPEAT
          SerLineRec.SETRANGE("Series Code", SerRelRec."Series Code");
          IF SerLineRec.FINDSET(FALSE, FALSE) THEN BEGIN
            REPEAT
              IF (SerLineRec.Open = TRUE) AND
                 (EstFrom >= SerLineRec."Starting No.") AND
                 ((EstFrom < SerLineRec."Ending No.") OR (SerLineRec."Ending No." = '')) THEN
                EstNos := SerLineRec."Series Code";
            UNTIL SerLineRec.NEXT = 0;
          END;
        UNTIL SerRelRec.NEXT = 0;
      END;

      NewEst := NoSeriesMgt.GetNextNo(EstNos,0D,FALSE);
    END;

    PROCEDURE ValidateEstimate@1210190001();
    VAR
      EstLineRec@1210190000 : Record 11125680;
      lvText000@1210190001 : TextConst 'ENU=Estimate Lines present in Destination.';
    BEGIN
      EstLineRec.RESET;
      EstLineRec.SETRANGE("Estimate No.", EstTo);
      //IF SubEstFrom <> '' THEN
      //  EstLineRec.SETFILTER("Sub-Estimate No.", SubEstFrom);
      IF EstLineRec.FINDFIRST THEN
        ERROR(lvText000);

      IF SelectExistingEstimate THEN BEGIN
        EstRec.GET(EstTo);
      END ELSE BEGIN
        IF EstRec.GET(EstTo) THEN BEGIN
          DetermineNewEstimate;
          EstTo := NewEst;
          ERROR(Text001);
        END;
      END;
    END;

    PROCEDURE StartUpdate@1210190000();
    VAR
      CopyCu@1210190000 : Codeunit 11125672;
      TryOutRec@1100485000 : Record 11012761;
      EstLineRec@1100485001 : Record 11125680;
      SelectType@1100485002 : Integer;
    BEGIN
      IF EstRec.GET(EstTo) THEN
        EstRec.TESTFIELD(Fixed, FALSE);
      ValidateEstimate;

      //IF CopySumSheet = CopySumSheet::EstFrom THEN
      //  SelectType := 0
      //ELSE
      //  SelectType := EstTypeToOpt + 1;  //db, 10-10-07

      SelectType := CopySumSheet; // 19-11-2008

      CopyCu.CopyEst(EstFrom, SubEstFrom, EstTo, EstToTxt, EstTypeToOpt,
        NormGroupTo, RateGroupTo, CustTo, RefDateTo, DiscRef1To, DiscRef2To, SkipLines, SelectType,
        CopyPriceAgreement); //mg.c, 11-10-11: M29108

      CLEAR(NoSeriesMgt);     //Enforce re-read of 'NoSeriesLine' record at next 'GetNextNo' (with FALSE)
      NewEst := NoSeriesMgt.GetNextNo(EstNos,0D,FALSE);
      IF NewEst = EstTo THEN
        NewEst := NoSeriesMgt.GetNextNo(EstNos,0D,TRUE);

      IF NormGroupFrom <> NormGroupTo THEN BEGIN
        NormGroupRec.GET(NormGroupTo);
        EstLineRec.SETRANGE("Estimate No.", EstTo);  //db, 20-08-07
        UpdateEst.ReplaceNormGroup(EstLineRec, NormGroupFrom, NormGroupTo, TRUE);
      END;
      IF RateGroupFrom <> RateGroupTo THEN BEGIN
        RateGroupRec.GET(RateGroupTo);
        UpdateEst.ReplaceRateGroup(EstTo, RateGroupFrom, RateGroupTo, TRUE);
      END;

      IF CopyTryOut AND TryOutRec.READPERMISSION THEN
        TryOutRec.CopyRelation(1, 1, EstFrom, EstTo);  //db, 04-10-07

      //fill keyfields of sourcetable to refresh mainform with copied data:
      "No." := EstTo;
      GET("No.");
      "Contact No." := ContactTo;
      "Contact Person No." := ContactPersonTo;
      MODIFY;
    END;

    PROCEDURE CheckCustContact@1100485000(lvAction@1100485001 : Integer);
    VAR
      ContBusRel@1100485000 : Record 5054;
      NumContact@1100485002 : Integer;
    BEGIN
      //db, 22-02-08
      IF lvAction = 0 THEN BEGIN
        ContBusRel.RESET;
        ContBusRel.SETCURRENTKEY("Link to Table", "No.");
        ContBusRel.SETRANGE("Link to Table", ContBusRel."Link to Table"::Customer);
        ContBusRel.SETRANGE("No.", CustTo);
        IF ContBusRel.FINDFIRST THEN BEGIN
          ContBusRel.CALCFIELDS("Contact Name");
          ContactTo := ContBusRel."Contact No.";
          ContactToTxt := ContBusRel."Contact Name";
        END;
      END;
      IF lvAction = 1 THEN BEGIN
        ContBusRel.RESET;
        ContBusRel.SETCURRENTKEY("Contact No.", "Business Relation Code");
        ContBusRel.SETRANGE("Contact No.", ContactTo);
        ContBusRel.SETRANGE("Link to Table", ContBusRel."Link to Table"::Customer);
        IF ContBusRel.FINDFIRST THEN BEGIN
          ContBusRel.CALCFIELDS("Contact Name");
          CustTo := ContBusRel."No.";
          CustToTxt := ContBusRel."Contact Name";
        END;
      END;

      ContactPersonTo := '';
      ContactPersonToTxt := '';

      ContactRec.RESET;
      ContactRec.SETRANGE("Company No.", ContactTo);
      ContactRec.SETRANGE(Type, ContactRec.Type::Person);
      NumContact := ContactRec.COUNT;
      IF NumContact = 1 THEN BEGIN
        IF ContactRec.FINDFIRST THEN BEGIN
          ContactPersonTo := ContactRec."No.";
          ContactPersonToTxt := ContactRec.Name;
        END;
      END;
    END;

    PROCEDURE LookupEstTo@1100485001();
    BEGIN
      EstRec.RESET;
      IF PAGE.RUNMODAL(PAGE::"Estimate List", EstRec) = ACTION::LookupOK THEN BEGIN
        IF SelectExistingEstimate = FALSE THEN EXIT;  //db, 09-05-08: M11303
        EstTo := EstRec."No.";
        EstToTxt := EstRec.Description;
        ValidateEstimate;
      END;
      CurrPage.UPDATE(TRUE);
    END;

    PROCEDURE GetCopiedEstTo@1100485003(VAR EstNo@1100485000 : Code[20]);
    BEGIN
      EstNo := CopiedEstTo;
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528400() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      StartUpdate;
      CopiedEstTo := EstTo;
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    PROCEDURE GetNewEstimate@1100525000() : Code[20];
    BEGIN
      //C028591: db, 19-01-16
      EXIT(EstTo);
    END;

    BEGIN
    END.
  }
}

