OBJECT Page 11125948 Plant Meter Reading
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Plant Meter Reading;
    SourceTable=Table11072671;
    DelayedInsert=Yes;
    PageType=List;
    OnAfterGetRecord=BEGIN
                       GetLineData();
                       SetEditable();
                     END;

    OnNewRecord=BEGIN
                  IF NOT CurrPage.EDITABLE THEN BEGIN
                    "Plant Type" := '';
                    "Plant No." := '';
                    "Plant Description" := '';
                  END ELSE BEGIN
                    FillDefaultMeterReadingType();
                    IF Date = 0D THEN
                      Date := TODAY;
                  END;
                END;

    OnAfterGetCurrRecord=BEGIN
                           GetLineData();
                           SetEditable();
                         END;

  }
  CONTROLS
  {
    { 1100529000;0;Container;
                ContainerType=ContentArea }

    { 1100529001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100529002;2;Field  ;
                SourceExpr="Plant Type";
                Editable=KeyFieldsEditable;
                OnValidate=BEGIN
                             GetLineData();
                           END;
                            }

    { 1100529003;2;Field  ;
                SourceExpr="Plant No.";
                Editable=KeyFieldsEditable;
                OnValidate=BEGIN
                             FillDefaultMeterReadingType();
                             GetLineData();
                           END;
                            }

    { 1100529004;2;Field  ;
                SourceExpr="Plant Description" }

    { 1100529005;2;Field  ;
                SourceExpr="Inspection No.";
                Visible=FALSE }

    { 1100529006;2;Field  ;
                SourceExpr="Meter Reading Type";
                Editable=KeyFieldsEditable;
                OnValidate=BEGIN
                             GetLineData();
                           END;
                            }

    { 1100529008;2;Field  ;
                SourceExpr=Date;
                Editable=KeyFieldsEditable;
                OnValidate=BEGIN
                             GetLineData();
                           END;
                            }

    { 1100529010;2;Field  ;
                CaptionML=ENU=Previous Date;
                SourceExpr=PreviousMeterReading.Date;
                Visible=FALSE;
                Editable=FALSE }

    { 1100529011;2;Field  ;
                CaptionML=ENU=Previous;
                DecimalPlaces=0:0;
                SourceExpr=PreviousMeterReading."Meter Reading";
                Visible=FALSE;
                Editable=FALSE }

    { 1100529009;2;Field  ;
                SourceExpr="Meter Reading";
                OnValidate=VAR
                             MeterReading@1100409000 : Record 11072671;
                           BEGIN
                             //T005361.sn
                             IF Date < TODAY THEN BEGIN
                               IF MeterReading.GET("Plant Type", "Plant No.", "Meter Reading Type", Date, "Service Order") THEN BEGIN
                                 IF NOT CONFIRM(Text001, FALSE,
                                   FIELDCAPTION("Meter Reading"), FIELDCAPTION("Plant No."), "Plant Type", "Plant No.",
                                   FIELDCAPTION("Meter Reading Type"), "Meter Reading Type", FIELDCAPTION(Date), Date)
                                 THEN
                                   "Meter Reading" := MeterReading."Meter Reading";
                               END;
                             END;
                             //T005361.en
                             CurrPage.UPDATE;
                             GetLineData();
                           END;
                            }

    { 1100529007;2;Field  ;
                SourceExpr="Unit of Measure" }

    { 1100529013;2;Field  ;
                CaptionML=ENU=Next Inspection at;
                DecimalPlaces=0:0;
                BlankZero=Yes;
                SourceExpr=NextInspectionAt;
                Editable=FALSE }

    { 1100529012;2;Field  ;
                CaptionML=ENU=Available;
                DecimalPlaces=0:0;
                SourceExpr=Available;
                Editable=FALSE;
                HideValue=AvailableHideValue }

    { 1100528800;2;Field  ;
                SourceExpr="Service Order" }

  }
  CODE
  {
    VAR
      PreviousMeterReading@1100529000 : Record 11072671;
      KeyFieldsEditable@1100409001 : Boolean INDATASET;
      AvailableHideValue@1100529003 : Boolean INDATASET;
      NextInspectionAt@1100529002 : Decimal;
      Available@1100529001 : Decimal;
      Text001@1100409000 : TextConst 'ENU=Modify %1, %2 ''%3-%4'', %5 ''%6'', %7 ''%8''. Are you sure?';

    LOCAL PROCEDURE FillDefaultMeterReadingType@1100529001();
    BEGIN
      IF ("Plant Type" <> '') AND ("Plant No." <> '') AND ("Meter Reading Type" = '') THEN BEGIN
        IF ("Plant Type" = xRec."Plant Type") AND ("Plant No." = xRec."Plant No.") AND (xRec."Meter Reading Type" <> '') THEN
          "Meter Reading Type" := xRec."Meter Reading Type"
        ELSE
          "Meter Reading Type" := DefaultMeterReadingType("Plant Type", "Plant No.");
        CALCFIELDS("Unit of Measure");
      END;
    END;

    LOCAL PROCEDURE GetLineData@1100529000();
    BEGIN
      GetPreviousMeterReadingRecord(Rec, PreviousMeterReading);
      NextInspectionAt := NextMeterReadingForInspections("Plant Type", "Plant No.", "Meter Reading Type");
      IF NextInspectionAt = 0 THEN BEGIN
        Available := 0;
        AvailableHideValue := TRUE;
      END ELSE BEGIN
        Available := NextInspectionAt - GetActualMeterReading("Plant Type", "Plant No.", "Meter Reading Type", "Service Order");
        AvailableHideValue := FALSE;
      END;
    END;

    PROCEDURE SetEditable@1100409000();
    VAR
      MeterReading@1100409000 : Record 11072671;
    BEGIN
      //T005361.n
      KeyFieldsEditable := TRUE;
      IF ("Plant Type" <> '') AND ("Plant No." <> '') AND ("Meter Reading Type" <> '') AND (Date <> 0D) THEN BEGIN
        IF MeterReading.GET("Plant Type", "Plant No.", "Meter Reading Type", Date, "Service Order") THEN
          KeyFieldsEditable := FALSE;
      END;
    END;

    BEGIN
    END.
  }
}

