OBJECT Page 11126401 Copy Sub.Port. Vendor
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Subcontractor Portal Vendor;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    PageType=StandardDialog;
    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK,ACTION::LookupOK];
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100525004;1;Group  ;
                CaptionML=ENU=Copy from;
                GroupType=Group }

    { 1100528601;2;Field  ;
                CaptionML=ENU=Vendor No.;
                SourceExpr=FromVendorNo;
                TableRelation="Subcont.Port. Vendor";
                OnValidate=VAR
                             SubcontPortVendor@1100528600 : Record 11126400;
                           BEGIN
                             IF FromVendorNo <> '' THEN
                               SubcontPortVendor.GET(FromVendorNo);
                           END;
                            }

    { 1100525001;1;Group  ;
                CaptionML=ENU=Copy to;
                GroupType=Group }

    { 1210190002;2;Field  ;
                CaptionML=ENU=Vendor No.;
                SourceExpr=ToVendorNo;
                TableRelation=Vendor }

  }
  CODE
  {
    VAR
      FromVendorNo@1210190006 : Code[20];
      ToVendorNo@1210190001 : Code[20];
      Text001@1100528300 : TextConst 'ENU=%1 must differ from the ''From'' Value.';
      Text002@1100528600 : TextConst 'ENU=%1 %2 already exists, copy setup fields?';
      ToVendorNoMess@1210190010 : TextConst 'ENU=To Vendor No.';
      CloseActionOK@1100528400 : Boolean;

    PROCEDURE SetStartValues@1210190000(ISubcontPortVendor@1210190000 : Record 11126400);
    BEGIN
      FromVendorNo := ISubcontPortVendor."Vendor No.";
      ToVendorNo := ISubcontPortVendor."Vendor No.";
    END;

    PROCEDURE CheckAndCopy@1210190001();
    VAR
      FromSubcontPortVendor@1100528600 : Record 11126400;
      ToSubcontPortVendor@1100528601 : Record 11126400;
    BEGIN
      IF ToVendorNo = FromVendorNo THEN
        ERROR(Text001, ToVendorNoMess);

      FromSubcontPortVendor.GET(FromVendorNo);
      IF NOT ToSubcontPortVendor.GET(ToVendorNo) THEN BEGIN
        ToSubcontPortVendor := FromSubcontPortVendor;
        ToSubcontPortVendor."Vendor No." := ToVendorNo;
        ToSubcontPortVendor.INSERT(TRUE);
      END ELSE BEGIN
        IF CONFIRM(STRSUBSTNO(Text002, ToSubcontPortVendor.TABLECAPTION, ToSubcontPortVendor."Vendor No.")) THEN BEGIN
          ToSubcontPortVendor.VALIDATE("Past Visible Weeks", FromSubcontPortVendor."Past Visible Weeks");
          ToSubcontPortVendor.VALIDATE("Future Visible Weeks", FromSubcontPortVendor."Future Visible Weeks");
          ToSubcontPortVendor.VALIDATE("Show Blocked Locations", FromSubcontPortVendor."Show Blocked Locations");
          ToSubcontPortVendor.VALIDATE("Contract Related", FromSubcontPortVendor."Contract Related");
          ToSubcontPortVendor.VALIDATE("Call Succession", FromSubcontPortVendor."Call Succession");
          ToSubcontPortVendor.VALIDATE("Additional Orders", FromSubcontPortVendor."Additional Orders");
          ToSubcontPortVendor.VALIDATE("User Maintenance", FromSubcontPortVendor."User Maintenance");
          ToSubcontPortVendor.VALIDATE("Document Management", FromSubcontPortVendor."Document Management");
          ToSubcontPortVendor.VALIDATE(Attachments, FromSubcontPortVendor.Attachments);
          ToSubcontPortVendor.VALIDATE("Allow Status Changes", FromSubcontPortVendor."Allow Status Changes");
          ToSubcontPortVendor.VALIDATE("Show WO by Act. Ref. Point", FromSubcontPortVendor."Show WO by Act. Ref. Point");
          ToSubcontPortVendor.MODIFY(TRUE);
        END;
      END;
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528400() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      CheckAndCopy;
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    BEGIN
    END.
  }
}

