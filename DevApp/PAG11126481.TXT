OBJECT Page 11126481 Settlement Sheet SF
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=Settlement Sheet List;
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table11012021;
    PageType=List;
    SourceTableTemporary=Yes;
    ActionList=ACTIONS
    {
      { 1100528216;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528215;1 ;Action    ;
                      CaptionML=ENU=Card;
                      RunObject=Page 11012046;
                      RunPageLink=Project No.=FIELD(Project No.),
                                  Principal=FIELD(Principal),
                                  Commission No.=FIELD(Commission No.),
                                  No.=FIELD(No.);
                      Image=EditLines }
      { 1100528212;1 ;Action    ;
                      CaptionML=ENU=Assign Settlement Sheets;
                      Promoted=Yes;
                      Image=SelectLineToApply;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ProjectCollectiveList@1100525000 : Record 11071875;
                               BEGIN
                                 ProjectCollectiveList.GET(CollectiveListNo);
                                 ProjectCollectiveList.AssignSettlementSheets();
                                 RefreshSettlementSheets(CollectiveListNo);
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1100528211;1 ;Action    ;
                      CaptionML=ENU=Unassign Settlement Sheets;
                      Promoted=Yes;
                      Image=ReverseLines;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 RemoveSettlementSheetFromList;
                                 RefreshSettlementSheets(CollectiveListNo);
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100528200;0;Container;
                ContainerType=ContentArea }

    { 1100528201;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100528202;2;Field  ;
                SourceExpr="Project No." }

    { 1100528203;2;Field  ;
                SourceExpr=Principal }

    { 1100528204;2;Field  ;
                SourceExpr="Commission No." }

    { 1100528205;2;Field  ;
                SourceExpr="No." }

    { 1100528206;2;Field  ;
                SourceExpr=Description }

    { 1100528207;2;Field  ;
                SourceExpr="Expected Invoice Date" }

    { 1100525000;2;Field  ;
                SourceExpr="To Invoice" }

    { 1100528208;2;Field  ;
                SourceExpr=Status }

  }
  CODE
  {
    VAR
      CollectiveListNo@1100528200 : Code[20];
      Text001@1100525000 : TextConst 'ENU=Decouple Settlementsheets (%1)?';

    PROCEDURE RefreshSettlementSheets@1100528602(ICollectiveListNo@1100528600 : Code[20]);
    VAR
      ProjectCollectiveList@1100528603 : Record 11071875;
      SettlementSheet@1100528601 : Record 11012021;
    BEGIN
      RESET;
      DELETEALL;

      CollectiveListNo := ICollectiveListNo;

      ProjectCollectiveList.GET(ICollectiveListNo);
      ProjectCollectiveList.MarkSettlementSheets(SettlementSheet);

      IF SettlementSheet.FINDSET THEN
        REPEAT
          Rec := SettlementSheet;
          INSERT;
        UNTIL SettlementSheet.NEXT = 0;
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE ShowSettlementSheetCard@1100528600();
    BEGIN
      PAGE.RUNMODAL(PAGE::"Settlement Sheet", Rec);
    END;

    PROCEDURE RemoveSettlementSheetFromList@1100528601();
    VAR
      SettlementSheet@1100528600 : Record 11012021;
    BEGIN
      IF "No." = '' THEN EXIT;

      CurrPage.SETSELECTIONFILTER(SettlementSheet);  //CALL C013884
      IF SettlementSheet.ISEMPTY THEN BEGIN
        SettlementSheet.COPY(Rec);
        SettlementSheet.SETRECFILTER;
      END;
      // SettlementSheet.MODIFYALL("Collective List No.", '', TRUE);
      //CALL C013884
      IF SettlementSheet.FINDSET(TRUE,FALSE) THEN BEGIN
        IF CONFIRM(Text001,FALSE, SettlementSheet.COUNT) THEN
          REPEAT
            SettlementSheet.VALIDATE("Collective List No.", '');
            SettlementSheet.MODIFY(TRUE);
          UNTIL SettlementSheet.NEXT = 0;
      END;
      //

      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

