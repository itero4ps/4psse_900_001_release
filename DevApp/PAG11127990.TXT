OBJECT Page 11127990 Symbrio Item Buffer Update
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PSSE;
  }
  PROPERTIES
  {
    CaptionML=ENU=Update Fields in Symbrio Item Buffer;
    SourceTable=Table11127976;
    PageType=NavigatePage;
    ActionList=ACTIONS
    {
      { 1100285000;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100285001;1 ;Action    ;
                      Name=RunUpdate;
                      CaptionML=ENU=Run Update;
                      PromotedIsBig=Yes;
                      Image=Absence;
                      OnAction=BEGIN
                                 IF (gvUpdateEDIDiscount)
                                   OR (gvUpdateDiscount2)
                                   OR (gvUpdateDiscount3)
                                   OR (gvUpdateDiscount4)
                                   OR (gvUpdateDiscount5)
                                   OR (gvUpdateDiscount6)
                                   OR (gvUpdateDateReceived)
                                   OR (gvUpdateCostObject)
                                   OR (gvUpdatePriceListCode)
                                   OR (gvUpdateAltUnit)
                                   OR (gvUpdateAltUnitFactor)
                                   OR (gvCreateNetPriceList)
                                   OR (gvCleanupItemCodeStrings)
                                   OR (gvCleanupItemDescrStrings)
                                   OR ((gvConvertStringFrom <> '') AND (gvConvertStringTo <> '')) THEN BEGIN
                                     IF ( CONFIRM(Text003) ) THEN
                                       UpdateFields();
                                 END ELSE BEGIN
                                   ERROR(Text006);
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100285002;;Container;
                Name=Content;
                ContainerType=ContentArea }

    { 1100285008;1;Group  ;
                Name=FilterGroup;
                CaptionML=ENU=Filter;
                GroupType=GridLayout }

    { 1100285009;2;Field  ;
                Name=UseFilterOption;
                CaptionML=ENU=Filter Options;
                OptionCaptionML=ENU=User filter in Article buffer, All Records;
                SourceExpr=gvUseFilterOption;
                Importance=Promoted }

    { 1100285005;1;Group  ;
                Name=EDIDiscountGroup;
                CaptionML=ENU=EDI Discount;
                GroupType=GridLayout;
                Layout=Columns }

    { 1100285003;2;Field  ;
                Name=EDIDiscount;
                CaptionML=ENU=New value;
                SourceExpr=gvEdiDiscount;
                Visible=TRUE;
                OnValidate=BEGIN
                             IF gvEdiDiscount <> '' THEN BEGIN
                               IF NOT CheckNumeric( gvEdiDiscount ) THEN
                                 ERROR(Text002, gvEdiDiscount);
                               gvUpdateEDIDiscount := TRUE;
                             END;
                           END;
                            }

    { 1100285004;2;Field  ;
                Name=UpdateEdiDiscount;
                CaptionML=ENU=Update;
                SourceExpr=gvUpdateEDIDiscount;
                Importance=Promoted }

    { 1100285006;1;Group  ;
                Name=Discount2Group;
                CaptionML=ENU=Discount 2;
                GroupType=GridLayout }

    { 1100285007;2;Field  ;
                Name=Discount2;
                CaptionML=ENU=New value;
                SourceExpr=gvDiscount2;
                OnValidate=BEGIN
                             IF gvDiscount2 <> '' THEN BEGIN
                               IF NOT CheckNumeric( gvDiscount2 ) THEN
                                 ERROR(Text002, gvDiscount2 );
                               gvUpdateDiscount2 := TRUE;
                             END;
                           END;
                            }

    { 1100285010;2;Field  ;
                Name=UpdateDiscount2;
                CaptionML=ENU=Update;
                SourceExpr=gvUpdateDiscount2;
                Importance=Promoted }

    { 1100285011;1;Group  ;
                Name=Discount3Group;
                CaptionML=ENU=Discount 3;
                GroupType=GridLayout }

    { 1100285012;2;Field  ;
                Name=Discount3;
                CaptionML=ENU=New value;
                SourceExpr=gvDiscount3;
                OnValidate=BEGIN
                             IF gvDiscount3 <> '' THEN BEGIN
                               IF NOT CheckNumeric( gvDiscount3 ) THEN
                                 ERROR(Text002, gvDiscount3 );
                               gvUpdateDiscount3 := TRUE;
                             END;
                           END;
                            }

    { 1100285013;2;Field  ;
                Name=UpdateDiscount3;
                CaptionML=ENU=Update;
                SourceExpr=gvUpdateDiscount3;
                Importance=Promoted }

    { 1100285014;1;Group  ;
                Name=Discount4Group;
                CaptionML=ENU=Discount 4;
                GroupType=GridLayout }

    { 1100285015;2;Field  ;
                Name=Discount4;
                CaptionML=ENU=New value;
                SourceExpr=gvDiscount4;
                OnValidate=BEGIN
                             IF gvDiscount4 <> '' THEN BEGIN
                               IF NOT CheckNumeric( gvDiscount4 ) THEN
                                 ERROR(Text002, gvDiscount4 );
                               gvUpdateDiscount4 := TRUE;
                             END;
                           END;
                            }

    { 1100285016;2;Field  ;
                Name=UpdateDiscount4;
                CaptionML=ENU=Update;
                SourceExpr=gvUpdateDiscount4;
                Importance=Promoted }

    { 1100285017;1;Group  ;
                Name=Discount5Group;
                CaptionML=ENU=Discount 5;
                GroupType=GridLayout }

    { 1100285018;2;Field  ;
                Name=Discount5;
                CaptionML=ENU=New value;
                SourceExpr=gvDiscount5;
                OnValidate=BEGIN
                             IF gvDiscount5 <> '' THEN BEGIN
                               IF NOT CheckNumeric( gvDiscount5 ) THEN
                                 ERROR(Text002, gvDiscount5 );
                               gvUpdateDiscount5 := TRUE;
                             END;
                           END;
                            }

    { 1100285019;2;Field  ;
                Name=UpdateDiscount5;
                CaptionML=ENU=Update;
                SourceExpr=gvUpdateDiscount5;
                Importance=Promoted }

    { 1100285020;1;Group  ;
                Name=Discount6Group;
                CaptionML=ENU=Discount 6;
                GroupType=GridLayout }

    { 1100285021;2;Field  ;
                Name=Discount6;
                CaptionML=ENU=New value;
                SourceExpr=gvDiscount6;
                OnValidate=BEGIN
                             IF gvDiscount6 <> '' THEN BEGIN
                               IF NOT CheckNumeric( gvDiscount6 ) THEN
                                 ERROR(Text002, gvDiscount6 );
                               gvUpdateDiscount6 := TRUE;
                             END;
                           END;
                            }

    { 1100285022;2;Field  ;
                Name=UpdateDiscount6;
                CaptionML=ENU=Update;
                SourceExpr=gvUpdateDiscount6;
                Importance=Promoted }

    { 1100285023;1;Group  ;
                Name=PriceDateGroup;
                CaptionML=ENU=Date received;
                GroupType=GridLayout }

    { 1100285024;2;Field  ;
                Name=DateReceived;
                CaptionML=ENU=New Date;
                SourceExpr=gvDateReceived;
                OnValidate=BEGIN
                             IF gvDateReceived <> 0D THEN
                               gvUpdateDateReceived := TRUE;
                           END;
                            }

    { 1100285025;2;Field  ;
                Name=UpdateDateReceived;
                CaptionML=ENU=Update;
                SourceExpr=gvUpdateDateReceived;
                Importance=Promoted }

    { 1100285026;1;Group  ;
                Name=CostObjectGroup;
                CaptionML=ENU=Cost Object;
                GroupType=GridLayout }

    { 1100285027;2;Field  ;
                Name=CostObject;
                CaptionML=ENU=New value;
                SourceExpr=gvCostObject;
                TableRelation="Dimension Value".Code WHERE (Cost Type=FILTER(Material),
                                                            Global Dimension No.=CONST(2));
                OnValidate=BEGIN
                             IF NOT CheckCostObject(gvCostObject) THEN
                               ERROR(Text005, gvCostObject);

                             IF gvCostObject <> '' THEN
                               gvUpdateCostObject := TRUE;
                           END;
                            }

    { 1100285028;2;Field  ;
                Name=UpdateCostObject;
                CaptionML=ENU=Update;
                SourceExpr=gvUpdateCostObject }

    { 1100285029;1;Group  ;
                Name=PriceListCodeGroup;
                CaptionML=ENU=Price List Code;
                GroupType=GridLayout }

    { 1100285030;2;Field  ;
                Name=PriceListCode;
                CaptionML=ENU=New value;
                SourceExpr=gvPriceListCode;
                TableRelation="Customer Price List" WHERE (Display Price List=CONST(Yes));
                OnValidate=BEGIN
                             IF NOT gvCustPriceList.GET(gvPriceListCode) THEN
                               ERROR(Text004, gvCustPriceList.FIELDCAPTION(Code), gvCustPriceList.TABLECAPTION);

                             IF gvPriceListCode <> '' THEN
                               gvUpdatePriceListCode := TRUE;
                           END;
                            }

    { 1100285031;2;Field  ;
                Name=UpdatePriceListCode;
                CaptionML=ENU=Update;
                SourceExpr=gvUpdatePriceListCode }

    { 1000000000;1;Group  ;
                Name=AltUnitGroup;
                CaptionML=ENU=Alternative Unit;
                GroupType=GridLayout }

    { 1100285032;2;Field  ;
                Name=AltUnit;
                CaptionML=ENU=Alternative Unit;
                SourceExpr=gvAltUnit;
                OnValidate=BEGIN
                             IF gvAltUnit <> '' THEN
                               gvUpdateAltUnit := TRUE;
                           END;
                            }

    { 1100285033;2;Field  ;
                Name=UpdateAltUnit;
                CaptionML=ENU=Update;
                SourceExpr=gvUpdateAltUnit }

    { 1100285034;2;Field  ;
                Name=AltUnitFactor;
                CaptionML=ENU=Alternative Unit Factor;
                SourceExpr=gvAltUnitFactor;
                OnValidate=BEGIN
                             IF gvAltUnitFactor <> '' THEN BEGIN
                               IF NOT CheckNumeric( gvAltUnitFactor ) THEN
                                 ERROR(Text002, gvAltUnitFactor );
                               gvUpdateAltUnitFactor := TRUE;
                             END;
                           END;
                            }

    { 1100285035;2;Field  ;
                Name=UpdateAltUnitFactor;
                CaptionML=ENU=Update;
                SourceExpr=gvUpdateAltUnitFactor }

    { 1000000001;1;Group  ;
                Name=Nettoprislista;
                CaptionML=ENU=Net Prices;
                GroupType=GridLayout }

    { 1000000002;2;Field  ;
                CaptionML=ENU=Replace Gross prices with calculates Net prices;
                SourceExpr=gvCreateNetPriceList }

    { 1000000003;1;Group  ;
                CaptionML=ENU=Text cleanups;
                GroupType=GridLayout;
                Layout=Rows }

    { 1000000004;2;Field  ;
                CaptionML=ENU=Cleanup leading/trailing and duplicate quotation marks - Item Code;
                SourceExpr=gvCleanupItemCodeStrings }

    { 1000000005;2;Field  ;
                CaptionML=ENU=Cleanup leading/trailing and duplicate quotation marks - Item Description;
                SourceExpr=gvCleanupItemDescrStrings }

    { 1000000006;2;Field  ;
                CaptionML=ENU=Replace string i article description;
                SourceExpr=gvConvertStringFrom }

    { 1000000007;2;Field  ;
                CaptionML=ENU=Replace with;
                SourceExpr=gvConvertStringTo }

  }
  CODE
  {
    VAR
      gvEdiDiscount@1100285000 : Text[10];
      gvUpdateEDIDiscount@1100285001 : Boolean;
      gvDiscount2@1100285002 : Text[20];
      gvUpdateDiscount2@1100285008 : Boolean;
      gvDiscount3@1100285009 : Text[20];
      gvUpdateDiscount3@1100285010 : Boolean;
      gvDiscount4@1100285012 : Text[20];
      gvUpdateDiscount4@1100285013 : Boolean;
      gvDiscount5@1100285014 : Text[20];
      gvUpdateDiscount5@1100285015 : Boolean;
      gvDiscount6@1100285016 : Text[20];
      gvUpdateDiscount6@1100285017 : Boolean;
      gvDateReceived@1100285018 : Date;
      gvUpdateDateReceived@1100285019 : Boolean;
      gvCostObject@1100285021 : Code[20];
      gvUpdateCostObject@1100285022 : Boolean;
      gvPriceListCode@1100285023 : Code[20];
      gvUpdatePriceListCode@1100285024 : Boolean;
      gvUseFilterOption@1100285003 : 'UseArticleBuffer,AllRecords';
      gvProgressWindow@1100285004 : Dialog;
      Text001@1100285005 : TextConst 'ENU=Processing data... @1@@@@@@@@@@';
      gvNoOfRecords@1100285006 : Integer;
      gvCounter@1100285007 : Integer;
      Text002@1100285011 : TextConst 'ENU=Cannot interpret %1 as a numerical value.';
      Text003@1100285020 : TextConst 'ENU=Do you want to start the update now?';
      gvCustPriceList@1100285025 : Record 11128273;
      Text004@1100285026 : TextConst 'ENU=%1 does not exists in table %2';
      gvDimvalues@1100285027 : Record 349;
      Text005@1100285028 : TextConst 'ENU=Cost object %1 does not exist.';
      gvAltUnit@1100285029 : Text[20];
      gvUpdateAltUnit@1100285031 : Boolean;
      gvAltUnitFactor@1100285030 : Text[20];
      gvUpdateAltUnitFactor@1100285032 : Boolean;
      Text006@1100285033 : TextConst 'ENU=You must select something to update';
      gvCreateNetPriceList@1000000000 : Boolean;
      gvNumericArtPrice@1000000001 : Decimal;
      gvNewArtPrice@1000000002 : Decimal;
      gvNumericDiscount@1000000003 : Decimal;
      gvRoundingPrecision@1000000004 : Decimal;
      gvCleanupItemCodeStrings@1000000005 : Boolean;
      gvCleanupItemDescrStrings@1000000006 : Boolean;
      gvConvertStringFrom@1000000007 : Text;
      gvConvertStringTo@1000000008 : Text;

    PROCEDURE UpdateFields@1100285024();
    VAR
      lvSymItmBuffer@1100285002 : Record 11127976;
      lvDoModify@1100285000 : Boolean;
      lvTempText@1000000000 : Text[255];
    BEGIN
      IF gvUseFilterOption = gvUseFilterOption::UseArticleBuffer THEN BEGIN
        lvSymItmBuffer.COPYFILTERS(Rec);
      END ELSE BEGIN
        CLEAR(lvSymItmBuffer);
      END;

      gvCounter := 0;

      IF lvSymItmBuffer.FINDSET(TRUE) THEN BEGIN

        gvNoOfRecords := lvSymItmBuffer.COUNT;
        gvProgressWindow.OPEN(Text001);

        REPEAT
          lvDoModify := FALSE;
          gvCounter := gvCounter + 1;
          gvProgressWindow.UPDATE(1, (gvCounter / gvNoOfRecords * 10000) DIV 1);
          IF (gvUpdateEDIDiscount) AND (lvSymItmBuffer.Ediscount <> gvEdiDiscount) THEN BEGIN
            lvSymItmBuffer.Ediscount := gvEdiDiscount;
            lvDoModify := TRUE;
          END;
          IF (gvUpdateDiscount2) AND (lvSymItmBuffer.Discount2 <> gvDiscount2) THEN BEGIN
            lvSymItmBuffer.Discount2 := gvDiscount2;
            lvDoModify := TRUE;
          END;
          IF (gvUpdateDiscount3) AND (lvSymItmBuffer.Discount3 <> gvDiscount3) THEN BEGIN
            lvSymItmBuffer.Discount3 := gvDiscount3;
            lvDoModify := TRUE;
          END;
          IF (gvUpdateDiscount4) AND (lvSymItmBuffer.Discount4 <> gvDiscount4) THEN BEGIN
            lvSymItmBuffer.Discount4 := gvDiscount4;
            lvDoModify := TRUE;
          END;
          IF (gvUpdateDiscount5) AND (lvSymItmBuffer.Discount5 <> gvDiscount5) THEN BEGIN
            lvSymItmBuffer.Discount5 := gvDiscount5;
            lvDoModify := TRUE;
          END;
          IF (gvUpdateDiscount6) AND (lvSymItmBuffer.Discount6 <> gvDiscount6) THEN BEGIN
            lvSymItmBuffer.Discount6 := gvDiscount6;
            lvDoModify := TRUE;
          END;
          IF (gvUpdateDateReceived) AND (lvSymItmBuffer."Date Received" <> gvDateReceived) THEN BEGIN
            lvSymItmBuffer."Date Received" := gvDateReceived;
            lvDoModify := TRUE;
          END;
          IF (gvUpdateCostObject) AND (lvSymItmBuffer.AccountRef1 <> gvCostObject) THEN BEGIN
            lvSymItmBuffer.AccountRef1 := gvCostObject;
            lvDoModify := TRUE;
          END;
          IF (gvUpdatePriceListCode) AND (lvSymItmBuffer."Price List Code" <> gvPriceListCode) THEN BEGIN
            lvSymItmBuffer."Price List Code" := gvPriceListCode;
            lvDoModify := TRUE;
          END;
          IF (gvUpdateAltUnit) AND (lvSymItmBuffer.AlternativeUnitMeasurement <> gvAltUnit) THEN BEGIN
            lvSymItmBuffer.AlternativeUnitMeasurement := gvAltUnit;
            lvDoModify := TRUE;
          END;
          IF (gvUpdateAltUnitFactor) AND (lvSymItmBuffer.AlternativeUnitFactor <> gvAltUnitFactor) THEN BEGIN
            lvSymItmBuffer.AlternativeUnitFactor := gvAltUnitFactor;
            lvDoModify := TRUE;
          END;
          //>> 160115 ITERO.AC Added logic to convert article price to net price in order to create a customer Net price list
          IF (gvCreateNetPriceList) THEN BEGIN
            IF (lvSymItmBuffer.ArticleNetPrice <> '' ) AND CheckNumeric(lvSymItmBuffer.ArticleNetPrice) THEN BEGIN
              IF (lvSymItmBuffer.ArticlePrice <> lvSymItmBuffer.ArticleNetPrice) THEN BEGIN
                lvSymItmBuffer.ArticlePrice := lvSymItmBuffer.ArticleNetPrice;
                lvDoModify := TRUE;
              END;
            END ELSE BEGIN
              IF (lvSymItmBuffer.Discount <> '' ) AND CheckNumeric( lvSymItmBuffer.Discount ) AND (lvSymItmBuffer.ArticlePrice <> '') AND (CheckNumeric(lvSymItmBuffer.ArticlePrice)) THEN BEGIN
                IF EVALUATE(gvNumericArtPrice, lvSymItmBuffer.ArticlePrice) AND EVALUATE(gvNumericDiscount, lvSymItmBuffer.Discount) THEN BEGIN
                  gvNewArtPrice := gvNumericArtPrice - (gvNumericArtPrice * gvNumericDiscount);
                  gvRoundingPrecision := 0.01;
                  lvSymItmBuffer.ArticlePrice := FORMAT(ROUND(gvNewArtPrice, gvRoundingPrecision), 0, 1);
                  lvDoModify := TRUE;
                END;
              END
            END;
            IF (lvDoModify) THEN BEGIN
              lvSymItmBuffer.Discount := '';
              lvSymItmBuffer.ArticleNetPrice := '';
            END;
          END;
          //<< 160115 ITERO.AC
          //>> 160117 ITERO.AC Cleaup Quote signs in Article code and/or Article Description
          IF (gvCleanupItemCodeStrings) THEN BEGIN
            lvTempText := CleanUpQuotation(lvSymItmBuffer.ArticleNo);
            IF ( lvTempText <> lvSymItmBuffer.ArticleNo) THEN BEGIN
              lvSymItmBuffer.ArticleNo := lvTempText;
              lvDoModify := TRUE;
            END;
          END;
          IF (gvCleanupItemDescrStrings) THEN BEGIN
            lvTempText := CleanUpQuotation(lvSymItmBuffer.ArticleName);
            IF ( lvTempText <> lvSymItmBuffer.ArticleName) THEN BEGIN
              lvSymItmBuffer.ArticleName := lvTempText;
              lvDoModify := TRUE;
            END;
          END;
          IF ((gvConvertStringFrom <> '') AND (gvConvertStringTo <> '')) THEN BEGIN
            lvTempText := ReplaceString(lvSymItmBuffer.ArticleName, gvConvertStringFrom, gvConvertStringTo);
            IF ( lvTempText <> lvSymItmBuffer.ArticleName) THEN BEGIN
              lvSymItmBuffer.ArticleName := lvTempText;
              lvDoModify := TRUE;
            END;
          END;
          //<< 160117 ITERO.AC
          IF lvDoModify THEN
            lvSymItmBuffer.MODIFY(FALSE);

        UNTIL lvSymItmBuffer.NEXT = 0;

        gvProgressWindow.CLOSE;

      END;
    END;

    PROCEDURE CheckNumeric@1100285003(pValueToCheck@1100285000 : Text[20]) : Boolean;
    VAR
      lvNumericVal@1100285001 : Decimal;
    BEGIN
      IF EVALUATE( lvNumericVal, pValueToCheck) THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE CheckCostObject@1100285040(pCostObject@1100285000 : Code[20]) : Boolean;
    BEGIN
      gvDimvalues.SETRANGE("Global Dimension No.", 2);
      gvDimvalues.SETRANGE(Code, pCostObject);

      IF gvDimvalues.FINDFIRST THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    LOCAL PROCEDURE CleanUpQuotation@1000000001(CleanupText@1000000000 : Text[255]) : Text[255];
    VAR
      lvReturnString@1000000001 : Text[255];
      lvCounter@1000000002 : Integer;
      lvTextLength@1000000003 : Integer;
      lvAddedOneQt@1000000004 : Boolean;
    BEGIN
      // 160117 ITERO.AC New function
      // This function removes leading, trailing quote signs
      // it also removes multiple quote signs within the text
      // Example:  '"KOPPLINGSKROPP TEMA 1800A 1/2"" UTV GŽNGA"' --> 'KOPPLINGSKROPP TEMA 1800A 1/2" UTV GŽNGA'

      lvReturnString := '';
      lvTextLength := STRLEN(CleanupText);
      lvCounter := 1;
      lvAddedOneQt := FALSE;
      WHILE lvCounter <= lvTextLength DO BEGIN

        IF (COPYSTR(CleanupText, lvCounter, 1) = '"') THEN BEGIN
          // Multiple quotes inside text, skip all but the first occurence
          IF (lvCounter > 1) AND (lvCounter < lvTextLength ) AND (COPYSTR(CleanupText, lvCounter + 1, 1) = '"') THEN BEGIN
            IF NOT ( lvAddedOneQt ) THEN BEGIN
              lvAddedOneQt := TRUE;
              lvReturnString := lvReturnString + COPYSTR(CleanupText, lvCounter, 1);
            END;
            lvCounter := lvCounter + 1;
          // Skip leading and trailing quotes
          END ELSE IF (lvCounter = 1) OR (lvCounter = lvTextLength) THEN BEGIN
            lvCounter := lvCounter + 1;
            lvAddedOneQt := FALSE;
          END ELSE BEGIN
            lvCounter := lvCounter + 1;
            lvAddedOneQt := FALSE;
          END;
        END ELSE BEGIN
          lvReturnString := lvReturnString + COPYSTR(CleanupText, lvCounter, 1);
          lvCounter := lvCounter + 1;
          lvAddedOneQt := FALSE;
        END;
      END;

      EXIT(lvReturnString);

      //ReplaceString(String : Text[256];Old : Text[256];New : Text[256]) : Text[256]
    END;

    LOCAL PROCEDURE ReplaceString@1000000014(String@1000000000 : Text[255];Old@1000000001 : Text[255];New@1000000002 : Text[255]) : Text[255];
    VAR
      Pos@1000000003 : Integer;
    BEGIN
      // 160117 ITERO.AC New function
      // This function removes replaces some characters in a text (String), removes (Old) and replaces with (New)

      Pos := STRPOS(String, Old);
      WHILE Pos <> 0 DO
      BEGIN
      String := DELSTR(String, Pos, STRLEN(Old));
      String := INSSTR(String, New, Pos);
      Pos := STRPOS(String, Old);
      END;
      EXIT(String);
    END;

    BEGIN
    {
      150916 ITERO.AC 1st Version: The purpose is to make it possible to update certain fields after import to Symbrio Item Buffer
                                   This Page is opened from the Symbrio Item Buffer page
      160115 ITERO.AC Added possibility to convert article price to net price in order to create a customer Net price list
      160117 ITERO.AC Cleanup leading/trailing and duplicate quotation marks - Item Code and Item Description
                      Also added Replace string and replace with for article description corrections
    }
    END.
  }
}

