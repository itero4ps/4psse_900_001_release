OBJECT Page 11130583 BIM Viewer
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=BIM Viewer;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table11125823;
    DataCaptionExpr=GetDataCaptionExpr();
    PageType=Worksheet;
    PromotedActionCategoriesML=ENU=New,Process,Report,Show,Service,Project;
    OnOpenPage=BEGIN
                 IF ("No." = '') OR (COUNT <> 1) THEN
                   ERROR('');

                 ServiceLocationNo := BimViewerMgt.GetDefaultLinkNoOfLinkType("No.", BimModelLink.Type::ServiceLocation);
                 NoOfLinkedServLocs := BimViewerMgt.GetNoOfLinksOfLinkType("No.", BimModelLink.Type::ServiceLocation);
                 SelectServiceLocationtEnabled := (NoOfLinkedServLocs > 0);
                 SetServiceLocationFilter;

                 ProjectNo := BimViewerMgt.GetDefaultLinkNoOfLinkType("No.", BimModelLink.Type::Project);
                 NoOfLinkedProjects := BimViewerMgt.GetNoOfLinksOfLinkType("No.", BimModelLink.Type::Project);
                 SelectProjectEnabled := (NoOfLinkedProjects > 0);
                 SetProjectFilter;

                 RunningGuid := BimViewerRunPageControl.GetRunningGuid;
               END;

    OnAfterGetRecord=BEGIN
                       LoadIfcFile;
                     END;

    OnQueryClosePage=BEGIN
                       IF NOT BimViewerRunPageControl.BimViewerObjectPagesControl('CHECK_CLOSE_VIEWER', 0, "No.", RunningGuid) THEN
                         EXIT(FALSE);
                     END;

    ActionList=ACTIONS
    {
      { 1100525002;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525006;1 ;ActionGroup;
                      CaptionML=ENU=Show }
      { 1100525012;2 ;Action    ;
                      CaptionML=ENU=Home;
                      Promoted=Yes;
                      Image=Home;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 CurrPage.XBimControl.ViewHome;
                               END;
                                }
      { 1100525007;2 ;Action    ;
                      CaptionML=ENU=Walls;
                      Promoted=Yes;
                      Image=Inventory;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 CurrPage.XBimControl.ShowWalls := (NOT CurrPage.XBimControl.ShowWalls);
                               END;
                                }
      { 1100525008;2 ;Action    ;
                      CaptionML=ENU=Windows;
                      Promoted=Yes;
                      Image=Bin;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 CurrPage.XBimControl.ShowWindows := (NOT CurrPage.XBimControl.ShowWindows);
                               END;
                                }
      { 1100525011;2 ;Action    ;
                      CaptionML=ENU=Doors;
                      Promoted=Yes;
                      Image=Register;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 CurrPage.XBimControl.ShowDoors := (NOT CurrPage.XBimControl.ShowDoors);
                               END;
                                }
      { 1100525013;2 ;Action    ;
                      CaptionML=ENU=Spaces;
                      Promoted=Yes;
                      Image=WorkCenter;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 CurrPage.XBimControl.ShowSpaces := (NOT CurrPage.XBimControl.ShowSpaces);
                               END;
                                }
      { 1100525003;1 ;ActionGroup;
                      CaptionML=ENU=Service }
      { 1100525009;2 ;Action    ;
                      CaptionML=ENU=Select Location;
                      Promoted=Yes;
                      Enabled=SelectServiceLocationtEnabled;
                      Image=Company;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 TESTFIELD("No.");
                                 BimViewerMgt.LookupBimModelLinkedServiceLocations("No.", ServiceLocationNo, (NoOfLinkedServLocs > 1));
                                 SetServiceLocationFilter;
                               END;
                                }
      { 1100525010;2 ;Action    ;
                      CaptionML=ENU=Service Objects;
                      Promoted=Yes;
                      Enabled=SelectServiceObjectEnabled;
                      Image=Item;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 ServiceObject@1100529001 : Record 11071691;
                                 BimViewerServiceObjects@1100529000 : Page 11130590;
                               BEGIN
                                 TESTFIELD("No.");
                                 IF BimViewerRunPageControl.BimViewerObjectPagesControl('CHECK_RUN_PAGE', PAGE::"BIM Viewer Service Objects", "No.", RunningGuid) THEN BEGIN
                                   ServiceObject.FILTERGROUP(4);
                                   ServiceObject.SETRANGE("Service Location No.", ServiceLocationNo);
                                   ServiceObject.FILTERGROUP(0);
                                   BimViewerServiceObjects.SETTABLEVIEW(ServiceObject);
                                   BimViewerServiceObjects.SetSelection("No.", RunningGuid, ServiceLocationNo, NoLoadedFiles, IfcFileName);
                                   BimViewerServiceObjects.SetBimAddIn(BimAddInControl);
                                   BimViewerServiceObjects.RUN;
                                 END;
                               END;
                                }
      { 1100525016;2 ;Action    ;
                      CaptionML=ENU=Create/Link Service Objects;
                      Promoted=Yes;
                      Enabled=CreateServiceObjectEnabled;
                      Image=NewItem;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 BimIfcObject@1100525001 : Record 11125826;
                                 TmpIfcObjectProperty@1100529002 : TEMPORARY Record 11020579;
                                 BimIfcObjects@1100525005 : Page 11130588;
                                 IfcObjects@1100525000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array" RUNONCLIENT;
                                 CreateFor@1100525006 : Integer;
                                 StandardObjectNo@1100525004 : Code[20];
                                 ServiceObjectNo@1100525008 : Code[20];
                                 PartOfIfcName@1100525002 : Text;
                                 IfcDefType@1100525003 : Text;
                                 PropertyName@1100529001 : Text;
                                 PropertyValue@1100529000 : Text;
                               BEGIN
                                 TESTFIELD("No.");
                                 BimViewerMgt.CreateServiceObjectsSelections(IfcObject, "No.", ServiceLocationNo, CreateFor,
                                   StandardObjectNo, PartOfIfcName, IfcDefType, PropertyName, PropertyValue, TmpIfcObjectProperty);
                                 IfcObjects := CurrPage.XBimControl.GetEntities(IfcDefType, PartOfIfcName, PropertyName, PropertyValue);
                                 CASE CreateFor OF
                                   1: //Create for current
                                     BEGIN
                                       BimViewerMgt.FillCurrentBimIfcObjectTableFields(IfcObject, BimIfcObject);
                                       ServiceObjectNo := BimViewerMgt.InsertServiceObjectFromBimIfcObject(
                                         BimIfcObject, "No.", StandardObjectNo, ServiceLocationNo, TmpIfcObjectProperty);
                                       IF ServiceObjectNo <> '' THEN
                                         MESSAGE(Text001, ServiceObjectNo);
                                     END;
                                   2:  //Create for selection
                                     BEGIN
                                       BimIfcObjects.SetBimAddIn(BimAddInControl);
                                       BimIfcObjects.SetSelectionsIfcObjectProperties(TmpIfcObjectProperty);
                                       BimIfcObjects.SetSelectionsCreateServiceObjects(IfcObjects, "No.", ServiceLocationNo, StandardObjectNo);
                                       BimIfcObjects.RUNMODAL;
                                     END;
                                   3:
                                     BEGIN
                                       BimIfcObjects.SetBimAddIn(BimAddInControl);
                                       BimIfcObjects.SetSelectionsIfcObjectProperties(TmpIfcObjectProperty);
                                       BimIfcObjects.SetSelectionsLinkServiceObjects(IfcObjects, "No.", ServiceLocationNo);
                                       BimIfcObjects.RUNMODAL;
                                     END;
                                 END;
                               END;
                                }
      { 1100529004;1 ;ActionGroup;
                      CaptionML=ENU=Project }
      { 1100529003;2 ;Action    ;
                      CaptionML=ENU=Select Project;
                      Promoted=Yes;
                      Enabled=SelectProjectEnabled;
                      Image=Company;
                      PromotedCategory=Category6;
                      OnAction=BEGIN
                                 TESTFIELD("No.");
                                 BimViewerMgt.LookupBimModelLinkedProjects("No.", ProjectNo, (NoOfLinkedProjects > 1));
                                 SetProjectFilter;
                               END;
                                }
      { 1100529002;2 ;Action    ;
                      CaptionML=ENU=Project Objects;
                      Promoted=Yes;
                      Enabled=SelectProjectObjectEnabled;
                      Image=Item;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 ProjectObject@1100525000 : Record 11229311;
                                 BimViewerProjectObjects@1100529000 : Page 11130591;
                               BEGIN
                                 TESTFIELD("No.");
                                 IF BimViewerRunPageControl.BimViewerObjectPagesControl('CHECK_RUN_PAGE', PAGE::"BIM Viewer Project Objects", "No.", RunningGuid) THEN BEGIN
                                   ProjectObject.FILTERGROUP(4);
                                   ProjectObject.SETRANGE("Project No.", ProjectNo);
                                   ProjectObject.FILTERGROUP(0);
                                   BimViewerProjectObjects.SETTABLEVIEW(ProjectObject);
                                   BimViewerProjectObjects.SetSelection("No.", RunningGuid, ProjectNo, NoLoadedFiles, IfcFileName);
                                   BimViewerProjectObjects.SetBimAddIn(BimAddInControl);
                                   BimViewerProjectObjects.RUN;
                                 END;
                               END;
                                }
      { 1100529001;2 ;Action    ;
                      CaptionML=ENU=Create Objects;
                      Promoted=Yes;
                      Enabled=CreateProjectObjectEnabled;
                      Image=NewItem;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 BimIfcObject@1100525001 : Record 11125826;
                                 TmpIfcObjectProperty@1100529003 : TEMPORARY Record 11020579;
                                 BimIfcObjects@1100525005 : Page 11130588;
                                 IfcObjects@1100525000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array" RUNONCLIENT;
                                 CreateFor@1100525006 : Integer;
                                 ItemNo@1100525004 : Code[20];
                                 CostObject@1100529000 : Code[20];
                                 ProjectObjectNo@1100525008 : Code[20];
                                 PartOfIfcName@1100525002 : Text;
                                 IfcDefType@1100525003 : Text;
                                 PropertyName@1100529002 : Text;
                                 PropertyValue@1100529001 : Text;
                               BEGIN
                                 TESTFIELD("No.");
                                 BimViewerMgt.CreateProjectObjectsSelections(IfcObject, "No.", ProjectNo, CreateFor,
                                   ItemNo, CostObject, PartOfIfcName, IfcDefType, PropertyName, PropertyValue, TmpIfcObjectProperty);
                                 IfcObjects := CurrPage.XBimControl.GetEntities(IfcDefType, PartOfIfcName, PropertyName, PropertyValue);
                                 CASE CreateFor OF
                                   1: //Create for current
                                     BEGIN
                                       BimViewerMgt.FillCurrentBimIfcObjectTableFields(IfcObject, BimIfcObject);
                                       ProjectObjectNo :=
                                         BimViewerMgt.InsertProjectObjectFromBimIfcObject(BimIfcObject, "No.", ItemNo, CostObject, ProjectNo, TmpIfcObjectProperty);
                                       IF ProjectObjectNo <> '' THEN
                                         MESSAGE(Text001, ProjectObjectNo);
                                     END;
                                   2:  //Create for selection
                                     BEGIN
                                       BimIfcObjects.SetBimAddIn(BimAddInControl);
                                       BimIfcObjects.SetSelectionsIfcObjectProperties(TmpIfcObjectProperty);
                                       BimIfcObjects.SetSelectionsCreateProjectObjects(IfcObjects, "No.", ProjectNo, ItemNo, CostObject);
                                       BimIfcObjects.RUNMODAL;
                                     END;
                                   3:
                                     BEGIN
                                       BimIfcObjects.SetBimAddIn(BimAddInControl);
                                       BimIfcObjects.SetSelectionsIfcObjectProperties(TmpIfcObjectProperty);
                                       BimIfcObjects.SetSelectionsLinkProjectObjects(IfcObjects, "No.", ProjectNo);
                                       BimIfcObjects.RUNMODAL;
                                     END;
                                 END;
                               END;
                                }
      { 1100529005;0 ;ActionContainer;
                      ActionContainerType=Reports }
      { 1100529006;1 ;Action    ;
                      CaptionML=ENU=Check List;
                      Enabled=Loaded;
                      Image=TestReport;
                      PromotedCategory=Report;
                      OnAction=VAR
                                 BimModelCheckList@1100529000 : Report 11125367;
                               BEGIN
                                 TESTFIELD("No.");
                                 BimModelCheckList.SetSelection("No.");
                                 BimModelCheckList.SetBimAddIn(BimAddInControl);
                                 BimModelCheckList.RUNMODAL;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100525000;;Container;
                ContainerType=ContentArea }

    { 1100525001;1;Field  ;
                Name=XBimControl;
                ControlAddIn=[Fps.Nav.Windows.Addin.Bim.Addin;PublicKeyToken=024bd013665211e9];
                OnControlAddIn=BEGIN
                                 IfcObject := CurrPage.XBimControl.GetSelectedEntity();
                                 CurrPage.BimViewerIfcObjectFB.PAGE.SetIfcObject("No.", IfcObject);
                                 IF (NOT CreateServiceObjectEnabled) AND (NoOfLinkedServLocs >= 1) THEN
                                   CreateServiceObjectEnabled := TRUE;
                                 IF (NOT CreateProjectObjectEnabled) AND (NoOfLinkedProjects >= 1) THEN
                                   CreateProjectObjectEnabled := TRUE;
                               END;

                ShowCaption=No }

    { 1100525005;0;Container;
                ContainerType=FactBoxArea }

    { 1100525004;1;Part   ;
                Name=BimViewerIfcObjectFB;
                PagePartID=Page11130585;
                PartType=Page }

    { 1100525014;1;Part   ;
                Name=BimViewerServObjectsFB;
                SubPageLink=Service Location No.=FIELD(FILTER(Service Location Filter));
                PagePartID=Page11130586;
                PartType=Page }

    { 1100529000;1;Part   ;
                Name=BimViewerProjObjectsFB;
                SubPageLink=Project No.=FIELD(FILTER(Project Filter));
                PagePartID=Page11130587;
                PartType=Page }

  }
  CODE
  {
    VAR
      BimModelIfcFileFilters@1100529010 : Record 11125827;
      BimModelLink@1100529005 : Record 11125824;
      BimViewerServiceObjects@1100525011 : Page 11130590;
      BimViewerMgt@1100525002 : Codeunit 11125822;
      BimViewerRunPageControl@1100529007 : Codeunit 11125823;
      IfcObject@1100525000 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IIfcObject" RUNONCLIENT;
      BimAddInControl@1100525005 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IXbimControl" RUNONCLIENT;
      Loaded@1100525006 : Boolean INDATASET;
      SelectServiceLocationtEnabled@1100525004 : Boolean INDATASET;
      SelectProjectEnabled@1100529000 : Boolean INDATASET;
      SelectServiceObjectEnabled@1100529009 : Boolean INDATASET;
      SelectProjectObjectEnabled@1100529008 : Boolean INDATASET;
      CreateServiceObjectEnabled@1100525008 : Boolean INDATASET;
      CreateProjectObjectEnabled@1100529001 : Boolean INDATASET;
      NoOfLinkedServLocs@1100525003 : Integer;
      NoOfLinkedProjects@1100529002 : Integer;
      NoLoadedFiles@1100529011 : Integer;
      ServiceLocationNo@1100525007 : Code[20];
      Text000a@1100525009 : TextConst 'ENU=Location';
      Text000b@1100529004 : TextConst 'ENU=Project';
      Text001@1100525001 : TextConst 'ENU=Created Object: %1';
      ProjectNo@1100529003 : Code[20];
      RunningGuid@1100529006 : Code[50];
      IfcFileName@1100529012 : Text;

    PROCEDURE SetIfcFileSelection@1100529001(VAR BimModelIfcFileFilters2@1100529000 : Record 11125827);
    BEGIN
      BimModelIfcFileFilters.COPY(BimModelIfcFileFilters2);
    END;

    LOCAL PROCEDURE LoadIfcFile@1100525003();
    VAR
      IfcFileArray@1100529002 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList" RUNONCLIENT;
    BEGIN
      IF (NOT Loaded) THEN BEGIN
        TESTFIELD("No.");

        NoLoadedFiles := 0;
        IfcFileName := '';
        BimModelIfcFileFilters.FILTERGROUP(9);
        BimModelIfcFileFilters.SETRANGE("BIM Model No.", "No.");
        BimModelIfcFileFilters.FILTERGROUP(0);
        BimModelIfcFileFilters.FINDSET;
        IfcFileArray := IfcFileArray.ArrayList();
        REPEAT
          BimModelIfcFileFilters.TESTFIELD("IFC File");
          BimModelIfcFileFilters.IfcFileExists;
          IfcFileArray.Add(BimModelIfcFileFilters."IFC File");
          NoLoadedFiles := NoLoadedFiles + 1;
          IF NoLoadedFiles = 1 THEN
            IfcFileName := BimModelIfcFileFilters."IFC File"
          ELSE
            IfcFileName := '';
        UNTIL BimModelIfcFileFilters.NEXT = 0;

        CurrPage.XBimControl.LoadIfcFiles(IfcFileArray.ToArray);
        Loaded := TRUE;
        SetControl;
      END;
    END;

    LOCAL PROCEDURE SetControl@1100525006();
    BEGIN
      BimAddInControl := CurrPage.XBimControl.GetControl;
      CurrPage.BimViewerServObjectsFB.PAGE.SetBimAddIn(BimAddInControl);
      CurrPage.BimViewerProjObjectsFB.PAGE.SetBimAddIn(BimAddInControl);
      SelectServiceObjectEnabled := (NoOfLinkedServLocs > 0);
      SelectProjectObjectEnabled := (NoOfLinkedProjects > 0);
    END;

    LOCAL PROCEDURE SetServiceLocationFilter@1100525000();
    BEGIN
      BimViewerMgt.SetUserDefaultLinkNoOfLinkType("No.", BimModelLink.Type::ServiceLocation, ServiceLocationNo);
      SETFILTER("Service Location Filter", '%1', ServiceLocationNo);
    END;

    LOCAL PROCEDURE SetProjectFilter@1100529000();
    BEGIN
      BimViewerMgt.SetUserDefaultLinkNoOfLinkType("No.", BimModelLink.Type::Project, ProjectNo);
      SETFILTER("Project Filter", '%1', ProjectNo);
    END;

    LOCAL PROCEDURE GetDataCaptionExpr@1100525001() Expression : Text;
    BEGIN
      Expression := "No."+ ' ' + Description;
      IF ServiceLocationNo <> '' THEN
        Expression := Expression + ' - ' + Text000a + ' ' + ServiceLocationNo;
      IF ProjectNo <> '' THEN
        Expression := Expression + ' - ' + Text000b + ' ' + ProjectNo;
    END;

    EVENT BimAddInControl@1100525005::ControlClosed@13(sender@1100525001 : Variant;e@1100525000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.EventArgs");
    BEGIN
    END;

    EVENT BimAddInControl@1100525005::SelectedEntityChanged@14(sender@1100525001 : Variant;e@1100525000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.EventArgs");
    BEGIN
    END;

    BEGIN
    END.
  }
}

