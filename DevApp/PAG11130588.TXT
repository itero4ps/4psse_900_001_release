OBJECT Page 11130588 IFC Objects
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=IFC Objects;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table11125826;
    DataCaptionExpr=DataCaptionExpression;
    PageType=List;
    SourceTableTemporary=Yes;
    PromotedActionCategoriesML=ENU=New,Process,Report,Object,Functions;
    OnInit=BEGIN
             ServiceObjectNoEditable := FALSE;
             ProjectObjectNoEditable := FALSE;
           END;

    OnOpenPage=VAR
                 BimViewerMgt@1100525000 : Codeunit 11125822;
               BEGIN
                 BimViewerMgt.FillSelectedBimIfcObjectTmpTable(IfcObjects, BimModelNo, LinkType, TRUE, TmpIfcObjectProperty, Rec);  //SourceTableTemporary is Yes!
                 CurrPage.EDITABLE(LinkMode);
                 IF LinkMode THEN BEGIN
                   IF (LinkType = BimModelLink.Type::ServiceLocation) AND (ServiceLocationNo <> '') THEN
                     ServiceObjectNoEditable := TRUE;
                   IF (LinkType = BimModelLink.Type::Project) AND (ProjectNo <> '') THEN
                     ProjectObjectNoEditable := TRUE;
                 END;
               END;

    OnModifyRecord=BEGIN
                     IF NOT CurrPage.EDITABLE THEN
                       ERROR('');
                   END;

    ActionList=ACTIONS
    {
      { 1100529003;  ;ActionContainer;
                      CaptionML=ENU=Object;
                      ActionContainerType=RelatedInformation }
      { 1100529002;1 ;Action    ;
                      CaptionML=ENU=Properties;
                      Promoted=Yes;
                      Image=NewResource;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 TmpIfcObjectPropertyToCopy@1100529002 : TEMPORARY Record 11020579;
                                 BimViewerMgt@1100529001 : Codeunit 11125822;
                                 IfcObject@1100529000 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IIfcObject" RUNONCLIENT;
                               BEGIN
                                 IF "Entry No." > 0 THEN BEGIN
                                   IfcObject := IfcObjects.GetValue("Entry No."-1);   // "Entry No." corr. with ArrayNo, see BimViewerMgt.FillSelectedBimIfcObjectTmpTable
                                   TmpIfcObjectPropertyToCopy.COPY(TmpIfcObjectProperty, TRUE);
                                   BimViewerMgt.ShowPropertiesCurrentIfcObject(IfcObject, TmpIfcObjectPropertyToCopy);
                                 END;
                               END;
                                }
      { 1100525013;  ;ActionContainer;
                      CaptionML=ENU=Functions;
                      ActionContainerType=ActionItems }
      { 1100525011;1 ;Action    ;
                      CaptionML=ENU=Create;
                      Promoted=Yes;
                      Visible=CreateMode;
                      Image=CreateBinContent;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 ServiceObject@1100525005 : Record 11071691;
                                 TmpBimIfcObject@1100525001 : TEMPORARY Record 11125826;
                                 BimViewerMgt@1100529000 : Codeunit 11125822;
                                 Counter@1100525003 : Integer;
                                 CreatedObjectNo@1100525002 : Code[20];
                                 LastCreatedObjectNo@1100525006 : Code[20];
                                 CreatedObjects@1100525004 : Text;
                               BEGIN
                                 TmpBimIfcObject.COPY(Rec, TRUE);
                                 CurrPage.SETSELECTIONFILTER(TmpBimIfcObject);
                                 IF NOT TmpBimIfcObject.FINDSET THEN
                                   ERROR(Text001);
                                 REPEAT
                                   IF LinkType = BimModelLink.Type::ServiceLocation THEN BEGIN
                                     CreatedObjectNo :=
                                       BimViewerMgt.InsertServiceObjectFromBimIfcObject(
                                         TmpBimIfcObject, BimModelNo, StandardObjectNo, ServiceLocationNo, TmpIfcObjectProperty);
                                   END;
                                   IF LinkType = BimModelLink.Type::Project THEN BEGIN
                                     CreatedObjectNo :=
                                       BimViewerMgt.InsertProjectObjectFromBimIfcObject(
                                         TmpBimIfcObject, BimModelNo, ItemNo, CostObject, ProjectNo, TmpIfcObjectProperty);
                                   END;
                                   IF CreatedObjectNo <> '' THEN BEGIN
                                     IF CreatedObjects = '' THEN
                                       CreatedObjects := CreatedObjectNo;
                                     LastCreatedObjectNo := CreatedObjectNo;
                                     Counter := Counter + 1;
                                   END;
                                   TmpBimIfcObject.DELETE;
                                 UNTIL TmpBimIfcObject.NEXT = 0;
                                 IF Counter > 1 THEN
                                   CreatedObjects := CreatedObjects + '..' + LastCreatedObjectNo;
                                 MESSAGE(Text002, CreatedObjects, Counter);

                                 CLEAR(TmpBimIfcObject);
                                 TmpBimIfcObject.COPY(Rec, TRUE);
                                 TmpBimIfcObject.RESET;
                                 IF TmpBimIfcObject.ISEMPTY THEN
                                   CurrPage.CLOSE;
                               END;
                                }
      { 1100525014;1 ;Action    ;
                      CaptionML=ENU=Delete;
                      Promoted=Yes;
                      Visible=CreateMode;
                      Image=Delete;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 TmpBimIfcObject@1100525000 : TEMPORARY Record 11125826;
                               BEGIN
                                 TmpBimIfcObject.COPY(Rec, TRUE);
                                 CurrPage.SETSELECTIONFILTER(TmpBimIfcObject);
                                 IF TmpBimIfcObject.ISEMPTY THEN
                                   EXIT;
                                 IF CONFIRM(Text000) THEN
                                   TmpBimIfcObject.DELETEALL;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100525000;0;Container;
                ContainerType=ContentArea }

    { 1100525001;1;Group  ;
                GroupType=Repeater }

    { 1100525005;2;Field  ;
                SourceExpr="Entry No." }

    { 1100525002;2;Field  ;
                SourceExpr=ID }

    { 1100525003;2;Field  ;
                SourceExpr=Name }

    { 1100525004;2;Field  ;
                SourceExpr="IFC Type" }

    { 1100525006;2;Field  ;
                SourceExpr=Type }

    { 1100525007;2;Field  ;
                SourceExpr=Site }

    { 1100525008;2;Field  ;
                SourceExpr=Building }

    { 1100525009;2;Field  ;
                SourceExpr=Floor }

    { 1100525010;2;Field  ;
                SourceExpr=Space }

    { 1100529000;2;Field  ;
                SourceExpr="Service Object No.";
                Editable=ServiceObjectNoEditable;
                OnValidate=VAR
                             ServiceObject@1100529000 : Record 11071691;
                           BEGIN
                             IF "Service Object No." <> xRec."Service Object No." THEN BEGIN
                               IF xRec."Service Object No." <> '' THEN BEGIN
                                 ServiceObject.GET(xRec."Service Object No.");
                                 ServiceObject.TESTFIELD("Service Location No.", ServiceLocationNo);
                                 ServiceObject."BIM Model No." := '';
                                 ServiceObject."IFC Object ID" := '';
                                 ServiceObject.MODIFY;
                               END;
                               IF "Service Object No." <> '' THEN BEGIN
                                 ServiceObject.GET("Service Object No.");
                                 ServiceObject.TESTFIELD("IFC Object ID", '');
                                 ServiceObject.TESTFIELD("Service Location No.", ServiceLocationNo);
                                 ServiceObject."BIM Model No." := BimModelNo;
                                 ServiceObject."IFC Object ID" := ID;
                                 ServiceObject.MODIFY;
                               END;
                             END;
                           END;

                OnLookup=VAR
                           ServiceObject@1100529000 : Record 11071691;
                         BEGIN
                           ServiceObject.SETCURRENTKEY("Service Location No.");
                           IF LinkMode AND ServiceObjectNoEditable THEN BEGIN
                             ServiceObject.SETRANGE("Service Location No.", ServiceLocationNo);
                             ServiceObject.SETFILTER("IFC Object ID", '%1', '');
                           END ELSE BEGIN
                             IF ("Service Object No." = '') OR (NOT ServiceObject.GET("Service Object No.")) THEN
                               ServiceObject.SETRANGE("Service Location No.", ServiceLocationNo)
                             ELSE
                               ServiceObject.SETRANGE("Service Location No.", ServiceObject."Service Location No.");
                           END;
                           ServiceObject."No." := Text;
                           IF NOT ServiceObjectNoEditable THEN
                             PAGE.RUNMODAL(0, ServiceObject)
                           ELSE BEGIN
                             IF PAGE.RUNMODAL(0, ServiceObject) = ACTION::LookupOK THEN BEGIN
                               IF ServiceObject."No." <> '' THEN BEGIN
                                 Text := ServiceObject."No.";
                                 EXIT(TRUE);
                               END;
                             END;
                           END;
                         END;
                          }

    { 1100529001;2;Field  ;
                SourceExpr="Project Object No.";
                Editable=ProjectObjectNoEditable;
                OnValidate=VAR
                             ProjectObject@1100529000 : Record 11229311;
                           BEGIN
                             IF "Project Object No." <> xRec."Project Object No." THEN BEGIN
                               IF xRec."Project Object No." <> '' THEN BEGIN
                                 ProjectObject.GET(xRec."Project Object No.");
                                 ProjectObject.TESTFIELD("Project No.", ProjectNo);
                                 ProjectObject."BIM Model No." := '';
                                 ProjectObject."IFC Object ID" := '';
                                 ProjectObject.MODIFY;
                               END;
                               IF "Project Object No." <> '' THEN BEGIN
                                 ProjectObject.GET("Project Object No.");
                                 ProjectObject.TESTFIELD("IFC Object ID", '');
                                 ProjectObject.TESTFIELD("Project No.", ProjectNo);
                                 ProjectObject."BIM Model No." := BimModelNo;
                                 ProjectObject."IFC Object ID" := ID;
                                 ProjectObject.MODIFY;
                               END;
                             END;
                           END;

                OnLookup=VAR
                           ProjectObject@1100529000 : Record 11229311;
                         BEGIN
                           ProjectObject.SETCURRENTKEY("Project No.");
                           IF LinkMode AND ProjectObjectNoEditable THEN BEGIN
                             ProjectObject.SETRANGE("Project No.", ProjectNo);
                             ProjectObject.SETFILTER("IFC Object ID", '%1', '');
                           END ELSE BEGIN
                             IF ("Project Object No." = '') OR (NOT ProjectObject.GET("Project Object No.")) THEN
                               ProjectObject.SETRANGE("Project No.", ProjectNo)
                             ELSE
                               ProjectObject.SETRANGE("Project No.", ProjectObject."Project No.");
                           END;
                           ProjectObject."No." := Text;
                           IF NOT ProjectObjectNoEditable THEN
                             PAGE.RUNMODAL(0, ProjectObject)
                           ELSE BEGIN
                             IF PAGE.RUNMODAL(0, ProjectObject) = ACTION::LookupOK THEN BEGIN
                               IF ProjectObject."No." <> '' THEN BEGIN
                                 Text := ProjectObject."No.";
                                 EXIT(TRUE);
                               END;
                             END;
                           END;
                         END;
                          }

  }
  CODE
  {
    VAR
      BimModelLink@1100529005 : Record 11125824;
      TmpIfcObjectProperty@1100529012 : TEMPORARY Record 11020579;
      BIMViewerRunPageControl@1100529015 : Codeunit 11125823;
      BimAddInControl@1100529014 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IXbimControl" RUNONCLIENT;
      IfcObjects@1100525000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array" RUNONCLIENT;
      LinkMode@1100529006 : Boolean INDATASET;
      CreateMode@1100525003 : Boolean INDATASET;
      ServiceObjectNoEditable@1100529007 : Boolean INDATASET;
      ProjectObjectNoEditable@1100529008 : Boolean INDATASET;
      LinkType@1100529000 : Option;
      BimModelNo@1100525009 : Code[20];
      ServiceLocationNo@1100525002 : Code[20];
      Text000@1100525008 : TextConst 'ENU=Do you want to delete the selected line(s)?';
      Text001@1100525004 : TextConst 'ENU=No IFC Objects selected';
      Text002@1100525005 : TextConst 'ENU=Created Service Object: %1 (%2)';
      StandardObjectNo@1100529001 : Code[20];
      ProjectNo@1100529002 : Code[20];
      ItemNo@1100529003 : Code[20];
      CostObject@1100529004 : Code[20];
      DataCaptionExpression@1100525006 : Text;
      Text003@1100525007 : TextConst 'ENU=Select for create';
      Text004@1100529009 : TextConst 'ENU=Link';
      Text005@1100529010 : TextConst 'ENU=Service Object';
      Text006@1100529011 : TextConst 'ENU=Project Object';
      IfcObjectIdSave@1100529013 : Text;

    PROCEDURE SetBimAddIn@1100529004(VAR BimAddInControl2@1100525000 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IXbimControl" RUNONCLIENT);
    BEGIN
      BimAddInControl := BimAddInControl2;
    END;

    PROCEDURE SetSelectionsIfcObjectProperties@1100529003(VAR TmpIfcObjectProperty2@1100529000 : TEMPORARY Record 11020579);
    BEGIN
      TmpIfcObjectProperty.COPY(TmpIfcObjectProperty2, TRUE);
    END;

    PROCEDURE SetSelectionsCreateServiceObjects@1100525000(VAR IfcObjects2@1100525000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array" RUNONCLIENT;BimModelNo2@1100525003 : Code[20];ServiceLocationNo2@1100529001 : Code[20];StandardObjectNo2@1100525002 : Code[20]);
    BEGIN
      CreateMode := TRUE;
      LinkType := BimModelLink.Type::ServiceLocation;
      DataCaptionExpression := Text003 + ' ' + Text005;
      IfcObjects := IfcObjects2;
      BimModelNo := BimModelNo2;
      ServiceLocationNo := ServiceLocationNo2;
      StandardObjectNo := StandardObjectNo2;
    END;

    PROCEDURE SetSelectionsCreateProjectObjects@1100529000(VAR IfcObjects2@1100525000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array" RUNONCLIENT;BimModelNo2@1100525003 : Code[20];ProjectNo2@1100529001 : Code[20];ItemNo2@1100525002 : Code[20];CostObject2@1100529002 : Code[20]);
    BEGIN
      CreateMode := TRUE;
      LinkType := BimModelLink.Type::Project;
      DataCaptionExpression := Text003 + ' ' + Text006;
      IfcObjects := IfcObjects2;
      BimModelNo := BimModelNo2;
      ProjectNo := ProjectNo2;
      ItemNo := ItemNo2;
      CostObject := CostObject2;
    END;

    PROCEDURE SetSelectionsLinkServiceObjects@1100529002(VAR IfcObjects2@1100525000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array" RUNONCLIENT;BimModelNo2@1100525003 : Code[20];ServiceLocationNo2@1100529001 : Code[20]);
    BEGIN
      LinkMode := TRUE;
      LinkType := BimModelLink.Type::ServiceLocation;
      DataCaptionExpression := Text004 + ' ' + Text005;
      IfcObjects := IfcObjects2;
      BimModelNo := BimModelNo2;
      ServiceLocationNo := ServiceLocationNo2;
    END;

    PROCEDURE SetSelectionsLinkProjectObjects@1100529001(VAR IfcObjects2@1100525000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array" RUNONCLIENT;BimModelNo2@1100525003 : Code[20];ProjectNo2@1100525002 : Code[20]);
    BEGIN
      LinkMode := TRUE;
      LinkType := BimModelLink.Type::Project;
      DataCaptionExpression := Text004 + ' ' + Text006;
      IfcObjects := IfcObjects2;
      BimModelNo := BimModelNo2;
      ProjectNo := ProjectNo2;
    END;

    EVENT BimAddInControl@1100529014::ControlClosed@13(sender@1100525001 : Variant;e@1100525000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.EventArgs");
    BEGIN
    END;

    EVENT BimAddInControl@1100529014::SelectedEntityChanged@14(sender@1100525001 : Variant;e@1100525000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.EventArgs");
    BEGIN
    END;

    BEGIN
    END.
  }
}

