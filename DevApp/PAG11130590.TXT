OBJECT Page 11130590 BIM Viewer Service Objects
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=BIM Viewer Service Objects;
    SourceTable=Table11071691;
    DataCaptionExpr=ServiceLocationNo;
    SourceTableView=SORTING(Service Location No.)
                    WHERE(Service Location No.=FILTER(<>''));
    PageType=List;
    CardPageID=Service Object;
    PromotedActionCategoriesML=ENU=New,Process,Report,Object,Functions;
    OnInit=BEGIN
             PageNo := PAGE::"BIM Viewer Service Objects";
           END;

    OnOpenPage=BEGIN
                 IF (BimModelNo <> '') AND (RunningGuid <> '') THEN
                   BIMViewerRunPageControl.BimViewerObjectPagesControl('SET_RUN_PAGE', PageNo, BimModelNo, RunningGuid);
               END;

    OnClosePage=BEGIN
                  IF (BimModelNo <> '') AND (RunningGuid <> '') THEN
                    BIMViewerRunPageControl.BimViewerObjectPagesControl('RESET_RUN_PAGE', PageNo, BimModelNo, RunningGuid);
                END;

    OnAfterGetRecord=BEGIN
                       IF NOT CheckServiceLocationFilter THEN
                         EXIT;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           IF (IfcObjectIdSave <> "IFC Object ID") THEN BEGIN
                             SelectIfcObjectIdInViewer("IFC Object ID");
                             IfcObjectIdSave := "IFC Object ID";
                           END;
                         END;

    ActionList=ACTIONS
    {
      { 1100529004;  ;ActionContainer;
                      CaptionML=ENU=Object;
                      ActionContainerType=RelatedInformation }
      { 1100529005;1 ;Action    ;
                      CaptionML=ENU=Properties;
                      RunObject=Page 11229429;
                      RunPageLink=Service Object No.=FIELD(No.);
                      Promoted=Yes;
                      Image=NewResource;
                      PromotedCategory=Category4 }
      { 1100529000;  ;ActionContainer;
                      CaptionML=ENU=Functions;
                      ActionContainerType=ActionItems }
      { 1100529001;1 ;Action    ;
                      CaptionML=ENU=Link current IFC Object;
                      Promoted=Yes;
                      Image=Link;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 BimViewerMgt@1100529001 : Codeunit 11125822;
                                 IfcObject@1100529000 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IIfcObject" RUNONCLIENT;
                                 IfcGuid@1100529002 : Text;
                               BEGIN
                                 TESTFIELD("No.");
                                 IfcObject := BimAddInControl.GetSelectedEntity();
                                 IfcGuid := IfcObject.Guid();
                                 BimViewerMgt.LinkServiceObjectToCurrIfcObject(Rec, BimModelNo, IfcGuid);
                               END;
                                }
      { 1100529003;1 ;Action    ;
                      CaptionML=ENU=Copy properties to current;
                      Promoted=Yes;
                      Image=CopyCostBudget;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 BimViewerMgt@1100529001 : Codeunit 11125822;
                                 IfcObject@1100529000 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IIfcObject" RUNONCLIENT;
                               BEGIN
                                 TESTFIELD("No.");
                                 TESTFIELD("IFC Object ID");
                                 IfcObject := BimAddInControl.GetSelectedEntity();
                                 BimViewerMgt.CopyIfcPropertiesToCurrObject(IfcObject, "No.", DATABASE::"Service-Object");
                               END;
                                }
      { 1100529002;1 ;Action    ;
                      CaptionML=ENU=Create URL;
                      Enabled=CreateURLEnabled;
                      Image=LinkWeb;
                      OnAction=VAR
                                 ServiceObject@1100529000 : Record 11071691;
                                 BimViewerMgt@1100529003 : Codeunit 11125822;
                                 IfcObject@1100529002 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IIfcObject" RUNONCLIENT;
                                 dict@1100529004 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Generic.Dictionary`2";
                                 URL_Web@1100529001 : Text;
                                 URL_Windows@1100529005 : Text;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ServiceObject);
                                 ServiceObject.FILTERGROUP(9);
                                 ServiceObject.SETFILTER("IFC Object ID", '<>%1', '');
                                 ServiceObject.FILTERGROUP(0);
                                 IF ServiceObject.FINDSET THEN BEGIN
                                   REPEAT
                                     URL_Web := GETURL(CLIENTTYPE::Web, COMPANYNAME, OBJECTTYPE::Page, PAGE::"Service Object", ServiceObject);
                                     URL_Windows := GETURL(CLIENTTYPE::Windows, COMPANYNAME, OBJECTTYPE::Page, PAGE::"Service Object", ServiceObject);
                                     IfcObject := BimAddInControl.GetEntity(ServiceObject."IFC Object ID");
                                     dict := dict.Dictionary();
                                     dict.Add('Service Web Client Url', URL_Web);
                                     dict.Add('Service Windows Client Url', URL_Windows);
                                     dict.Add('Service Object No', ServiceObject."No.");
                                     IfcObject.SetFpsInformation(dict);
                                   UNTIL ServiceObject.NEXT = 0;
                                 END;
                                 BimAddInControl.Save();
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100525000;1;Group  ;
                GroupType=Repeater }

    { 1100525016;2;Field  ;
                SourceExpr="Service Location No.";
                Visible=FALSE }

    { 1100525001;2;Field  ;
                SourceExpr="No." }

    { 1100525008;2;Field  ;
                SourceExpr="IFC Object ID" }

    { 1100528604;2;Field  ;
                SourceExpr=Description }

    { 1100528616;2;Field  ;
                SourceExpr="Description 2" }

    { 1100525003;2;Field  ;
                SourceExpr="Standard Object No." }

    { 1100525011;2;Field  ;
                SourceExpr="Standard Object Description";
                Visible=FALSE }

    { 1100528617;2;Field  ;
                SourceExpr="Standard Object Description 2";
                Visible=FALSE }

    { 1100528000;2;Field  ;
                SourceExpr=Building }

    { 1100528001;2;Field  ;
                SourceExpr=Floor }

    { 1100528002;2;Field  ;
                SourceExpr=Room }

    { 1100528613;2;Field  ;
                SourceExpr="Brand Code" }

    { 1100525007;2;Field  ;
                SourceExpr="Brand Description" }

    { 1100528614;2;Field  ;
                SourceExpr="Model Code" }

    { 1100409002;2;Field  ;
                SourceExpr="Model Description" }

    { 1100528615;2;Field  ;
                SourceExpr="Type Code" }

    { 1100409001;2;Field  ;
                SourceExpr="Type Description" }

    { 1100525009;2;Field  ;
                SourceExpr="In use since" }

    { 1100528600;2;Field  ;
                DrillDown=No;
                SourceExpr=Discipline }

    { 1100528602;2;Field  ;
                DrillDown=No;
                SourceExpr="Trade Speciality" }

    { 1100528606;2;Field  ;
                SourceExpr=Quantity;
                Visible=FALSE }

    { 1100528603;2;Field  ;
                SourceExpr="Customer Feature" }

    { 1100409003;2;Field  ;
                SourceExpr="Object Customer" }

    { 1100528601;2;Field  ;
                SourceExpr=Blocked }

  }
  CODE
  {
    VAR
      BimModelUserDefaults@1100529005 : Record 11125825;
      BIMViewerRunPageControl@1100529001 : Codeunit 11125823;
      BimAddInControl@1100529000 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IXbimControl" RUNONCLIENT;
      OnClose@1100529009 : Boolean;
      CreateURLEnabled@1100529011 : Boolean INDATASET;
      PageNo@1100529007 : Integer;
      BimModelNo@1100529002 : Code[20];
      ServiceLocationNo@1100529004 : Code[20];
      RunningGuid@1100529003 : Code[50];
      IfcObjectIdSave@1100525001 : Text;
      Text001@1100529006 : TextConst 'ENU=New %1 selected in BIM Viewer, do ''Refresh'' page';
      Text002@1100529008 : TextConst 'ENU=Not active anymore, page will be clsed';
      IfcFileName@1100529010 : Text;

    PROCEDURE SetSelection@1100529000(BimModelNo2@1100529001 : Code[20];RunningGuid2@1100529000 : Code[50];ServiceLocationNo2@1100529002 : Code[20];NoLoadedFiles@1100529003 : Integer;FileNameLoaded@1100529004 : Text);
    BEGIN
      BimModelNo := BimModelNo2;
      RunningGuid := RunningGuid2;
      ServiceLocationNo := ServiceLocationNo2;
      IF NoLoadedFiles = 1 THEN
        IfcFileName := FileNameLoaded;
      CreateURLEnabled := (NoLoadedFiles >= 1);
    END;

    PROCEDURE SetBimAddIn@1100525000(VAR BimAddInControl2@1100525000 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IXbimControl" RUNONCLIENT);
    BEGIN
      BimAddInControl := BimAddInControl2;
    END;

    LOCAL PROCEDURE SelectIfcObjectIdInViewer@1100525001(IfcObjctId@1100525000 : Text);
    BEGIN
      IF (IfcObjctId <> '') THEN
        BimAddInControl.SelectEntity(IfcObjctId);
    END;

    LOCAL PROCEDURE CheckServiceLocationFilter@1100529001() : Boolean;
    BEGIN
      IF OnClose THEN
        EXIT(FALSE);
      IF NOT BIMViewerRunPageControl.BimViewerObjectPagesControl('PAGE_RUNNING', PageNo, BimModelNo, RunningGuid) THEN BEGIN
        MESSAGE(Text002);
        OnClose := TRUE;
        CurrPage.CLOSE;
        EXIT(FALSE);
      END;
      IF BimModelUserDefaults.GET(BimModelNo, USERID) THEN BEGIN
        IF (BimModelUserDefaults."Service Location No." <> ServiceLocationNo) AND (BimModelUserDefaults."Service Location No." <> '') THEN BEGIN
          ServiceLocationNo := BimModelUserDefaults."Service Location No.";
          FILTERGROUP(4);
          SETRANGE("Service Location No.", ServiceLocationNo);
          FILTERGROUP(0);
          MESSAGE(Text001, FIELDCAPTION("Service Location No."));
          EXIT(FALSE);
        END;
      END;
      EXIT(TRUE);
    END;

    EVENT BimAddInControl@1100529000::ControlClosed@13(sender@1100525001 : Variant;e@1100525000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.EventArgs");
    BEGIN
    END;

    EVENT BimAddInControl@1100529000::SelectedEntityChanged@14(sender@1100525001 : Variant;e@1100525000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.EventArgs");
    BEGIN
    END;

    BEGIN
    END.
  }
}

