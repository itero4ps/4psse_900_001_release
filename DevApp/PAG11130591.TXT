OBJECT Page 11130591 BIM Viewer Project Objects
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=BIM Viewer Project Objects;
    SourceTable=Table11229311;
    DataCaptionExpr=ProjectNo;
    SourceTableView=SORTING(Project No.)
                    WHERE(Project No.=FILTER(<>''));
    PageType=List;
    CardPageID=Project Object;
    PromotedActionCategoriesML=ENU=New,Process,Report,Object,Functions;
    OnInit=BEGIN
             PageNo := PAGE::"BIM Viewer Project Objects";
           END;

    OnOpenPage=BEGIN
                 IF (BimModelNo <> '') AND (RunningGuid <> '') THEN
                   BIMViewerRunPageControl.BimViewerObjectPagesControl('SET_RUN_PAGE', PageNo, BimModelNo, RunningGuid);
               END;

    OnClosePage=BEGIN
                  IF (BimModelNo <> '') AND (RunningGuid <> '') THEN
                    BIMViewerRunPageControl.BimViewerObjectPagesControl('RESET_RUN_PAGE', PageNo, BimModelNo, RunningGuid);
                END;

    OnAfterGetRecord=BEGIN
                       IF NOT CheckProjectFilter THEN
                         EXIT;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           IF (IfcObjectIdSave <> "IFC Object ID") THEN BEGIN
                             SelectIfcObjectIdInViewer("IFC Object ID");
                             IfcObjectIdSave := "IFC Object ID";
                           END;
                         END;

    ActionList=ACTIONS
    {
      { 1100529010;  ;ActionContainer;
                      CaptionML=ENU=Object;
                      ActionContainerType=RelatedInformation }
      { 1100529009;1 ;Action    ;
                      CaptionML=ENU=Properties;
                      RunObject=Page 11126606;
                      RunPageLink=Project Object No.=FIELD(No.);
                      Promoted=Yes;
                      Image=NewResource;
                      PromotedCategory=Category4 }
      { 1100529001;  ;ActionContainer;
                      CaptionML=ENU=Functions;
                      ActionContainerType=ActionItems }
      { 1100529000;1 ;Action    ;
                      CaptionML=ENU=Link current IFC Object;
                      Promoted=Yes;
                      Image=Link;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 BimViewerMgt@1100529001 : Codeunit 11125822;
                                 IfcObject@1100529000 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IIfcObject" RUNONCLIENT;
                                 IfcGuid@1100529002 : Text;
                               BEGIN
                                 TESTFIELD("No.");
                                 IfcObject := BimAddInControl.GetSelectedEntity();
                                 IfcGuid := IfcObject.Guid();
                                 BimViewerMgt.LinkProjectObjectToCurrIfcObject(Rec, BimModelNo, IfcGuid);
                               END;
                                }
      { 1100529007;1 ;Action    ;
                      CaptionML=ENU=Copy properties to current;
                      Promoted=Yes;
                      Image=CopyCostBudget;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 BimViewerMgt@1100529001 : Codeunit 11125822;
                                 IfcObject@1100529000 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IIfcObject" RUNONCLIENT;
                               BEGIN
                                 TESTFIELD("No.");
                                 TESTFIELD("IFC Object ID");
                                 IfcObject := BimAddInControl.GetSelectedEntity();
                                 BimViewerMgt.CopyIfcPropertiesToCurrObject(IfcObject, "No.", DATABASE::"Project Object");
                               END;
                                }
      { 1100529006;1 ;Action    ;
                      CaptionML=ENU=Create URL;
                      Enabled=CreateURLEnabled;
                      Image=LinkWeb;
                      OnAction=VAR
                                 ProjectObject@1100529000 : Record 11229311;
                                 BimViewerMgt@1100529003 : Codeunit 11125822;
                                 IfcObject@1100529002 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IIfcObject" RUNONCLIENT;
                                 dict@1100529004 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Generic.Dictionary`2";
                                 URL_Web@1100529001 : Text;
                                 URL_Windows@1100529005 : Text;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ProjectObject);
                                 ProjectObject.FILTERGROUP(9);
                                 ProjectObject.SETFILTER("IFC Object ID", '<>%1', '');
                                 ProjectObject.FILTERGROUP(0);
                                 IF ProjectObject.FINDSET THEN BEGIN
                                   REPEAT
                                     URL_Web := GETURL(CLIENTTYPE::Web, COMPANYNAME, OBJECTTYPE::Page, PAGE::"Project Object", ProjectObject);
                                     URL_Windows := GETURL(CLIENTTYPE::Windows, COMPANYNAME, OBJECTTYPE::Page, PAGE::"Project Object", ProjectObject);
                                     IfcObject := BimAddInControl.GetEntity(ProjectObject."IFC Object ID");
                                     dict := dict.Dictionary();
                                     dict.Add('Project Web Client Url', URL_Web);
                                     dict.Add('Project Windows Client Url', URL_Windows);
                                     dict.Add('Project Object No', ProjectObject."No.");
                                     IfcObject.SetFpsInformation(dict);
                                   UNTIL ProjectObject.NEXT = 0;
                                 END;
                                 BimAddInControl.Save();
                               END;
                                }
      { 1100529005;1 ;Action    ;
                      CaptionML=ENU=Create purchase Order;
                      Promoted=Yes;
                      Image=MakeOrder;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 ProjectObject@1100529000 : Record 11229311;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ProjectObject);
                                 CreatePurchaseOrder(ProjectObject);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100525000;1;Group  ;
                GroupType=Repeater }

    { 1100525016;2;Field  ;
                SourceExpr="Project No.";
                Visible=FALSE }

    { 1100525001;2;Field  ;
                SourceExpr="No." }

    { 1100525008;2;Field  ;
                SourceExpr="IFC Object ID" }

    { 1100528604;2;Field  ;
                SourceExpr=Description }

    { 1100528616;2;Field  ;
                SourceExpr="Description 2" }

    { 1100525003;2;Field  ;
                SourceExpr="Item No." }

    { 1100525011;2;Field  ;
                SourceExpr="Item Description";
                Visible=FALSE }

    { 1100528617;2;Field  ;
                SourceExpr="Cost Object" }

    { 1100528000;2;Field  ;
                SourceExpr=Building }

    { 1100528001;2;Field  ;
                SourceExpr=Floor }

    { 1100528002;2;Field  ;
                SourceExpr=Room }

    { 1100528601;2;Field  ;
                SourceExpr=Blocked }

    { 1100529002;2;Field  ;
                SourceExpr="Vendor No." }

    { 1100529003;2;Field  ;
                DrillDown=No;
                SourceExpr="Vendor Name" }

    { 1100529004;2;Field  ;
                SourceExpr="Purchase Order No." }

  }
  CODE
  {
    VAR
      BimModelUserDefaults@1100529005 : Record 11125825;
      BIMViewerRunPageControl@1100529001 : Codeunit 11125823;
      BimAddInControl@1100529000 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IXbimControl" RUNONCLIENT;
      OnClose@1100529009 : Boolean;
      CreateURLEnabled@1100529010 : Boolean INDATASET;
      PageNo@1100529007 : Integer;
      BimModelNo@1100529002 : Code[20];
      ProjectNo@1100529004 : Code[20];
      RunningGuid@1100529003 : Code[50];
      IfcObjectIdSave@1100525001 : Text;
      Text001@1100529006 : TextConst 'ENU=New %1 selected in BIM Viewer, do ''Refresh'' page';
      Text002@1100529008 : TextConst 'ENU=Not active anymore, page will be clsed';
      IFcFileName@1100529011 : Text;

    PROCEDURE SetSelection@1100529000(BimModelNo2@1100529001 : Code[20];RunningGuid2@1100529000 : Code[50];ProjectNo2@1100529002 : Code[20];NoLoadedFiles@1100529003 : Integer;FileNameLoaded@1100529004 : Text);
    BEGIN
      BimModelNo := BimModelNo2;
      RunningGuid := RunningGuid2;
      ProjectNo := ProjectNo2;
      IF NoLoadedFiles = 1 THEN
        IFcFileName := FileNameLoaded;
      CreateURLEnabled := (NoLoadedFiles >= 1);
    END;

    PROCEDURE SetBimAddIn@1100525000(VAR BimAddInControl2@1100525000 : DotNet "'Fps.Nav.Windows.Addin.Bim.Interface, Version=1.0.0.0, Culture=neutral, PublicKeyToken=024bd013665211e9'.Fps.Nav.Windows.Addin.Bim.Interface.IXbimControl" RUNONCLIENT);
    BEGIN
      BimAddInControl := BimAddInControl2;
    END;

    LOCAL PROCEDURE SelectIfcObjectIdInViewer@1100525001(IfcObjctId@1100525000 : Text);
    BEGIN
      IF (IfcObjctId <> '') THEN
        BimAddInControl.SelectEntity(IfcObjctId);
    END;

    LOCAL PROCEDURE CheckProjectFilter@1100529001() : Boolean;
    BEGIN
      IF OnClose THEN
        EXIT(FALSE);
      IF NOT BIMViewerRunPageControl.BimViewerObjectPagesControl('PAGE_RUNNING', PageNo, BimModelNo, RunningGuid) THEN BEGIN
        MESSAGE(Text002);
        OnClose := TRUE;
        CurrPage.CLOSE;
        EXIT(FALSE);
      END;
      IF BimModelUserDefaults.GET(BimModelNo, USERID) THEN BEGIN
        IF (BimModelUserDefaults."Project No." <> ProjectNo) AND (BimModelUserDefaults."Project No." <> '') THEN BEGIN
          ProjectNo := BimModelUserDefaults."Project No.";
          FILTERGROUP(4);
          SETRANGE("Project No.", ProjectNo);
          FILTERGROUP(0);
          MESSAGE(Text001, FIELDCAPTION("Project No."));
          EXIT(FALSE);
        END;
      END;
      EXIT(TRUE);
    END;

    EVENT BimAddInControl@1100529000::ControlClosed@13(sender@1100525001 : Variant;e@1100525000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.EventArgs");
    BEGIN
    END;

    EVENT BimAddInControl@1100529000::SelectedEntityChanged@14(sender@1100525001 : Variant;e@1100525000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.EventArgs");
    BEGIN
    END;

    BEGIN
    END.
  }
}

