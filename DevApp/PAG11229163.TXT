OBJECT Page 11229163 Cockpit Customer Ledg. Ent. FB
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Customer Details;
    SourceTable=Table21;
    PageType=CardPart;
    OnAfterGetRecord=BEGIN
                       UpdateFields;
                     END;

  }
  CONTROLS
  {
    { 1100528700;0;Container;
                ContainerType=ContentArea }

    { 1100528701;1;Field  ;
                CaptionML=ENU=Customer;
                SourceExpr=Customer.Name }

    { 1100528702;1;Field  ;
                CaptionML=ENU=Phone No.;
                SourceExpr=Customer."Phone No." }

    { 1100528703;1;Field  ;
                CaptionML=ENU=Sales (LCY);
                SourceExpr=Customer."Sales (LCY)" }

    { 1100528704;1;Field  ;
                CaptionML=ENU=Avg. paying time (days);
                SourceExpr=PayedInDays }

    { 1100528705;1;Field  ;
                CaptionML=ENU=# Payed in Time;
                DecimalPlaces=0:0;
                SourceExpr=NoPayedOnTime }

    { 1100528706;1;Field  ;
                CaptionML=ENU=# Payed to late;
                DecimalPlaces=0:0;
                SourceExpr=NoPayedToLate }

    { 1100525004;1;Group  ;
                CaptionML=ENU=Primary Contact;
                GroupType=Group }

    { 1100525000;2;Field  ;
                CaptionML=ENU=Name;
                SourceExpr=PrimaryContact.Name }

    { 1100525001;2;Field  ;
                CaptionML=ENU=Phone No.;
                SourceExpr=PrimaryContact."Phone No." }

    { 1100525002;2;Field  ;
                CaptionML=ENU=Phone No. Work;
                SourceExpr=PrimaryContact."Phone No. Work" }

    { 1100525003;2;Field  ;
                CaptionML=ENU=Mobile Phone No.;
                SourceExpr=PrimaryContact."Mobile Phone No." }

  }
  CODE
  {
    VAR
      Customer@1100528701 : Record 18;
      PrimaryContact@1100525000 : Record 5050;
      NoPayedOnTime@1100528707 : Decimal;
      NoPayedToLate@1100528706 : Decimal;
      NoOfEntries@1100528704 : Decimal;
      PayedInDays@1100528703 : Decimal;

    PROCEDURE UpdateFields@1100528700();
    VAR
      CustLedgerEntry@1100528700 : Record 21;
    BEGIN
      CLEAR(Customer);
      FILTERGROUP(4);
      COPYFILTER("Date Filter", Customer."Date Filter");
      FILTERGROUP(0);
      IF Customer.GET("Customer No.") THEN
        Customer.CALCFIELDS("Sales (LCY)");

      //C019790.sn
      CLEAR(PrimaryContact);
      IF NOT PrimaryContact.GET(Customer."Primary Contact No.") THEN
        PrimaryContact.Name := Customer.Contact;
      //C019790.en

      NoPayedOnTime := 0;
      NoPayedToLate := 0;
      NoOfEntries := 0;
      PayedInDays := 0;

      CustLedgerEntry.RESET;
      CustLedgerEntry.SETCURRENTKEY("Document Type","Customer No.","Posting Date");
      CustLedgerEntry.SETFILTER("Document Type", '%1|%2',
        CustLedgerEntry."Document Type"::Invoice,
        CustLedgerEntry."Document Type"::"Finance Charge Memo");
      CustLedgerEntry.SETRANGE("Customer No.", "Customer No.");
      FILTERGROUP(4);
      COPYFILTER("Date Filter", CustLedgerEntry."Posting Date");
      FILTERGROUP(0);
      CustLedgerEntry.SETRANGE(Open, FALSE);
      CustLedgerEntry.SETFILTER("Closed at Date", '<>%1', 0D);
      IF CustLedgerEntry.FINDSET THEN BEGIN
        REPEAT
          NoOfEntries := NoOfEntries + 1;
          PayedInDays := PayedInDays + (CustLedgerEntry."Closed at Date" - CustLedgerEntry."Document Date");
          IF CustLedgerEntry."Closed at Date" > CustLedgerEntry."Due Date" THEN
            NoPayedToLate := NoPayedToLate + 1
          ELSE
            NoPayedOnTime := NoPayedOnTime + 1;
        UNTIL CustLedgerEntry.NEXT = 0;
      END;

      IF NoOfEntries <> 0 THEN
        PayedInDays := PayedInDays / NoOfEntries;
    END;

    BEGIN
    END.
  }
}

