OBJECT Page 11229229 Level Occupation FB
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Level Occupation Prognosis FB;
    SourceTable=Table5200;
    PageType=CardPart;
    OnAfterGetCurrRecord=BEGIN
                           LevelHours := GetLevelHours;
                           NormHoursYear := GetNormHoursYear;
                         END;

  }
  CONTROLS
  {
    { 1100528600;;Container;
                ContainerType=ContentArea }

    { 1100528601;1;Field  ;
                CaptionML=ENU=Norm Hours per Year;
                SourceExpr=NormHoursYear }

    { 1100528602;1;Field  ;
                CaptionML=ENU=Assigned Hours per Year;
                SourceExpr=LevelHours }

    { 1100528603;1;Field  ;
                CaptionML=ENU=To assign;
                SourceExpr=NormHoursYear - LevelHours }

  }
  CODE
  {
    VAR
      PeriodFilter@1100528600 : Text[250];
      OccupationType@1100528602 : 'Employee,Department,Territory';
      OccupationSourceCode@1100528601 : Code[20];
      NormHoursYear@1100528604 : Decimal;
      LevelHours@1100528603 : Decimal;

    PROCEDURE SetOccupationFilter@1100528602(IOccupationType@1100528600 : 'Employee,Department,Territory';IOccupationSourceCode@1100528601 : Code[20]);
    BEGIN
      OccupationType := IOccupationType;
      OccupationSourceCode := IOccupationSourceCode;
      NormHoursYear := GetNormHoursYear;
    END;

    PROCEDURE SetPeriodFilter@1100528601(IPeriodFilter@1100528600 : Text[250]);
    BEGIN
      PeriodFilter := IPeriodFilter;
    END;

    PROCEDURE GetLevelHours@1100485003() OccupationPrognosis : Decimal;
    VAR
      PrognosisDetail@1100485000 : Record 11012884;
    BEGIN
      PrognosisDetail.SETCURRENTKEY(Type);
      PrognosisDetail.SETRANGE(Type, PrognosisDetail.Type::Occupation);
      CASE OccupationType OF
        OccupationType::Employee:
          PrognosisDetail.SETRANGE("Source Code", "No.");
        OccupationType::Department:
          BEGIN
            PrognosisDetail.SETRANGE("Source Code", '');
            PrognosisDetail.SETRANGE("Global Dimension 1 Code", OccupationSourceCode);
            PrognosisDetail.SETRANGE("Territory Code", '');
          END;
        OccupationType::Territory:
          BEGIN
            PrognosisDetail.SETRANGE("Source Code", '');
            PrognosisDetail.SETRANGE("Global Dimension 1 Code", '');
            PrognosisDetail.SETRANGE("Territory Code", OccupationSourceCode);
          END;
      END;
      PrognosisDetail.SETFILTER(Date, PeriodFilter);
      PrognosisDetail.CALCSUMS(Hours);
      EXIT(PrognosisDetail.Hours);
    END;

    LOCAL PROCEDURE GetNormHoursYear@1100528600() : Decimal;
    VAR
      Employee@1100528600 : Record 5200;
    BEGIN
      IF OccupationSourceCode = '' THEN
        EXIT;
      CASE OccupationType OF
        OccupationType::Employee:
          Employee.SETRANGE("No.", OccupationSourceCode);
        OccupationType::Department:
          Employee.SETRANGE("Global Dimension 1 Code", OccupationSourceCode);
        OccupationType::Territory:
          Employee.SETRANGE("Territory Code", OccupationSourceCode);
      END;
      Employee.CALCSUMS("Norm Hours Year");
      EXIT(Employee."Norm Hours Year");
    END;

    BEGIN
    END.
  }
}

