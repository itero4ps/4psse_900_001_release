OBJECT Page 11229294 Repeat Work Order
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Repeat Work Order;
    SourceTable=Table11229279;
    PageType=StandardDialog;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             RecurrencePerWeek := 1;
             StartingDate := TODAY;
           END;

    OnOpenPage=VAR
                 WorkOrder@1100525000 : Record 11229279;
               BEGIN
                 IF NOT StartedFromNAV THEN BEGIN
                   WorkOrder.GET("No.");
                   SetOriginalWorkOrder(WorkOrder);
                   StartedFromNAV := FALSE;
                   CreateWorkOrdersVisible := TRUE;
                 END;
               END;

    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK,ACTION::LookupOK];
                       IF CloseActionOK THEN BEGIN
                         IF (EndingDate = 0D) AND (NoOfRecurrences = 0) THEN
                           ERROR(Text000);
                         IF StartingDate = 0D THEN
                           ERROR(Text001);
                         IF EndingDate <> 0D THEN
                           IF CREATEDATETIME(StartingDate, StartingTime) > CREATEDATETIME(EndingDate, EndingTime) THEN
                             ERROR(Text002);
                         IF NOT StartedFromNAV THEN
                           CreateWorkOrders;
                       END;
                     END;

    ActionList=ACTIONS
    {
      { 1100525000;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525001;1 ;Action    ;
                      CaptionML=ENU=Create Work Orders;
                      Visible=CreateWorkOrdersVisible;
                      OnAction=BEGIN
                                 CreateWorkOrders;
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100528600;;Container;
                ContainerType=ContentArea }

    { 1100528601;1;Group  ;
                CaptionML=ENU=Work Order Data;
                GroupType=Group }

    { 1100528602;2;Field  ;
                SourceExpr="Project Planning Activity No." }

    { 1100528603;2;Field  ;
                SourceExpr=Description }

    { 1100528604;2;Field  ;
                SourceExpr="Resource Type" }

    { 1100528605;2;Field  ;
                SourceExpr="Resource No." }

    { 1100528622;2;Field  ;
                CaptionML=ENU=Starting Time;
                SourceExpr=StartingTime }

    { 1100528623;2;Field  ;
                CaptionML=ENU=Ending Time;
                SourceExpr=EndingTime }

    { 1100528606;2;Field  ;
                SourceExpr="Expected Hours" }

    { 1100528607;2;Field  ;
                SourceExpr="Duration Time" }

    { 1100528608;2;Field  ;
                SourceExpr="Schedule Relation" }

    { 1100528609;1;Group  ;
                Name=Herhalingspatroon;
                CaptionML=ENU=Repeat Pattern;
                GroupType=Group }

    { 1100528610;2;Field  ;
                CaptionML=ENU=Recurs every X week/weeks;
                SourceExpr=RecurrencePerWeek }

    { 1100528611;2;Field  ;
                CaptionML=ENU=Monday;
                SourceExpr=Monday }

    { 1100528612;2;Field  ;
                CaptionML=ENU=Tuesday;
                SourceExpr=Tuesday }

    { 1100528613;2;Field  ;
                CaptionML=ENU=Wednesday;
                SourceExpr=Wednesday }

    { 1100528614;2;Field  ;
                CaptionML=ENU=Thursday;
                SourceExpr=Thursday }

    { 1100528615;2;Field  ;
                CaptionML=ENU=Friday;
                SourceExpr=Friday }

    { 1100528616;2;Field  ;
                CaptionML=ENU=Saturday;
                SourceExpr=Saturday }

    { 1100528617;2;Field  ;
                CaptionML=ENU=Sunday;
                SourceExpr=Sunday }

    { 1100528618;1;Group  ;
                CaptionML=ENU=Recurrence Range;
                GroupType=Group }

    { 1100528619;2;Field  ;
                CaptionML=ENU=Start;
                SourceExpr=StartingDate }

    { 1100528620;2;Field  ;
                CaptionML=ENU=Ends on;
                SourceExpr=EndingDate }

    { 1100528621;2;Field  ;
                CaptionML=ENU=Ends after X recurrences;
                SourceExpr=NoOfRecurrences }

  }
  CODE
  {
    VAR
      Monday@1100528600 : Boolean;
      Tuesday@1100528601 : Boolean;
      Wednesday@1100528602 : Boolean;
      Thursday@1100528603 : Boolean;
      Friday@1100528604 : Boolean;
      Saturday@1100528605 : Boolean;
      Sunday@1100528606 : Boolean;
      RecurrencePerWeek@1100528607 : Integer;
      StartingDate@1100528608 : Date;
      StartingTime@1100528614 : Time;
      EndingDate@1100528609 : Date;
      EndingTime@1100528615 : Time;
      NoOfRecurrences@1100528610 : Integer;
      CloseActionOK@1100528611 : Boolean;
      Text000@1100528612 : TextConst 'ENU=Ends on or Ends after has to be given';
      Text001@1100528613 : TextConst 'ENU=Start has to be given';
      Text002@1100528616 : TextConst 'ENU=Ending Time must be greater than Starting Time';
      CreateWorkOrdersVisible@1100525000 : Boolean INDATASET;
      StartedFromNAV@1100525001 : Boolean;

    PROCEDURE SetOriginalWorkOrder@1100528600(IWorkOrder@1100528600 : Record 11229279);
    BEGIN
      Rec := IWorkOrder;
      StartedFromNAV := TRUE;
      INSERT;

      InitData;
    END;

    PROCEDURE CreateWorkOrders@1100528601();
    VAR
      WorkOrder@1100528603 : Record 11229279;
      LastWorkOrder@1100528610 : Record 11229279;
      DateRec@1100528600 : Record 2000000007;
      RecurrenceCount@1100528601 : Integer;
      WeekCount@1100528602 : Integer;
      NewStartingDT@1100528604 : DateTime;
      NewEndingDT@1100528605 : DateTime;
      NewYear@1100528606 : Integer;
      DayNo@1100528607 : Integer;
      DoCreateWorkOrder@1100528608 : Boolean;
      Duration@1100528609 : Duration;
    BEGIN
      DateRec.SETRANGE("Period Type", DateRec."Period Type"::Week);
      DateRec.SETFILTER("Period Start", '<=%1', StartingDate);
      IF NOT DateRec.FINDLAST THEN
        EXIT;
      IF EndingDate <> 0D THEN BEGIN
        DateRec.SETRANGE("Period Start", DateRec."Period Start", EndingDate)
      END ELSE
        DateRec.SETFILTER("Period Start", '>=%1', DateRec."Period Start");
      IF DateRec.FINDSET THEN
        REPEAT
          IF WeekCount MOD RecurrencePerWeek = 0 THEN BEGIN
            FOR DayNo := 1 TO 7 DO BEGIN
              CASE DayNo OF
                1: DoCreateWorkOrder := Monday;
                2: DoCreateWorkOrder := Tuesday;
                3: DoCreateWorkOrder := Wednesday;
                4: DoCreateWorkOrder := Thursday;
                5: DoCreateWorkOrder := Friday;
                6: DoCreateWorkOrder := Saturday;
                7: DoCreateWorkOrder := Sunday;
              END;
              IF DateRec."Period Start" + (DayNo -1) < StartingDate THEN
                DoCreateWorkOrder := FALSE;
              IF (NoOfRecurrences > 0) AND (RecurrenceCount >= NoOfRecurrences) THEN
                DoCreateWorkOrder := FALSE;
              IF DoCreateWorkOrder THEN BEGIN
                NewStartingDT := CREATEDATETIME(DateRec."Period Start" + (DayNo -1), StartingTime);
                IF ("Starting Date/Time" <> 0DT) AND ("Ending Date/Time" >= "Starting Date/Time") THEN
                  Duration := "Ending Date/Time" - "Starting Date/Time"
                ELSE
                  Duration := 0;
                IF EndingTime <> 0T THEN BEGIN
                  IF Duration = 0 THEN
                    Duration := EndingTime - StartingTime;
                END ELSE BEGIN
                  IF Duration = 0 THEN
                    Duration := "Duration Time" * 60 * 60 * 1000;
                  IF Duration = 0 THEN
                    Duration := "Expected Hours" * 60 * 60 * 1000;
                END;
                NewEndingDT := NewStartingDT + Duration;

                CLEAR(WorkOrder);
                WorkOrder.InitRecordOnNew("Source Type", "Source No.", "Source Type");
                CLEAR(WorkOrder."Starting Date/Time");
                CLEAR(WorkOrder."Ending Date/Time");
                WorkOrder.INSERT(TRUE);
                WorkOrder.VALIDATE("Resource Type", "Resource Type");
                WorkOrder.VALIDATE("Resource No.", "Resource No.");
                WorkOrder.VALIDATE(Description, Description);
                WorkOrder.VALIDATE("Starting Date/Time", NewStartingDT);
                WorkOrder.VALIDATE("Ending Date/Time", NewEndingDT);
                WorkOrder.VALIDATE("Expected Hours", "Expected Hours");
                WorkOrder.VALIDATE("Duration Time", "Duration Time");
                WorkOrder.VALIDATE("Project Planning Activity No.", "Project Planning Activity No.");
                WorkOrder.VALIDATE("Schedule Relation", "Schedule Relation");
                IF "Schedule Relation" <> "Schedule Relation"::" " THEN
                  IF LastWorkOrder."No." <> '' THEN BEGIN
                    WorkOrder."Schedule Relation" := "Schedule Relation";
                    WorkOrder."Related Work Order No." := LastWorkOrder."No.";
                  END;
                WorkOrder.MODIFY(TRUE);
                LastWorkOrder := WorkOrder;
                RecurrenceCount += 1;
              END;
            END;
          END;
          WeekCount += 1;
        UNTIL (DateRec.NEXT = 0) OR ((RecurrenceCount >= NoOfRecurrences) AND (NoOfRecurrences > 0));
    END;

    PROCEDURE InitData@1100528604();
    VAR
      StartingDate@1100528600 : Date;
    BEGIN
      StartingDate := DT2DATE("Starting Date/Time");
      IF StartingDate = 0D THEN
        StartingDate := TODAY;
      CASE DATE2DWY(StartingDate, 1) OF
        1: Monday := TRUE;
        2: Tuesday := TRUE;
        3: Wednesday := TRUE;
        4: Thursday := TRUE;
        5: Friday := TRUE;
        6: Saturday := TRUE;
        7: Sunday := TRUE;
      END;

      StartingTime := DT2TIME("Starting Date/Time");
      EndingTime := DT2TIME("Ending Date/Time");
    END;

    PROCEDURE "---"@1100528603();
    BEGIN
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528402() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      CreateWorkOrders;
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    BEGIN
    END.
  }
}

