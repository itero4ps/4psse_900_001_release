OBJECT Page 11229388 Company Chart (Stats)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Company Chart (Statistics);
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table11072203;
    PageType=ListPlus;
    OnInit=BEGIN
             PeriodType := PeriodType::Month;
             NoOfColumns := 12;

             DetermineMeasurementIndexes;
           END;

    OnOpenPage=BEGIN
                 Date."Period Start" := WORKDATE;
                 PeriodFormManagement.FindDate('=<', Date, PeriodType);
                 BaseDate := Date."Period Start";
               END;

    ActionList=ACTIONS
    {
      { 1100528615;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528614;1 ;Action    ;
                      CaptionML=ENU=Previous;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PeriodFormManagement.FindDate('<', Date, PeriodType);
                                 BaseDate := Date."Period Start";
                                 UpdateChart;
                               END;
                                }
      { 1100528613;1 ;Action    ;
                      CaptionML=ENU=Next;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PeriodFormManagement.FindDate('>', Date, PeriodType);
                                 BaseDate := Date."Period Start";
                                 UpdateChart;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100528612;;Container;
                ContainerType=ContentArea }

    { 1100528611;1;Group  ;
                CaptionML=ENU=General;
                GroupType=Group }

    { 1100528610;2;Field  ;
                CaptionML=ENU=Age Filter;
                SourceExpr=AgeFilter;
                OnValidate=BEGIN
                             UpdateChart;
                           END;
                            }

    { 1100528603;1;Group  ;
                CaptionML=ENU=Diagram;
                GroupType=Group }

    { 1100528604;2;Field  ;
                Name=Chart;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.BusinessChart;PublicKeyToken=31bf3856ad364e35] }

    { 1100528602;1;Group  ;
                CaptionML=ENU=Options;
                GroupType=Group }

    { 1100528601;2;Field  ;
                CaptionML=ENU=View by;
                OptionCaptionML=ENU=Day,Week,Month,Quarter,Year,Period;
                SourceExpr=PeriodType;
                OnValidate=BEGIN
                             PeriodFormManagement.FindDate('=<>', Date, PeriodType);
                             UpdateChart;
                           END;
                            }

    { 1100528600;2;Field  ;
                CaptionML=ENU=No. Of Periods;
                SourceExpr=NoOfColumns;
                OnValidate=BEGIN
                             IF NoOfColumns <= 0 THEN
                               ERROR(Text001);
                             UpdateChart;
                           END;
                            }

  }
  CODE
  {
    VAR
      BusinessChartBuffer@1100528602 : Record 485;
      Date@1100528601 : Record 2000000007;
      PeriodFormManagement@1100528600 : Codeunit 359;
      PeriodType@1100528603 : ',Week,Month,Quarter,Year';
      Text000@1100528614 : TextConst 'ENU=Day,Week,Month,Quarter,Year,Period';
      Text001@1100528613 : TextConst 'ENU=No. of Periods must be at least 1';
      TextNoOfObjects@1100528607 : TextConst 'ENU=Objects';
      TextNoOfServiceOrders@1100528606 : TextConst 'ENU=Service Orders';
      TextNoOfRepeatVisits@1100528605 : TextConst 'ENU=Repeat Visits';
      BaseDate@1100528610 : Date;
      NoOfObjectsIndex@1100528609 : Integer;
      NoOfServiceOrdersIndex@1100528608 : Integer;
      NoOfRepeatVisitsIndex@1100528604 : Integer;
      NoOfColumns@1100528611 : Integer;
      AgeFilter@1100528612 : Text;

    PROCEDURE UpdateChart@1100528601();
    VAR
      CompanyStats@1100528603 : Record 11072203;
      Date@1100528604 : Record 2000000007;
      Offset@1100528602 : Integer;
      ColumnName@1100528601 : Text[30];
      PeriodDate@1100528600 : Date;
    BEGIN
      WITH BusinessChartBuffer DO BEGIN
        Initialize;
        AddMeasure(TextNoOfObjects, NoOfObjectsIndex, "Data Type"::Decimal, "Chart Type"::Line);
        AddMeasure(TextNoOfServiceOrders, NoOfServiceOrdersIndex, "Data Type"::Decimal, "Chart Type"::Line);
        AddMeasure(TextNoOfRepeatVisits, NoOfRepeatVisitsIndex, "Data Type"::Decimal, "Chart Type"::Line);
        SetXAxis(GetPeriodTypeText(PeriodType), "Data Type"::String);

        Date."Period Start" := BaseDate;
        PeriodFormManagement.FindDate('=<>', Date, PeriodType);

        FOR Offset := 0 TO NoOfColumns -1 DO BEGIN
          IF Offset > 0 THEN
            PeriodFormManagement.NextDate(1, Date, PeriodType);
          PeriodDate := Date."Period Start";
          ColumnName := PeriodFormManagement.CreatePeriodFormat(PeriodType, Date."Period Start");

          AddColumn(ColumnName);

          CompanyStats.COPY(Rec);
          IF AgeFilter <> '' THEN
            CompanyStats.SETFILTER("Age Filter", AgeFilter);
          CompanyStats.SETRANGE("Period Filter", Date."Period Start", Date."Period End");
          CompanyStats.SETFILTER("Start Date Filter", '<=%1', Date."Period End");
          CompanyStats.SETFILTER("Ending Date Filter", '%1|>%2', 0D, Date."Period End");
          CompanyStats.CALCFIELDS("No. of Objects", "No. of Service Orders", "No. of Repeat Visits");

          BusinessChartBuffer.SetValueByIndex(NoOfObjectsIndex -1, Offset, CompanyStats."No. of Objects");
          BusinessChartBuffer.SetValueByIndex(NoOfServiceOrdersIndex -1, Offset, CompanyStats."No. of Service Orders");
          BusinessChartBuffer.SetValueByIndex(NoOfRepeatVisitsIndex -1, Offset, CompanyStats."No. of Repeat Visits");
        END;
        Update(CurrPage.Chart);
      END;
    END;

    PROCEDURE GetPeriod@1100528604(IOffset@1100528600 : Integer;VAR ODate@1100528601 : Record 2000000007);
    BEGIN
      ODate."Period Start" := BaseDate;
      PeriodFormManagement.FindDate('=<>', ODate, PeriodType);
      IF IOffset <> 0 THEN
        PeriodFormManagement.NextDate(IOffset, ODate, PeriodType);
    END;

    PROCEDURE GetPeriodTypeText@1100528610(IPeriodType@1100528600 : Option) PeriodTypeText : Text;
    VAR
      OptionFieldTools@1100528601 : Codeunit 11012271;
    BEGIN
      OptionFieldTools.OptionValueToReadableOption(Text000, IPeriodType, PeriodTypeText);
    END;

    PROCEDURE DetermineMeasurementIndexes@1100528611();
    BEGIN
      NoOfObjectsIndex := 1;
      NoOfServiceOrdersIndex := 2;
      NoOfRepeatVisitsIndex := 3;
    END;

    EVENT Chart@-1100528604::DataPointClicked@1(point@1100528600 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartDataPoint");
    BEGIN
    END;

    EVENT Chart@-1100528604::DataPointDoubleClicked@2(point@1100528600 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartDataPoint");
    BEGIN
    END;

    EVENT Chart@-1100528604::AddInReady@3();
    BEGIN
      UpdateChart;
    END;

    BEGIN
    END.
  }
}

