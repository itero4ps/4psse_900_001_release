OBJECT Page 11229500 Database Logins to User
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Database Logins to User;
    SourceTable=Table11229891;
    PageType=List;
    OnAfterGetRecord=BEGIN
                       WindowsUserName := IdentityManagement.UserName("Windows Security ID");
                     END;

  }
  CONTROLS
  {
    { 1100525000;0;Container;
                ContainerType=ContentArea }

    { 1100525001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100525002;2;Field  ;
                SourceExpr="User ID" }

    { 1100525003;2;Field  ;
                SourceExpr=Password }

    { 1100525004;2;Field  ;
                SourceExpr=Name }

    { 1100525005;2;Field  ;
                SourceExpr="Expiration Date" }

    { 1100525008;2;Field  ;
                SourceExpr="Windows Security ID";
                Visible=FALSE }

    { 1100525007;2;Field  ;
                Name=Windows User Name;
                CaptionML=ENU=Windows User Name;
                SourceExpr=WindowsUserName;
                Importance=Promoted;
                OnValidate=VAR
                             UserSID@1000 : Text;
                           BEGIN
                             //IF "Windows Security ID" = '' THEN BEGIN
                               UserSID := SID(WindowsUserName);
                               WindowsUserName := IdentityManagement.UserName(UserSID);
                               IF WindowsUserName <> '' THEN BEGIN
                                 "Windows Security ID" := UserSID;
                                 MODIFY;
                                 ValidateSid;
                                 ValidateUserName;
                               END ELSE
                                 ERROR(Text001,WindowsUserName);
                             //END;
                           END;

                OnAssistEdit=VAR
                               DSOP@1000 : DotNet "'Microsoft.Dynamics.Nav.Management.DSObjectPickerWrapper, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Management.DSObjectPicker.DSObjectPickerWrapper" RUNONCLIENT;
                               result@1001 : Text;
                             BEGIN
                               DSOP := DSOP.DSObjectPickerWrapper;
                               result := DSOP.InvokeDialogAndReturnSid;
                               IF result <> '' THEN BEGIN
                                 "Windows Security ID" := result;
                                 ValidateSid;
                                 WindowsUserName := IdentityManagement.UserName("Windows Security ID");
                                 ValidateUserName;
                               END;
                             END;
                              }

    { 1100525006;2;Field  ;
                SourceExpr=Processed }

  }
  CODE
  {
    VAR
      UserSecID@1100525008 : Record 2000000120;
      IdentityManagement@1100525007 : Codeunit 9801;
      WindowsUserName@1100525006 : Text[208];
      Text001@1100525002 : TextConst 'ENU=The account %1 is not a valid Windows account.';
      Text002@1100525001 : TextConst 'ENU=The account %1 already exists.';
      Text003@1100525000 : TextConst 'ENU=The account %1 is not allowed.';

    PROCEDURE ValidateSid@1();
    VAR
      User@1000 : Record 2000000120;
    BEGIN
      IF "Windows Security ID" = '' THEN
        ERROR(Text001,WindowsUserName);

      IF ("Windows Security ID" = 'S-1-1-0') OR ("Windows Security ID" = 'S-1-5-7') OR ("Windows Security ID" = 'S-1-5-32-544') THEN
        ERROR(Text003,IdentityManagement.UserName("Windows Security ID"));

      User.SETCURRENTKEY("Windows Security ID");
      User.SETFILTER("Windows Security ID","Windows Security ID");
      User.SETFILTER("User Security ID",'<>%1',"User Security ID");
      IF User.FINDFIRST THEN
        ERROR(Text002,WindowsUserName);
    END;

    LOCAL PROCEDURE ValidateUserName@3();
    VAR
      UserManagement4PSUpgtk@1000 : Codeunit 11012380;
      User@1100525000 : Record 2000000120;
    BEGIN
      User.SETRANGE("User Name","User ID");
      IF User.FINDFIRST THEN BEGIN
        User."User Name" := WindowsUserName;
        User."Full Name" := Name;
        User."Windows Security ID" := "Windows Security ID";
        User.MODIFY;

        UserManagement4PSUpgtk.RenameUser(
          "User ID",
          User."User Name",
          0);
        Processed := TRUE;
        "User Security ID" := User."User Security ID";
        CurrPage.UPDATE;
      END;
    END;

    BEGIN
    END.
  }
}

