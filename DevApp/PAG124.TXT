OBJECT Page 124 Comment Sheet
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW16.00,4PS9.00,4PSSE;
  }
  PROPERTIES
  {
    Permissions=TableData 18=m,
                TableData 23=m,
                TableData 27=m,
                TableData 11072003=m;
    CaptionML=ENU=Comment Sheet;
    MultipleNewLines=Yes;
    LinksAllowed=No;
    SourceTable=Table97;
    DelayedInsert=Yes;
    DataCaptionFields=No.;
    PageType=List;
    AutoSplitKey=Yes;
    OnOpenPage=BEGIN
                 CurrPage.EDITABLE(CommentIsEditable);//**4PS.n
               END;

    OnClosePage=BEGIN
                  //**4PS.sn
                  IF UpdateMain THEN BEGIN
                    CASE "Table Name" OF
                      "Table Name"::Item:
                        BEGIN
                          ItemRec.GET("No.");
                          ItemRec.MODIFY(TRUE);
                        END;
                      "Table Name"::Customer:
                        BEGIN
                          CustRec.GET("No.");
                          CustRec.MODIFY(TRUE);
                        END;
                      "Table Name"::Vendor:
                        BEGIN
                          VendorRec.GET("No.");
                          VendorRec.MODIFY(TRUE);
                        END;
                      //  IME459 ITERO.MH 160429 <<
                      "Table Name"::Job:
                        BEGIN
                          JobRec.GET("No.");
                          JobRec.MODIFY(TRUE);
                        END;
                      //  IME459 ITERO.MH 160429 >>
                    END;
                  END;
                  //**4PS.en
                END;

    OnNewRecord=BEGIN
                  SetUpNewLine;
                END;

    OnInsertRecord=BEGIN
                     UpdateMain := TRUE; //**4PS.n
                   END;

    OnModifyRecord=BEGIN
                     UpdateMain := TRUE; //**4PS.n
                   END;

    OnDeleteRecord=BEGIN
                     UpdateMain := TRUE; //**4PS.n
                   END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100485000;1 ;Action    ;
                      CaptionML=ENU=&Text Editor;
                      Promoted=Yes;
                      Image=SNInfo;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 TextEditor@1210190001 : Codeunit 11012001;
                               BEGIN
                                 //**4PS
                                 IF CurrPage.EDITABLE THEN
                                   TextEditor.CommentLine(Rec);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr=Date }

    { 1100525000;2;Field  ;
                SourceExpr="Time Created";
                Visible=FALSE }

    { 1100525001;2;Field  ;
                SourceExpr="Language Code";
                Importance=Additional }

    { 1100485090;2;Field  ;
                SourceExpr="Comment Code" }

    { 6   ;2   ;Field     ;
                SourceExpr=Code;
                OnValidate=BEGIN
                             CodeOnAfterValidate;
                           END;

                OnLookup=VAR
                           TxtRec@1100485000 : Record 7;
                         BEGIN
                           //**4PS
                           TxtRec.Code := Code;
                           IF PAGE.RUNMODAL(PAGE::"Standard Text Codes", TxtRec) = ACTION::LookupOK THEN BEGIN
                             IF  (TxtRec.Code <> Code) AND (TxtRec.Code <> '') THEN BEGIN
                               Code := TxtRec.Code;
                               GetStandardText;
                             END;
                           END;
                         END;
                          }

    { 4   ;2   ;Field     ;
                SourceExpr=Comment }

    { 1100485001;2;Field  ;
                SourceExpr="Created by";
                Visible=FALSE }

  }
  CODE
  {
    VAR
      ItemRec@1100485006 : Record 27;
      CustRec@1100485011 : Record 18;
      VendorRec@1100485012 : Record 23;
      JobRec@1100285100 : Record 11072003;
      CompRel@1100485005 : Record 11012059;
      RelShipMgtSetup@1100485014 : Record 5079;
      CentrManagementCU@1100485004 : Codeunit 11012010;
      NavBln@1100485003 : Boolean;
      DiscBln@1100485002 : Boolean;
      DiscGrpBln@1100525000 : Boolean;
      RcpBln@1100485001 : Boolean;
      ProspBln@1100485007 : Boolean;
      ContBln@1100485008 : Boolean;
      CustBln@1100485009 : Boolean;
      VendBln@1100485010 : Boolean;
      UpdateMain@1100485000 : Boolean;

    PROCEDURE GetStandardText@1210190000();
    VAR
      TxtRec@1210190003 : Record 7;
      LineTxtRec@1210190002 : Record 280;
      CommLineRec@1210190001 : Record 97;
      CurrLino@1210190000 : Integer;
      lvText11012000@1100485001 : TextConst 'ENU=Not enough space to insert text lines, not all text lines are added.';
    BEGIN
      //**4PS
      IF Code = '' THEN
        EXIT;

      CurrPage.SAVERECORD;
      CurrLino := "Line No.";
      IF TxtRec.GET(Code) THEN BEGIN
        IF (TxtRec.Description <> '') THEN BEGIN
          Comment := TxtRec.Description;
          MODIFY;
        END;
        LineTxtRec.SETRANGE("Table Name", LineTxtRec."Table Name"::"Standard Text");
        LineTxtRec.SETRANGE("No.", Code);
        IF LineTxtRec.FIND('-') THEN BEGIN
          REPEAT
            CommLineRec := Rec;
            CommLineRec.Comment := LineTxtRec.Text;
            CommLineRec."Line No." := CurrLino + 1;
            CommLineRec.Date := 0D;
            CommLineRec.Code := '';
            IF NOT CommLineRec.INSERT THEN BEGIN
              MESSAGE(lvText11012000);
              EXIT;
            END;
            CurrLino := CommLineRec."Line No.";
          UNTIL LineTxtRec.NEXT = 0;
        END;
      END;
    END;

    LOCAL PROCEDURE CodeOnAfterValidate@19050293();
    BEGIN
      //**4PS
      IF Code <> xRec.Code THEN
        GetStandardText;
    END;

    LOCAL PROCEDURE CommentIsEditable@1100528400() : Boolean;
    VAR
      CentralDataMgt@1100528400 : Codeunit 11012010;
      NavBln@1100528403 : Boolean;
      DiscBln@1100528402 : Boolean;
      DiscGrpBln@1100528300 : Boolean;
      RcpBln@1100528401 : Boolean;
      CompanyRelationsCentralMgt@1100528404 : Record 11012059;
    BEGIN
      //**4PS.sn
      CASE "Table Name" OF
        "Table Name"::Customer:
          BEGIN
            IF NOT CompanyRelationsCentralMgt.READPERMISSION THEN
              EXIT(FALSE);
            EXIT(CentralDataMgt.CustCommentIsEditable("No."));
          END;
        "Table Name"::Vendor:
          BEGIN
            IF NOT CompanyRelationsCentralMgt.READPERMISSION THEN
              EXIT(FALSE);
            EXIT(CentralDataMgt.VendorCommentIsEditable("No."));
          END;
        "Table Name"::Item:
          BEGIN
            IF NOT CompanyRelationsCentralMgt.READPERMISSION THEN
              EXIT(FALSE);
            CentralDataMgt.ItemFormEditable(NavBln,DiscBln,DiscGrpBln,RcpBln);
            EXIT(NOT NavBln);
          END;
        "Table Name"::"Standard Object":
          BEGIN
            IF NOT CompanyRelationsCentralMgt.READPERMISSION THEN
              EXIT(FALSE);
            EXIT(CentralDataMgt.StandardObjectFormEditable);
          END;
        ELSE
          EXIT(TRUE);
      END;
      //**4PS.en
    END;

    PROCEDURE IsUpdated@1100528300() : Boolean;
    BEGIN
      EXIT(UpdateMain);
    END;

    BEGIN
    {
      4PS, 24-08-2012, call C000623, modify permissions added for table item,customer,vendor
      160429 IME459 ITERO.MH - added Job table to be included in "OnClosePage"-functionality with Modify-permission
    }
    END.
  }
}

