OBJECT Page 560 Dimension Value List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW17.00,4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Dimension Value List;
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table349;
    DataCaptionExpr=GetFormCaption;
    SourceTableView=SORTING(Dimension Code,Code);
    PageType=List;
    OnOpenPage=VAR
                 DPA_DimensionValue@1100526000 : Record 349;
                 i@1100526001 : Integer;
               BEGIN
                 //**4PS.sn
                 GLSetup.GET;
                 IF FilteredOnGlobalDim1 THEN BEGIN
                   WITH DPA_DimensionValue DO BEGIN
                     DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));

                     FOR i := 10 DOWNTO 0 DO BEGIN
                       FILTERGROUP(i);
                       Rec.FILTERGROUP(i);
                       COPYFILTER("Global Dimension 1 Code", Rec.Code);
                     END;
                   END;
                 END;

                 IF GLSetup."Global Dimension 2 Code" = GETFILTER("Dimension Code") THEN
                   DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));

                 IF GETFILTER(Blocked) = '' THEN //C022587
                   ShowTable;
                 //**4PS.en
               END;

    OnAfterGetRecord=BEGIN
                       NameIndent := 0;
                       //**4PS.sn
                       IF ("Global Dimension No." = 2) AND (GETFILTER("Project Filter") <> '') THEN BEGIN
                         CALCFIELDS("Project Related Name");
                         IF "Project Related Name" <> '' THEN
                           Name := "Project Related Name";
                       END;

                       FormatLines;
                     END;

    OnNewRecord=BEGIN
                  DPA_SetDefaultDepartment; //**4PS.n
                END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Editable=FALSE;
                IndentationColumnName=NameIndent;
                IndentationControls=Name;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr=Code;
                Style=Strong;
                StyleExpr=Emphasize }

    { 4   ;2   ;Field     ;
                SourceExpr=Name;
                Style=Strong;
                StyleExpr=Emphasize }

    { 6   ;2   ;Field     ;
                SourceExpr="Dimension Value Type";
                Visible=FALSE }

    { 8   ;2   ;Field     ;
                SourceExpr=Totaling;
                Visible=FALSE }

    { 10  ;2   ;Field     ;
                SourceExpr=Blocked;
                Visible=FALSE }

    { 12  ;2   ;Field     ;
                SourceExpr="Consolidation Code";
                Visible=FALSE }

    { 1100409000;1;Group  ;
                GroupType=Group }

    { 1100409001;2;Field  ;
                CaptionML=ENU=Show only Active Dimension Values;
                SourceExpr=ShowOnlyActiveCostObjects;
                OnValidate=BEGIN
                             //**4PS
                             ShowTable;
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      Text000@1001 : TextConst 'ENU=Shortcut Dimension %1';
      Emphasize@19004235 : Boolean INDATASET;
      NameIndent@19079073 : Integer INDATASET;
      ShowOnlyActiveCostObjects@1100528800 : Boolean;
      DPA_IsHardFilter@1100000999 : Boolean;

    PROCEDURE GetSelectionFilter@4() : Text;
    VAR
      DimVal@1004 : Record 349;
      SelectionFilterManagement@1001 : Codeunit 46;
    BEGIN
      CurrPage.SETSELECTIONFILTER(DimVal);
      EXIT(SelectionFilterManagement.GetSelectionFilterForDimensionValue(DimVal));
    END;

    PROCEDURE SetSelection@3(VAR DimVal@1001 : Record 349);
    BEGIN
      CurrPage.SETSELECTIONFILTER(DimVal);
    END;

    LOCAL PROCEDURE GetFormCaption@1() : Text[250];
    BEGIN
      IF GETFILTER("Dimension Code") <> '' THEN
        EXIT(GETFILTER("Dimension Code"));

      IF GETFILTER("Global Dimension No.") = '1' THEN
        EXIT(GLSetup."Global Dimension 1 Code");

      IF GETFILTER("Global Dimension No.") = '2' THEN
        EXIT(GLSetup."Global Dimension 2 Code");

      EXIT(STRSUBSTNO(Text000,"Global Dimension No."));
    END;

    LOCAL PROCEDURE FormatLines@19039177();
    BEGIN
      Emphasize := "Dimension Value Type" <> "Dimension Value Type"::Standard;
      NameIndent := Indentation;
    END;

    PROCEDURE ShowTable@1210190000();
    BEGIN
      //**4PS
      IF ShowOnlyActiveCostObjects THEN
        SETRANGE(Blocked, FALSE)
      ELSE
        SETRANGE(Blocked);
    END;

    LOCAL PROCEDURE FilteredOnGlobalDim1@1100528400() : Boolean;
    VAR
      DimensionValue@1100528400 : Record 349;
      I@1100528401 : Integer;
    BEGIN
      //**4PS.sn
      GLSetup.GET;
      DimensionValue.COPY(Rec);
      FOR I := 10 DOWNTO 0 DO BEGIN
        DimensionValue.FILTERGROUP(I);
        IF (GLSetup."Global Dimension 1 Code" = DimensionValue.GETFILTER("Dimension Code")) OR
           (DimensionValue.GETFILTER("Global Dimension No.") = '1')
        THEN
          EXIT(TRUE);
      END;
      //**4PS.en
    END;

    BEGIN
    {
      4PS - 26-09-06 Sort Order changed to Code,Glob. Dim Code. Result: startvalue is same as fieldvalue from which this form is called.
    }
    END.
  }
}

