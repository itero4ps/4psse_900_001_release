OBJECT Page 6085594 Document Card
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.4.05,4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Document Card;
    InsertAllowed=No;
    LinksAllowed=No;
    SourceTable=Table6085590;
    DataCaptionExpr="No." + ' - ' + GetSourceName;
    PageType=Card;
    CardPageID=Document Card;
    PromotedActionCategoriesML=ENU=0,Process,2,Document,Template,Vendor,Customer;
    OnOpenPage=VAR
                 Channel@1000000000 : Code[50];
               BEGIN
                 Channel := CREATEGUID;
                 EventNotifierSource.ADDTEXT(Channel);

                 CurrPage.CaptureUI.PAGE.SetConfig(CurrPage.Header.PAGE.CAPTION,CurrPage.Lines.PAGE.CAPTION,Channel);
                 IsOpen := TRUE;
               END;

    OnAfterGetRecord=VAR
                       Template@161024012 : Record 6085579;
                       CaptureXmlDoc@1000000000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
                     BEGIN
                       SourceID := GetSourceID;
                       TemplSearchText := ''; //**4PS.n Call C020636

                       IF ISNULL(CaptureXmlDoc) OR IsOpen THEN BEGIN
                         EnableFields;
                         CaptureAddinLib.BuildLineFieldOrderCommand("Template No.",CaptureXmlDoc);
                         CurrPage.CaptureUI.PAGE.SendCommand(CaptureXmlDoc);
                         CLEAR(CaptureXmlDoc);
                         EnableFields;
                         IsOpen := FALSE;
                         IF Template.GET("Template No.") THEN
                           Template.CALCFIELDS("No. of Search Texts")
                         ELSE
                           CLEAR(Template);

                         FirstLineHasCaptions := Template."First Table Line Has Captions";
                         TemplSearchTexts := Template."No. of Search Texts";
                         TemplSearchText := Template."Search Text"; //**4PS Call C020636

                       END;
                     END;

    ActionList=ACTIONS
    {
      { 1000000032;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1000000031;1 ;ActionGroup;
                      Name=<Action1000000026>;
                      CaptionML=ENU=F&unctions }
      { 161024025;2  ;Action    ;
                      Name=<Action1000000025>;
                      ShortCutKey=Ctrl+F9;
                      CaptionML=ENU=&Recognize Fields;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=SuggestVendorBills;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 CaptureEngine@1000000000 : Codeunit 6085575;
                                 SourceNo@1000000002 : Code[200];
                               BEGIN
                                 SourceNo := GetSourceID;
                                 CaptureEngine.CaptureDocument(Rec);

                                 IF (GetSourceID = '') THEN
                                   EXIT;

                                 IF (GetSourceID = SourceNo) AND ("Template No." = '') THEN BEGIN
                                     COMMIT;
                                     VALIDATE("Template No.",GetDefaultTemplate(TRUE));
                                     MODIFY(TRUE);
                                     CaptureEngine.CaptureDocument(Rec);
                                 END;

                                 CurrPage.Header.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Header page
                                 CurrPage.Comments.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Comments page
                                 CurrPage.UPDATE(FALSE); // This will restore the dropped relationship

                                 EnableFields;

                                 CurrPage.CaptureUI.PAGE.RedrawValues;
                                 CurrPage.CaptureUI.PAGE.SetSendAllPendingCommands(TRUE);

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 161024023;2  ;Action    ;
                      Name=<Action1000000036>;
                      ShortCutKey=Ctrl+M;
                      CaptionML=ENU=&Match Lines;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Splitlines;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 Template@1000000001 : Record 6085579;
                               BEGIN
                                 TestSourceNoAndTemplNo;

                                 TestStatus;

                                 Template.GET("Template No.");
                                 Template.TESTFIELD("Codeunit ID: Show Match");
                                 CODEUNIT.RUN(Template."Codeunit ID: Show Match",Rec);
                                 FIND('=');
                                 ValidateDocument;
                               END;
                                }
      { 6085585 ;2   ;Action    ;
                      Name=<Action1000000019>;
                      ShortCutKey=Shift+Ctrl+P;
                      CaptionML=ENU=&Show PDF File;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=View;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 OpenPDFFile;
                               END;
                                }
      { 161024022;2  ;Action    ;
                      Name=<Action1000000037>;
                      ShortCutKey=F7;
                      CaptionML=ENU=&Validate;
                      Promoted=Yes;
                      Image=CheckRulesSyntax;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 ValidateDocument;
                               END;
                                }
      { 161024021;2  ;Action    ;
                      Name=<Action161024012>;
                      CaptionML=ENU=Crea&te/Select Template;
                      OnAction=VAR
                                 Template@161024014 : Record 6085579;
                                 CaptureEngine@161024013 : Codeunit 6085575;
                                 NewTemplateNo@161024012 : Code[20];
                               BEGIN
                                 TestStatus;

                                 NewTemplateNo := Template.CreateNew("Document Category Code","Source Record ID Tree ID",FALSE);
                                 IF (NewTemplateNo <> '') THEN BEGIN
                                   VALIDATE("Template No.",NewTemplateNo);
                                   MODIFY(TRUE);

                                   CaptureEngine.CaptureDocument(Rec);
                                   EnableFields;

                                   CurrPage.Header.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Header page
                                   CurrPage.Comments.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Comments page
                                   CurrPage.UPDATE(FALSE); // This will restore the dropped relationship

                                   EnableFields;

                                   CurrPage.CaptureUI.PAGE.RedrawValues;
                                   CurrPage.CaptureUI.PAGE.SetSendAllPendingCommands(TRUE);
                                 END;
                               END;
                                }
      { 6085580 ;2   ;Action    ;
                      CaptionML=ENU=&Navigate;
                      Promoted=Yes;
                      Image=MakeOrder;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 ShowRegisteredDoc;
                               END;
                                }
      { 6085578 ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=Move to &Company;
                      Promoted=Yes;
                      Image=Export;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 Document@6085573 : Record 6085590;
                               BEGIN
                                 Document := Rec;
                                 Document.SETRECFILTER;
                                 CODEUNIT.RUN(CODEUNIT::"Doc. - Move to Company",Document);
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 6085579 ;2   ;Action    ;
                      CaptionML=ENU=Show Original &E-mail;
                      Image=SendApprovalRequest;
                      OnAction=BEGIN
                                 OpenEMailFile;
                               END;
                                }
      { 6085576 ;2   ;Action    ;
                      Name=<Action1000000023>;
                      ShortCutKey=Ctrl+S;
                      CaptionML=ENU=&Template Card;
                      RunObject=Page 6085584;
                      RunPageView=SORTING(No.);
                      RunPageLink=No.=FIELD(Template No.);
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Edit;
                      PromotedCategory=Category5 }
      { 6085577 ;2   ;Action    ;
                      Name=<Action161024014>;
                      CaptionML=ENU=&Add Template Field;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Post;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 Template@161024012 : Record 6085579;
                               BEGIN
                                 TestSourceNoAndTemplNo;

                                 IF Template.GET("Template No.") THEN
                                   IF Template.ManuallyAddTemplField THEN BEGIN
                                     CurrPage.Header.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Header page
                                     CurrPage.Comments.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Comments page
                                     CurrPage.UPDATE(FALSE); // This will restore the dropped relationship

                                     EnableFields;

                                     CurrPage.CaptureUI.PAGE.RedrawValues;
                                     CurrPage.CaptureUI.PAGE.SetSendAllPendingCommands(TRUE);

                                     CurrPage.UPDATE(FALSE);
                                   END;
                               END;
                                }
      { 6085575 ;2   ;Action    ;
                      Name=<Action1000000027>;
                      ShortCutKey=Ctrl+T;
                      CaptionML=ENU=&Translations;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Track;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 Template@1000000001 : Record 6085579;
                               BEGIN
                                 Template.GET("Template No.");
                                 Template.TESTFIELD("Codeunit ID: Transl. (Doc)");
                                 CODEUNIT.RUN(Template."Codeunit ID: Transl. (Doc)",Rec);
                               END;
                                }
      { 6085592 ;1   ;ActionGroup;
                      CaptionML=ENU=Vendor }
      { 6085591 ;2   ;Action    ;
                      CaptionML=ENU=Vendor Card;
                      Promoted=Yes;
                      Visible=IsVendor;
                      Image=Vendor;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 Vendor@1160040000 : Record 23;
                               BEGIN
                                 Vendor.GET(GetSourceID);
                                 PAGE.RUN(PAGE::"Vendor Card",Vendor);
                               END;
                                }
      { 6085590 ;2   ;Action    ;
                      CaptionML=ENU=&Unposted Documents;
                      Promoted=Yes;
                      Visible=IsVendor;
                      Image=Invoice;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 PurchHeader@1160040000 : Record 38;
                               BEGIN
                                 PurchHeader.SETRANGE("Pay-to Vendor No.",GetSourceID);
                                 PAGE.RUN(PAGE::"Purchase Documents",PurchHeader);
                               END;
                                }
      { 6085589 ;2   ;Action    ;
                      CaptionML=ENU=Ledger E&ntries;
                      Promoted=Yes;
                      Visible=IsVendor;
                      Image=VendorLedger;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 VendLedgEntry@1160040000 : Record 25;
                               BEGIN
                                 VendLedgEntry.SETCURRENTKEY("Vendor No.");
                                 VendLedgEntry.SETRANGE("Vendor No.",GetSourceID);
                                 PAGE.RUN(PAGE::"Vendor Ledger Entries",VendLedgEntry);
                               END;
                                }
      { 6085588 ;1   ;ActionGroup;
                      CaptionML=ENU=Customer }
      { 6085587 ;2   ;Action    ;
                      CaptionML=ENU=&Customer Card;
                      Promoted=Yes;
                      Visible=IsCustomer;
                      Image=Customer;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 Cust@1160040000 : Record 18;
                               BEGIN
                                 Cust.GET(GetSourceID);
                                 PAGE.RUN(PAGE::"Customer Card",Cust);
                               END;
                                }
      { 6085586 ;2   ;Action    ;
                      CaptionML=ENU=&Unposted Documents;
                      Promoted=Yes;
                      Visible=IsCustomer;
                      Image=Invoice;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 Salesheader@1160040000 : Record 36;
                               BEGIN
                                 Salesheader.SETRANGE("Sell-to Customer No.",GetSourceID);
                                 //PAGE.RUN(PAGE::"sales Documents",salesHeader);
                               END;
                                }
      { 6085573 ;2   ;Action    ;
                      CaptionML=ENU=Ledger E&ntries;
                      Promoted=Yes;
                      Visible=IsCustomer;
                      Image=VendorLedger;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 CustLedgEntry@1160040000 : Record 21;
                               BEGIN
                                 CustLedgEntry.SETCURRENTKEY("Customer No.");
                                 CustLedgEntry.SETRANGE("Customer No.",GetSourceID);
                                 PAGE.RUN(PAGE::"Customer Ledger Entries",CustLedgEntry);
                               END;
                                }
      { 1000000025;1 ;ActionGroup;
                      Name=<Action1000000022>;
                      CaptionML=ENU=&Registration }
      { 1000000024;2 ;Action    ;
                      Name=<Action1000000020>;
                      ShortCutKey=F9;
                      Ellipsis=Yes;
                      CaptionML=ENU=&Register;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Approve;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Template@1000000001 : Record 6085579;
                                 lDocumentOnHoldCode@1100528200 : Record 11020676;
                               BEGIN
                                 //**4PS.sn
                                 IF "Document On Hold Code" <> '' THEN BEGIN
                                   IF lDocumentOnHoldCode.GET("Document On Hold Code") THEN
                                     lDocumentOnHoldCode.TESTFIELD(Blocking, FALSE);
                                 END;
                                 //**4PS.en

                                 Rec.RegisterYN;

                                 EnableFields;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1000000023;2 ;Action    ;
                      Name=<Action1000000035>;
                      CaptionML=ENU=Re&ject;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Reject;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Reject;
                                 COMMIT;

                                 EnableFields;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 6085574 ;2   ;Action    ;
                      CaptionML=ENU=Re&open;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ReOpen;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Reopen;
                                 COMMIT;

                                 EnableFields;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1100528205;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1100528206;1 ;ActionGroup;
                      CaptionML=ENU=Document;
                      ActionContainerType=NewDocumentItems }
      { 1100528207;2 ;Action    ;
                      CaptionML=ENU=Document words;
                      RunObject=Page 11126509;
                      RunPageLink=Document No.=FIELD(No.);
                      Image=EntriesList;
                      RunPageMode=View }
    }
  }
  CONTROLS
  {
    { 1000000000;;Container;
                ContainerType=ContentArea }

    { 1000000006;1;Group  ;
                CaptionML=ENU=General;
                GroupType=Group }

    { 1000000012;2;Group  ;
                GroupType=Group }

    { 1000000018;3;Field  ;
                SourceExpr="No." }

    { 1100409001;3;Field  ;
                SourceExpr="Document Category Code";
                OnValidate=VAR
                             i@1100409001 : Integer;
                           BEGIN
                             //**4PS Call C020637 sn
                             IF xRec."Document Category Code" <> "Document Category Code" THEN BEGIN
                               FOR i := 0 TO 10 DO
                                 BEGIN
                                   FILTERGROUP(i);
                                   IF GETFILTER("Document Category Code") <> '' THEN BEGIN
                                     IF "Document Category Code" <> GETFILTER("Document Category Code") THEN
                                       MESSAGE(Text11012001, "No.");
                                   END;
                                 END;
                             END;

                             FILTERGROUP(0);
                             CurrPage.SAVERECORD;

                             IF NOT FIND('=<>') THEN
                               CurrPage.CLOSE;


                             CurrPage.UPDATE(FALSE);
                             GetTemplate;
                             EnableFields;
                             //**4PS Call C020637 sn
                           END;
                            }

    { 1100528200;3;Field  ;
                SourceExpr=Company }

    { 1000000007;3;Field  ;
                CaptionML=ENU=Source ID;
                SourceExpr=SourceID;
                CaptionClass='3,' + GetSourceNoCaption;
                Editable=AllowEdit;
                OnValidate=VAR
                             CaptureEngine@6085573 : Codeunit 6085575;
                           BEGIN
                             SetManual(TRUE);
                             SetSourceID(SourceID);

                             IF "Template No." = '' THEN
                               ValidateDocument
                             ELSE BEGIN
                               COMMIT;
                               CaptureEngine.CaptureDocument(Rec);
                             END;

                             EnableFields;
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           RecIDMgt@6085573 : Codeunit 6085604;
                           CaptureEngine@6085575 : Codeunit 6085575;
                         BEGIN
                           IF NOT RecIDMgt.LookupRecordID("Document Category Code",Text) THEN
                             EXIT;

                           SetManual(TRUE);
                           SetSourceID(Text);

                           IF "Template No." = '' THEN
                             ValidateDocument
                           ELSE BEGIN
                             COMMIT;
                             CaptureEngine.CaptureDocument(Rec);
                           END;

                           EnableFields;
                           CurrPage.UPDATE(FALSE);
                         END;
                          }

    { 1000000008;3;Field  ;
                SourceExpr=GetSourceName;
                CaptionClass='3,' + GetSourceNameCaption;
                Editable=AllowEdit }

    { 1100528202;3;Field  ;
                SourceExpr="E-mail Date" }

    { 1000000013;2;Group  ;
                GroupType=Group }

    { 1000000014;3;Field  ;
                SourceExpr="Template No.";
                Importance=Promoted;
                Editable=AllowEdit;
                OnValidate=VAR
                             CaptureEngine@6085573 : Codeunit 6085575;
                           BEGIN
                             CurrPage.SAVERECORD;
                             IF "Template No." <> '' THEN BEGIN
                               COMMIT;
                               CaptureEngine.CaptureDocument(Rec);
                             END;
                             EnableFields;

                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1000000009;3;Field  ;
                SourceExpr="Template Description";
                Importance=Promoted;
                Visible=FALSE }

    { 1100409003;3;Field  ;
                Name=TemplSearchText;
                CaptionML=ENU=Search Text;
                SourceExpr=TemplSearchText;
                Editable=AllowEdit;
                OnLookup=VAR
                           DocumentWord@1100409000 : Record 6085592;
                           Template@1100409001 : Record 6085579;
                         BEGIN
                           //**4PS Call C020636
                           IF AllowEdit THEN BEGIN
                             DocumentWord.SETRANGE("Document No.", "No.");
                             IF PAGE.RUNMODAL(PAGE::"Document Words", DocumentWord) = ACTION::LookupOK THEN BEGIN
                               TemplSearchText := DocumentWord.Word;
                               IF Template.GET("Template No.") THEN BEGIN
                                 Template."Search Text" := TemplSearchText;
                                 Template.MODIFY(TRUE);
                               END;
                             END;
                           END;
                         END;
                          }

    { 161024019;3;Field   ;
                CaptionML=ENU=No. of Search Texts;
                BlankZero=Yes;
                SourceExpr=TemplSearchTexts;
                Editable=FALSE;
                OnDrillDown=VAR
                              TemplSearchText@161024012 : Record 6085588;
                            BEGIN
                              TESTFIELD("Template No.");
                              TemplSearchText.SETRANGE("Template No.","Template No.");
                              PAGE.RUN(0,TemplSearchText);
                            END;
                             }

    { 1000000016;3;Field  ;
                SourceExpr=Status;
                Editable=FALSE }

    { 1000000017;3;Field  ;
                SourceExpr=OK }

    { 161024013;3;Field   ;
                CaptionML=ENU=First Table Line Has Captions;
                SourceExpr=FirstLineHasCaptions;
                OnValidate=VAR
                             Template@161024012 : Record 6085579;
                           BEGIN
                             Template.GET("Template No.");
                             Template."First Table Line Has Captions" := FirstLineHasCaptions;
                             Template.MODIFY;
                           END;
                            }

    { 1100528201;3;Field  ;
                SourceExpr="Amounts Only" }

    { 1100528203;3;Field  ;
                SourceExpr="Invoice Lines Input" }

    { 1100528204;3;Field  ;
                SourceExpr="Document On Hold Code";
                OnValidate=BEGIN
                             MODIFY;
                           END;
                            }

    { 1100409000;3;Field  ;
                SourceExpr="Document Workflow Code" }

    { 1000000005;3;Field  ;
                Name=EventNotifier;
                SourceExpr=EventNotifierSource;
                ControlAddIn=[Akeyto.DocumentCapture.Dynamics.Nav.EventNotifier;PublicKeyToken=61f876910b9e346c];
                OnControlAddIn=VAR
                                 XmlLib@1161054001 : Codeunit 6085573;
                                 Command@1161054000 : Code[1024];
                               BEGIN
                                 Command := CaptureAddinLib.BigTextToText(Data);
                                 CASE Command OF
                                   'UPDATEDOCUMENT':;
                                   'UPDATEHEADER':
                                     BEGIN
                                       ValidateDocument;
                                       CurrPage.Header.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Header page
                                       CurrPage.Comments.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Comments page
                                       CurrPage.UPDATE(FALSE); // This will restore the dropped relationship
                                     END;
                                   'UPDATELINE':
                                     BEGIN
                                       CurrPage.Lines.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Line page
                                       CurrPage.UPDATE(FALSE); // This will restore the dropped relationship
                                     END;
                                 END;
                               END;
                                }

    { 1000000015;1;Part   ;
                Name=Header;
                CaptionML=ENU=Header;
                SubPageView=SORTING(Document No.,Sort Order);
                SubPageLink=Document No.=FIELD(No.);
                PagePartID=Page6085595 }

    { 1000000004;1;Part   ;
                Name=Lines;
                CaptionML=ENU=Lines;
                PagePartID=Page6085597 }

    { 1000000002;1;Part   ;
                Name=Comments;
                CaptionML=ENU=Comments;
                SubPageView=SORTING(Document No.);
                SubPageLink=Document No.=FIELD(No.);
                PagePartID=Page6085599 }

    { 1000000010;;Container;
                ContainerType=FactBoxArea }

    { 1000000001;1;Part   ;
                Name=CaptureUI;
                CaptionML=ENU=Source Document;
                SubPageView=SORTING(No.);
                SubPageLink=No.=FIELD(No.);
                PagePartID=Page6085593 }

  }
  CODE
  {
    VAR
      DCSetup@1000000013 : Record 6085573;
      CaptureMgnt@1000000011 : Codeunit 6085576;
      CaptureAddinLib@1000000001 : Codeunit 6085660;
      SourceID@6085573 : Text[250];
      EventNotifierSource@1000000000 : BigText;
      TemplSearchTexts@161024014 : Integer;
      IsOpen@1000000010 : Boolean;
      AllowEdit@1000000008 : Boolean INDATASET;
      FirstLineHasCaptions@161024012 : Boolean;
      ShowFirstLineHasCaptions@161024013 : Boolean INDATASET;
      IsVendor@6085575 : Boolean INDATASET;
      IsCustomer@6085574 : Boolean INDATASET;
      Text11012001@1100409000 : TextConst 'ENU=The Category is not within the filter. The Document %1 is anbandoned.';
      TemplSearchText@1100409001 : Text[200];

    PROCEDURE EnableFields@1000000017();
    VAR
      TemplateField@1000000000 : Record 6085580;
    BEGIN
      AllowEdit := Status = Status::Open;

      TemplateField.SETCURRENTKEY("Template No.",Type,"Sort Order");
      TemplateField.SETRANGE("Template No.","Template No.");
      TemplateField.SETRANGE(Type,TemplateField.Type::Header);
      IF TemplateField.FINDFIRST THEN;

      CurrPage.Header.PAGE.UpdatePage;
      CurrPage.Comments.PAGE.UpdatePage;

      CurrPage.Lines.PAGE.SetDocument(Rec);
      CurrPage.Lines.PAGE.UpdatePage;

      IsVendor := GetSourceTableNo = DATABASE::Vendor;
      IsCustomer := GetSourceTableNo = DATABASE::Customer;
    END;

    PROCEDURE GetTemplate@1000000005() : Boolean;
    VAR
      Template@1100409000 : Record 6085579;
      TemplSearchText@1100409001 : Text[200];
    BEGIN
      //**4PS
      CALCFIELDS("Template Description");
      IF NOT Template.GET("Template No.") THEN
        CLEAR(Template);
      TemplSearchText := Template."Search Text";
      FirstLineHasCaptions := Template."First Table Line Has Captions";
      Template.CALCFIELDS("No. of Search Texts");
      TemplSearchTexts := Template."No. of Search Texts";
      EXIT(Template."No." <> '');
    END;

    BEGIN
    END.
  }
}

