OBJECT Page 6085595 Document Header Fields ListPar
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW16.00.00.4.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Document Header Fields ListPar;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table6085595;
    SourceTableView=SORTING(Document No.,Sort Order);
    PageType=ListPart;
    SourceTableTemporary=Yes;
    OnFindRecord=VAR
                   Doc@1000000003 : Record 6085590;
                 BEGIN
                   FILTERGROUP(4);
                   IF GETFILTER("Document No.") = '2..1' THEN BEGIN // This fixes a MS bug in RTC
                     FILTERGROUP(0);
                     EXIT;
                   END;

                   IF NOT Doc.GET(GETFILTER("Document No.")) THEN
                     CLEAR(Doc);

                   IF (Doc."No." <> Document."No.") OR (Doc."Template No." <> Document."Template No.") THEN BEGIN
                     Document := Doc;
                     InitFields;
                     CurrPage.UPDATE(FALSE);
                   END;

                   FILTERGROUP(0);
                   EXIT(FIND(Which));
                 END;

    OnAfterGetRecord=VAR
                       Field@161024012 : Record 6085580;
                     BEGIN
                       AllowEdit := Document.Status = Document.Status::Open;
                       IF AllowEdit THEN BEGIN
                         IF Field.GET(Document."Template No.",Field.Type::Header,"Field Code") THEN
                           AllowEdit := Field.Formula = '';
                       END;
                     END;

  }
  CONTROLS
  {
    { 1000000000;;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                GroupType=Repeater }

    { 1000000002;2;Field  ;
                CaptionML=ENU=Field;
                SourceExpr=Description;
                Enabled=FALSE;
                Editable=FALSE }

    { 1000000003;2;Field  ;
                SourceExpr=Value;
                Editable=AllowEdit;
                OnValidate=VAR
                             Field@1000000001 : Record 6085580;
                           BEGIN
                             Document.FIND('=');
                             Document.TESTFIELD(Status,Document.Status::Open);
                             Document.TESTFIELD("Template No.");
                             Field.GET(Document."Template No.",Field.Type::Header,"Field Code");
                             CaptureMgnt.UpdateFieldValue("Document No.","Page No.",0,Field,Value,TRUE);
                             Value := CaptureMgnt.GetValueAsText(Document."No.",0,Field);
                             "Is Valid" := CaptureMgnt.IsValidValue(Field,Document."No.",0);
                             Document.FIND('=');
                             Document.ValidateDocument;
                           END;

                OnLookup=VAR
                           Field@1000000001 : Record 6085580;
                           NewText@1000000000 : Text[1024];
                         BEGIN
                           Field.GET(Document."Template No.",Field.Type::Header,"Field Code");
                           NewText := CaptureMgnt.LookupValue("Document No.",Field,0,Text);
                           IF NewText <> '' THEN BEGIN
                             Text := NewText;
                             EXIT(TRUE);
                           END;
                         END;

                OnAssistEdit=VAR
                               Field@1000000000 : Record 6085580;
                             BEGIN
                               Field.GET(Document."Template No.",Field.Type::Header,"Field Code");
                               Field.SETRECFILTER;
                               PAGE.RUNMODAL(PAGE::"Template Field Card",Field);
                             END;
                              }

    { 1000000004;2;Field  ;
                CaptionML=ENU=OK;
                SourceExpr="Is Valid";
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Document@1000000000 : Record 6085590;
      TempDocField@1000000001 : Record 6085595;
      CaptureMgnt@1000000002 : Codeunit 6085576;
      EventNotifier@1000000003 : BigText;
      AllowEdit@1000000004 : Boolean INDATASET;

    PROCEDURE InitFields@1000000003();
    VAR
      Field@1000000000 : Record 6085580;
      DocValue@1000000001 : Record 6085593;
    BEGIN
      DELETEALL;
      Field.SETCURRENTKEY("Template No.",Type,"Sort Order");
      Field.SETRANGE("Template No.",Document."Template No.");
      Field.SETRANGE(Type,Field.Type::Header);
      IF Field.FINDSET THEN
        REPEAT
          "Document No." := Document."No.";
          "Field Code" := Field.Code;
          "Sort Order" := Field."Sort Order";
          Description := Field."Field Name";
          Value := CaptureMgnt.GetValueAsText(Document."No.",0,Field);
          IF DocValue.GET(Document."No.",TRUE,Field.Code,0) THEN
            "Page No." := DocValue."Page No.";
          "Is Valid" := CaptureMgnt.IsValidValue(Field,Document."No.",0);
          INSERT;
        UNTIL Field.NEXT = 0;
      IF FINDFIRST THEN;
    END;

    PROCEDURE UpdatePage@1000000000();
    BEGIN
      InitFields;
    END;

    BEGIN
    END.
  }
}

