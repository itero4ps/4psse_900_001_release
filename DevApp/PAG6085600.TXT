OBJECT Page 6085600 Document List With Image
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.4.05;
  }
  PROPERTIES
  {
    CaptionML=ENU=Document Journal;
    InsertAllowed=No;
    SourceTable=Table6085590;
    PageType=Worksheet;
    PromotedActionCategoriesML=ENU=0,Process,2,Document,Template,Vendor,Customer;
    OnOpenPage=VAR
                 UpgradeMgt@1160040000 : Codeunit 6085606;
                 CODC@1160040001 : Codeunit 6085617;
                 Channel@1000000000 : Code[50];
               BEGIN
                 UpgradeMgt.CheckDataVersion;

                 CODC.IsCompanyActive(FALSE);

                 FILTERGROUP := 2;
                 IF GETFILTER("Document Category Code") <> '' THEN
                   CurrentDocCategory := GETRANGEMIN("Document Category Code");
                 FILTERGROUP := 0;
                 UpdateList;

                 Channel := CREATEGUID;
                 EventNotifierSource.ADDTEXT(Channel);

                 CurrPage.CaptureUI.PAGE.SetConfig(CurrPage.Header.PAGE.CAPTION,'',Channel);
               END;

    OnAfterGetRecord=VAR
                       Template@1000000000 : Record 6085579;
                     BEGIN
                       SetManual(TRUE);
                       SourceID := GetSourceID;

                       IF Template.GET("Template No.") THEN
                         Template.CALCFIELDS("No. of Search Texts")
                       ELSE
                         CLEAR(Template);

                       TemplSearchText := Template."Search Text";
                       TemplSearchTexts := Template."No. of Search Texts";
                       EnableFields;
                       UpdateStatusText;
                     END;

    ActionList=ACTIONS
    {
      { 1000000028;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1000000026;1 ;ActionGroup;
                      CaptionML=ENU=F&unctions }
      { 6085576 ;2   ;Action    ;
                      Name=<Action63>;
                      ShortCutKey=Shift+Ctrl+E;
                      CaptionML=ENU=&Document Card;
                      RunObject=Page 6085594;
                      RunPageView=SORTING(No.);
                      RunPageLink=No.=FIELD(No.);
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=EditLines;
                      PromotedCategory=Category4 }
      { 161024022;2  ;Action    ;
                      Name=<Action1000000025>;
                      ShortCutKey=Ctrl+F9;
                      CaptionML=ENU=&Recognize Fields;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=SuggestVendorBills;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 Documents@161024013 : Record 6085590;
                                 Document@161024014 : Record 6085590;
                                 CaptureEngine@1000000000 : Codeunit 6085575;
                                 SourceNo@1000000002 : Code[200];
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(Documents);
                                 IF Documents.COUNT > 1 THEN BEGIN
                                   IF NOT CONFIRM(Text001,TRUE,Documents.COUNT) THEN
                                     ERROR('');

                                   IF Documents.FINDSET THEN
                                     REPEAT
                                       Document := Documents;
                                       CaptureEngine.CaptureDocument(Document);
                                       COMMIT;
                                     UNTIL Documents.NEXT = 0;
                                 END ELSE BEGIN
                                   SourceNo := GetSourceID;
                                   CaptureEngine.CaptureDocument(Rec);

                                   IF (GetSourceID = '') THEN
                                     EXIT;

                                   IF (GetSourceID = SourceNo) AND ("Template No." = '') THEN BEGIN
                                       COMMIT;
                                       VALIDATE("Template No.",GetDefaultTemplate(TRUE));
                                       MODIFY(TRUE);
                                       CaptureEngine.CaptureDocument(Rec);
                                   END;
                                 END;

                                 CurrPage.Header.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Header page
                                 CurrPage.Comments.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Comments page
                                 CurrPage.UPDATE(FALSE); // This will restore the dropped relationship

                                 EnableFields;

                                 CurrPage.CaptureUI.PAGE.RedrawValues;
                                 CurrPage.CaptureUI.PAGE.SetSendAllPendingCommands(TRUE);

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 161024016;2  ;Action    ;
                      Name=<Action1000000036>;
                      ShortCutKey=Ctrl+M;
                      CaptionML=ENU=&Match Lines;
                      Promoted=Yes;
                      Image=Splitlines;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 Template@1000000001 : Record 6085579;
                               BEGIN
                                 TestSourceNoAndTemplNo;

                                 TestStatus;

                                 Template.GET("Template No.");
                                 Template.TESTFIELD("Codeunit ID: Show Match");
                                 CODEUNIT.RUN(Template."Codeunit ID: Show Match",Rec);
                                 FIND('=');
                                 ValidateDocument;
                               END;
                                }
      { 6085587 ;2   ;Action    ;
                      ShortCutKey=Shift+Ctrl+P;
                      CaptionML=ENU=&Show PDF File;
                      Promoted=Yes;
                      Image=View;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 OpenPDFFile;
                               END;
                                }
      { 161024018;2  ;Action    ;
                      Name=<Action1000000037>;
                      ShortCutKey=F7;
                      CaptionML=ENU=&Validate;
                      Promoted=Yes;
                      Image=CheckRulesSyntax;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 ValidateDocument;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 6085591 ;2   ;Action    ;
                      CaptionML=ENU=&Navigate;
                      Promoted=Yes;
                      Image=MakeOrder;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 ShowRegisteredDoc;
                               END;
                                }
      { 161024017;2  ;Action    ;
                      Name=<Action161024012>;
                      CaptionML=ENU=Crea&te/Select Template;
                      OnAction=VAR
                                 Template@161024014 : Record 6085579;
                                 CaptureEngine@161024013 : Codeunit 6085575;
                                 NewTemplateNo@161024012 : Code[20];
                               BEGIN
                                 TestStatus;

                                 NewTemplateNo := Template.CreateNew("Document Category Code","Source Record ID Tree ID",FALSE);
                                 IF (NewTemplateNo <> '') THEN BEGIN
                                   VALIDATE("Template No.",NewTemplateNo);
                                   MODIFY(TRUE);

                                   CaptureEngine.CaptureDocument(Rec);
                                   EnableFields;

                                   CurrPage.Header.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Header page
                                   CurrPage.Comments.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Comments page
                                   CurrPage.UPDATE(FALSE); // This will restore the dropped relationship

                                   EnableFields;

                                   CurrPage.CaptureUI.PAGE.RedrawValues;
                                   CurrPage.CaptureUI.PAGE.SetSendAllPendingCommands(TRUE);
                                 END;
                               END;
                                }
      { 6085582 ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=Move to &Company;
                      Promoted=Yes;
                      Image=Export;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 Documents@161024012 : Record 6085590;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(Documents);
                                 CODEUNIT.RUN(CODEUNIT::"Doc. - Move to Company",Documents);
                               END;
                                }
      { 6085593 ;2   ;Action    ;
                      Name=<Action1160040000>;
                      CaptionML=ENU=Split and Merge;
                      Promoted=Yes;
                      Image=Documents;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 SplitAndMerge@1160040001 : Page 6085607;
                               BEGIN
                                 SplitAndMerge.SetParam(CurrentDocCategory,Rec);
                                 SplitAndMerge.RUNMODAL;
                                 CurrPage.UPDATE(FALSE);
                                 IF FIND('=') THEN
                                   CurrPage.CaptureUI.PAGE.UpdatePage;
                               END;
                                }
      { 6085574 ;2   ;Action    ;
                      CaptionML=ENU=Show Original &E-mail;
                      Image=SendApprovalRequest;
                      OnAction=BEGIN
                                 OpenEMailFile;
                               END;
                                }
      { 6085577 ;2   ;Action    ;
                      ShortCutKey=Ctrl+S;
                      CaptionML=ENU=&Template Card;
                      RunObject=Page 6085584;
                      RunPageView=SORTING(No.);
                      RunPageLink=No.=FIELD(Template No.);
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Edit;
                      PromotedCategory=Category5 }
      { 6085581 ;2   ;Action    ;
                      Name=<Action161024014>;
                      CaptionML=ENU=&Add Template Field;
                      Promoted=Yes;
                      Image=Post;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 Template@161024012 : Record 6085579;
                               BEGIN
                                 TestSourceNoAndTemplNo;

                                 IF Template.GET("Template No.") THEN
                                   IF Template.ManuallyAddTemplField THEN BEGIN
                                     CurrPage.Header.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Header page
                                     CurrPage.Comments.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Comments page
                                     CurrPage.UPDATE(FALSE); // This will restore the dropped relationship

                                     EnableFields;

                                     CurrPage.CaptureUI.PAGE.RedrawValues;
                                     CurrPage.CaptureUI.PAGE.SetSendAllPendingCommands(TRUE);

                                     CurrPage.UPDATE(FALSE);
                                   END;
                               END;
                                }
      { 6085575 ;2   ;Action    ;
                      Name=<Action1000000027>;
                      ShortCutKey=Ctrl+T;
                      CaptionML=ENU=&Translations;
                      Promoted=Yes;
                      Image=Track;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 Template@1000000001 : Record 6085579;
                               BEGIN
                                 Template.GET("Template No.");
                                 Template.TESTFIELD("Codeunit ID: Transl. (Doc)");
                                 CODEUNIT.RUN(Template."Codeunit ID: Transl. (Doc)",Rec);
                               END;
                                }
      { 6085590 ;1   ;ActionGroup;
                      CaptionML=ENU=Vendor }
      { 6085589 ;2   ;Action    ;
                      Name=<Action1160040010>;
                      CaptionML=ENU=Vendor Card;
                      Promoted=Yes;
                      Visible=IsVendor;
                      Image=Vendor;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 Vendor@1160040000 : Record 23;
                               BEGIN
                                 Vendor.GET(GetSourceID);
                                 PAGE.RUN(PAGE::"Vendor Card",Vendor);
                               END;
                                }
      { 6085588 ;2   ;Action    ;
                      Name=<Action1160040014>;
                      CaptionML=ENU=&Unposted Documents;
                      Promoted=Yes;
                      Visible=IsVendor;
                      Image=Invoice;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 PurchHeader@1160040000 : Record 38;
                               BEGIN
                                 PurchHeader.SETRANGE("Pay-to Vendor No.",GetSourceID);
                                 PAGE.RUN(PAGE::"Purchase Documents",PurchHeader);
                               END;
                                }
      { 6085586 ;2   ;Action    ;
                      Name=<Action1160040012>;
                      CaptionML=ENU=Ledger E&ntries;
                      Promoted=Yes;
                      Visible=IsVendor;
                      Image=VendorLedger;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 VendLedgEntry@1160040000 : Record 25;
                               BEGIN
                                 VendLedgEntry.SETCURRENTKEY("Vendor No.");
                                 VendLedgEntry.SETRANGE("Vendor No.",GetSourceID);
                                 PAGE.RUN(PAGE::"Vendor Ledger Entries",VendLedgEntry);
                               END;
                                }
      { 6085585 ;1   ;ActionGroup;
                      Name=<Action1160040011>;
                      CaptionML=ENU=Customer }
      { 6085584 ;2   ;Action    ;
                      Name=<Action1160040015>;
                      CaptionML=ENU=&Customer Card;
                      Promoted=Yes;
                      Visible=IsCustomer;
                      Image=Customer;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 Cust@1160040000 : Record 18;
                               BEGIN
                                 Cust.GET(GetSourceID);
                                 PAGE.RUN(PAGE::"Customer Card",Cust);
                               END;
                                }
      { 6085580 ;2   ;Action    ;
                      CaptionML=ENU=Ledger E&ntries;
                      Promoted=Yes;
                      Visible=IsCustomer;
                      Image=VendorLedger;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 CustLedgEntry@1160040000 : Record 21;
                               BEGIN
                                 CustLedgEntry.SETCURRENTKEY("Customer No.");
                                 CustLedgEntry.SETRANGE("Customer No.",GetSourceID);
                                 PAGE.RUN(PAGE::"Customer Ledger Entries",CustLedgEntry);
                               END;
                                }
      { 1000000022;1 ;ActionGroup;
                      CaptionML=ENU=&Register }
      { 1000000020;2 ;Action    ;
                      ShortCutKey=F9;
                      CaptionML=ENU=&Register;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Approve;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Template@1000000001 : Record 6085579;
                               BEGIN
                                 Rec.RegisterYN;

                                 IF ISEMPTY THEN
                                   CurrPage.CaptureUI.PAGE.ClearImage;
                               END;
                                }
      { 1000000029;2 ;Action    ;
                      Name=<Action1000000035>;
                      CaptionML=ENU=Re&ject;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Reject;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Reject;
                                 CurrPage.UPDATE(FALSE);

                                 IF ISEMPTY THEN
                                   CurrPage.CaptureUI.PAGE.ClearImage;
                               END;
                                }
      { 6085573 ;2   ;Action    ;
                      CaptionML=ENU=Re&open;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ReOpen;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Reopen;
                                 CurrPage.UPDATE(FALSE);

                                 IF ISEMPTY THEN
                                   CurrPage.CaptureUI.PAGE.ClearImage;
                               END;
                                }
      { 6085578 ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=Register Batch;
                      Promoted=Yes;
                      Image=PostBatch;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Document@161024012 : Record 6085590;
                               BEGIN
                                 Document.COPY(Rec);
                                 Document.MARKEDONLY(TRUE);
                                 IF Document.ISEMPTY THEN BEGIN
                                   CurrPage.SETSELECTIONFILTER(Document);
                                   IF Document.COUNT < 2 THEN BEGIN
                                     Document.MARKEDONLY(FALSE);
                                     Document.COPYFILTERS(Rec);
                                   END;
                                 END;

                                 IF NOT CONFIRM(Text002,TRUE,Document.COUNT) THEN
                                   EXIT;

                                 REPORT.RUNMODAL(REPORT::"Batch Register Documents",FALSE,FALSE,Document);
                                 CurrPage.UPDATE(FALSE);

                                 IF ISEMPTY THEN
                                   CurrPage.CaptureUI.PAGE.ClearImage;
                               END;
                                }
      { 6085579 ;2   ;Action    ;
                      CaptionML=ENU=&Filter;
                      Promoted=Yes;
                      Image=FilterLines;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 FilterSourceRecPage@6085573 : Page 6085609;
                                 NewSourceRecTreeId@6085574 : Integer;
                               BEGIN
                                 IF "Document Category Code" <> '' THEN BEGIN
                                   FilterSourceRecPage.SetValues("Document Category Code","Source Record ID Tree ID");
                                   FilterSourceRecPage.LOOKUPMODE(TRUE);
                                   IF FilterSourceRecPage.RUNMODAL = ACTION::LookupOK THEN
                                     FilterSourceRecPage.GetValues(NewSourceRecTreeId);
                                 END;

                                 IF NewSourceRecTreeId <> 0 THEN
                                   SETRANGE("Source Record ID Tree ID",NewSourceRecTreeId)
                                 ELSE
                                   SETRANGE("Source Record ID Tree ID");
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000016;1;Group  ;
                GroupType=Group }

    { 1000000017;2;Field  ;
                CaptionML=ENU=Document Category;
                SourceExpr=CurrentDocCategory;
                OnValidate=BEGIN
                             UpdateList;

                             EnableFields;
                           END;

                OnLookup=VAR
                           DocCat@1000000000 : Record 6085575;
                         BEGIN
                           IF DocCat.GET(CurrentDocCategory) THEN;
                           IF PAGE.RUNMODAL(0,DocCat) = ACTION::LookupOK THEN BEGIN
                             Text := DocCat.Code;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 1000000018;2;Field  ;
                CaptionML=ENU=State Filter;
                OptionCaptionML=ENU=Open,Registered,Rejected,All;
                SourceExpr=StatusFilter;
                OnValidate=BEGIN
                             UpdateList
                           END;
                            }

    { 1000000021;2;Field  ;
                Name=EventNotifier;
                SourceExpr=EventNotifierSource;
                ControlAddIn=[Akeyto.DocumentCapture.Dynamics.Nav.EventNotifier;PublicKeyToken=61f876910b9e346c];
                OnControlAddIn=BEGIN
                                 IF Index = 0 THEN
                                   HandleSimpleCommand(CaptureAddinLib.BigTextToText(Data))
                               END;
                                }

    { 1000000001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1000000002;2;Field  ;
                SourceExpr="No.";
                Editable=FALSE }

    { 1000000003;2;Field  ;
                SourceExpr=OK }

    { 6085592;2;Field     ;
                SourceExpr="Match Status";
                Visible=FALSE }

    { 1000000004;2;Field  ;
                SourceExpr="Document Category Code";
                Visible=FALSE;
                Enabled=AllowEdit }

    { 1000000006;2;Field  ;
                CaptionML=ENU=Source ID;
                SourceExpr=SourceID;
                Enabled=AllowEdit;
                OnValidate=VAR
                             CaptureEngine@161024013 : Codeunit 6085575;
                           BEGIN
                             SetManual(TRUE);
                             SetSourceID(SourceID);

                             IF "Template No." = '' THEN
                               ValidateDocument
                             ELSE BEGIN
                               COMMIT;
                               CaptureEngine.CaptureDocument(Rec);
                             END;

                             EnableFields;
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           RecIDMgt@161024012 : Codeunit 6085604;
                           CaptureEngine@161024015 : Codeunit 6085575;
                         BEGIN
                           IF NOT RecIDMgt.LookupRecordID("Document Category Code",Text) THEN
                             EXIT;

                           SetManual(TRUE);
                           SetSourceID(Text);

                           IF "Template No." = '' THEN
                             ValidateDocument
                           ELSE BEGIN
                             COMMIT;
                             CaptureEngine.CaptureDocument(Rec);
                           END;

                           EnableFields;
                           CurrPage.UPDATE(FALSE);
                         END;
                          }

    { 1000000034;2;Field  ;
                CaptionML=ENU=Name;
                SourceExpr=GetSourceName }

    { 1000000007;2;Field  ;
                SourceExpr="Template No.";
                Enabled=AllowEdit;
                OnValidate=VAR
                             CaptureEngine@161024013 : Codeunit 6085575;
                           BEGIN
                             CurrPage.SAVERECORD;
                             IF "Template No." <> '' THEN BEGIN
                               COMMIT;
                               CaptureEngine.CaptureDocument(Rec);
                             END;
                             EnableFields;

                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 161024012;2;Field   ;
                CaptionML=ENU=No. of Purch. Orders;
                BlankZero=Yes;
                SourceExpr=PurchDocMgt.GetNoOfPurchOrders(Rec);
                OnDrillDown=BEGIN
                              PurchDocMgt.LookupNoOfPurchOrders(Rec);
                            END;
                             }

    { 161024013;2;Field   ;
                CaptionML=ENU=No. of Purch. Return Orders;
                BlankZero=Yes;
                SourceExpr=PurchDocMgt.GetNoOfPurchReturnOrders(Rec);
                Visible=FALSE;
                OnDrillDown=BEGIN
                              PurchDocMgt.LookupNoOfPurchReturnOrders(Rec);
                            END;
                             }

    { 1000000008;2;Field  ;
                SourceExpr="Template Description";
                Visible=FALSE;
                Editable=FALSE }

    { 2000000000;2;Field  ;
                CaptionML=ENU=Search Text;
                SourceExpr=TemplSearchText;
                Enabled=AllowEdit;
                OnValidate=VAR
                             Template@1000000000 : Record 6085579;
                           BEGIN
                             IF Template.GET("Template No.") THEN BEGIN
                               Template."Search Text" := COPYSTR(TemplSearchText,1,MAXSTRLEN(Template."Search Text"));
                               Template.MODIFY(TRUE);

                               CurrPage.UPDATE(FALSE);
                             END;
                           END;
                            }

    { 6085594;2;Field     ;
                SourceExpr="Document Status Text";
                Editable=FALSE }

    { 161024021;2;Field   ;
                CaptionML=ENU=No. of Search Texts;
                BlankZero=Yes;
                SourceExpr=TemplSearchTexts;
                Editable=FALSE;
                OnDrillDown=VAR
                              TemplSearchText@161024012 : Record 6085588;
                            BEGIN
                              TESTFIELD("Template No.");
                              TemplSearchText.SETRANGE("Template No.","Template No.");
                              PAGE.RUN(0,TemplSearchText);
                            END;
                             }

    { 1000000009;2;Field  ;
                SourceExpr=Description;
                Visible=FALSE;
                Enabled=AllowEdit }

    { 1000000010;2;Field  ;
                SourceExpr=Status;
                Visible=FALSE;
                Editable=FALSE }

    { 1000000011;2;Field  ;
                SourceExpr="No. of Pages";
                Visible=FALSE }

    { 1000000014;1;Part   ;
                Name=Header;
                CaptionML=ENU=Document Header;
                SubPageView=SORTING(Document No.,Sort Order);
                SubPageLink=Document No.=FIELD(No.);
                PagePartID=Page6085595;
                ShowFilter=No }

    { 1000000033;1;Part   ;
                Name=Comments;
                CaptionML=ENU=Comments;
                SubPageView=SORTING(Document No.);
                SubPageLink=Document No.=FIELD(No.);
                PagePartID=Page6085599;
                Enabled=FALSE;
                ShowFilter=No }

    { 1000000013;;Container;
                ContainerType=FactBoxArea }

    { 1000000012;1;Part   ;
                Name=CaptureUI;
                CaptionML=ENU=Document;
                SubPageView=SORTING(No.);
                SubPageLink=No.=FIELD(No.);
                PagePartID=Page6085593 }

  }
  CODE
  {
    VAR
      DCSetup@1000000013 : Record 6085573;
      CaptureAddinLib@1000000011 : Codeunit 6085660;
      PurchDocMgt@161024012 : Codeunit 6085709;
      SourceID@161024015 : Text[250];
      EventNotifierSource@1000000008 : BigText;
      CurrentDocCategory@1000000017 : Code[20];
      StatusFilter@1000000016 : 'Open,Registered,Rejected,All';
      TemplSearchText@1000000019 : Code[200];
      TemplSearchTexts@161024013 : Integer;
      AllowEdit@1000000005 : Boolean INDATASET;
      Text001@161024014 : TextConst 'ENU=Do you want to recognize fields on %1 document(s)?';
      IsVendor@1160040000 : Boolean INDATASET;
      IsCustomer@1160040001 : Boolean INDATASET;
      Text002@1160040002 : TextConst 'ENU=Do you want to register %1 document(s)?';

    PROCEDURE EnableFields@1000000017();
    VAR
      TemplateField@1000000000 : Record 6085580;
    BEGIN
      AllowEdit := Status = Status::Open;

      TemplateField.SETCURRENTKEY("Template No.",Type,"Sort Order");
      TemplateField.SETRANGE("Template No.","Template No.");
      TemplateField.SETRANGE(Type,TemplateField.Type::Header);
      IF TemplateField.FINDFIRST THEN;

      IsVendor := GetSourceTableNo = DATABASE::Vendor;
      IsCustomer := GetSourceTableNo = DATABASE::Customer;
    END;

    PROCEDURE HandleSimpleCommand@1000000006(Command@1000000000 : Text[1024]);
    BEGIN
      CASE Command OF
        'UPDATEDOCUMENT',
        'UPDATEHEADER':
          BEGIN
            ValidateDocument;
            CurrPage.Header.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Header page
            CurrPage.Comments.PAGE.UpdatePage; //RTC will drop the relationship between this page and the Comments page
            CurrPage.UPDATE(FALSE); // This will restore the dropped relationship
          END;
      END;
    END;

    PROCEDURE UpdateList@1000000003();
    BEGIN
      FILTERGROUP := 2;

      IF StatusFilter <> StatusFilter::All THEN
        SETRANGE(Status,StatusFilter)
      ELSE
        SETRANGE(Status);

      IF CurrentDocCategory <> '' THEN
        SETRANGE("Document Category Code",CurrentDocCategory)
      ELSE
        SETRANGE("Document Category Code");

      FILTERGROUP := 0;
      IF NOT FINDFIRST THEN;
      CurrPage.UPDATE(FALSE);
    END;

    BEGIN
    END.
  }
}

