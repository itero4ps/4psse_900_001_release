OBJECT Page 6085703 Purch. Invoice Match Subpage
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW16.00.00.4.05,4PS9.00;
  }
  PROPERTIES
  {
    Permissions=TableData 121=rm;
    CaptionML=ENU=Purch. Invoice Match Subpage;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table121;
    PageType=ListPart;
    OnAfterGetRecord=BEGIN
                       DocNoHideValue := NOT IsFirstDocLine;
                       IF PurchHeader."No." <> "Order No." THEN BEGIN
                         IF NOT PurchHeader.GET(PurchHeader."Document Type"::Order,"Order No.") THEN
                           CLEAR(PurchHeader);

                         IF PurchHeader."Currency Code" <> '' THEN
                           Currency.GET(PurchHeader."Currency Code");
                         Currency.InitRoundingPrecision
                       END;

                       IF NOT PurchLine.GET(PurchHeader."Document Type",PurchHeader."No.","Order Line No.") THEN
                         CLEAR(PurchLine);

                       MatchQty := GetMatchedQty(Rec,FALSE);
                       MatchAmount := GetMatchedAmount(Rec, FALSE); // 4PS
                       GetMatchedPriceInfo;

                       IsStrong := MatchQty <> 0;
                     END;

    ActionList=ACTIONS
    {
      { 1160040003;  ;ActionContainer;
                      Name=<Action1900000004>;
                      ActionContainerType=ActionItems }
      { 1160040002;1 ;ActionGroup;
                      Name=<Action1907935204>;
                      CaptionML=ENU=&Line;
                      Image=Line }
      { 1160040001;2 ;Action    ;
                      Name=<Action1902027204>;
                      ShortCutKey=Shift+Ctrl+E;
                      CaptionML=ENU=Card;
                      RunObject=Page 136;
                      RunPageLink=No.=FIELD(Document No.);
                      Image=Edit }
    }
  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 161024017;1;Field   ;
                Name=EventNotifier;
                SourceExpr=EventNotifierSource;
                ControlAddIn=[Akeyto.DocumentCapture.Dynamics.Nav.EventNotifier;PublicKeyToken=61f876910b9e346c] }

    { 1000000001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1000000002;2;Field  ;
                Lookup=Yes;
                SourceExpr="Document No.";
                Visible=FALSE;
                Editable=FALSE;
                HideValue=DocNoHideValue;
                Style=Strong;
                StyleExpr=IsStrong;
                OnLookup=BEGIN
                           ShowDocument;
                         END;
                          }

    { 1000000003;2;Field  ;
                Lookup=Yes;
                SourceExpr="Order No.";
                Editable=FALSE;
                HideValue=DocNoHideValue;
                Style=Strong;
                StyleExpr=IsStrong;
                OnLookup=BEGIN
                           ShowDocument;
                         END;
                          }

    { 1000000004;2;Field  ;
                SourceExpr="Buy-from Vendor No.";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000005;2;Field  ;
                SourceExpr=Type;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=IsStrong }

    { 1000000006;2;Field  ;
                SourceExpr="No.";
                Editable=FALSE;
                Style=Strong;
                StyleExpr=IsStrong }

    { 1000000007;2;Field  ;
                SourceExpr="Variant Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000008;2;Field  ;
                SourceExpr=Description;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=IsStrong }

    { 1160040005;2;Field  ;
                SourceExpr="Description 2";
                Visible=FALSE;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=IsStrong }

    { 1000000009;2;Field  ;
                BlankZero=Yes;
                SourceExpr=Quantity;
                Editable=FALSE }

    { 1000000010;2;Field  ;
                SourceExpr="Currency Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000011;2;Field  ;
                CaptionML=ENU=Direct Unit Cost (Receipt);
                SourceExpr=PurchLine."Direct Unit Cost";
                Editable=FALSE }

    { 161024016;2;Field   ;
                CaptionML=ENU=Line Discount % (Receipt);
                BlankZero=Yes;
                SourceExpr="Line Discount %";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000012;2;Field  ;
                SourceExpr="Shortcut Dimension 1 Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000013;2;Field  ;
                SourceExpr="Shortcut Dimension 2 Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000014;2;Field  ;
                SourceExpr="Location Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000015;2;Field  ;
                SourceExpr="Unit of Measure Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000016;2;Field  ;
                SourceExpr="Unit of Measure";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000017;2;Field  ;
                SourceExpr="Appl.-to Item Entry";
                Visible=FALSE;
                Editable=FALSE }

    { 161024013;2;Field   ;
                CaptionML=ENU=Matched Quantity;
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=MatchQty;
                Style=Strong;
                StyleExpr=TRUE;
                OnValidate=BEGIN
                             Match(Rec,TRUE);
                           END;

                OnAssistEdit=BEGIN
                               Match(Rec,FALSE);
                             END;
                              }

    { 1100528200;2;Field  ;
                CaptionML=ENU=Matched amount;
                BlankZero=Yes;
                SourceExpr=MatchAmount;
                OnValidate=BEGIN
                             Match(Rec,TRUE);
                           END;

                OnAssistEdit=BEGIN
                               Match(Rec,FALSE);
                             END;
                              }

    { 161024015;2;Field   ;
                CaptionML=ENU=Direct Unit Cost (Invoice);
                DecimalPlaces=2:5;
                BlankZero=Yes;
                SourceExpr=MatchUnitCost;
                OnValidate=BEGIN
                             Match(Rec,TRUE);
                           END;

                OnAssistEdit=BEGIN
                               Match(Rec,FALSE);
                             END;
                              }

    { 161024012;2;Field   ;
                CaptionML=ENU=Line Discount % (Invoice);
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=MatchLineDiscPct;
                Visible=FALSE;
                OnValidate=BEGIN
                             Match(Rec,TRUE);
                           END;

                OnAssistEdit=BEGIN
                               Match(Rec,FALSE);
                             END;
                              }

    { 1160040000;2;Field  ;
                CaptionML=ENU=Line Amount (Invoice);
                BlankZero=Yes;
                SourceExpr=MatchLineAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE }

    { 1160040004;2;Field  ;
                CaptionML=ENU=Difference;
                SourceExpr=(PurchLine."Direct Unit Cost" * MatchQty) - (MatchLineAmount) }

    { 161024014;2;Field   ;
                CaptionML=ENU=Total Matched Quantity;
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=GetMatchedQty(Rec,TRUE);
                Editable=FALSE;
                OnDrillDown=BEGIN
                              ShowMatchSpec(Rec);
                            END;
                             }

    { 1000000020;2;Field  ;
                SourceExpr="Job No.";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000021;2;Field  ;
                SourceExpr="Prod. Order No.";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000022;2;Field  ;
                SourceExpr="Expected Receipt Date";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000023;2;Field  ;
                SourceExpr="Qty. Rcd. Not Invoiced";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000024;2;Field  ;
                SourceExpr="Quantity Invoiced";
                Editable=FALSE }

  }
  CODE
  {
    VAR
      TempPurchRcptLine@1000000011 : TEMPORARY Record 121;
      PurchHeader@1160040002 : Record 38;
      PurchLine@1160040001 : Record 39;
      Currency@1160040000 : Record 4;
      PurchDocMgnt@1000000009 : Codeunit 6085709;
      Text001@1000000016 : TextConst 'ENU=Manual Match';
      Text002@1000000015 : TextConst 'ENU=(%1 pages in total)';
      Text003@1000000014 : TextConst 'ENU=Page %1';
      Text005@1000000008 : TextConst 'ENU=Do you want to reset matches for all document lines?';
      Text006@1000000022 : TextConst 'ENU=(%1 pages in total)';
      Text007@1000000021 : TextConst 'ENU=Page %1';
      Document@1000000000 : Record 6085590;
      EventNotifierSource@161024015 : BigText;
      Channel@161024014 : GUID;
      MatchQty@1000000001 : Decimal;
      MatchUnitCost@161024012 : Decimal;
      MatchLineDiscPct@161024013 : Decimal;
      MatchLineAmount@1160040003 : Decimal;
      DocNoHideValue@1000000002 : Boolean INDATASET;
      IsStrong@1160040004 : Boolean INDATASET;
      MatchAmount@1100528200 : Decimal;
      Text11012001@1100528204 : TextConst 'ENU=Document is: Only amounts';
      Text11012002@1100528203 : TextConst 'ENU=Document expects quantities.';
      Text11012003@1100528202 : TextConst 'ENU=Document expects only amounts, purchase order %1 contains quantities.';
      Text11012004@1100528201 : TextConst 'ENU=Doument expects quantities, purchase order %1 is only amounts.';
      Text11012005@1100528000 : TextConst 'ENU=Matching amount is larger then the purchase amount, continue?';
      Text11012006@1100528001 : TextConst 'ENU=Matching quantity is larger then the purchase quantity, continue?';

    PROCEDURE Match@1000000001(VAR PurchRcptLine@1000000002 : Record 121;IsValidate@1000000000 : Boolean);
    VAR
      PurchDocMatch@1000000001 : Record 6085700;
      DocumentLine@1000000006 : Record 6085596;
      Comment@1000000004 : Record 6085594;
      Field@1000000005 : Record 6085580;
      CreateNewMatch@1000000003 : Boolean;
      PrevQty@1000000007 : Decimal;
      PrevAmount@1100528201 : Decimal;
      PurchaseHeader@1100528200 : Record 38;
    BEGIN
      //**4PS.sn
      IF PurchaseHeader.GET(PurchaseHeader."Document Type"::Order,"Order No.") THEN BEGIN
        IF Document."Amounts Only" AND (MatchQty <> 0) THEN
          ERROR(Text11012001);

        IF (NOT Document."Amounts Only") AND (MatchAmount <> 0) THEN
          ERROR(Text11012002);

        IF Document."Amounts Only" AND (NOT PurchaseHeader."Amounts only") THEN
          ERROR(Text11012003, "Order No.");

        IF (NOT Document."Amounts Only") AND (PurchaseHeader."Amounts only") THEN
          ERROR(Text11012004, "Order No.");
      END;
      //**4PS.en

      PurchDocMatch.SETRANGE("Document No.",Document."No.");
      PurchDocMatch.SETRANGE("Purch. Doc. No.",PurchRcptLine."Document No.");
      PurchDocMatch.SETRANGE("Purch. Line No.",PurchRcptLine."Line No.");
      PurchDocMatch.CALCSUMS(Quantity);
      PrevQty := PurchDocMatch.Quantity;

      //**4PS.sn
      IF PurchDocMatch.FINDSET THEN BEGIN
        REPEAT
          IF PurchDocMatch.Quantity <> 0 THEN
            PrevAmount += PurchDocMatch.Quantity *
                          PurchDocMatch."Direct Unit Cost" *
                          (100 - PurchDocMatch."Line Discount %") / 100
          ELSE
            PrevAmount += PurchDocMatch."Direct Unit Cost" *
                          (100 - PurchDocMatch."Line Discount %") / 100;
        UNTIL PurchDocMatch.NEXT = 0;
      END;
      //**4PS.en

      PurchDocMatch.DELETEALL;

      IF NOT IsValidate THEN BEGIN
        IF PrevQty <> 0 THEN
          MatchQty := 0
        ELSE BEGIN
          MatchQty := PurchRcptLine."Qty. Rcd. Not Invoiced";
          MatchUnitCost := PurchLine."Direct Unit Cost";
          MatchLineDiscPct := PurchLine."Line Discount %";
        END;
        //**4PS.sn
        IF PrevAmount <> 0 THEN
          MatchAmount := 0
        ELSE
          MatchAmount := PurchRcptLine."Amnt. Rcd. Not Invoiced";
        //**4PS.en
      END ELSE
        IF PrevQty = 0 THEN BEGIN
          MatchUnitCost := PurchLine."Direct Unit Cost";
          MatchLineDiscPct := PurchLine."Line Discount %";
        END;

      //**4PS.sn
      // IF PurchRcptLine."Qty. Rcd. Not Invoiced" > 0 THEN BEGIN
      IF (PurchRcptLine."Qty. Rcd. Not Invoiced" > 0) OR (PurchRcptLine."Amnt. Rcd. Not Invoiced" > 0) THEN BEGIN
        IF MatchAmount < 0 THEN
          MatchAmount := 0
        ELSE
          IF MatchAmount > PurchRcptLine."Amnt. Rcd. Not Invoiced" - GetMatchedAmount(PurchRcptLine,TRUE) THEN
            IF NOT CONFIRM(Text11012005, FALSE) THEN   //CALL C017698
              MatchAmount := PurchRcptLine."Amnt. Rcd. Not Invoiced" - GetMatchedAmount(PurchRcptLine,TRUE);
      //
        IF MatchQty < 0 THEN
          MatchQty := 0
        ELSE
          IF MatchQty > PurchRcptLine."Qty. Rcd. Not Invoiced" - GetMatchedQty(PurchRcptLine,TRUE) THEN
            IF NOT CONFIRM(Text11012006, FALSE) THEN   //CALL C020644
              MatchQty := PurchRcptLine."Qty. Rcd. Not Invoiced" - GetMatchedQty(PurchRcptLine,TRUE);
      END ELSE BEGIN
        IF MatchQty > 0 THEN
          MatchQty := 0
        ELSE
          IF MatchQty < PurchRcptLine."Qty. Rcd. Not Invoiced" - GetMatchedQty(PurchRcptLine,TRUE) THEN
            IF NOT CONFIRM(Text11012006, FALSE) THEN   //CALL C020644
              MatchQty := PurchRcptLine."Qty. Rcd. Not Invoiced" - GetMatchedQty(PurchRcptLine,TRUE);
        //**4PS.sn
        IF MatchAmount > 0 THEN
          MatchAmount := 0
        ELSE
          IF MatchAmount < PurchRcptLine."Amnt. Rcd. Not Invoiced" - GetMatchedAmount(PurchRcptLine,TRUE) THEN
            IF NOT CONFIRM(Text11012005, FALSE) THEN   //CALL C017698
              MatchAmount := PurchRcptLine."Amnt. Rcd. Not Invoiced" - GetMatchedAmount(PurchRcptLine,TRUE);
        //**4PS.en
      END;

      IF NOT IsValidate THEN
        PurchRcptLine.MODIFY(TRUE);

      //**4PS.sn
      // IF MatchQty <> 0 THEN BEGIN
      IF (MatchQty <> 0) OR (MatchAmount <> 0) THEN BEGIN
      //**4PS.en
        PurchDocMatch.INIT;
        PurchDocMatch."Purch. Doc. Type" := PurchDocMatch."Purch. Doc. Type"::Receipt;
        PurchDocMatch."Document No." := Document."No.";
        PurchDocMatch."Purch. Doc. No." := PurchRcptLine."Document No.";
        PurchDocMatch."Purch. Line No." := PurchRcptLine."Line No.";
        PurchDocMatch.Quantity := MatchQty;
        //**4PS.sn
        IF MatchQty = 0 THEN
          PurchDocMatch."Direct Unit Cost" := MatchAmount
        ELSE
        //**4PS.en
          PurchDocMatch."Direct Unit Cost" := MatchUnitCost;
        PurchDocMatch."Line Discount %" := MatchLineDiscPct;
        PurchDocMatch.INSERT(TRUE);
        //**4PS.sn
        IF MatchQty = 0 THEN
          MatchUnitCost := MatchAmount;
        //**4PS.en
      END ELSE BEGIN
        MatchUnitCost := 0;
        MatchLineDiscPct := 0;
      END;

      IF Document."Match Status" <> Document."Match Status"::"Manually Matched" THEN BEGIN
        Document.FIND('=');
        Document."Match Status" := Document."Match Status"::"Manually Matched";
        Document.MODIFY;
        Document.DeleteComments(Comment.Area::Match);
        Comment.Add(Document,Field,0,Comment.Area::Match,Comment."Comment Type"::Information,Text001);
      END;

      GetMatchedPriceInfo;

      CLEAR(EventNotifierSource);
      EventNotifierSource.ADDTEXT(FORMAT(TIME) + 'RAISEMESSAGE' + FORMAT(Channel));
    END;

    PROCEDURE GetMatchedQty@1000000005(VAR PurchRcptLine@1000000002 : Record 121;All@1000000001 : Boolean) : Decimal;
    VAR
      PurchDocMatch@1000000000 : Record 6085700;
      PurchLine@1160040000 : Record 39;
    BEGIN
      IF NOT All THEN
        PurchDocMatch.SETRANGE("Document No.",Document."No.");
      PurchDocMatch.SETRANGE("Purch. Doc. Type",PurchDocMatch."Purch. Doc. Type"::Receipt);
      PurchDocMatch.SETRANGE("Purch. Doc. No.",PurchRcptLine."Document No.");
      PurchDocMatch.SETRANGE("Purch. Line No.",PurchRcptLine."Line No.");
      IF NOT PurchDocMatch.ISEMPTY THEN BEGIN
        PurchDocMatch.CALCSUMS(Quantity);
        EXIT(PurchDocMatch.Quantity);
      END ELSE IF All THEN BEGIN
        PurchLine.SETCURRENTKEY("Document Type","Receipt No.","Receipt Line No.");
        PurchLine.SETRANGE("Receipt No.",PurchRcptLine."Document No.");
        PurchLine.SETRANGE("Receipt Line No.",PurchRcptLine."Line No.");
        PurchLine.CALCSUMS(Quantity);
        EXIT(PurchLine.Quantity);
      END;
    END;

    PROCEDURE GetMatchedPriceInfo@161024012() : Decimal;
    VAR
      PurchDocMatch@1000000000 : Record 6085700;
      LineDiscountAmount@1160040000 : Decimal;
    BEGIN
      PurchDocMatch.SETRANGE("Document No.",Document."No.");
      PurchDocMatch.SETRANGE("Purch. Doc. Type",PurchDocMatch."Purch. Doc. Type"::Receipt);
      PurchDocMatch.SETRANGE("Purch. Doc. No.","Document No.");
      PurchDocMatch.SETRANGE("Purch. Line No.","Line No.");
      IF NOT PurchDocMatch.FINDFIRST THEN
        CLEAR(PurchDocMatch);
      MatchUnitCost := PurchDocMatch."Direct Unit Cost";
      MatchLineDiscPct := PurchDocMatch."Line Discount %";

      LineDiscountAmount :=
        ROUND(
          ROUND(MatchQty * MatchUnitCost,Currency."Amount Rounding Precision") *
          PurchDocMatch."Line Discount %" / 100,
          Currency."Amount Rounding Precision");

      MatchLineAmount :=
          ROUND(MatchQty * MatchUnitCost,Currency."Amount Rounding Precision") - LineDiscountAmount;
    END;

    LOCAL PROCEDURE IsFirstDocLine@2() : Boolean;
    VAR
      PurchRcptLine@1000 : Record 121;
    BEGIN
      TempPurchRcptLine.RESET;
      TempPurchRcptLine.COPYFILTERS(Rec);
      TempPurchRcptLine.SETRANGE("Document No.","Document No.");
      IF NOT TempPurchRcptLine.FIND('-') THEN BEGIN
        PurchRcptLine.COPYFILTERS(Rec);
        PurchRcptLine.SETRANGE("Document No.","Document No.");
        IF PurchRcptLine.FIND('-') THEN BEGIN
          TempPurchRcptLine := PurchRcptLine;
          TempPurchRcptLine.INSERT;
        END;
      END;
      IF "Line No." = TempPurchRcptLine."Line No." THEN
        EXIT(TRUE);
    END;

    PROCEDURE UpdatePage@1000000002();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE ShowDocument@1000000000();
    VAR
      PurchRcptHeader@1000000000 : Record 120;
    BEGIN
      PurchRcptHeader.GET("Document No.");
      PAGE.RUN(PAGE::"Posted Purchase Receipt",PurchRcptHeader);
    END;

    PROCEDURE SetDocument@1000000006(VAR NewDocument@1000000000 : Record 6085590);
    BEGIN
      Document := NewDocument;
    END;

    PROCEDURE SetChannel@161024013(NewChannel@161024012 : GUID);
    BEGIN
      Channel := NewChannel;
    END;

    PROCEDURE ShowMatchSpec@1160040000(PurchRcptLine@1160040002 : Record 121);
    VAR
      MatchSpec@161024012 : TEMPORARY Record 6085761;
      PurchDocMatch@1160040001 : Record 6085700;
      PurchLine@1160040000 : Record 39;
    BEGIN
      PurchDocMatch.SETRANGE("Purch. Doc. Type",PurchDocMatch."Purch. Doc. Type"::Receipt);
      PurchDocMatch.SETRANGE("Purch. Doc. No.",PurchRcptLine."Document No.");
      PurchDocMatch.SETRANGE("Purch. Line No.",PurchRcptLine."Line No.");
      IF PurchDocMatch.FINDSET THEN
        REPEAT
          MatchSpec.Type := MatchSpec.Type::Document;
          MatchSpec."Document No." := PurchDocMatch."Document No.";
          MatchSpec.Quantity := PurchDocMatch.Quantity;
          MatchSpec.INSERT;
        UNTIL PurchDocMatch.NEXT = 0;

      PurchLine.SETCURRENTKEY("Document Type","Receipt No.","Receipt Line No.");
      PurchLine.SETRANGE("Document Type",PurchLine."Document Type"::Invoice);
      PurchLine.SETRANGE("Receipt No.",PurchRcptLine."Document No.");
      PurchLine.SETRANGE("Receipt Line No.",PurchRcptLine."Line No.");
      IF PurchLine.FINDSET THEN
        REPEAT
          MatchSpec.Type := MatchSpec.Type::"Purchase Invoice";
          MatchSpec."Document No." := PurchLine."Document No.";
          MatchSpec.Quantity := PurchLine.Quantity;
          MatchSpec.INSERT;
        UNTIL PurchLine.NEXT = 0;

      PAGE.RUN(0,MatchSpec);
    END;

    PROCEDURE GetMatchedAmount@1000000017(VAR PurchRcptLine@1000000002 : Record 121;All@1000000001 : Boolean) ReturnAmount : Decimal;
    VAR
      PurchDocMatch@1000000000 : Record 6085700;
      PurchLine@1000000003 : Record 39;
    BEGIN
      //**4PS
      IF NOT All THEN
        PurchDocMatch.SETRANGE("Document No.",Document."No.");
      PurchDocMatch.SETRANGE("Document No.", Document."No.");
      PurchDocMatch.SETRANGE("Purch. Doc. Type", PurchDocMatch."Purch. Doc. Type"::Receipt);
      PurchDocMatch.SETRANGE("Purch. Doc. No.", "Document No.");
      PurchDocMatch.SETRANGE("Purch. Line No.", "Line No.");
      IF NOT PurchDocMatch.ISEMPTY THEN BEGIN
        REPEAT
          IF PurchDocMatch.Quantity <> 0 THEN
            ReturnAmount += (PurchDocMatch.Quantity * PurchDocMatch."Direct Unit Cost" *
                             (100 - PurchDocMatch."Line Discount %") / 100)
          ELSE
            ReturnAmount += (PurchDocMatch."Direct Unit Cost" * (100 - PurchDocMatch."Line Discount %") / 100)
        UNTIL PurchDocMatch.NEXT = 0;
        EXIT(ReturnAmount);
      END ELSE
        IF All THEN BEGIN
          PurchLine.SETCURRENTKEY("Document Type","Receipt No.","Receipt Line No.");
          PurchLine.SETRANGE("Receipt No.",PurchRcptLine."Document No.");
          PurchLine.SETRANGE("Receipt Line No.",PurchRcptLine."Line No.");
          PurchLine.CALCSUMS(Amount);
          EXIT(PurchLine.Amount);
        END;
    END;

    BEGIN
    END.
  }
}

