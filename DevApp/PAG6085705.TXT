OBJECT Page 6085705 Purch. Ret. Shpt. Match Subpag
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW16.00.00.4.05;
  }
  PROPERTIES
  {
    Permissions=TableData 6651=rm;
    CaptionML=ENU=Purch. Ret. Shpt. Match Subpage;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table6651;
    PageType=ListPart;
    OnAfterGetRecord=BEGIN
                       DocNoHideValue := NOT IsFirstDocLine;
                       IF PurchHeader."No." <> "Return Order No." THEN BEGIN
                         IF NOT PurchHeader.GET(PurchHeader."Document Type"::"Return Order","Return Order No.") THEN
                           CLEAR(PurchHeader);

                         IF PurchHeader."Currency Code" <> '' THEN
                           Currency.GET(PurchHeader."Currency Code");
                         Currency.InitRoundingPrecision
                       END;

                       IF NOT PurchLine.GET(PurchHeader."Document Type",PurchHeader."No.","Return Order Line No.") THEN
                         CLEAR(PurchLine);

                       MatchQty := GetMatchedQty(Rec,FALSE);
                       GetMatchedPriceInfo;

                       IsStrong := MatchQty <> 0;
                     END;

  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 161024016;1;Field   ;
                Name=EventNotifier;
                SourceExpr=EventNotifierSource;
                ControlAddIn=[Akeyto.DocumentCapture.Dynamics.Nav.EventNotifier;PublicKeyToken=61f876910b9e346c] }

    { 1000000001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1000000043;2;Field  ;
                SourceExpr="Document No.";
                Visible=FALSE;
                Editable=FALSE;
                HideValue=DocNoHideValue;
                Style=Strong;
                StyleExpr=isstrong }

    { 1000000003;2;Field  ;
                SourceExpr="Return Order No.";
                Editable=FALSE;
                HideValue=DocNoHideValue;
                Style=Strong;
                StyleExpr=isstrong }

    { 1000000042;2;Field  ;
                SourceExpr="Buy-from Vendor No.";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000041;2;Field  ;
                SourceExpr=Type;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=isstrong }

    { 1000000040;2;Field  ;
                SourceExpr="No.";
                Editable=FALSE;
                Style=Strong;
                StyleExpr=isstrong }

    { 1000000039;2;Field  ;
                SourceExpr="Variant Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000038;2;Field  ;
                SourceExpr=Description;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=isstrong }

    { 1160040002;2;Field  ;
                SourceExpr="Description 2";
                Visible=FALSE;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=IsStrong }

    { 1000000037;2;Field  ;
                SourceExpr="Return Reason Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000005;2;Field  ;
                SourceExpr=Quantity;
                Editable=FALSE }

    { 1000000036;2;Field  ;
                Lookup=No;
                DrillDown=No;
                SourceExpr="Currency Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000002;2;Field  ;
                CaptionML=ENU=Direct Unit Cost (Shipment);
                SourceExpr=PurchLine."Direct Unit Cost";
                Editable=FALSE }

    { 161024014;2;Field   ;
                CaptionML=ENU=Line Discount % (Shipment);
                BlankZero=Yes;
                SourceExpr="Line Discount %";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000035;2;Field  ;
                SourceExpr="Shortcut Dimension 1 Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000034;2;Field  ;
                SourceExpr="Shortcut Dimension 2 Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000033;2;Field  ;
                SourceExpr="Location Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000032;2;Field  ;
                SourceExpr="Unit of Measure Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000031;2;Field  ;
                SourceExpr="Unit of Measure";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000030;2;Field  ;
                SourceExpr="Appl.-to Item Entry";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000006;2;Field  ;
                CaptionML=ENU=Matched Quantity;
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=MatchQty;
                Style=Strong;
                StyleExpr=TRUE;
                OnValidate=BEGIN
                             Match(Rec,TRUE);
                           END;

                OnAssistEdit=BEGIN
                               Match(Rec,FALSE);
                             END;
                              }

    { 161024012;2;Field   ;
                CaptionML=ENU=Direct Unit Cost (Cr. Memo);
                DecimalPlaces=2:5;
                BlankZero=Yes;
                SourceExpr=MatchUnitCost;
                OnValidate=BEGIN
                             Match(Rec,TRUE);
                           END;

                OnAssistEdit=BEGIN
                               Match(Rec,FALSE);
                             END;
                              }

    { 161024013;2;Field   ;
                CaptionML=ENU=Line Discount % (Invoice);
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=MatchLineDiscPct;
                Visible=FALSE;
                OnValidate=BEGIN
                             Match(Rec,TRUE);
                           END;

                OnAssistEdit=BEGIN
                               Match(Rec,FALSE);
                             END;
                              }

    { 1160040001;2;Field  ;
                CaptionML=ENU=Line Amount (Credit Memo);
                BlankZero=Yes;
                SourceExpr=MatchLineAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE }

    { 1160040000;2;Field  ;
                CaptionML=ENU=Difference;
                SourceExpr=(PurchLine."Direct Unit Cost" * MatchQty) - (MatchLineAmount) }

    { 161024015;2;Field   ;
                CaptionML=ENU=Total Matched Quantity;
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=GetMatchedQty(Rec,TRUE);
                Editable=FALSE;
                OnDrillDown=BEGIN
                              ShowMatchSpec(Rec);
                            END;
                             }

    { 1000000028;2;Field  ;
                SourceExpr="Job No.";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000027;2;Field  ;
                SourceExpr="Prod. Order No.";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000009;2;Field  ;
                SourceExpr="Return Qty. Shipped Not Invd.";
                Visible=FALSE;
                Editable=FALSE }

    { 1000000004;2;Field  ;
                SourceExpr="Quantity Invoiced";
                Editable=FALSE }

  }
  CODE
  {
    VAR
      TempReturnShptLine@1000000011 : TEMPORARY Record 6651;
      PurchHeader@1160040002 : Record 38;
      PurchLine@1160040001 : Record 39;
      Currency@1160040000 : Record 4;
      PurchDocMgnt@1000000009 : Codeunit 6085709;
      Text001@1000000016 : TextConst 'ENU=Manual Match';
      Text002@1000000015 : TextConst 'ENU=(%1 pages in total)';
      Text003@1000000014 : TextConst 'ENU=Page %1';
      Text004@1000000010 : TextConst 'ENU=DOCUMENT.TIFF';
      Text005@1000000008 : TextConst 'ENU=Do you want to reset matches for all document lines?';
      Text006@1000000022 : TextConst 'ENU=(%1 pages in total)';
      Text007@1000000021 : TextConst 'ENU=Page %1';
      Document@1000000000 : Record 6085590;
      EventNotifierSource@161024015 : BigText;
      Channel@161024014 : GUID;
      MatchQty@1000000001 : Decimal;
      MatchUnitCost@161024013 : Decimal;
      MatchLineDiscPct@161024012 : Decimal;
      MatchLineAmount@1160040003 : Decimal;
      DocNoHideValue@1000000002 : Boolean INDATASET;
      IsStrong@1160040004 : Boolean INDATASET;

    PROCEDURE Match@1000000001(VAR ReturnShptLine@1000000002 : Record 6651;IsValidate@1000000000 : Boolean);
    VAR
      PurchDocMatch@1000000001 : Record 6085700;
      DocumentLine@1000000006 : Record 6085596;
      Comment@1000000004 : Record 6085594;
      Field@1000000005 : Record 6085580;
      CreateNewMatch@1000000003 : Boolean;
      PrevQty@1000000007 : Decimal;
    BEGIN
      PurchDocMatch.SETRANGE("Document No.",Document."No.");
      PurchDocMatch.SETRANGE("Purch. Doc. No.",ReturnShptLine."Document No.");
      PurchDocMatch.SETRANGE("Purch. Line No.",ReturnShptLine."Line No.");
      PurchDocMatch.CALCSUMS(Quantity);
      PrevQty := PurchDocMatch.Quantity;
      PurchDocMatch.DELETEALL;

      IF NOT IsValidate THEN BEGIN
        IF PrevQty <> 0 THEN
          MatchQty := 0
        ELSE BEGIN
          MatchQty := ReturnShptLine."Return Qty. Shipped Not Invd.";
          MatchUnitCost := PurchLine."Direct Unit Cost";
          MatchLineDiscPct := PurchLine."Line Discount %";
        END;
      END ELSE IF PrevQty = 0 THEN BEGIN
        MatchUnitCost := PurchLine."Direct Unit Cost";
        MatchLineDiscPct := PurchLine."Line Discount %";
      END;

      IF ReturnShptLine."Return Qty. Shipped Not Invd." > 0 THEN BEGIN
        IF MatchQty < 0 THEN
          MatchQty := 0
        ELSE IF MatchQty > ReturnShptLine."Return Qty. Shipped Not Invd." - GetMatchedQty(ReturnShptLine,TRUE) THEN
          MatchQty := ReturnShptLine."Return Qty. Shipped Not Invd." - GetMatchedQty(ReturnShptLine,TRUE);
      END ELSE BEGIN
        IF MatchQty > 0 THEN
          MatchQty := 0
        ELSE IF MatchQty < ReturnShptLine."Return Qty. Shipped Not Invd." - GetMatchedQty(ReturnShptLine,TRUE) THEN
          MatchQty := ReturnShptLine."Return Qty. Shipped Not Invd." - GetMatchedQty(ReturnShptLine,TRUE);
      END;

      IF NOT IsValidate THEN
        ReturnShptLine.MODIFY(TRUE);

      IF MatchQty <> 0 THEN BEGIN
        PurchDocMatch.INIT;
        PurchDocMatch."Purch. Doc. Type" := PurchDocMatch."Purch. Doc. Type"::"Return Shipment";
        PurchDocMatch."Document No." := Document."No.";
        PurchDocMatch."Purch. Doc. No." := ReturnShptLine."Document No.";
        PurchDocMatch."Purch. Line No." := ReturnShptLine."Line No.";
        PurchDocMatch.Quantity := MatchQty;
        PurchDocMatch."Direct Unit Cost" := MatchUnitCost;
        PurchDocMatch."Line Discount %" := MatchLineDiscPct;
        PurchDocMatch.INSERT(TRUE);
      END ELSE BEGIN
        MatchUnitCost := 0;
        MatchLineDiscPct := 0;
      END;

      IF Document."Match Status" <> Document."Match Status"::"Manually Matched" THEN BEGIN
        Document.FIND('=');
        Document."Match Status" := Document."Match Status"::"Manually Matched";
        Document.MODIFY;
        Document.DeleteComments(Comment.Area::Match);
        Comment.Add(Document,Field,0,Comment.Area::Match,Comment."Comment Type"::Information,Text001);
      END;

      GetMatchedPriceInfo;

      CLEAR(EventNotifierSource);
      EventNotifierSource.ADDTEXT(FORMAT(TIME) + 'RAISEMESSAGE' + FORMAT(Channel));
    END;

    PROCEDURE GetMatchedQty@1000000005(VAR ReturnShptLine@1000000002 : Record 6651;All@1000000001 : Boolean) : Decimal;
    VAR
      PurchDocMatch@1000000000 : Record 6085700;
      PurchLine@1160040000 : Record 39;
      Qty@1160040001 : Decimal;
    BEGIN
      IF NOT All THEN
        PurchDocMatch.SETRANGE("Document No.",Document."No.");
      PurchDocMatch.SETRANGE("Purch. Doc. Type",PurchDocMatch."Purch. Doc. Type"::"Return Shipment");
      PurchDocMatch.SETRANGE("Purch. Doc. No.",ReturnShptLine."Document No.");
      PurchDocMatch.SETRANGE("Purch. Line No.",ReturnShptLine."Line No.");
      IF NOT PurchDocMatch.ISEMPTY THEN BEGIN
        PurchDocMatch.CALCSUMS(Quantity);
        EXIT(PurchDocMatch.Quantity);
      END ELSE IF All THEN BEGIN
        PurchLine.SETRANGE("Return Shipment No.",ReturnShptLine."Document No.");
        PurchLine.SETRANGE("Return Shipment Line No.",ReturnShptLine."Line No.");
        IF PurchLine.FINDSET THEN
          REPEAT
            Qty += PurchLine.Quantity;
          UNTIL PurchLine.NEXT = 0;
      END;
      EXIT(Qty);
    END;

    PROCEDURE GetMatchedPriceInfo@161024012() : Decimal;
    VAR
      PurchDocMatch@1000000000 : Record 6085700;
      LineDiscountAmount@1160040000 : Decimal;
    BEGIN
      PurchDocMatch.SETRANGE("Document No.",Document."No.");
      PurchDocMatch.SETRANGE("Purch. Doc. Type",PurchDocMatch."Purch. Doc. Type"::"Return Shipment");
      PurchDocMatch.SETRANGE("Purch. Doc. No.","Document No.");
      PurchDocMatch.SETRANGE("Purch. Line No.","Line No.");
      IF NOT PurchDocMatch.FINDFIRST THEN
        CLEAR(PurchDocMatch);
      MatchUnitCost := PurchDocMatch."Direct Unit Cost";
      MatchLineDiscPct := PurchDocMatch."Line Discount %";

      LineDiscountAmount :=
        ROUND(
          ROUND(MatchQty * MatchUnitCost,Currency."Amount Rounding Precision") *
          PurchDocMatch."Line Discount %" / 100,
          Currency."Amount Rounding Precision");

      MatchLineAmount :=
          ROUND(MatchQty * MatchUnitCost,Currency."Amount Rounding Precision") - LineDiscountAmount;
    END;

    LOCAL PROCEDURE IsFirstDocLine@2() : Boolean;
    VAR
      ReturnShptLine@1000 : Record 6651;
    BEGIN
      TempReturnShptLine.RESET;
      TempReturnShptLine.COPYFILTERS(Rec);
      TempReturnShptLine.SETRANGE("Document No.","Document No.");
      IF NOT TempReturnShptLine.FIND('-') THEN BEGIN
        ReturnShptLine.COPYFILTERS(Rec);
        ReturnShptLine.SETRANGE("Document No.","Document No.");
        IF ReturnShptLine.FIND('-') THEN BEGIN
          TempReturnShptLine := ReturnShptLine;
          TempReturnShptLine.INSERT;
        END;
      END;
      IF "Line No." = TempReturnShptLine."Line No." THEN
        EXIT(TRUE);
    END;

    PROCEDURE UpdatePage@1000000002();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE ShowDocument@1000000000();
    VAR
      PurchRetShptHeader@1000000000 : Record 6650;
    BEGIN
      PurchRetShptHeader.GET("Document No.");
      PAGE.RUN(PAGE::"Posted Return Shipment",PurchRetShptHeader);
    END;

    PROCEDURE SetDocument@1000000006(VAR NewDocument@1000000000 : Record 6085590);
    BEGIN
      Document := NewDocument;
    END;

    PROCEDURE SetChannel@161024013(NewChannel@161024012 : GUID);
    BEGIN
      Channel := NewChannel;
    END;

    PROCEDURE ShowMatchSpec@1160040000(VAR ReturnShptLine@1160040002 : Record 6651);
    VAR
      MatchSpec@161024012 : TEMPORARY Record 6085761;
      PurchDocMatch@1160040001 : Record 6085700;
      PurchLine@1160040000 : Record 39;
    BEGIN
      PurchDocMatch.SETRANGE("Purch. Doc. Type",PurchDocMatch."Purch. Doc. Type"::"Return Shipment");
      PurchDocMatch.SETRANGE("Purch. Doc. No.",ReturnShptLine."Document No.");
      PurchDocMatch.SETRANGE("Purch. Line No.",ReturnShptLine."Line No.");
      IF PurchDocMatch.FINDSET THEN
        REPEAT
          MatchSpec.Type := MatchSpec.Type::Document;
          MatchSpec."Document No." := PurchDocMatch."Document No.";
          MatchSpec.Quantity := PurchDocMatch.Quantity;
          MatchSpec.INSERT;
        UNTIL PurchDocMatch.NEXT = 0;

      PurchLine.SETRANGE("Document Type",PurchLine."Document Type"::"Credit Memo");
      PurchLine.SETRANGE("Return Shipment No.",ReturnShptLine."Document No.");
      PurchLine.SETRANGE("Return Shipment Line No.",ReturnShptLine."Line No.");
      IF PurchLine.FINDSET THEN
        REPEAT
          MatchSpec.Type := MatchSpec.Type::"Purchase Credit Memo";
          MatchSpec."Document No." := PurchLine."Document No.";
          MatchSpec.Quantity := PurchLine.Quantity;
          MatchSpec.INSERT;
        UNTIL PurchLine.NEXT = 0;

      PAGE.RUN(0,MatchSpec);
    END;

    BEGIN
    END.
  }
}

