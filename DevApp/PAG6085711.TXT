OBJECT Page 6085711 Purch. Approval Req. Entries
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW16.00.00.4.04,4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Approval Request Entries;
    InsertAllowed=No;
    LinksAllowed=No;
    SourceTable=Table454;
    PageType=List;
    OnOpenPage=VAR
                 DCApprovalsBridge@1160040000 : Codeunit 6085790;
               BEGIN
                 SETRANGE(Status);
                 SETRANGE("Due Date");

                 //**4PS.sn
                 CASE GETFILTER("Table ID") OF
                 FORMAT(DATABASE::"Purch. Inv. Header"):
                   BEGIN
                     FILTERGROUP(2);
                     SETRANGE("Table ID",DATABASE::"Purch. Inv. Header");
                     FILTERGROUP(0);
                   END;
                 FORMAT(DATABASE::"Purch. Cr. Memo Hdr."):
                   BEGIN
                     FILTERGROUP(2);
                     SETRANGE("Table ID",DATABASE::"Purch. Cr. Memo Hdr.");
                     FILTERGROUP(0);
                   END;
                 FORMAT(DATABASE::"Purchase Header"):
                   BEGIN
                 //**4PS.en
                     FILTERGROUP(2);
                     SETRANGE("Table ID",DATABASE::"Purchase Header");
                     FILTERGROUP(0);
                 //**4PS.sn
                   END;
                 END;
                 //**4PS.en
               END;

    OnFindRecord=VAR
                   Found@1100525000 : Boolean;
                 BEGIN
                   //**4PS
                   Found := FindPosition(Which);
                   EXIT(Found);
                 END;

    OnNextRecord=VAR
                   lDocument@1100525001 : Record 454;
                   NextStep@1100525000 : Integer;
                 BEGIN
                   //**4PS
                   lDocument.COPY(Rec);
                   NextStep := NextPosition(lDocument,Steps);
                   IF NextStep <> 0 THEN Rec.COPY(lDocument);
                   EXIT(NextStep);
                 END;

    OnAfterGetRecord=BEGIN
                       Overdue := Overdue::" ";
                       IF FormatField(Rec) THEN
                         Overdue := Overdue::Yes;

                       //**4PS.so
                       //IF NOT PurchHeader.GET("Document Type","Document No.") THEN
                       //  CLEAR(PurchHeader);
                       //**4PS.eo
                       //**4PS.sn
                       CASE "Table ID" OF
                         DATABASE::"Purchase Header":
                         BEGIN
                           IF NOT PurchHeader.GET("Document Type","Document No.") THEN
                             CLEAR(PurchHeader);
                           END;
                         DATABASE::"Purch. Inv. Header":
                         BEGIN
                           IF NOT PurchInvHeader.GET("Document No.") THEN
                             CLEAR(PurchInvHeader);
                         END;
                         DATABASE::"Purch. Cr. Memo Hdr.":
                         BEGIN
                           IF NOT PurchCrMemoHdr.GET("Document No.") THEN
                             CLEAR(PurchCrMemoHdr);
                         END;
                       END;

                       IF PurchHeader."Currency Factor" <> 0 THEN
                         CurrencyFactor := PurchHeader."Currency Factor"
                       ELSE
                         CurrencyFactor := 1;
                       //**4PS.sn
                       ApproverID := "Approver ID";
                     END;

    OnNewRecord=BEGIN
                  //**4PS.sn
                  FILTERGROUP(2);
                  CASE GETFILTER("Table ID") OF
                    FORMAT(DATABASE::"Purch. Inv. Header"):
                      BEGIN
                        "Table ID" := DATABASE::"Purch. Inv. Header";
                      END;
                    FORMAT(DATABASE::"Purch. Cr. Memo Hdr."):
                      BEGIN
                        "Table ID" := DATABASE::"Purch. Cr. Memo Hdr.";
                      END;
                    FORMAT(DATABASE::"Purchase Header"):
                      BEGIN
                        "Table ID" := DATABASE::"Purchase Header";
                      END;
                  END;
                  FILTERGROUP(0);
                END;

    OnModifyRecord=BEGIN
                     EXIT(FALSE);
                   END;

    OnDeleteRecord=VAR
                     DCApprovalMgnt@161024012 : Codeunit 6085722;
                   BEGIN
                     //**4PS.sn
                     IF "Table ID" IN [DATABASE::"Purch. Inv. Header", DATABASE::"Purch. Cr. Memo Hdr."] THEN
                       DCApprovalMgnt.ManuallyDeleleteAppvlEntry4PS(Rec)
                     ELSE
                     //
                       DCApprovalMgnt.ManuallyDeleleteAppvlEntry(Rec);
                     EXIT(FALSE)
                   END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 33      ;1   ;ActionGroup;
                      CaptionML=ENU=&Show }
      { 161024017;2  ;Action    ;
                      Name=<Action161024017>;
                      ShortCutKey=Shift+Ctrl+P;
                      CaptionML=ENU=&PDF File;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Document@161024012 : Record 6085590;
                               BEGIN
                                 //**4PS.so
                                 //Document.SETCURRENTKEY("Created Doc. Type","Created Doc. Subtype","Created Doc. ID","Created Doc. Ref. No.");
                                 //Document.SETRANGE("Created Doc. Type",DATABASE::"Purchase Header");
                                 //Document.SETRANGE("Created Doc. Subtype","Document Type");
                                 //Document.SETRANGE("Created Doc. ID","Document No.");
                                 //Document.FINDFIRST;
                                 //Document.OpenPDFFile;
                                 //**4PS.eo
                                 //**4PS.sn
                                 CASE "Table ID" OF
                                   DATABASE::"Purchase Header":
                                   BEGIN
                                     Document.SETCURRENTKEY("Created Doc. Type","Created Doc. Subtype","Created Doc. ID","Created Doc. Ref. No.");
                                     Document.SETRANGE("Created Doc. Type",DATABASE::"Purchase Header");
                                     Document.SETRANGE("Created Doc. Subtype","Document Type");
                                     Document.SETRANGE("Created Doc. ID","Document No.");
                                     IF Document.FINDFIRST THEN
                                       Document.OpenPDFFile;
                                   END;
                                   DATABASE::"Purch. Inv. Header":
                                   BEGIN
                                     PurchInvHeader.GET("Document No.");
                                     IF Document.GET(PurchInvHeader."Ext. Document ID") THEN
                                       Document.OpenPDFFile;
                                   END;
                                   DATABASE::"Purch. Cr. Memo Hdr.":
                                   BEGIN
                                     PurchCrMemoHdr.GET("Document No.");
                                     IF Document.GET(PurchCrMemoHdr."Ext. Document ID") THEN
                                       Document.OpenPDFFile;
                                   END;
                                 END;
                                 //**4PS.en
                               END;
                                }
      { 35      ;2   ;Action    ;
                      CaptionML=ENU=Document;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Edit;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DCApprovalMgnt@161024014 : Codeunit 6085722;
                               BEGIN
                                 { //**4PS.so
                                 IF NOT PurchHeader.GET("Document Type","Document No.") THEN
                                   EXIT;
                                 CASE "Document Type" OF
                                   "Document Type"::Quote:
                                     PAGE.RUN(PAGE::"Purchase Quote",PurchHeader);
                                   "Document Type"::Order:
                                     PAGE.RUN(PAGE::"Purchase Order",PurchHeader);
                                   "Document Type"::Invoice:
                                     PAGE.RUN(PAGE::"Purch. Invoice With Image",PurchHeader);
                                   "Document Type"::"Credit Memo":
                                     PAGE.RUN(PAGE::"Purch. Credit Memo With Image",PurchHeader);
                                   "Document Type"::"Blanket Order":
                                     PAGE.RUN(PAGE::"Blanket Purchase Order",PurchHeader);
                                   "Document Type"::"Return Order":
                                     PAGE.RUN(PAGE::"Purchase Return Order",PurchHeader);
                                 END;
                                 } //**4PS.eo
                                 ShowRecord; //**4PS.n
                               END;
                                }
      { 36      ;2   ;Action    ;
                      CaptionML=ENU=Comments;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ViewComments;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ApprovalCmtMgnt@1000 : Codeunit 6085600;
                               BEGIN
                                 ApprovalCmtMgnt.ShowCmtFromAppvlEntry(Rec);
                               END;
                                }
      { 39      ;2   ;Action    ;
                      CaptionML=ENU=O&verdue Entries;
                      Image=OverdueEntries;
                      OnAction=BEGIN
                                 SETFILTER(Status,'%1|%2',Status::Created,Status::Open);
                                 SETFILTER("Due Date",'<%1',TODAY);
                               END;
                                }
      { 40      ;2   ;Action    ;
                      CaptionML=ENU=All Entries;
                      Image=Entries;
                      OnAction=BEGIN
                                 SETRANGE(Status);
                                 SETRANGE("Due Date");
                               END;
                                }
      { 1100525002;2 ;Action    ;
                      CaptionML=ENU=Filters;
                      OnAction=BEGIN
                                 FilterSetup;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1160040000;1 ;Action    ;
                      CaptionML=ENU=Add Approver;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Add;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DCApprovalMgt@1160040000 : Codeunit 6085722;
                               BEGIN
                                 DCApprovalMgt.AddApproverAfter(Rec);
                               END;
                                }
      { 25      ;1   ;Action    ;
                      Name=<Action25>;
                      CaptionML=ENU=&Forward;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Delegate;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ApprovalEntry@1001 : Record 454;
                                 TempApprovalEntry@1003 : Record 454;
                                 DCApprovalMgmt@1000 : Codeunit 6085722;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ApprovalEntry);

                                 CurrPage.SETSELECTIONFILTER(TempApprovalEntry);
                                 IF TempApprovalEntry.FIND('-') THEN BEGIN
                                   TempApprovalEntry.SETFILTER(Status,'<>%1',TempApprovalEntry.Status::Open);
                                   IF NOT TempApprovalEntry.ISEMPTY THEN
                                     ERROR(Text001);
                                 END;

                                 IF ApprovalEntry.FIND('-') THEN BEGIN
                                     IF (ApprovalEntry."Sender ID" = USERID) OR
                                       DCApprovalMgmt.IsApprovalAdminUser(FALSE) OR
                                      (ApprovalEntry."Approver ID" = USERID)
                                   THEN BEGIN
                                     REPEAT
                                       DCApprovalMgmt.DelegateApprovalRequest(ApprovalEntry);
                                     UNTIL ApprovalEntry.NEXT = 0;
                                   END;
                                 END;
                                 MESSAGE(Text002);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 37  ;2   ;Field     ;
                CaptionML=ENU=Overdue;
                ToolTipML=ENU=Overdue Entry;
                OptionCaptionML=ENU="Yes, ";
                SourceExpr=Overdue;
                Visible=FALSE;
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                CaptionML=ENU=Document Type;
                SourceExpr="Document Type";
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                CaptionML=ENU=Document No.;
                SourceExpr="Document No.";
                Editable=FALSE }

    { 161024013;2;Field   ;
                CaptionML=ENU=Buy-from Vendor No.;
                SourceExpr=GetBuyFromVendor;
                Editable=FALSE }

    { 161024012;2;Field   ;
                CaptionML=ENU=Buy-from Vendor Name;
                SourceExpr=GetBuyFromVendorName;
                Editable=FALSE }

    { 16  ;2   ;Field     ;
                CaptionML=ENU=Approver ID;
                SourceExpr=ApproverID;
                OnValidate=VAR
                             DCApprovalMgnt@161024012 : Codeunit 6085722;
                           BEGIN
                             "Approver ID" := ApproverID;
                             DCApprovalMgnt.ManuallyEditAppvlEntry(Rec);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           UserSetup@1160040000 : Record 91;
                           DCApprovalMgnt@1160040001 : Codeunit 6085722;
                         BEGIN
                           IF UserSetup.GET("Approver ID") THEN;
                           IF PAGE.RUNMODAL(PAGE::"Purch. Delegate to User",UserSetup) = ACTION::LookupOK THEN BEGIN
                             "Approver ID" := UserSetup."User ID";
                             DCApprovalMgnt.ManuallyEditAppvlEntry(Rec);
                             CurrPage.UPDATE(FALSE);
                           END;
                         END;
                          }

    { 18  ;2   ;Field     ;
                CaptionML=ENU=Status;
                SourceExpr=Status;
                Editable=FALSE }

    { 1160040010;2;Field  ;
                CaptionML=ENU=On Hold;
                SourceExpr=GetOnHold;
                Editable=FALSE }

    { 1160040003;2;Field  ;
                CaptionML=ENU=Currency Code;
                SourceExpr="Currency Code";
                Editable=FALSE }

    { 1160040008;2;Field  ;
                CaptionML=ENU=Amount;
                SourceExpr=Amount;
                Editable=FALSE }

    { 1160040007;2;Field  ;
                CaptionML=ENU=VAT Amount;
                SourceExpr="Amount Incl. VAT" - Amount;
                Visible=FALSE;
                Editable=FALSE }

    { 1160040006;2;Field  ;
                CaptionML=ENU=Amount Incl. VAT;
                SourceExpr="Amount Incl. VAT";
                Editable=FALSE }

    { 1160040005;2;Field  ;
                CaptionML=ENU=Amount (LCY);
                SourceExpr="Amount (LCY)";
                Visible=FALSE;
                Editable=FALSE }

    { 1160040004;2;Field  ;
                CaptionML=ENU=VAT Amount (LCY);
                SourceExpr="Amount Incl. VAT (LCY)" - "Amount (LCY)";
                Visible=FALSE;
                Editable=FALSE }

    { 1160040002;2;Field  ;
                CaptionML=ENU=Amount Incl. VAT (LCY);
                SourceExpr="Amount Incl. VAT (LCY)";
                Visible=FALSE;
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                CaptionML=ENU=Salespers./Purch. Code;
                SourceExpr="Salespers./Purch. Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1160040001;2;Field  ;
                CaptionML=ENU=Original Approver ID;
                SourceExpr="Original Approver ID";
                Visible=FALSE;
                Editable=FALSE }

    { 20  ;2   ;Field     ;
                CaptionML=ENU=Date-Time Sent for Approval;
                SourceExpr="Date-Time Sent for Approval";
                Visible=FALSE;
                Editable=FALSE }

    { 29  ;2   ;Field     ;
                CaptionML=ENU=Comment;
                SourceExpr=Comment;
                Editable=FALSE }

    { 31  ;2   ;Field     ;
                CaptionML=ENU=Due Date;
                SourceExpr="Due Date";
                Editable=FALSE }

    { 11012000;2;Field    ;
                CaptionML=ENU=Available Credit Limit (LCY);
                SourceExpr="Available Credit Limit (LCY)";
                Editable=FALSE }

    { 1100525003;1;Group  ;
                CaptionML=ENU=Filters }

    { 1100525001;2;Field  ;
                CaptionML=ENU=Vendor Filter;
                SourceExpr=VendorNoFilter;
                Importance=Promoted;
                OnLookup=BEGIN
                           FilterSetup;
                         END;
                          }

    { 1100525000;2;Field  ;
                CaptionML=ENU=Vendor Name Filter;
                SourceExpr=VendorNameFilter;
                Importance=Promoted;
                OnLookup=BEGIN
                           FilterSetup;
                         END;
                          }

  }
  CODE
  {
    VAR
      PurchHeader@161024012 : Record 38;
      Text001@1003 : TextConst 'ENU=You can only forward open approvals entries.';
      Text002@1004 : TextConst 'ENU=The selected approvals have been forwarded.';
      Overdue@1005 : 'Yes, ';
      ApproverID@1160040000 : Code[50];
      CurrencyFactor@1100528204 : Decimal;
      PurchInvHeader@1100528203 : Record 122;
      PurchCrMemoHdr@1100528202 : Record 124;
      DocMgt@1100528201 : Codeunit 6085578;
      DCApprovalMgnt@1100528200 : Codeunit 6085722;
      VendorNameFilter@1100525000 : Text[250];
      VendorNoFilter@1100525001 : Text[250];

    PROCEDURE FormatField@1(Rec@1000 : Record 454) : Boolean;
    BEGIN
      IF (Status IN [Status::Created,Status::Open]) THEN BEGIN
        IF (Rec."Due Date" < TODAY) THEN
          EXIT(TRUE)
        ELSE
          EXIT(FALSE);
      END;
    END;

    PROCEDURE GetBuyFromVendor@1100528203() : Text[30];
    BEGIN
      //**4PS
      CASE "Table ID" OF
        DATABASE::"Purchase Header":
        BEGIN
          EXIT(PurchHeader."Buy-from Vendor No.")
        END;
        DATABASE::"Purch. Inv. Header":
        BEGIN
          EXIT(PurchInvHeader."Buy-from Vendor No.")
        END;
        DATABASE::"Purch. Cr. Memo Hdr.":
        BEGIN
          EXIT(PurchCrMemoHdr."Buy-from Vendor No.")
        END;
      END;
    END;

    PROCEDURE GetBuyFromVendorName@1100528202() : Text[80];
    BEGIN
      //**4PS
      CASE "Table ID" OF
        DATABASE::"Purchase Header":
        BEGIN
          EXIT(PurchHeader."Buy-from Vendor Name")
        END;
        DATABASE::"Purch. Inv. Header":
        BEGIN
          EXIT(PurchInvHeader."Buy-from Vendor Name")
        END;
        DATABASE::"Purch. Cr. Memo Hdr.":
        BEGIN
          EXIT(PurchCrMemoHdr."Buy-from Vendor Name")
        END;
      END;
    END;

    PROCEDURE GetOnHold@1100528200() : Text[30];
    BEGIN
      //**4PS
      CASE "Table ID" OF
        DATABASE::"Purchase Header":
        BEGIN
          EXIT(PurchHeader."On Hold")
        END;
        DATABASE::"Purch. Inv. Header":
        BEGIN
          EXIT(PurchInvHeader."On Hold")
        END;
        DATABASE::"Purch. Cr. Memo Hdr.":
        BEGIN
          EXIT(PurchCrMemoHdr."On Hold")
        END;
      END;
    END;

    PROCEDURE FilterSetup@1100409000();
    VAR
      SetFilterDCDocument@1100525002 : Page 11126591;
      lVendorNameFilter@1100525001 : Text[250];
      lVendorNoFilter@1100525000 : Text[250];
    BEGIN
      //**4PS
      CLEAR(SetFilterDCDocument);
      SetFilterDCDocument.FillDefaults(VendorNoFilter,VendorNameFilter);

      SetFilterDCDocument.RUNMODAL;

      lVendorNoFilter := VendorNoFilter;
      lVendorNameFilter := VendorNameFilter;

      IF SetFilterDCDocument.GetDefaults(lVendorNoFilter,lVendorNameFilter) THEN BEGIN
        VendorNoFilter := lVendorNoFilter;
        VendorNameFilter := lVendorNameFilter;
        CurrPage.UPDATE(FALSE);
      END;
      CLEAR(SetFilterDCDocument);
    END;

    PROCEDURE FindPosition@1100409003(Which@1100409001 : Text) : Boolean;
    VAR
      lDocument@1100409000 : Record 454;
      i@1100409002 : Integer;
    BEGIN
      //**4PS
      lDocument.COPY(Rec);
      IF Which = '' THEN Which := '=';
      FOR i := 1 TO STRLEN(Which) DO
      CASE Which[i] OF
        '-': BEGIN
          IF Rec.FIND('-') THEN REPEAT
            IF ShowThisRecord(Rec) THEN EXIT(TRUE);
          UNTIL Rec.NEXT = 0;
        END;
        '+': BEGIN
          IF Rec.FIND('+') THEN REPEAT
            IF ShowThisRecord(Rec) THEN EXIT(TRUE);
          UNTIL Rec.NEXT(-1) = 0;
        END;
        '=': BEGIN
          IF Rec.FIND THEN
            IF ShowThisRecord(Rec) THEN EXIT(TRUE);
        END;
        '>': BEGIN
          IF NextPosition(Rec, 1) <> 0 THEN
            EXIT(TRUE);
        END;
        '<': BEGIN
          IF NextPosition(Rec, -1) <> 0 THEN
            EXIT(TRUE);
        END;
      END;
      Rec.COPY(lDocument);
      EXIT(FALSE);
    END;

    PROCEDURE NextPosition@1100409004(VAR lDocument@1100409000 : Record 454;Steps@1100409001 : Integer) StepsCount : Integer;
    VAR
      RecRef2@1100409003 : Record 454;
      RecRef3@1100409004 : Record 454;
      Direction@1100409002 : Integer;
    BEGIN
      //**4PS
      IF Steps = 0 THEN EXIT;
      IF Steps > 0 THEN
        Direction := 1
      ELSE
        Direction := -1;
      RecRef2.COPY(lDocument);
      RecRef3.COPY(lDocument);
      REPEAT
        IF lDocument.NEXT(Direction) = 0 THEN BEGIN
          lDocument.COPY(RecRef3);
          EXIT;
        END;
        IF ShowThisRecord(lDocument) THEN BEGIN
          RecRef3.COPY(lDocument);
          StepsCount += Direction;
        END;
      UNTIL ABS(StepsCount) >= ABS(Steps);
      IF StepsCount = 0 THEN
        lDocument.COPY(RecRef2)
      ELSE
        lDocument.COPY(RecRef3);
    END;

    PROCEDURE ShowThisRecord@1100525002(lApprovalEntry@1100528200 : Record 454) : Boolean;
    VAR
      DCApprovalManagement@1100528201 : Codeunit 6085722;
    BEGIN
      //**4PS
      IF (VendorNoFilter <> '') THEN
        IF NOT DCApprovalManagement.GetBuyFromVendorTest(lApprovalEntry, VendorNoFilter) THEN
         EXIT(FALSE);

      IF (VendorNameFilter <> '') THEN
        IF NOT DCApprovalManagement.GetBuyFromVendorNameTest(lApprovalEntry, VendorNameFilter) THEN
         EXIT(FALSE);

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

