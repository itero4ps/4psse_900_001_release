OBJECT Page 6085759 Document Files Factbox
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.4.04;
  }
  PROPERTIES
  {
    CaptionML=ENU=Documents;
    InsertAllowed=No;
    LinksAllowed=No;
    SourceTable=Table6085756;
    PageType=CardPart;
    SourceTableTemporary=Yes;
  }
  CONTROLS
  {
    { 161024012;0;Container;
                ContainerType=ContentArea }

    { 6085573;1;Field     ;
                Name=DocDragAndDropAddIn;
                SourceExpr=DocDragAndDropAddInSource;
                ControlAddIn=[Continia.DocumentCapture.Dynamics.Nav.DocDragAndDrop110;PublicKeyToken=afaa4797ccef7206];
                OnControlAddIn=BEGIN
                                 CLEAR(OutXmlDoc);
                                 AddInResponse.Load(Data);
                                 HandleResponse;
                                 CLEAR(AddInResponse);
                               END;
                                }

  }
  CODE
  {
    VAR
      CaptureAddinLib@161024013 : Codeunit 6085660;
      AddInResponse@161024016 : Codeunit 6085661;
      OutXmlDoc@161024014 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      RecID@161024015 : RecordID;
      IsInfoPane@161024031 : Boolean;
      CreatedDocType@161024017 : Integer;
      CreatedDocSubtype@161024018 : Integer;
      CreatedDocID@161024019 : Code[20];
      CreatedDocRefNo@161024020 : Integer;
      DocDragAndDropAddInSource@161024021 : Text;
      Text001@161024023 : TextConst 'ENU=Open Document,Edit Properties';
      Text002@161024024 : TextConst 'ENU=Drop Files Here';
      Text003@161024025 : TextConst 'ENU=Browse';
      Text004@161024026 : TextConst 'ENU=New';
      Text005@161024027 : TextConst 'ENU=You must enter a description to create a new document.';
      Text006@161024028 : TextConst 'ENU=[No Name]';
      Text007@161024029 : TextConst 'ENU=There are no %1 that has %2.';
      Text008@161024030 : TextConst 'ENU=Delete %1?';

    PROCEDURE SetDocument@1160040001(NewCreatedDocType@161024014 : Integer;NewCreatedDocSubtype@161024015 : Integer;NewCreatedDocID@161024016 : Code[20];NewCreatedDocRefNo@161024017 : Integer;ShowDetails@1160040000 : Boolean);
    VAR
      EmptyRecId@1160040001 : RecordID;
    BEGIN
      UpdatePage(NOT ShowDetails,EmptyRecId,NewCreatedDocType,NewCreatedDocSubtype,NewCreatedDocID,NewCreatedDocRefNo);
    END;

    PROCEDURE SetRecId@1160040000(NewRecID@161024012 : RecordID;ShowDetails@161024014 : Boolean);
    BEGIN
      UpdatePage(NOT ShowDetails,NewRecID,0,0,'',0);
    END;

    PROCEDURE UpdatePage@161024014(NewIsInfoPane@161024013 : Boolean;NewRecID@161024012 : RecordID;NewCreatedDocType@161024014 : Integer;NewCreatedDocSubtype@161024015 : Integer;NewCreatedDocID@161024016 : Code[20];NewCreatedDocRefNo@161024017 : Integer);
    VAR
      DocAttachMgt@161024018 : Codeunit 6085602;
    BEGIN
      IsInfoPane := NewIsInfoPane;
      RecID := NewRecID;
      CreatedDocType := NewCreatedDocType;
      CreatedDocSubtype := NewCreatedDocSubtype;
      CreatedDocID := NewCreatedDocID;
      CreatedDocRefNo := NewCreatedDocRefNo;

      DocAttachMgt.CreateTempDocumentList(Rec,RecID,CreatedDocType,CreatedDocSubtype,CreatedDocID,CreatedDocRefNo);

      UpdateInfoBar;
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE UpdateInfoBar@161024016();
    VAR
      AppMgnt@161024012 : Codeunit 1;
    BEGIN
      CaptureAddinLib.BuildFileMenuCommand(OutXmlDoc,Text002,Text003,Text004);
      CaptureAddinLib.BuildDocListSetupCommand(OutXmlDoc,IsInfoPane);
      CaptureAddinLib.BuildDocListContentCommand(OutXmlDoc,Rec,IsInfoPane);

      CaptureAddinLib.XmlToText(OutXmlDoc,DocDragAndDropAddInSource);
      CLEAR(OutXmlDoc);
    END;

    PROCEDURE HandleResponse@161024017();
    VAR
      ResponseCommand@1160040000 : Text[50];
    BEGIN
      ResponseCommand := AddInResponse.Command;

      CASE ResponseCommand OF
        'Visible':
          EXIT;  // This event is raised by the Client Add-in after it is visible. This is to fix a bug in the NAV 2009 RTC Client

        'NewFile':
          NewFileCmd;

        'CopyFileStarting':
          CopyFileStartingCmd;

        'FileCopyCompleted':
          FileCopyCompletedCmd;

        'FileClosed':
          FileClosedCmd;

        'ContextActionClicked':
          ContextActionClickedCmd;

        'DoubleClick':
          DoubleClickCmd;
      END;

      IF NOT ISNULL(OutXmlDoc) THEN BEGIN
        CaptureAddinLib.XmlToText(OutXmlDoc,DocDragAndDropAddInSource);
        CLEAR(OutXmlDoc);
      END;
    END;

    PROCEDURE NewFileCmd@161024019();
    VAR
      DCSetup@1160040000 : Record 6085573;
      Document@161024018 : Record 6085590;
      DocAttachMgt@161024016 : Codeunit 6085602;
      NewDocDialog@161024014 : Page 6085780;
      CategoryCode@161024017 : Code[20];
      DocType@161024013 : Text[1024];
      DocDesc@161024012 : Text[1024];
      NewDescription@161024015 : Text[1024];
      EditFilePath@1160040001 : Text;
    BEGIN
      DCSetup.GET;

      DocType := AddInResponse.GetText('Type');
      DocDesc := AddInResponse.GetText('Description');

      NewDocDialog.SetValues(RecID,DocDesc);
      NewDocDialog.LOOKUPMODE := TRUE;

      IF NewDocDialog.RUNMODAL = ACTION::LookupOK THEN BEGIN
        NewDocDialog.GetValues(CategoryCode,NewDescription);
        IF DELCHR(NewDescription,'=',' ') = '' THEN
          ERROR(Text005);

        DocAttachMgt.CreateDocument(Document,RecID,CreatedDocType,CreatedDocSubtype,CreatedDocID,CreatedDocRefNo,
          CategoryCode,NewDescription,DocType);

        CASE DCSetup."Document Storage" OF
          DCSetup."Document Storage"::"Local Servers" :
            CaptureAddinLib.BuildCreateNewFileCommand(OutXmlDoc,DocType,Document.GetMiscFileName,Document."No.",FALSE);

          DCSetup."Document Storage"::"Cloud Servers" :
            BEGIN
              EditFilePath := GetTempFileName(Document);
              CaptureAddinLib.BuildCreateNewFileCommand(OutXmlDoc,DocType,EditFilePath,Document."No.",TRUE);
            END;
        END;

        UpdatePage(IsInfoPane,RecID,CreatedDocType,CreatedDocSubtype,CreatedDocID,CreatedDocRefNo);
      END;
    END;

    PROCEDURE CopyFileStartingCmd@161024020();
    VAR
      DCSetup@1160040000 : Record 6085573;
      DocCat@161024018 : Record 6085575;
      Document@161024021 : Record 6085590;
      DocAttachMgt@161024020 : Codeunit 6085602;
      FileManagement@1160040002 : Codeunit 419;
      FileSysMgt@6085573 : Codeunit 6085584;
      RecRef@161024019 : RecordRef;
      CategoryCode@161024015 : Code[20];
      FullFilename@161024014 : Text[1024];
      Filename@161024013 : Text[1024];
      Extension@161024012 : Text[20];
      ServerFileTempPath@1160040001 : Text[1024];
      FileIndex@161024016 : Integer;
      NumberOfFiles@161024017 : Integer;
      MoveFile@161024022 : Boolean;
    BEGIN
      FullFilename := AddInResponse.GetText('FullFilename');
      Filename := AddInResponse.GetText('Filename');
      Extension := AddInResponse.GetText('Extension');
      CategoryCode := AddInResponse.GetText('CategoryCode');
      FileIndex := AddInResponse.GetInt('FileIndex');
      NumberOfFiles := AddInResponse.GetInt('NumberOfFiles');
      MoveFile := AddInResponse.GetText('Action') = 'Move';

      IF (CategoryCode = '') AND (FORMAT(RecID) <> '') THEN BEGIN
        RecRef.GET(RecID);
        DocCat.SETRANGE("Source Table No.",RecRef.NUMBER);
        DocCat.SETRANGE("Allow Drag-and-Drop",TRUE);
        IF NOT DocCat.FINDFIRST THEN BEGIN
          DocCat.SETRANGE("Source Table No.",0);
          IF NOT DocCat.FINDFIRST THEN
            ERROR(Text007,DocCat.TABLECAPTION,DocCat.FIELDCAPTION("Allow Drag-and-Drop"));
        END;

        IF DocCat.COUNT > 1 THEN
          IF PAGE.RUNMODAL(0,DocCat) <> ACTION::LookupOK THEN
            ERROR('');

        CategoryCode := DocCat.Code;
      END;

      DocAttachMgt.CreateDocument(Document,RecID,CreatedDocType,CreatedDocSubtype,CreatedDocID,CreatedDocRefNo,
        CategoryCode,Filename,Extension);
      COMMIT;

      CASE DCSetup."Document Storage" OF
        DCSetup."Document Storage"::"Local Servers" :
          IF MoveFile THEN
            CaptureAddinLib.BuildMoveFileCommand(OutXmlDoc,FullFilename,Document.GetMiscFileName,CategoryCode)
          ELSE
            CaptureAddinLib.BuildCopyFileCommand(OutXmlDoc,FullFilename,Document.GetMiscFileName,CategoryCode);

        DCSetup."Document Storage"::"Cloud Servers" :
          BEGIN
            ServerFileTempPath := FileManagement.UploadFileSilent(FullFilename);
            FileSysMgt.DeleteFile(Document.GetMiscFileName,TRUE);
            FileSysMgt.CopyFile(ServerFileTempPath,Document.GetMiscFileName);
            FileSysMgt.DeleteFile(ServerFileTempPath,TRUE);
            IF MoveFile THEN
              FileManagement.DeleteClientFile(FullFilename);
          END;
      END
    END;

    PROCEDURE FileCopyCompletedCmd@161024021();
    BEGIN
      UpdatePage(IsInfoPane,RecID,CreatedDocType,CreatedDocSubtype,CreatedDocID,CreatedDocRefNo);
    END;

    PROCEDURE FileClosedCmd@1160040042();
    VAR
      DCSetup@1160040000 : Record 6085573;
      Document@1160040005 : Record 6085590;
      FileManagement@1160040004 : Codeunit 419;
      FileSysMgt@6085573 : Codeunit 6085584;
      EditFilePath@1160040003 : Text[1024];
      ServerFileTempPath@1160040002 : Text[1024];
      DocumentNo@1160040001 : Code[20];
    BEGIN
      DCSetup.GET;

      IF DCSetup."Document Storage" = DCSetup."Document Storage" THEN BEGIN
        DocumentNo := AddInResponse.GetText('DocumentNo');
        Document.GET(DocumentNo);
        EditFilePath := GetTempFileName(Document);
        ServerFileTempPath := FileManagement.UploadFileSilent(EditFilePath);
        FileSysMgt.DeleteFile(Document.GetMiscFileName,TRUE);
        FileSysMgt.CopyFile(ServerFileTempPath,Document.GetMiscFileName);
        FileSysMgt.DeleteFile(ServerFileTempPath,TRUE);
        FileManagement.DeleteClientFile(EditFilePath);
      END;
    END;

    PROCEDURE ContextActionClickedCmd@161024022();
    VAR
      Document@161024014 : Record 6085590;
      RenameDocDialog@161024015 : Page 6085781;
      DocAttachMgt@161024017 : Codeunit 6085602;
      ActionName@161024013 : Code[20];
      DocumentNo@161024012 : Code[20];
      NewDescription@161024016 : Text[80];
    BEGIN
      ActionName := AddInResponse.GetText('Name');
      DocumentNo := AddInResponse.GetText('DocumentNo');
      Document.GET(DocumentNo);

      CASE ActionName OF
        'OPEN':
          BEGIN
            IF Document."File Type" = Document."File Type"::OCR THEN
              CaptureAddinLib.BuildOpenFileCommand(OutXmlDoc,Document.GetPDFileName,"No.",FALSE)
            ELSE
              CaptureAddinLib.BuildOpenFileCommand(OutXmlDoc,Document.GetMiscFileName,"No.",FALSE);
          END;

        'RENAME':
          BEGIN
            Rec.RESET;
            Rec.SETRANGE("No.",DocumentNo);
            Rec.FINDFIRST;
            Rec.RESET;

            RenameDocDialog.SetValues(Description);
            RenameDocDialog.LOOKUPMODE := TRUE;
            IF RenameDocDialog.RUNMODAL = ACTION::LookupOK THEN BEGIN
              RenameDocDialog.GetValues(NewDescription);
              IF DELCHR(NewDescription,'=',' ') = '' THEN
                ERROR(Text005);
              IF Description <> NewDescription THEN BEGIN
                Description := NewDescription;
                MODIFY;
                DocAttachMgt.ModifyDocument(Rec);
              END;

              UpdateInfoBar;
            END;
          END;

        'DELETE':
          BEGIN
            Rec.RESET;
            Rec.SETRANGE("No.",DocumentNo);
            Rec.FINDFIRST;
            IF CONFIRM(Text008,FALSE,TABLECAPTION) THEN
              IF DocAttachMgt.DeleteDocument(Rec) THEN
                UpdatePage(IsInfoPane,RecID,CreatedDocType,CreatedDocSubtype,CreatedDocID,CreatedDocRefNo);

          END;
      END;
    END;

    PROCEDURE DoubleClickCmd@161024023();
    VAR
      DCSetup@1160040000 : Record 6085573;
      Document@161024012 : Record 6085590;
      FileManagement@1160040002 : Codeunit 419;
      EditFilePath@1160040001 : Text[1024];
    BEGIN
      DCSetup.GET;

      Document.GET(AddInResponse.GetText('DocumentNo'));

      IF Document."File Type" = Document."File Type"::OCR THEN
        CaptureAddinLib.BuildOpenFileCommand(OutXmlDoc,Document.GetPDFileName,"No.",FALSE)
      ELSE
        CASE DCSetup."Document Storage" OF
          DCSetup."Document Storage"::"Local Servers" :
            CaptureAddinLib.BuildOpenFileCommand(OutXmlDoc,Document.GetMiscFileName,Document."No.",FALSE);

          DCSetup."Document Storage"::"Cloud Servers" :
            BEGIN
              EditFilePath := GetTempFileName(Document);
              FileManagement.DownloadToFile(Document.GetMiscFileName,EditFilePath);
              CaptureAddinLib.BuildOpenFileCommand(OutXmlDoc,EditFilePath,Document."No.",TRUE);
            END;
        END;
    END;

    PROCEDURE GetTempFileName@1160040012(Document@1160040003 : Record 6085590) : Text[1024];
    VAR
      DCSetup@1160040002 : Record 6085573;
      PathClient@1160040001 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.Path" RUNONCLIENT;
      FullFilename@1160040000 : Text[1024];
    BEGIN
      WITH Document DO BEGIN
        IF "No."= '' THEN
          EXIT('');

        DCSetup.GET;

        FullFilename := PathClient.GetTempPath + "No." + '.' + "File Extension";

        CreateDirectory(FullFilename);
        EXIT(FullFilename);
      END;
    END;

    EVENT OutXmlDoc@161024014::NodeInserting@93(sender@6085574 : Variant;e@6085573 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT OutXmlDoc@161024014::NodeInserted@94(sender@6085574 : Variant;e@6085573 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT OutXmlDoc@161024014::NodeRemoving@95(sender@6085574 : Variant;e@6085573 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT OutXmlDoc@161024014::NodeRemoved@96(sender@6085574 : Variant;e@6085573 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT OutXmlDoc@161024014::NodeChanging@97(sender@6085574 : Variant;e@6085573 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT OutXmlDoc@161024014::NodeChanged@98(sender@6085574 : Variant;e@6085573 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    BEGIN
    END.
  }
}

