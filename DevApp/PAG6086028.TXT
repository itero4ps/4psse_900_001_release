OBJECT Page 6086028 Web Portal User Company (WS)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCOW17.10.00.4.04,DCW17.10.00.4.04;
  }
  PROPERTIES
  {
    SourceTable=Table6086006;
    PageType=List;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 UpdateWebUserCompany;
               END;

    OnAfterGetRecord=BEGIN
                       Display := "Company Name";
                     END;

  }
  CONTROLS
  {
    { 161024012;0;Container;
                ContainerType=ContentArea }

    { 161024013;1;Group   ;
                Name=Group;
                GroupType=Repeater }

    { 161024014;2;Field   ;
                Name=CompanyCode;
                SourceExpr="Company Code" }

    { 161024015;2;Field   ;
                Name=UserId;
                SourceExpr="User Id" }

    { 161024016;2;Field   ;
                Name=NoOfDocForApproval;
                SourceExpr="No. of Documents for Approval" }

    { 6085573;2;Field     ;
                Name=NoOfSharedDocuments;
                SourceExpr="No. of Shared Documents" }

    { 161024017;2;Field   ;
                Name=Display;
                SourceExpr=Display }

  }
  CODE
  {
    VAR
      WebUserCompany@1000 : TEMPORARY Record 6086006;
      Display@161024013 : Text[1024] INDATASET;

    PROCEDURE UpdateWebUserCompany@161024013();
    VAR
      Company@161024015 : Record 2000000006;
      COSetup@161024014 : Record 6086500;
      UserSetup@161024017 : Record 91;
      ApprEntry@161024018 : Record 454;
      WebSubMenu@6085573 : Record 6086011;
      DCApprovalMgnt@6085574 : Codeunit 6085722;
      User@161024013 : Code[50];
    BEGIN
      IF Company.FINDFIRST THEN
        REPEAT
          IF COSetup.CHANGECOMPANY(Company.Name) THEN BEGIN
            IF COSetup.READPERMISSION THEN BEGIN
              IF COSetup.GET THEN BEGIN
                IF COSetup."Enable Web Approval" THEN BEGIN
                  UserSetup.CHANGECOMPANY(Company.Name);
                  IF UserSetup.READPERMISSION THEN BEGIN
                    IF UserSetup.GET(USERID) THEN BEGIN
                      WebUserCompany."Company Code" := GetCompanyCode(Company.Name);
                      WebUserCompany."Company Name" := Company.Name;
                      WebUserCompany."User Id" := USERID;
                      IF NOT WebUserCompany.FIND('=') THEN BEGIN
                        ApprEntry.CHANGECOMPANY(Company.Name);
                        ApprEntry.SETCURRENTKEY("Approver ID",Status);
                        ApprEntry.SETRANGE("Approver ID",USERID);
                        ApprEntry.SETRANGE(Status,ApprEntry.Status::Open);
                        ApprEntry.SETRANGE("Table ID",DATABASE::"Purchase Header");
                        WebUserCompany."No. of Documents for Approval" := ApprEntry.COUNT;

                        ApprEntry.SETRANGE("Approver ID",DCApprovalMgnt.GetApprovalSharingToUserFilter(USERID));
                        WebUserCompany."No. of Shared Documents" := ApprEntry.COUNT;
                        ApprEntry.SETRANGE("Approver ID",USERID);

                        WebSubMenu.CHANGECOMPANY(Company.Name);
                        IF WebSubMenu.FINDFIRST THEN
                          REPEAT
                            IF WebSubMenu."Approval Code Filter" <> '' THEN
                              ApprEntry.SETFILTER("Approval Code",WebSubMenu."Approval Code Filter");

                            IF WebSubMenu."Table ID Filter" <> '' THEN
                              ApprEntry.SETFILTER("Table ID",WebSubMenu."Table ID Filter");

                            IF WebSubMenu."Document Type Filter" <> '' THEN
                              ApprEntry.SETFILTER("Document Type",WebSubMenu."Document Type Filter");

                            WebUserCompany."No. of Documents for Approval" += ApprEntry.COUNT;


                            ApprEntry.SETRANGE("Approver ID",DCApprovalMgnt.GetApprovalSharingToUserFilter(USERID));
                            WebUserCompany."No. of Shared Documents" += ApprEntry.COUNT;
                            ApprEntry.SETRANGE("Approver ID",USERID);
                          UNTIL WebSubMenu.NEXT = 0;

                        WebUserCompany.INSERT;

                        Rec := WebUserCompany;
                        Rec.INSERT;
                      END;
                    END;
                  END;
                END;
              END;
            END;
          END;
        UNTIL Company.NEXT = 0;
    END;

    PROCEDURE GetCompanyCode@1160040001(_CompanyName@1160040004 : Text[30]) : Code[10];
    VAR
      DCSetup@1160040000 : Record 6085573;
      Obj@1160040003 : Record 2000000001;
      RecRef@1160040001 : RecordRef;
      FieldRef@1160040002 : FieldRef;
    BEGIN
      DCSetup.CHANGECOMPANY(_CompanyName);
      IF DCSetup.GET THEN
        EXIT(DCSetup."Company Code");

      IF NOT Obj.GET(Obj.Type::Table,'',6086300) THEN
        EXIT;

      RecRef.OPEN(6086300,FALSE,_CompanyName);
      IF NOT RecRef.FINDFIRST THEN
        EXIT;

      FieldRef := RecRef.FIELD(2);
      EXIT(FieldRef.VALUE);
    END;

    BEGIN
    END.
  }
}

