OBJECT Report 11012033 Get Projects
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Get Projects;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 8019;    ;DataItem;                    ;
               DataItemTable=Table11072003;
               OnPreDataItem=BEGIN
                               IF ResultType = ResultType::Preclosure THEN
                               BEGIN
                                 Job.FILTERGROUP(9);   // melding 17077 , abnders wordt selectie vanaf form overschreven,
                                 SETFILTER("Preclosure Result", '<>%1', "Preclosure Result"::No);
                                 Job.FILTERGROUP(0);
                               END ELSE
                                 SETRANGE("Provision Loss", TRUE);

                               IF PostingDate = 0D THEN
                                 ERROR(Text001);
                             END;

               OnAfterGetRecord=VAR
                                  LineNo@1100528240 : Integer;
                                BEGIN
                                  IF Job."Allow Precl. R./Prov. Loss" = Job."Allow Precl. R./Prov. Loss"::No THEN
                                    CurrReport.SKIP;

                                  IF (Job."Allow Precl. R./Prov. Loss" = Job."Allow Precl. R./Prov. Loss"::"Preclosure Result") AND
                                     (ResultType = ResultType::Provision) THEN
                                    CurrReport.SKIP;

                                  IF (Job."Allow Precl. R./Prov. Loss" = Job."Allow Precl. R./Prov. Loss"::"Provision Loss") AND
                                     (ResultType = ResultType::Preclosure) THEN
                                    CurrReport.SKIP;

                                  ProjPreclResultRec.RESET;
                                  ProjPreclResultRec.SETCURRENTKEY("Project No.","Line No.");
                                  ProjPreclResultRec.SETRANGE("Project No.", "No.");
                                  LineNo := 10000;  //Select new lineno.
                                  IF ProjPreclResultRec.FINDLAST THEN
                                    LineNo := ProjPreclResultRec."Line No." + 10000;

                                  ProjPreclResultRec.RESET;
                                  ProjPreclResultRec.SETCURRENTKEY("Project No.","Posting Date","Line No.");
                                  ProjPreclResultRec.SETRANGE("Project No.", "No.");
                                  ProjPreclResultRec.SETFILTER("Posting Date", '>%1', PostingDate);
                                  IF ProjPreclResultRec.ISEMPTY THEN BEGIN   //Only when there are not entries with newer posting dates
                                    ProjPreclResultRec.SETRANGE("Posting Date", PostingDate);
                                    ProjPreclResultRec.SETRANGE(Posted, FALSE);
                                    IF ProjPreclResultRec.ISEMPTY THEN BEGIN   //Only when there are no entries on posting date which are not posted
                                      ProjPreclResultRec.SETRANGE(Posted, TRUE);
                                      IF ProjPreclResultRec.ISEMPTY OR AddNew THEN BEGIN
                                        ProjPreclResultRec.RESET;

                                        IF ResultType = ResultType::Provision THEN
                                          IF ProjPreclResultRec.CheckPreclResultPresent(FALSE) THEN
                                            CurrReport.SKIP;

                                        ProjPreclResultRec.INIT; //** Call C010248
                                        ProjPreclResultRec.VALIDATE("Project No.", "No.");
                                        ProjPreclResultRec.VALIDATE("Line No.", LineNo);
                                        ProjPreclResultRec.VALIDATE("Posting Date", PostingDate);
                                        ProjPreclResultRec."Result Type" := ResultType;
                                        ProjPreclResultRec."Prognosis Level" := "Prognosis Level"; //C000501

                                        IF ProjPreclResultRec.INSERT(TRUE) THEN;

                                      END;
                                    END;
                                  END;
                                END;

               ReqFilterFields=No.,Project Status,Preclosure Result }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1100525000;2;Field  ;
                  CaptionML=ENU=Result Type;
                  OptionCaptionML=ENU=Preclosure Result,Provision Loss;
                  SourceExpr=ResultType }

      { 1100525002;2;Field  ;
                  CaptionML=ENU=Posting Date;
                  SourceExpr=PostingDate }

      { 1100528240;2;Field  ;
                  CaptionML=ENU=Add new Line No. when there is a posted record on the posting date;
                  SourceExpr=AddNew }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ProjPreclResultRec@1100525002 : Record 11012093;
      ResultType@1100525000 : 'Preclosure,Provision';
      PostingDate@1100525001 : Date;
      Text001@1100525003 : TextConst 'ENU=Select a Posting Date in the Option Tab';
      AddNew@1100528240 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

