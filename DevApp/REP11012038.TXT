OBJECT Report 11012038 Close Projects
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00,4PSSE;
  }
  PROPERTIES
  {
    CaptionML=ENU=Close Projects;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF PostingDate = 0D THEN
                    ERROR(Text000);
                END;

    OnPostReport=VAR
                   ClosingProjPrevGenJnlReport@1100525000 : Report 11020272;
                 BEGIN
                   IF NOT (TempGenJnlLineRec1.ISEMPTY AND TempGenJnlLineRec1.ISEMPTY) AND
                     PreviewClosedProjects THEN
                   BEGIN
                     ClosingProjPrevGenJnlReport.ImportTemp1(TempGenJnlLineRec1);
                     ClosingProjPrevGenJnlReport.ImportTemp2(TempGenJnlLineRec2);
                     ClosingProjPrevGenJnlReport.RUNMODAL;
                   END;

                   ClosingProcedure.DisplayError;
                   ClosingProcedure.StartGenJnl;
                   CLEAR(ClosingProcedure);
                 END;

  }
  DATASET
  {
    { 8019;    ;DataItem;                    ;
               DataItemTable=Table11072003;
               OnPreDataItem=BEGIN
                               IF PostClosedProjects THEN BEGIN
                                 IF GETFILTER("Project Status") <> '' THEN
                                   IF NOT CONFIRM(Text006) THEN
                                     ERROR(Text007);

                                 SETRANGE("Project Status", "Project Status"::"Administrative Finished");
                               END ELSE BEGIN
                                 FILTERGROUP(6);
                                 SETFILTER("Project Status", '<=%1', "Project Status"::"Administrative Finished");
                                 ProjRec.FILTERGROUP(6);
                                 ProjRec.COPYFILTERS(Job);
                                 ProjRec.FILTERGROUP(0);
                                 FILTERGROUP(0);
                               END;

                               ProjRec.COPYFILTERS(Job);
                               IF NOT ProjRec.FINDFIRST THEN
                                 MESSAGE(Text001);
                             END;

               OnAfterGetRecord=VAR
                                  Job2@1210190001 : Record 11072003;
                                  ControlSnapshotMgt@1210190000 : Codeunit 11012244;
                                BEGIN
                                  CLEAR(ClosingProcedure); //Needed because each Project is committed now in ClosingProcedure.
                                  ClosingProcedure.SetSelection(
                                    gvType::Project, Job."No.", '', '', PostingDate,
                                    IgnorePurchase, IgnoreSales, IgnoreFuturePostings, IgnoreServiceOrders, FALSE, '', NOT PostClosedProjects);
                                  ClosingProcedure.SelSelectionProjInvent(IgnoreProjectInventory);
                                  ClosingProcedure.SetSelectionProvisionsProject(ProvisionsCosts, ProvisionsRevenues);
                                  ClosingProcedure.SetSelectionIgnorePlant(IgnorePlant);

                                  ClosingProcedure.Check;

                                  // jhoek.160611.begin: melding C-027725
                                  IF BuildSnapshot THEN BEGIN
                                    Job2 := Job;
                                    Job2.SETRECFILTER;
                                    ControlSnapshotMgt.BuildSnapshot(Job2,TODAY);
                                  END;
                                  // jhoek.160611.end

                                  IF PostClosedProjects THEN
                                    ClosingProcedure.Post;

                                  IF PreviewClosedProjects THEN BEGIN
                                    ClosingProcedure.SetPreview(TRUE);
                                    ClosingProcedure.Post;
                                    ClosingProcedure.ExportTempGenJnlLine1(TempGenJnlLineRec1);
                                    ClosingProcedure.ExportTempGenJnlLine2(TempGenJnlLineRec2);
                                  END;
                                END;

               ReqFilterFields=No.,Project Status,Main Project }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   JobSetup.GET;

                   IF JobSetup."Provisions at Closure" THEN BEGIN
                     IgnorePurchAtProvisionsEditabl := TRUE;
                     IgnoreSalesAtProvisionsEditabl := TRUE;
                   END ELSE BEGIN
                     IgnorePurchAtProvisionsEditabl := FALSE;
                     IgnoreSalesAtProvisionsEditabl := FALSE;
                   END;

                   //Call C013951 Save Values uitgezet.
                   //IF NOT FromRentalContractQte THEN BEGIN
                   //  PostingDate := 0D;
                   //  PostClosedProjects := FALSE;
                   //END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 2   ;2   ;Field     ;
                  CaptionML=ENU=Posting Date;
                  SourceExpr=PostingDate }

      { 8   ;2   ;Field     ;
                  Name=IgnoreSalesControl;
                  CaptionML=ENU=Ignore Sales Control;
                  SourceExpr=IgnoreSales;
                  OnValidate=BEGIN
                               IF IgnoreSales THEN
                                 MESSAGE(Text004 + Text005);
                             END;
                              }

      { 4   ;2   ;Field     ;
                  Name=IgnorePurchaseControl;
                  CaptionML=ENU=Ignore Purchase Control;
                  SourceExpr=IgnorePurchase;
                  OnValidate=BEGIN
                               IF IgnorePurchase THEN
                                 MESSAGE(Text002 + Text003)
                             END;
                              }

      { 1100525001;2;Field  ;
                  Name=IgnorePlantControl;
                  CaptionML=ENU=Ignore Plant Location Control;
                  SourceExpr=IgnorePlant }

      { 1100485000;2;Field  ;
                  CaptionML=ENU=Ignore Future Posting Control;
                  SourceExpr=IgnoreFuturePostings }

      { 1100525000;2;Field  ;
                  CaptionML=ENU=Ignore Project Inventory;
                  SourceExpr=IgnoreProjectInventory }

      { 1100525002;2;Field  ;
                  CaptionML=ENU=Ignore Service Orders;
                  SourceExpr=IgnoreServiceOrders }

      { 1210190001;2;Field  ;
                  CaptionML=ENU=Create Provisions Revenues from;
                  OptionCaptionML=ENU=Actual Data,Prognosis Data,No Provision;
                  SourceExpr=ProvisionsRevenues;
                  Editable=IgnoreSalesAtProvisionsEDITABL }

      { 1210190000;2;Field  ;
                  CaptionML=ENU=Create Provisions Costs from;
                  OptionCaptionML=ENU=Actual Data,Prognosis Data,No Provision;
                  SourceExpr=ProvisionsCosts;
                  Editable=IgnorePurchAtProvisionsEDITABL }

      { 1210190003;2;Field  ;
                  CaptionML=ENU=Build Snapshot;
                  SourceExpr=BuildSnapshot }

      { 1100528240;2;Field  ;
                  Name=PreviewClosedProjects;
                  CaptionML=ENU=Preview Gen. Journal Lines;
                  SourceExpr=PreviewClosedProjects;
                  OnValidate=BEGIN
                               //db, 25-09-14
                               IF PreviewClosedProjects THEN
                                 PostClosedProjects := FALSE;
                             END;
                              }

      { 1210190002;2;Field  ;
                  Name=PostClosedProjects;
                  CaptionML=ENU=Post;
                  SourceExpr=PostClosedProjects;
                  OnValidate=BEGIN
                               //db, 25-09-14
                               IF PostClosedProjects THEN
                                 PreviewClosedProjects := FALSE;
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@11012042 : TextConst 'ENU=Select Posting Date on Option Tab';
      Text001@11012043 : TextConst 'ENU=There are no Projects within the selected range';
      ProjRec@11012000 : Record 11072003;
      JobSetup@1210190009 : Record 315;
      ClosingProcedure@1210190007 : Codeunit 11012046;
      PostingDate@1210190001 : Date;
      gvType@1210190000 : 'Project,Service Contract,Service Order';
      PostClosedProjects@1210190006 : Boolean;
      IgnorePurchase@11012040 : Boolean;
      IgnoreSales@11012041 : Boolean;
      IgnoreFuturePostings@1100485002 : Boolean;
      IgnoreProjectInventory@1100525000 : Boolean;
      Text002@1210190005 : TextConst 'ENU=Ignoring the Purchase Check could result in closing Projects too early.\';
      Text003@1210190004 : TextConst 'ENU=There could still be Purchase Orders present.';
      Text004@1210190003 : TextConst 'ENU=Ignoring the Sales Check could result in closing Projects too early.\';
      Text005@1210190002 : TextConst 'ENU=There could still not Invoiced Cost Plus Entries or Installment Lines present.';
      Text006@1100485000 : TextConst 'ENU="You have chosen to post and you''ve put a filter on Project Status. Would you like to continue posting Projects with Project Status Administrative Finished? "';
      Text007@1100485001 : TextConst 'ENU=Closing is stopped.';
      IgnoreServiceOrders@1100525003 : Boolean;
      BuildSnapshot@1210190008 : Boolean;
      ProvisionsRevenues@1210190010 : 'Actual Data,Prognosis Data,No Provision';
      ProvisionsCosts@1210190011 : 'Actual Data,Prognosis Data,No Provision';
      IgnorePurchAtProvisionsEditabl@1210190013 : Boolean INDATASET;
      IgnoreSalesAtProvisionsEditabl@1210190012 : Boolean INDATASET;
      PreviewClosedProjects@1100528240 : Boolean;
      TempGenJnlLineRec1@1100528242 : TEMPORARY Record 81;
      TempGenJnlLineRec2@1100528241 : TEMPORARY Record 81;
      FromRentalContractQte@1100525002 : Boolean;
      IgnorePlant@1100525001 : Boolean;

    PROCEDURE FillSelectionRentalContractQte@1100485000();
    BEGIN
      //* Aanroep vanaf verhuurcontractofferte (F11071965) vanaf delete trigger.
      PostingDate := TODAY;
      IgnoreSales := FALSE;
      IgnorePurchase := FALSE;
      IgnoreFuturePostings := FALSE;
      PostClosedProjects := TRUE;
      FromRentalContractQte := TRUE;
    END;

    BEGIN
    {
      160711 ITERO.SB RFC-122 Added field "Main project" as ReqFilterFields
    }
    END.
  }
  RDLDATA
  {
  }
}

