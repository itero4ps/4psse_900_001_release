OBJECT Report 11012091 Create Periods LOSA (CTB)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Create Losa Periods;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  DateRec.SETRANGE("Period Type",DateRec."Period Type"::Year);
                  DateRec.SETRANGE("Period No.",YearInt);
                  IF NOT DateRec.FIND('-') THEN
                    ERROR(Text000,YearInt);
                  DateRec.SETRANGE("Period No.");
                  DateRec.SETRANGE("Period Start",CALCDATE('<-4D>', DateRec."Period Start"),DateRec."Period End");  //db, 16-03-11

                  CASE PeriodType OF
                    PeriodType::Week,PeriodType::"4-Weeks":DateRec.SETRANGE("Period Type",DateRec."Period Type"::Week);
                    PeriodType::Month:DateRec.SETRANGE("Period Type",DateRec."Period Type"::Month);
                    PeriodType::Quarter:DateRec.SETRANGE("Period Type",DateRec."Period Type"::Quarter);
                  END;

                  IF DateRec.FIND('-') THEN BEGIN
                    REPEAT
                      //The result of DATE2DWY and DATE2DWM has a alight difference. 01-01-2010 returns 2009 and 2010.
                      IF ((PeriodType IN [PeriodType::Week,PeriodType::"4-Weeks"]) AND (DATE2DWY(DateRec."Period Start", 3) = YearInt)) OR
                         ((PeriodType IN [PeriodType::Month,PeriodType::Quarter]) AND (DATE2DMY(DateRec."Period Start", 3) = YearInt)) THEN
                      BEGIN
                        CASE PeriodType OF
                          PeriodType::"4-Weeks":
                            BEGIN
                              AddPeriod := FALSE;
                              IF DATE2DWY(DateRec."Period Start", 2) MOD 4 = 1 THEN BEGIN
                                PeriodInt := PeriodInt + 1;
                                AddPeriod := TRUE;
                              END;
                            END;
                          ELSE
                            BEGIN
                              AddPeriod := TRUE;
                              PeriodInt := DateRec."Period No.";
                            END;
                        END;

                        IF AddPeriod THEN BEGIN
                          IF NOT LosaPeriod.GET(YearInt,PeriodType,PeriodInt) THEN BEGIN
                            LosaPeriod.INIT;
                            LosaPeriod.Year := YearInt;
                            LosaPeriod."Period Type" := PeriodType;
                            LosaPeriod."Period No." := PeriodInt;
                            LosaPeriod."Start Date" := DateRec."Period Start";
                            IF PeriodType = PeriodType::"4-Weeks" THEN
                              LosaPeriod.Description := FORMAT(PeriodInt)
                            ELSE
                              LosaPeriod.Description := DateRec."Period Name";
                            LosaPeriod.INSERT;
                          END;
                        END;
                      END;
                    UNTIL DateRec.NEXT = 0;
                  END;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF YearInt = 0 THEN
                     YearInt := DATE2DWY(TODAY,3);
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1210190000;2;Field  ;
                  CaptionML=ENU=Year;
                  SourceExpr=YearInt }

      { 1210190002;2;Field  ;
                  CaptionML=ENU=Period Type;
                  OptionCaptionML=ENU=Week,4-Weeks,Month,Quarter;
                  SourceExpr=PeriodType }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      LosaPeriod@1005 : Record 11012379;
      DateRec@1100485000 : Record 2000000007;
      YearInt@1210190000 : Integer;
      Text000@1210190004 : TextConst 'ENU=Losa Period %1 %2';
      PeriodInt@1100485001 : Integer;
      PeriodType@1210190005 : 'Week,4-Weeks,Month,Quarter';
      AddPeriod@1100485002 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

