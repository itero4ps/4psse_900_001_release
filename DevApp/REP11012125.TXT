OBJECT Report 11012125 Copy Cost Obj.Sls Price Cust
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Cost Object Sales Prices from Customer to Customer;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF ToCustCde = '' THEN
                    ERROR(Text000);
                  IF ToRefDate = 0D THEN
                    ERROR(Text001);

                  IF CopyStandardSalesPrices THEN BEGIN
                    CustomerRec.SETFILTER("No.", '%1', '');
                    IF ("Cust - Cost Object Sales Price".GETFILTER("Customer No.") <> '') AND
                       ("Cust - Cost Object Sales Price".GETFILTER("Customer No.") <> CustomerRec.GETFILTER("No."))
                    THEN BEGIN
                      IF NOT CONFIRM(Text002, FALSE, "Cust - Cost Object Sales Price".GETFILTER("Customer No.")) THEN
                        ERROR('');
                    END;
                    CustomerRec.RESET;
                  END;

                  CustomerRec.GET(ToCustCde);
                  CustSalesPriceAdded := FALSE;
                END;

    OnPostReport=BEGIN
                   IF CustSalesPriceAdded THEN BEGIN
                     CustomerRec.GET(ToCustCde);
                     CustomerRec."Cost Plus Sales Prices" := TRUE;
                     CustomerRec."Cost Plus Sales Prices From" := TODAY;
                     CustomerRec.MODIFY;
                   END;
                 END;

  }
  DATASET
  {
    { 2193;    ;DataItem;                    ;
               DataItemTable=Table11012080;
               OnPreDataItem=BEGIN
                               IF CopyStandardSalesPrices THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF NOT CustCostObjSalesPriceRec.GET(ToCustCde, "Cost Object", ToRefDate) THEN BEGIN
                                    CustCostObjSalesPriceRec := "Cust - Cost Object Sales Price";
                                    CustCostObjSalesPriceRec."Customer No." := ToCustCde;
                                    CustCostObjSalesPriceRec."Starting Date" := ToRefDate;
                                    CustCostObjSalesPriceRec."Ending Date" := 0D;
                                    CustCostObjSalesPriceRec.INSERT;
                                    CustSalesPriceAdded := TRUE;
                                  END;
                                END;

               ReqFilterFields=Cost Object,Customer No.,Starting Date }

    { 6363;    ;DataItem;                    ;
               DataItemTable=Table349;
               DataItemTableView=SORTING(Code,Global Dimension No.)
                                 WHERE(Global Dimension No.=CONST(2),
                                       Cost Type=FILTER(Material..Sundry),
                                       Sales Price=FILTER(<>0));
               OnPreDataItem=BEGIN
                               IF NOT CopyStandardSalesPrices THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF NOT CustCostObjSalesPriceRec.GET(ToCustCde, Code, ToRefDate) THEN BEGIN
                                    CustCostObjSalesPriceRec.INIT;
                                    CustCostObjSalesPriceRec."Cost Object" := Code;
                                    CustCostObjSalesPriceRec."Customer No." := ToCustCde;
                                    CustCostObjSalesPriceRec."Starting Date" := ToRefDate;
                                    CustCostObjSalesPriceRec."Sales Price (LCY)" := "Sales Price";
                                    CustCostObjSalesPriceRec.INSERT;
                                    CustSalesPriceAdded := TRUE;
                                  END;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=No;
      OnOpenPage=BEGIN
                   IF gRefDate <> 0D THEN
                     ToRefDate := gRefDate;
                   IF ToRefDate = 0D THEN
                     ToRefDate := TODAY;
                   CopyStandardSalesPrices := FALSE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1210190000;2;Field  ;
                  CaptionML=ENU=To Customer;
                  SourceExpr=ToCustCde;
                  TableRelation=Customer }

      { 1100485000;2;Field  ;
                  CaptionML=ENU=To Reference Date;
                  NotBlank=Yes;
                  SourceExpr=ToRefDate }

      { 1100485002;2;Field  ;
                  CaptionML=ENU=Copy Standard Prices;
                  SourceExpr=CopyStandardSalesPrices }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CustomerRec@1100485001 : Record 18;
      CustCostObjSalesPriceRec@1100485000 : Record 11012080;
      Text000@1210190003 : TextConst 'ENU=Enter ''To Customer'' in Option Tab';
      gRefDate@1100485006 : Date;
      ToRefDate@1100485005 : Date;
      CopyStandardSalesPrices@1100485004 : Boolean;
      CustSalesPriceAdded@1100485003 : Boolean;
      ToCustCde@1100485002 : Code[20];
      Text001@1100485007 : TextConst 'ENU=Enter ''To Reference Date'' in Option Tab';
      Text002@1100485008 : TextConst 'ENU=Customer No. to copy is filled (%1).\ \Are you sure you want to copy the standard sales price of the cost object?';

    PROCEDURE SetDefaultSelections@1100485003(IRefDate@1100485000 : Date);
    BEGIN
      gRefDate := IRefDate;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

