OBJECT Report 11012290 Generate Vendor Assessment
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Generate Vendor Assessment;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF (AssDate = 0D) THEN
                    ERROR(Text000);

                  IF NOT Job.GET(Job.GETFILTER("No.")) THEN
                    ERROR(Text001);

                  TmpProjVendRec.RESET;
                  TmpProjVendRec.DELETEALL;
                END;

  }
  DATASET
  {
    { 8019;    ;DataItem;                    ;
               DataItemTable=Table11072003;
               DataItemTableView=SORTING(No.);
               OnAfterGetRecord=BEGIN
                                  PurLineRec.RESET;
                                  PurLineRec.SETCURRENTKEY("Document Type", "Job No.");

                                  CASE AssessmentBasedOn OF
                                    AssessmentBasedOn::Quote:
                                      PurLineRec.SETRANGE("Document Type", PurLineRec."Document Type"::Quote);
                                    AssessmentBasedOn::Order:
                                      PurLineRec.SETRANGE("Document Type", PurLineRec."Document Type"::Order);
                                    AssessmentBasedOn::Both:
                                      PurLineRec.SETFILTER("Document Type",'%1|%2',PurLineRec."Document Type"::Quote, PurLineRec."Document Type"::Order);
                                  END;

                                  PurLineRec.SETRANGE("Job No.", "No.");
                                  IF SubcontractingOnly THEN                                                      // Call C021757.n
                                  PurLineRec.SETRANGE("Cost Type", PurLineRec."Cost Type"::Subcontracting);       // Call C021757.n
                                  PurLineRec.SETFILTER("Buy-from Vendor No.", '<>%1', '');
                                  IF PurLineRec.FINDSET THEN BEGIN
                                    TmpProjVendRec.RESET;
                                    REPEAT
                                      IF NOT TmpProjVendRec.GET(PurLineRec."Job No.", PurLineRec."Buy-from Vendor No.") THEN BEGIN
                                        TmpProjVendRec."Project No." := PurLineRec."Job No.";
                                        TmpProjVendRec.Principal := PurLineRec."Buy-from Vendor No.";
                                        VendAssCritRec.SETRANGE("Vendor No.", TmpProjVendRec.Principal);
                                        IF VendAssCritRec.FIND('-') THEN
                                          TmpProjVendRec."Generate Installments" := TRUE
                                        ELSE
                                          TmpProjVendRec."Generate Installments" := FALSE;
                                        // Call C021757.sn
                                        TmpProjVendRec."Cost Plus Customer" := FALSE;
                                        TmpProjVendRec."Contract Amount (LCY)" := 0;
                                        // Call C021757.en
                                        TmpProjVendRec.INSERT;
                                        // Call C021757.sn
                                        IF NOT TmpVendRec.GET(PurLineRec."Buy-from Vendor No.") THEN BEGIN
                                          TmpVendRec."No." := PurLineRec."Buy-from Vendor No.";
                                          TmpVendRec.INSERT;
                                        END;
                                        // Call C021757.en
                                      END;
                                      // Call C021757.sn
                                      IF (PurLineRec.Type <> PurLineRec.Type::" ") AND (PurLineRec."Cost Type" = PurLineRec."Cost Type"::Subcontracting) THEN
                                        TmpProjVendRec."Cost Plus Customer" := TRUE;
                                      TmpProjVendRec."Contract Amount (LCY)" := TmpProjVendRec."Contract Amount (LCY)" + PurLineRec."Amount (LCY)";
                                      TmpProjVendRec.MODIFY;
                                      // Call C021757.en
                                    UNTIL PurLineRec.NEXT = 0;
                                  END;
                                END;

               OnPostDataItem=VAR
                                TotalAmount@1100525000 : Decimal;
                              BEGIN
                                // Call C021757.sn
                                IF SubcontractingOnly OR (MinimumAmount > 0) THEN BEGIN

                                  TmpVendRec.RESET;

                                  IF TmpVendRec.FINDSET THEN BEGIN
                                    REPEAT

                                      IF SubcontractingOnly THEN BEGIN
                                        TmpProjVendRec.RESET;
                                        TmpProjVendRec.SETCURRENTKEY(Principal,"Project No.");
                                        TmpProjVendRec.SETRANGE("Cost Plus Customer", TRUE);
                                        TmpProjVendRec.SETRANGE(Principal, TmpVendRec."No.");
                                        IF TmpProjVendRec.ISEMPTY THEN BEGIN
                                          TmpProjVendRec.SETRANGE("Cost Plus Customer");
                                          TmpProjVendRec.DELETEALL;
                                        END;
                                      END;

                                      IF MinimumAmount > 0 THEN BEGIN
                                        TotalAmount := 0;
                                        TmpProjVendRec.RESET;
                                        TmpProjVendRec.SETCURRENTKEY(Principal,"Project No.");
                                        TmpProjVendRec.SETRANGE(Principal, TmpVendRec."No.");
                                        IF TmpProjVendRec.FINDSET THEN BEGIN
                                          REPEAT
                                            TotalAmount := TotalAmount + TmpProjVendRec."Contract Amount (LCY)";
                                          UNTIL TmpProjVendRec.NEXT = 0;
                                          IF TotalAmount < MinimumAmount THEN
                                            TmpProjVendRec.DELETEALL;
                                        END;
                                      END;

                                    UNTIL TmpVendRec.NEXT = 0;
                                  END;
                                END;
                                // Call C021757.en
                              END;

               ReqFilterFields=No. }

    { 2089;    ;DataItem;                    ;
               DataItemTable=Table11012291;
               DataItemTableView=SORTING(Department,Criterion);
               OnPreDataItem=BEGIN
                               TmpProjVendRec.RESET;
                               IF TmpProjVendRec.FIND('-') THEN BEGIN
                                 REPEAT
                                   IF VendAssRec.GET(TmpProjVendRec."Project No.", AssDate, TmpProjVendRec.Principal) THEN
                                     TmpProjVendRec.DELETE
                                   ELSE BEGIN
                                     VendAssRec."Project No." := TmpProjVendRec."Project No.";
                                     VendAssRec.Date := AssDate;
                                     VendAssRec."Vendor No." := TmpProjVendRec.Principal;
                                     VendAssRec.INSERT;
                                   END;
                                 UNTIL TmpProjVendRec.NEXT = 0;
                               END;

                               IF NOT TmpProjVendRec.FIND('-') THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=VAR
                                  lvInsertCrit@1210190000 : Boolean;
                                BEGIN
                                  IF TmpProjVendRec.FIND('-') THEN BEGIN
                                    REPEAT
                                      lvInsertCrit := TRUE;
                                      IF (NOT Mandatory) AND TmpProjVendRec."Generate Installments" THEN BEGIN  //* Criterion by Vendor present
                                        IF NOT VendAssCritRec.GET(TmpProjVendRec.Principal, Department, Criterion) THEN
                                          lvInsertCrit := FALSE;
                                      END;
                                      IF lvInsertCrit THEN BEGIN
                                        VendAssLineRec.INIT;
                                        VendAssLineRec."Project No." := TmpProjVendRec."Project No.";
                                        VendAssLineRec.Date := AssDate;
                                        VendAssLineRec."Vendor No." := TmpProjVendRec.Principal;
                                        VendAssLineRec.Department := Department;
                                        VendAssLineRec.Criterion := Criterion;
                                        VendAssLineRec."Weight Factor" := "Weight Factor";
                                        VendAssLineRec.Score := 0;
                                        VendAssLineRec.Motivation := '';
                                        VendAssLineRec.INSERT;
                                      END;
                                    UNTIL TmpProjVendRec.NEXT = 0;
                                  END;
                                END;

               ReqFilterFields=Department,Criterion }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               AssessmentBasedOn := AssessmentBasedOn::Both;
             END;

      OnOpenPage=BEGIN
                   AssDate := 0D;
                 END;

    }
    CONTROLS
    {
      { 1100409001;0;Container;
                  ContainerType=ContentArea }

      { 1100409000;1;Group  ;
                  CaptionML=ENU=Options }

      { 1100409002;2;Field  ;
                  CaptionML=ENU=Assessment Date;
                  SourceExpr=AssDate }

      { 1100409003;2;Field  ;
                  CaptionML=ENU=Assessment based on;
                  OptionCaptionML=ENU=Quote,Order,Both;
                  SourceExpr=AssessmentBasedOn }

      { 1100525000;2;Field  ;
                  CaptionML=ENU=Subcontracting;
                  SourceExpr=SubcontractingOnly }

      { 1100525001;2;Field  ;
                  CaptionML=ENU=Minimum Amount;
                  SourceExpr=MinimumAmount }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PurLineRec@1210190002 : Record 39;
      VendAssRec@1210190005 : Record 11012292;
      VendAssLineRec@1210190006 : Record 11012293;
      VendAssCritRec@1210190007 : Record 11012296;
      TmpProjVendRec@1210190003 : TEMPORARY Record 11012005;
      AssDate@1210190000 : Date;
      Text000@1210190001 : TextConst 'ENU=Assessment Date must be filled';
      Text001@1210190004 : TextConst 'ENU=There must be selected one project.';
      AssessmentBasedOn@1210190008 : 'Quote,Order,Both';
      SubcontractingOnly@1100525000 : Boolean;
      MinimumAmount@1100525001 : Decimal;
      TmpVendRec@1100525002 : TEMPORARY Record 23;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

