OBJECT Report 11012320 Update Discount Terms (Sales)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Update Discount Terms (Sales);
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 5431;    ;DataItem;                    ;
               DataItemTable=Table11012312;
               DataItemTableView=ORDER(Descending);
               OnPreDataItem=BEGIN
                               status.OPEN(Text001);
                               LastUpdateTime := TIME;
                               NoOfLines := COUNT;
                               StatusCounter := 0;
                             END;

               OnAfterGetRecord=VAR
                                  DiscPerc@1100525000 : Decimal;
                                BEGIN
                                  StatusCounter := StatusCounter+1;

                                  IF (TIME > LastUpdateTime + 1000) THEN BEGIN
                                    status.UPDATE(1,ROUND(StatusCounter/NoOfLines*10000,1));
                                    LastUpdateTime := TIME;
                                  END;

                                  IF "Purchase Price" <> 0 THEN BEGIN
                                    IF DiscTermPresentByGroup(DiscPerc) THEN BEGIN
                                      IF UpdateByDiscountGroup = UpdateByDiscountGroup::Discount THEN  //db, 01-12-14: C013443
                                        UpdateDiscount(DiffPerc1,DiscPerc)
                                      ELSE
                                        UpdatePrice(DiscPerc);
                                    END ELSE BEGIN
                                      UpdatePrice(DiffPerc2);
                                    END;
                                  END ELSE BEGIN
                                    CalcPurchDisc := GetPurchaseDiscount;  //C019636
                                    IF (CalcPurchDisc >= LowerLimit) AND (CalcPurchDisc <= UpperLimit) THEN  //C019636
                                      UpdateDiscount(DiffPerc1,0);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                status.CLOSE;
                              END;

               ReqFilterFields=Discount Level,Discount Reference,Vendor,Starting Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF DiffPerc1 = 0 THEN
                     DiscMethod1 := DiscMethod1::Decrease;

                   IF DiffPerc2 = 0 THEN
                     DiscMethod2 := DiscMethod2::Increase;

                   IF UpperLimit = 0 THEN
                     UpperLimit := 100;
                 END;

    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  CaptionML=ENU=Update Discount Percentage;
                  SourceExpr=UpdateDiscPerc;
                  Style=Strong;
                  StyleExpr=TRUE;
                  OnValidate=BEGIN
                               IF UpdateDiscPerc = FALSE THEN BEGIN
                                 DiffPerc1 := 0;
                                 RoundPerc := 0;
                               END;
                             END;
                              }

      { 1100525003;2;Field  ;
                  CaptionML=ENU=Discount Method;
                  OptionCaptionML=ENU=Replace,Subtract,Decrease Relative;
                  SourceExpr=DiscMethod1;
                  Editable=UpdateDiscPerc }

      { 1100525004;2;Field  ;
                  CaptionML=ENU=Discount Difference (Purchase-Sales);
                  SourceExpr=DiffPerc1;
                  Editable=UpdateDiscPerc }

      { 1100525005;2;Field  ;
                  CaptionML=ENU=Round Percentage;
                  SourceExpr=RoundPerc;
                  Editable=UpdateDiscPerc }

      { 1100525010;2;Field  ;
                  CaptionML=ENU=Purchase Discount Percentage Filter (Lower Limit);
                  SourceExpr=LowerLimit;
                  Editable=UpdateDiscPerc }

      { 1100525011;2;Field  ;
                  CaptionML=ENU=Purchase Discount Percentage Filter (Upper Limit);
                  SourceExpr=UpperLimit;
                  Editable=UpdateDiscPerc }

      { 1100525006;2;Field  ;
                  CaptionML=ENU=Update Price Agreement;
                  SourceExpr=UpdatePriceAgreement;
                  Style=Strong;
                  StyleExpr=TRUE;
                  OnValidate=BEGIN
                               IF UpdatePriceAgreement = FALSE THEN BEGIN
                                 DiffPerc2 := 0;
                                 RoundAmnt := 0;
                               END;
                             END;
                              }

      { 1100525007;2;Field  ;
                  CaptionML=ENU=Discount Method;
                  OptionCaptionML=ENU=Copy Purchase Price,,Increase Relative;
                  SourceExpr=DiscMethod2;
                  Editable=UpdatePriceAgreement }

      { 1100525008;2;Field  ;
                  CaptionML=ENU=Discount Difference (Purchase-Sales);
                  SourceExpr=DiffPerc2;
                  Editable=UpdatePriceAgreement }

      { 1100525009;2;Field  ;
                  CaptionML=ENU=Round Amount;
                  SourceExpr=RoundAmnt;
                  Editable=UpdatePriceAgreement }

      { 1100409000;2;Field  ;
                  CaptionML=ENU=Update By Discount Group based on;
                  OptionCaptionML=ENU=" ,Price,Discount";
                  SourceExpr=UpdateByDiscountGroup }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      DiffPerc1@1100485002 : Decimal;
      DiffPerc2@1100525008 : Decimal;
      PurchDisc@1100525000 : Decimal;
      SalesDisc@1100525002 : Decimal;
      PurchPrice@1100525003 : Decimal;
      SalesPrice@1100525004 : Decimal;
      RoundAmnt@1100525005 : Decimal;
      RoundPerc@1100525006 : Decimal;
      DiscMethod1@1100525001 : 'Replace,Subtract,Decrease';
      DiscMethod2@1100525007 : 'Replace,Add,Increase';
      status@1100485006 : Dialog;
      NoOfLines@1100485005 : Integer;
      StatusCounter@1100485004 : Integer;
      LastUpdateTime@1100485003 : Time;
      Text001@1100485007 : TextConst 'ENU=Update Discount @1@@@@@@@@@@@@@@@@@@';
      UpdateDiscPerc@1100525011 : Boolean INDATASET;
      UpdatePriceAgreement@1100525010 : Boolean INDATASET;
      UpdateByDiscountGroup@1100409000 : ' ,Price,Discount';
      LowerLimit@1100525013 : Decimal;
      UpperLimit@1100525012 : Decimal;
      CalcPurchDisc@1100525009 : Decimal;

    PROCEDURE DiscTermPresentByGroup@1100525000(VAR DiscPerc@1100525002 : Decimal) : Boolean;
    VAR
      lvTradeItemRec@1100525000 : Record 11012317;
      lvDiscRec@1100525001 : Record 11012312;
    BEGIN
      IF UpdateByDiscountGroup = UpdateByDiscountGroup::" " THEN EXIT(FALSE);  //db, 01-12-14: C013443

      WITH "Discount Term History" DO BEGIN
        DiscPerc := 0;
        IF "Item Code" = '' THEN EXIT(FALSE);
        IF NOT lvTradeItemRec.GET(Vendor, "Item Code") THEN EXIT (FALSE);

        lvDiscRec.SETRANGE(Vendor, Vendor);
        lvDiscRec.SETRANGE("Discount Group", lvTradeItemRec."Discount Group Code");
        lvDiscRec.SETFILTER("Item Code", '%1', '');
        lvDiscRec.SETRANGE("Discount Level", "Discount Level");
        lvDiscRec.SETRANGE("Discount Reference", "Discount Reference");
        lvDiscRec.SETFILTER("Starting Date", '..%1', "Starting Date");
        IF lvDiscRec.FINDLAST THEN BEGIN
          DiscPerc := lvDiscRec."Sales Discount Percentage";
          EXIT(TRUE);
        END;
      END;

      EXIT(FALSE);
    END;

    PROCEDURE UpdateDiscount@1100525004(DiffPerc@1100525000 : Decimal;BasePerc@1100409000 : Decimal);
    BEGIN
      IF UpdateDiscPerc = FALSE THEN
        IF BasePerc = 0 THEN  //db, 01-12-14: C013443
          EXIT;

      WITH "Discount Term History" DO BEGIN
        IF BasePerc <> 0 THEN  //db, 01-12-14: C013443
          PurchDisc := BasePerc
        ELSE
          PurchDisc := GetPurchaseDiscount;
        CASE DiscMethod1 OF
          DiscMethod1::Replace:
            SalesDisc := DiffPerc;
          DiscMethod1::Subtract:
            SalesDisc := PurchDisc - DiffPerc;
          DiscMethod1::Decrease:
            SalesDisc := PurchDisc * (100-DiffPerc)/100;
        END;
        IF SalesDisc < 0 THEN SalesDisc := 0;
        IF SalesDisc > 100 THEN SalesDisc := 100;
        IF RoundPerc > 0 THEN
          "Sales Discount Percentage" := ROUND(SalesDisc, RoundPerc, '>')
        ELSE
          "Sales Discount Percentage" := SalesDisc;
        MODIFY(TRUE);  //db, 24-03-10 (check Central Mgt)
      END;
    END;

    PROCEDURE UpdatePrice@1100525003(DiffPerc@1100525000 : Decimal);
    VAR
      lvTradeItemRec@1100525001 : Record 11012317;
    BEGIN
      IF UpdatePriceAgreement = FALSE THEN EXIT;  //db, 14-01-10

      WITH "Discount Term History" DO BEGIN
        PurchPrice := "Purchase Price";
        CASE DiscMethod2 OF
          DiscMethod2::Replace:
            SalesPrice := PurchPrice;
          DiscMethod2::Add,
          DiscMethod2::Increase:
            BEGIN
              IF UpdateByDiscountGroup <> UpdateByDiscountGroup::" " THEN BEGIN  //db, 01-12-14: C013443
                IF lvTradeItemRec.GET(Vendor, "Item Code") THEN BEGIN
                  lvTradeItemRec.GetActualPrice(TODAY, '', '');
                  PurchPrice := lvTradeItemRec."Gross Price";
                  DiffPerc := -DiffPerc;
                END;
              END;
              SalesPrice := PurchPrice * (100+DiffPerc)/100;
            END;
        END;
        IF SalesPrice < 0 THEN SalesPrice := 0;
        IF RoundAmnt > 0 THEN
          "Sales Price" := ROUND(SalesPrice, RoundAmnt, '>')
        ELSE
          "Sales Price" := ROUND(SalesPrice);
        MODIFY(TRUE);  //db, 24-03-10 (check Central Mgt)
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

