OBJECT Report 11012347 Compress Discount Term History
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Compress Discount Term History;
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   MESSAGE(Text001, NumProcessed1+NumProcessed2, NumDeleted1+NumDeleted2);  //db, 17-04-13: C006604
                 END;

  }
  DATASET
  {
    { 5431;    ;DataItem;DiscountHistory1    ;
               DataItemTable=Table11012312;
               DataItemTableView=SORTING(Discount Level,Discount Reference,Vendor,Discount Group,Item Code,Starting Date)
                                 ORDER(Ascending);
               OnPreDataItem=BEGIN
                               BeforeDiscountHistory1;
                             END;

               OnAfterGetRecord=BEGIN
                                  ProcessDiscountHistory1;
                                END;

               OnPostDataItem=BEGIN
                                AfterDiscountHistory1;
                              END;

               ReqFilterFields=Discount Level,Vendor,Discount Group,Item Code }

    { 1100525000;;DataItem;DiscountHistory2  ;
               DataItemTable=Table11012312;
               DataItemTableView=SORTING(Discount Level,Discount Reference,Vendor,Discount Group,Item Code,Starting Date)
                                 ORDER(Ascending);
               OnPreDataItem=BEGIN
                               BeforeDiscountHistory2;
                             END;

               OnAfterGetRecord=BEGIN
                                  ProcessDiscountHistory2;
                                END;

               OnPostDataItem=BEGIN
                                AfterDiscountHistory2;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PrevDiscountHistory@1210190017 : Record 11012312;
      NoOfRecs@1210190002 : Integer;
      Counter@1210190001 : Integer;
      NoOfDeletes@1100409001 : Integer;
      Window@1210190000 : Dialog;
      Text000@1210190003 : TextConst 'ENU=Compress Discount Term History (without Ending Date)... @1@@@@@@@@@@@@@';
      PrevVendor@1210190015 : Code[15];
      PrevDiscountGroup@1210190014 : Code[35];
      PrevItemCode@1210190013 : Code[20];
      PrevDiscountPercentage1@1210190012 : Decimal;
      PrevDiscountPercentage2@1210190011 : Decimal;
      PrevDiscountPercentage3@1210190010 : Decimal;
      PrevPurchasePrice@1210190009 : Decimal;
      PrevCurrency@1210190008 : Code[10];
      PrevSalesDiscountGroup@1210190007 : Code[10];
      PrevMaximumPriceDifference@1210190006 : Decimal;
      PrevSalesDiscountPercentage@1210190005 : Decimal;
      PrevSalesPrice@1210190004 : Decimal;
      Text001@1100409000 : TextConst 'ENU=Checked: %1\\Deleted: %2';
      NumProcessed1@1100525000 : Integer;
      NumProcessed2@1100525001 : Integer;
      NumDeleted1@1100525002 : Integer;
      NumDeleted2@1100525003 : Integer;
      EmptyDate@1100525004 : Date;
      Text002@1100525005 : TextConst 'ENU=Compress Discount Term History (with Ending Date)... @1@@@@@@@@@@@@@';

    PROCEDURE BeforeDiscountHistory1@1100525000();
    BEGIN
      DiscountHistory1.SETRANGE("Ending Date", 0D);
      NoOfRecs := DiscountHistory1.COUNT;
      Counter:= 0;
      NoOfDeletes := 0;
      Window.OPEN(Text000);  //db, 17-04-13: C006604

      PrevDiscountHistory.INIT;
      CLEAR(PrevDiscountHistory);
    END;

    PROCEDURE ProcessDiscountHistory1@1100525001();
    BEGIN
      Counter += 1;
      Window.UPDATE(1, ROUND(Counter / NoOfRecs * 10000, 1));

      WITH DiscountHistory1 DO BEGIN
        IF (PrevDiscountHistory.Vendor                       = Vendor                      ) AND
           (PrevDiscountHistory."Discount Group"             = "Discount Group"            ) AND
           (PrevDiscountHistory."Item Code"                  = "Item Code"                 ) AND
           (PrevDiscountHistory."Discount Level"             = "Discount Level"            ) AND
           (PrevDiscountHistory."Discount Reference"         = "Discount Reference"        ) AND
           (PrevDiscountHistory."Discount Percentage 1"      = "Discount Percentage 1"     ) AND
           (PrevDiscountHistory."Discount Percentage 2"      = "Discount Percentage 2"     ) AND
           (PrevDiscountHistory."Discount Percentage 3"      = "Discount Percentage 3"     ) AND
           (PrevDiscountHistory."Purchase Price"             = "Purchase Price"            ) AND
           (PrevDiscountHistory.Currency                     = Currency                    ) AND
           (PrevDiscountHistory."Sales Discount Group"       = "Sales Discount Group"      ) AND
           (PrevDiscountHistory."Maximum % Price Difference" = "Maximum % Price Difference") AND
           (PrevDiscountHistory."Sales Discount Percentage"  = "Sales Discount Percentage" ) AND
           (PrevDiscountHistory."Sales Price"                = "Sales Price"               )
        THEN BEGIN
          DELETE(TRUE);
          NoOfDeletes += 1;
        END ELSE BEGIN
          PrevDiscountHistory.COPY(DiscountHistory1);
        END;
      END;
    END;

    PROCEDURE AfterDiscountHistory1@1100525002();
    BEGIN
      Window.CLOSE;

      NumProcessed1 := Counter;
      NumDeleted1 :=  NoOfDeletes;
    END;

    PROCEDURE BeforeDiscountHistory2@1100525004();
    BEGIN
      DiscountHistory2.COPY(DiscountHistory1);
      DiscountHistory2.SETFILTER("Ending Date", '<>%1', 0D);
      NoOfRecs := DiscountHistory2.COUNT;
      Counter:=0;
      NoOfDeletes := 0;
      Window.OPEN(Text002);  //db, 17-04-13: C006604

      PrevDiscountHistory.INIT;
      CLEAR(PrevDiscountHistory);
    END;

    PROCEDURE ProcessDiscountHistory2@1100525005();
    VAR
      lvCompress@1100525000 : Boolean;
    BEGIN
      Counter += 1;
      Window.UPDATE(1, ROUND(Counter / NoOfRecs * 10000, 1));

      WITH DiscountHistory2 DO BEGIN
        IF (PrevDiscountHistory.Vendor                       = Vendor                      ) AND
           (PrevDiscountHistory."Discount Group"             = "Discount Group"            ) AND
           (PrevDiscountHistory."Item Code"                  = "Item Code"                 ) AND
           (PrevDiscountHistory."Discount Level"             = "Discount Level"            ) AND
           (PrevDiscountHistory."Discount Reference"         = "Discount Reference"        ) AND
           (PrevDiscountHistory."Discount Percentage 1"      = "Discount Percentage 1"     ) AND
           (PrevDiscountHistory."Discount Percentage 2"      = "Discount Percentage 2"     ) AND
           (PrevDiscountHistory."Discount Percentage 3"      = "Discount Percentage 3"     ) AND
           (PrevDiscountHistory."Purchase Price"             = "Purchase Price"            ) AND
           (PrevDiscountHistory.Currency                     = Currency                    ) AND
           (PrevDiscountHistory."Sales Discount Group"       = "Sales Discount Group"      ) AND
           (PrevDiscountHistory."Ending Date"               <> 0D                          ) AND
           (PrevDiscountHistory."Maximum % Price Difference" = "Maximum % Price Difference") AND
           (PrevDiscountHistory."Sales Discount Percentage"  = "Sales Discount Percentage" ) AND
           (PrevDiscountHistory."Sales Price"                = "Sales Price"               )
        THEN BEGIN
          //db.sn, 10-06-13: C006604
          lvCompress := FALSE;
          IF ("Starting Date" >= PrevDiscountHistory."Starting Date") AND
             ("Starting Date" <= PrevDiscountHistory."Ending Date") AND
             (PrevDiscountHistory."Ending Date" <= "Ending Date") THEN
            lvCompress := TRUE;
          IF ("Starting Date" = PrevDiscountHistory."Ending Date") OR
             ("Starting Date" = PrevDiscountHistory."Ending Date" + 1) THEN
            lvCompress := TRUE;
          //db.en, 10-06-13: C006604
          IF lvCompress = TRUE THEN BEGIN
            PrevDiscountHistory.GET(Vendor, "Discount Group", "Item Code",
              "Discount Level", "Discount Reference",
              PrevDiscountHistory."Starting Date");
            PrevDiscountHistory.VALIDATE("Ending Date", "Ending Date");
            PrevDiscountHistory.MODIFY(TRUE);

            DELETE(TRUE);
            NoOfDeletes += 1;
          END ELSE BEGIN
            PrevDiscountHistory.COPY(DiscountHistory2);
          END;
        END ELSE BEGIN
          PrevDiscountHistory.COPY(DiscountHistory2);
        END;
      END;
    END;

    PROCEDURE AfterDiscountHistory2@1100525006();
    BEGIN
      Window.CLOSE;

      NumProcessed2 := Counter;
      NumDeleted2 :=  NoOfDeletes;
    END;

    BEGIN
    {
      db, 17-04-13: redesign report (C006604)
      - split selection due to check on ending date
    }
    END.
  }
  RDLDATA
  {
  }
}

