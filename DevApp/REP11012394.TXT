OBJECT Report 11012394 Upd. Exp. Installm. Inv. Dates
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    Permissions=TableData 11012018=rm;
    CaptionML=ENU=Update Exp. Installmt. Invoice Dates;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 5136;    ;DataItem;                    ;
               DataItemTable=Table11012018;
               DataItemTableView=SORTING(Project No.,Principal);
               OnPreDataItem=BEGIN
                               TestForValidDateFormula(DateFormula);
                               TestForOnlyOnePrincipal("Project Installment");

                               IgnoreInvoicedInstallments("Project Installment");
                               IgnoreEmptyExpInvoiceDates("Project Installment");

                               IF ISEMPTY THEN
                                 ERROR(Text002);
                             END;

               OnAfterGetRecord=VAR
                                  ProjectInstallment@1210190000 : Record 11012018;
                                BEGIN
                                  UpdateExpectedInvoiceDate("Project Installment", DateFormula);
                                END;

               ReqFilterFields=Project No.,Principal,Installment No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      CaptionML=ENU=Date Formula;
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  Name=Date Formula;
                  CaptionML=ENU=Date Formula;
                  SourceExpr=DateFormula }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      DateFormula@1210190000 : DateFormula;
      Text000@1210190001 : TextConst 'ENU=Please specify a date formula.';
      Text001@1210190002 : TextConst 'ENU=You cannot update the expected invoice dates for more than one principal at a time.';
      Text002@1210190003 : TextConst 'ENU=No installments were updated.';

    LOCAL PROCEDURE TestForValidDateFormula@1210190002(DateFormula@1210190000 : DateFormula);
    BEGIN
      IF FORMAT(DateFormula) = '' THEN
        ERROR(Text000);
    END;

    LOCAL PROCEDURE TestForOnlyOnePrincipal@1210190006(VAR ProjectInstallment@1210190000 : Record 11012018);
    BEGIN
      CASE TRUE OF
        ProjectInstallment.GETFILTER("Project No.") = '':
          ERROR(Text001);
        ProjectInstallment.GETRANGEMIN("Project No.") <> ProjectInstallment.GETRANGEMAX("Project No."):
          ERROR(Text001);
        ProjectInstallment.GETFILTER(Principal) = '':
          ERROR(Text001);
        ProjectInstallment.GETRANGEMIN(Principal) <> ProjectInstallment.GETRANGEMAX(Principal):
          ERROR(Text001);
      END;
    END;

    LOCAL PROCEDURE IgnoreInvoicedInstallments@1210190000(VAR ProjectInstallment@1210190000 : Record 11012018);
    BEGIN
      ProjectInstallment.SETRANGE("Invoice in Process", 0);
      ProjectInstallment.SETRANGE("Credit Memo in Process", 0);
      ProjectInstallment.SETRANGE("Invoiced Price", 0);
    END;

    LOCAL PROCEDURE IgnoreEmptyExpInvoiceDates@1210190001(VAR ProjectInstallment@1210190000 : Record 11012018);
    BEGIN
      ProjectInstallment.SETFILTER(ProjectInstallment."Expected Invoice Date", '<>%1', 0D);
    END;

    LOCAL PROCEDURE UpdateExpectedInvoiceDate@1210190010(ProjectInstallment@1210190000 : Record 11012018;DateFormula@1210190001 : DateFormula);
    BEGIN
      ProjectInstallment."Expected Invoice Date" := CALCDATE(DateFormula, ProjectInstallment."Expected Invoice Date");
      ProjectInstallment.MODIFY(TRUE);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

