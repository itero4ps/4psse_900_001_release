OBJECT Report 11012400 Frost Out Registration
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Frost Out Registration;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  CompanyInformation.GET;
                  HumanResourcesSetup.GET;
                  Now := CURRENTDATETIME;
                END;

  }
  DATASET
  {
    { 2460;    ;DataItem;                    ;
               DataItemTable=Table11020670;
               DataItemTableView=SORTING(Register Date,Register Time,Reason Code,Post Code);
               OnAfterGetRecord=VAR
                                  RevDate@1210190000 : Code[20];
                                  RecRef@1100528700 : RecordRef;
                                  RIDTableLineNo@1100528701 : Integer;
                                BEGIN
                                  RevDate := FORMAT("Register Date", 0, '<Year4><Filler Character,0><Month,2><Day,2>') +
                                    FORMAT("Register Time", 0, '<Hours24,2><Filler Character,0><Minutes,2>');

                                  GenericTempTable.GetGenericTempTable(RevDate,
                                    FORMAT("Register Date"),
                                    FORMAT("Register Time", 0, '<Hours24,2><Filler Character,0>:<Minutes,2>'),
                                    "Reason Code", "Post Code", '');

                                  GenericTempTable.Text1 := Address;
                                  GenericTempTable.Int1 += 1;
                                  GenericTempTable.MODIFY;

                                  RIDTableLineNo := RIDTableLineNo + 1;

                                  RIDTempTable.GetGenericTempTable(
                                    GenericTempTable."Key Code1",
                                    GenericTempTable."Key Code2",
                                    GenericTempTable."Key Code3",
                                    GenericTempTable."Key Code4",
                                    GenericTempTable."Key Code5",
                                    FORMAT(RIDTableLineNo));

                                  RecRef.GETTABLE("Frost Out Registration");
                                  RIDTempTable.RID1 := RecRef.RECORDID;
                                  RIDTempTable.MODIFY;

                                  "Print Date Time" := Now;
                                  "Printed by" := USERID;
                                  MODIFY(TRUE);
                                END;

               ReqFilterFields=Register Date }

    { 5444;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               OnPreDataItem=BEGIN
                               RecCount := GenericTempTable.COUNT;
                               DocCount := 1;

                               EmployeeCountTotal := 0;
                               FileName := STRSUBSTNO('%1%2-%3', HumanResourcesSetup."Frost Out Output Directory",
                                 "Frost Out Registration".TABLECAPTION,
                                 FORMAT(Now, 0, '<year4><Month,2><Day,2>-<hours24,2><minutes,2><seconds,2>'));

                               HumanResourcesSetup.TESTFIELD("Registration Template");
                             END;

               OnAfterGetRecord=VAR
                                  FileName2@1100528700 : Text[250];
                                BEGIN
                                  IF (Integer.Number < 0) THEN BEGIN
                                    CreateNewDoc;
                                    Integer.Number := 1;
                                    GenericTempTable.RESET;
                                    GenericTempTable.FINDFIRST;
                                    CreateDocumentFlag := TRUE;
                                  END ELSE
                                    GenericTempTable.NEXT;

                                  IF CreateDocumentFlag THEN BEGIN
                                    CreateDocument(GenericTempTable."Key Code2", DocumentNo);
                                    CreateDocumentFlag := FALSE;
                                  END;

                                  CreateDocumentLinks(DocumentNo);

                                  EmployeeCountTotal += GenericTempTable.Int1;

                                  IF (Integer.Number MOD 9 = 1) THEN
                                    FillHeaderBookmarks;

                                  FillLineBookmarks;

                                  IF (Integer.Number = RecCount) OR (Integer.Number MOD 9 = 0) THEN BEGIN
                                    WordAutomation.FillBookmark('totAantalWerknemers', STRSUBSTNO('%1', EmployeeCountTotal),
                                      FALSE, FALSE, FALSE, FALSE);
                                    EmployeeCountTotal := 0;
                                    CloseDoc(FileName2);
                                    UpdateDocumentFileName(DocumentNo, FileName2);
                                    IF (Integer.Number < RecCount) THEN BEGIN
                                      CreateNewDoc;
                                      CreateDocumentFlag := TRUE;
                                    END;
                                  END;

                                  IF (Integer.Number >= RecCount) THEN
                                    CurrReport.BREAK;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CompanyInformation@1210190000 : Record 79;
      HumanResourcesSetup@1100528300 : Record 5218;
      GenericTempTable@1210190003 : TEMPORARY Record 11020579;
      RIDTempTable@1100528701 : TEMPORARY Record 11020579;
      RecCount@1210190001 : Integer;
      EmployeeCountTotal@1100528302 : Integer;
      DocCount@1210190007 : Integer;
      FileName@1100528303 : Text[250];
      WordAutomation@1210190002 : Codeunit 11012400;
      GranulePermissionManagment@1100528702 : Codeunit 11012279;
      Now@1210190008 : DateTime;
      CreateDocumentFlag@1100528703 : Boolean;
      DocumentNo@1100528700 : Code[20];

    LOCAL PROCEDURE FillHeaderBookmarks@1210190002();
    BEGIN
      WordAutomation.FillBookmark('bedrijfsnaam', CompanyInformation.Name, FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('loonheffingennr', CompanyInformation."Income Tax No.", FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('aansluitingsnummer', CompanyInformation."Affiliation No.", FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('adres', CompanyInformation.Address, FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('postcode', CompanyInformation."Post Code", FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('vestigingsplaats', CompanyInformation.City, FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('naamContactpersoon', HumanResourcesSetup."Frost Out Contact",
        FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('telefoonnummer', HumanResourcesSetup."Contact Phone No.", FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('verzenddatum', STRSUBSTNO('%1', TODAY), FALSE, FALSE, FALSE, FALSE);
    END;

    LOCAL PROCEDURE FillLineBookmarks@1210190005();
    VAR
      LineCount@1210190000 : Integer;
    BEGIN
      LineCount := Integer.Number MOD 9;
      IF (LineCount = 0) THEN
        LineCount:=9;

      WordAutomation.FillBookmark(STRSUBSTNO('ingangsdatumVerzuim%1', LineCount),
        GenericTempTable."Key Code2", FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark(STRSUBSTNO('tijdstipVerzuim%1', LineCount),
        GenericTempTable."Key Code3", FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark(STRSUBSTNO('redenVerzuim%1', LineCount),
        GenericTempTable."Key Code4", FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark(STRSUBSTNO('aantalWerknemers%1', LineCount),
        STRSUBSTNO('%1', GenericTempTable.Int1), FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark(STRSUBSTNO('naamWerkobject%1', LineCount),
        GenericTempTable.Text1, FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark(STRSUBSTNO('postcode%1', LineCount),
        GenericTempTable."Key Code5", FALSE, FALSE, FALSE, FALSE);
    END;

    LOCAL PROCEDURE CloseDoc@1210190003(VAR FileName2@1100528702 : Text[250]);
    BEGIN
      FileName2 := STRSUBSTNO('%1-%2', FileName, DocCount);
      WordAutomation.SaveAs(FileName2);
      WordAutomation.Close;
      WordAutomation.Terminate;
      DocCount += 1;
    END;

    LOCAL PROCEDURE CreateNewDoc@1210190004();
    BEGIN
      WordAutomation.Initialise;
      WordAutomation.Open(TRUE, HumanResourcesSetup."Registration Template", FALSE);
    END;

    LOCAL PROCEDURE CreateDocument@1100528700(RegisterDate@1100528700 : Code[20];VAR DocumentNo@1100528702 : Code[20]);
    VAR
      DocumentProperties@1100528703 : Record 11012746;
      FrostOutRegistration@1100528708 : Record 11020670;
    BEGIN
      IF NOT GranulePermissionManagment.PermissionDocumentMgtExtra THEN
        EXIT;

      DocumentNo := '';

      DocumentProperties.INIT;
      DocumentProperties.Description := STRSUBSTNO('%1 %2', FrostOutRegistration.TABLECAPTION, RegisterDate);
      DocumentProperties."External Document" := FALSE;
      DocumentProperties.INSERT(TRUE);

      DocumentNo := DocumentProperties."No.";
    END;

    LOCAL PROCEDURE CreateDocumentLinks@1100528702(DocumentNo@1100528703 : Code[20]);
    VAR
      DocumentLinkManagement@1100528702 : Codeunit 11012401;
      DocumentProperties@1100528701 : Record 11012746;
      DocumentLinkRID@1100528700 : ARRAY [5] OF RecordID;
    BEGIN
      IF NOT GranulePermissionManagment.PermissionDocumentMgtExtra THEN
        EXIT;

      DocumentProperties.GET(DocumentNo);

      RIDTempTable.SETRANGE("Key Code1", GenericTempTable."Key Code1");
      RIDTempTable.SETRANGE("Key Code2", GenericTempTable."Key Code2");
      RIDTempTable.SETRANGE("Key Code3", GenericTempTable."Key Code3");
      RIDTempTable.SETRANGE("Key Code4", GenericTempTable."Key Code4");
      RIDTempTable.SETRANGE("Key Code5", GenericTempTable."Key Code5");
      IF RIDTempTable.FINDSET THEN
        REPEAT
          CLEAR(DocumentLinkRID);
          DocumentLinkRID[2] := RIDTempTable.RID1;
          DocumentLinkManagement.CreateDocumentLink(DocumentProperties, DocumentLinkRID);
        UNTIL RIDTempTable.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateDocumentFileName@1100528701(DocumentNo@1100528700 : Code[20];FileName@1100528701 : Text[250]);
    VAR
      DocumentProperties@1100528702 : Record 11012746;
      GranuleDocumentMgtExtra@1100528703 : Codeunit 11012433;
    BEGIN
      IF NOT GranulePermissionManagment.PermissionDocumentMgtExtra THEN
        EXIT;

      DocumentProperties.GET(DocumentNo);
      DocumentProperties.VALIDATE(File, FileName);
      DocumentProperties.MODIFY;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

