OBJECT Report 11012563 Bulk Edit Rental Free Periods
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Change Calendars collectively;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF (DateFrom = 0D) OR (DateTo = 0D) THEN
                    ERROR(Text000);

                  IF ((DateTo - DateFrom + 1) > 1000) THEN
                    ERROR(Text002 + Text003);

                  IF (NOT ModifySaturday) AND
                     (NOT ModifySunday) AND
                     (NOT ModifyOtherDays)
                  THEN
                    ERROR(Text001);

                  IF ProcessType <> ProcessType::Modify THEN
                    ValidateRentalCalendarCopyFrom();

                  Counter := 0;
                  PeriodNoFilterTxt := '';

                  IF ModifyOtherDays THEN
                    PeriodNoFilterTxt := '1|2|3|4|5';
                  IF ModifySaturday THEN BEGIN
                    IF PeriodNoFilterTxt <> '' THEN
                      PeriodNoFilterTxt := PeriodNoFilterTxt + '|';
                    PeriodNoFilterTxt := PeriodNoFilterTxt + '6';
                  END;
                  IF ModifySunday THEN BEGIN
                    IF PeriodNoFilterTxt <> '' THEN
                      PeriodNoFilterTxt := PeriodNoFilterTxt + '|';
                    PeriodNoFilterTxt := PeriodNoFilterTxt + '7';
                  END;
                END;

    OnPostReport=BEGIN
                   IF Counter = 0 THEN
                     MESSAGE(Text004)
                   ELSE
                     MESSAGE(Text005,Counter);
                 END;

  }
  DATASET
  {
    { 3584;    ;DataItem;                    ;
               DataItemTable=Table11012568;
               DataItemTableView=SORTING(Type,Territory Code,Plant Calendar);
               ReqFilterFields=Type,Territory Code,Plant Calendar }

    { 9857;1   ;DataItem;                    ;
               DataItemTable=Table2000000007;
               DataItemTableView=SORTING(Period Type,Period Start)
                                 WHERE(Period Type=CONST(Date));
               OnPreDataItem=BEGIN
                               Date.SETRANGE("Period Start", DateFrom, DateTo);
                               Date.SETFILTER("Period No.", PeriodNoFilterTxt);
                             END;

               OnAfterGetRecord=VAR
                                  RentalCalDayFrom@1100525000 : Record 11012569;
                                BEGIN
                                  IF ProcessType = ProcessType::Modify THEN BEGIN
                                    CASE Date."Period No." OF
                                      1,2,3,4,5:
                                        ChangeRentalCalendarDay(OtherDaysNewValue);
                                      6:
                                        ChangeRentalCalendarDay(SaturdayNewValue);
                                      7:
                                        ChangeRentalCalendarDay(SundayNewValue);
                                    END;
                                  END ELSE BEGIN
                                    IF RentalCalDayFrom.GET(CalendarTypeFrom, TerritoryCodeFrom, PlantCalendarFrom, "Period Start") THEN
                                      ChangeRentalCalendarDay(2)  //* 1=Rental Free Day
                                    ELSE BEGIN
                                      IF ProcessType = ProcessType::CopyAll THEN
                                        ChangeRentalCalendarDay(1)  //* 0=Rental Day
                                    END;
                                  END;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      RefreshOnActivate=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1100485000;2;Field  ;
                  CaptionML=ENU=Date From;
                  SourceExpr=DateFrom;
                  OnValidate=BEGIN
                               IF (DateTo <> 0D) AND (DateTo < DateFrom) THEN
                                 DateTo := DateFrom;
                             END;
                              }

      { 1100485002;2;Field  ;
                  CaptionML=ENU=Date To;
                  SourceExpr=DateTo;
                  OnValidate=BEGIN
                               IF (DateTo <> 0D) AND (DateTo < DateFrom) THEN
                                 ERROR(Text006);
                             END;
                              }

      { 1210190005;2;Field  ;
                  CaptionML=ENU=Process Type;
                  OptionCaptionML=ENU=Modify,Copy All,Copy Rental Free;
                  SourceExpr=ProcessType;
                  OnValidate=BEGIN
                               SetEnabled();
                               RequestOptionsPage.UPDATE(FALSE);
                             END;
                              }

      { 1210190000;1;Group  ;
                  CaptionML=ENU=New Values;
                  GroupType=Group }

      { 7   ;2   ;Field     ;
                  CaptionML=ENU=Saturday;
                  OptionCaptionML=ENU=Don't Change, ,Rental Free;
                  SourceExpr=SaturdayNewValue }

      { 8   ;2   ;Field     ;
                  CaptionML=ENU=Sunday;
                  OptionCaptionML=ENU=Don't Change, ,Rental Free;
                  SourceExpr=SundayNewValue }

      { 9   ;2   ;Field     ;
                  CaptionML=ENU=Other Days;
                  OptionCaptionML=ENU=Don't Change, ,Rental Free;
                  SourceExpr=OtherDaysNewValue }

      { 1210190001;1;Group  ;
                  CaptionML=ENU=Copy from;
                  GroupType=Group }

      { 1210190002;2;Field  ;
                  CaptionML=ENU=Type;
                  OptionCaptionML=ENU=Standard,Region,Plant;
                  SourceExpr=CalendarTypeFrom;
                  OnValidate=BEGIN
                               IF CalendarTypeFrom <> CalendarTypeFrom::Region THEN
                                 TerritoryCodeFrom := '';
                               IF CalendarTypeFrom <> CalendarTypeFrom::Plant THEN
                                 PlantCalendarFrom := '';

                               SetEnabled();
                               RequestOptionsPage.UPDATE(FALSE);
                             END;
                              }

      { 1210190003;2;Field  ;
                  CaptionML=ENU=Territory Code;
                  SourceExpr=TerritoryCodeFrom;
                  TableRelation=Territory;
                  OnValidate=BEGIN
                               IF TerritoryCodeFrom <> '' THEN
                                 ValidateRentalCalendarCopyFrom();
                             END;
                              }

      { 1210190004;2;Field  ;
                  CaptionML=ENU=Plant Calendar;
                  SourceExpr=PlantCalendarFrom;
                  TableRelation="Plant Calendar";
                  OnValidate=BEGIN
                               IF PlantCalendarFrom <> '' THEN
                                 ValidateRentalCalendarCopyFrom();
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@11012010 : TextConst 'ENU=Date selection (from - to) not filled out';
      Text001@11012011 : TextConst 'ENU=Make a selection first.';
      Text002@11012012 : TextConst 'ENU=To Protect the Database a Range of 1000 Days will be maintained. \';
      Text003@11012013 : TextConst 'ENU=Reduce the Date Selection (From - To)';
      Text004@11012014 : TextConst 'ENU=There are no Calendar Entries created.';
      Text005@11012015 : TextConst 'ENU=There are %1 Calandar Entries created.';
      RentalCalendarDayRec@1100485007 : Record 11012569;
      DateFrom@1100485000 : Date;
      DateTo@1100485001 : Date;
      ProcessType@1100525000 : 'Modify,CopyAll,CopyRentalFree';
      CalendarTypeFrom@1100525001 : 'Standard,Region,Plant';
      ModifySaturday1@1100485006 : Boolean;
      ModifySunday1@1100485005 : Boolean;
      ModifyOtherDays1@1100485004 : Boolean;
      SaturdayNewValue@11012005 : 'Don''t Change, ,Rental Free';
      SundayNewValue@11012006 : 'Don''t Change, ,Rental Free';
      OtherDaysNewValue@11012007 : 'Don''t Change, ,Rental Free';
      Counter@11012009 : Integer;
      Text006@1100485002 : TextConst 'ENU=''Date To'' may not be less than ''Date From''';
      TerritoryCodeFrom@1100525002 : Code[10];
      PlantCalendarFrom@1100525003 : Code[10];
      PeriodNoFilterTxt@1100485003 : Text[20];
      SaturdayNewValueEnabled@1210190000 : Boolean INDATASET;
      SundayNewValueEnabled@1210190001 : Boolean INDATASET;
      RemainingDaysNewValueEnabled@1210190002 : Boolean INDATASET;
      CalendarTypeFromEnabled@1210190003 : Boolean INDATASET;
      TerritoryCodeFromEnabled@1210190004 : Boolean INDATASET;
      PlantCalendarFromEnabled@1210190005 : Boolean INDATASET;

    PROCEDURE SetEnabled@1100525000();
    BEGIN
      SaturdayNewValueEnabled := ((ProcessType = ProcessType::Modify));
      SundayNewValueEnabled := ((ProcessType = ProcessType::Modify));
      RemainingDaysNewValueEnabled := ((ProcessType = ProcessType::Modify));
      CalendarTypeFromEnabled := (ProcessType <> ProcessType::Modify);
      TerritoryCodeFromEnabled := ((ProcessType <> ProcessType::Modify) AND (CalendarTypeFrom = CalendarTypeFrom::Region));
      PlantCalendarFromEnabled := ((ProcessType <> ProcessType::Modify) AND (CalendarTypeFrom = CalendarTypeFrom::Plant));
    END;

    PROCEDURE ValidateRentalCalendarCopyFrom@1100525001();
    VAR
      RentalCalendar@1100525000 : Record 11012568;
    BEGIN
      IF ProcessType = ProcessType::Modify THEN
        EXIT;

      RentalCalendar.GET(CalendarTypeFrom, TerritoryCodeFrom, PlantCalendarFrom);
    END;

    PROCEDURE ChangeRentalCalendarDay@1(RentalFreeValue@11012001 : 'Don''t Change, ,Rental Free');
    BEGIN
      IF RentalCalendarDayRec.GET(
         "Rental Calendar".Type, "Rental Calendar"."Territory Code",
         "Rental Calendar"."Plant Calendar", Date."Period Start")
      THEN BEGIN
        IF RentalFreeValue = RentalFreeValue::" " THEN BEGIN
              RentalCalendarDayRec.DELETE;
              Counter := Counter + 1;
            END;
      END ELSE BEGIN
        IF RentalFreeValue = RentalFreeValue::"Rental Free" THEN BEGIN
          RentalCalendarDayRec.INIT;
          RentalCalendarDayRec.Type := "Rental Calendar".Type;
          RentalCalendarDayRec."Territory Code" := "Rental Calendar"."Territory Code";
          RentalCalendarDayRec."Plant Calendar" := "Rental Calendar"."Plant Calendar";
          RentalCalendarDayRec.Date := Date."Period Start";
          RentalCalendarDayRec.INSERT;
          Counter := Counter + 1;
        END;
      END;
    END;

    PROCEDURE ModifySaturday@1210190004() : Boolean;
    BEGIN
      EXIT(SaturdayNewValue <> SaturdayNewValue::"Don't Change");
    END;

    PROCEDURE ModifySunday@1210190005() : Boolean;
    BEGIN
      EXIT(SundayNewValue <> SundayNewValue::"Don't Change");
    END;

    PROCEDURE ModifyOtherDays@1210190006() : Boolean;
    BEGIN
      EXIT(OtherDaysNewValue <> OtherDaysNewValue::"Don't Change");
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

