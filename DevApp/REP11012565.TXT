OBJECT Report 11012565 Post-Batch Invoice Proposal
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Post-Batch Invoice Proposal;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6640;    ;DataItem;                    ;
               DataItemTable=Table36;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Invoice Proposal),
                                       Plant Invoice=CONST(Yes));
               ReqFilterHeadingML=ENU=Invoice Proposal;
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);
                               CounterTotal := COUNT;
                               Window.OPEN(Text001);

                               PlantSetupRec.GET;
                             END;

               OnAfterGetRecord=VAR
                                  lvPostViaIC@1100485000 : Boolean;
                                BEGIN
                                  Counter := Counter + 1;
                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                                  CLEAR(SalesPost);

                                  CheckDistrOverSubProjects();

                                  lvPostViaIC := FALSE;
                                  IF (("Company Name" <> COMPANYNAME) AND ("Company Name" <> '')) THEN
                                    lvPostViaIC := PlantSetupRec.CheckPostInvoiceProposalViaIC("Company Name");

                                  SalesPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                                  IF lvPostViaIC THEN
                                    ProposalPosted := SalesPost.PostInvoiceProposalviaIC("Sales Header")
                                  ELSE
                                    ProposalPosted := SalesPost.RUN("Sales Header");
                                  COMMIT;

                                  IF ProposalPosted THEN BEGIN
                                    CounterOK := CounterOK + 1;
                                    IF MARKEDONLY THEN
                                      MARK(FALSE);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                MESSAGE(Text002,CounterOK,CounterTotal);
                              END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Field     ;
                  CaptionML=ENU=Posting Date;
                  SourceExpr=PostingDateReq }

      { 2   ;2   ;Field     ;
                  CaptionML=ENU=Replace Posting Date;
                  SourceExpr=ReplacePostingDate }

      { 3   ;2   ;Field     ;
                  CaptionML=ENU=Replace Document Date;
                  SourceExpr=ReplaceDocumentDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@11012009 : TextConst 'ENU=Please enter the posting date.';
      Text001@11012010 : TextConst 'ENU=Posting Invoice Proposals   #1########## @2@@@@@@@@@@@@@';
      Text002@11012011 : TextConst 'ENU=%1 of the %2 invoice proposals posted.';
      PlantSetupRec@1210190001 : Record 11012550;
      SalesPost@11012001 : Codeunit 80;
      Window@11012002 : Dialog;
      PostingDateReq@11012003 : Date;
      CounterTotal@11012004 : Integer;
      Counter@11012005 : Integer;
      CounterOK@11012006 : Integer;
      ReplacePostingDate@11012007 : Boolean;
      ReplaceDocumentDate@11012008 : Boolean;
      ProposalPosted@1210190000 : Boolean;

    PROCEDURE CheckDistrOverSubProjects@1100525000();
    VAR
      DistrOverSubProjectsCU@1100525001 : Codeunit 11020222;
    BEGIN
      DistrOverSubProjectsCU.CheckPlantDistrOverSubProjects("Sales Header");
      COMMIT;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

