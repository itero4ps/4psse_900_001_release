OBJECT Report 11012580 Update Rental Rates from Comp.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Update Actual Rental Rates from  Rate Components;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   PlantSetupRec.GET;
                   IF PlantSetupRec."Post Rent by Rate Component" AND
                      (PlantSetupRec."Type Division of Rate Comp." = PlantSetupRec."Type Division of Rate Comp."::DiffOnResult)
                   THEN
                     ERROR(Text000, PlantSetupRec.FIELDCAPTION("Type Division of Rate Comp."), PlantSetupRec."Type Division of Rate Comp.");
                 END;

  }
  DATASET
  {
    { 5440;    ;DataItem;                    ;
               DataItemTable=Table11012567;
               DataItemTableView=SORTING(Rental Type,Plant Type,Plant No.,Starting Date)
                                 WHERE(Rental Type=FILTER(Rental|Plant Hours),
                                       Plant No.=FILTER(''));
               OnAfterGetRecord=BEGIN
                                  CALCFIELDS("Internal Rate on Components","External Rate on Components");

                                  InternalRate := "Internal Rate on Components";
                                  ExternalRate := "External Rate on Components";

                                  IF ("Plant No." = '') AND ("Rental Type" IN ["Rental Type"::Rental, "Rental Type"::"Plant Hours"]) THEN BEGIN
                                    IF ("Expected Occupation Perc." <> 0) THEN BEGIN
                                      InternalRate := InternalRate * (100 / "Expected Occupation Perc.");
                                      ExternalRate := ExternalRate * (100 / "Expected Occupation Perc.");
                                    END;
                                  END;
                                  GLSetup.GET;
                                  InternalRate := ROUND(InternalRate, GLSetup."Unit-Amount Rounding Precision");
                                  ExternalRate := ROUND(ExternalRate, GLSetup."Unit-Amount Rounding Precision");

                                  CASE "Rental Type" OF
                                    "Rental Type"::Rental:
                                      BEGIN
                                        IF (InternalRate <> 0) THEN
                                          "Rental Rate (Days)" := InternalRate;
                                        IF (ExternalRate <> 0) THEN
                                          "Rental Rate External (Days)" := ExternalRate;
                                      END;
                                    "Rental Type"::"Plant Hours":
                                      BEGIN
                                        IF (InternalRate <> 0) THEN
                                          "Rental Rate (Hours)" := InternalRate;
                                        IF (ExternalRate <> 0) THEN
                                          "Rental Rate External (Hours)" := ExternalRate;
                                      END;
                                  END;

                                  MODIFY;
                                END;

               ReqFilterFields=Rental Type,Plant Type,Starting Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PlantSetupRec@1100485001 : Record 11012550;
      GLSetup@1100485000 : Record 98;
      InternalRate@1210190001 : Decimal;
      ExternalRate@1210190002 : Decimal;
      Text000@1100485002 : TextConst 'ENU=When Plant Setup %1 is ''%2'' the rates must be filled munually, update rates not allowed.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

