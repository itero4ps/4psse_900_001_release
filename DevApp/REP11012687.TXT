OBJECT Report 11012687 Export Hours (External)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Export Hours;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   ProjSetup.GET;
                 END;

    OnPreReport=VAR
                  TempBlob@1100528403 : Record 99008535;
                  ThreeTierAutomationMgt@1100528402 : Codeunit 419;
                  OStream@1100528401 : OutStream;
                  ExportHoursExternal@1100528400 : XMLport 11012375;
                BEGIN
                  TempBlob.Blob.CREATEOUTSTREAM(OStream);
                  ExportHoursExternal.SETDESTINATION(OStream);
                  ExportHoursExternal.SetGlobalVars(
                    DocumentCde,
                    gvStartDate,
                    gvEndDate,
                    EmployerCde,
                    NotExportedLinesOnly,
                    AllCompanies,
                    DiffPeriodADP,
                    PostPaymentADP,
                    FileName);
                  ExportHoursExternal.EXPORT;
                  ThreeTierAutomationMgt.BLOBExport4PS(
                    TempBlob, ExportHoursExternal.FILENAME, FALSE, FALSE);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100528410;;Container;
                  ContainerType=ContentArea }

      { 1100528409;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 1100528408;2;Field  ;
                  Name=DocumentCde;
                  CaptionML=ENU=Document No.;
                  SourceExpr=DocumentCde;
                  Editable=DocumentCdeEditable;
                  OnValidate=VAR
                               lDocNo@1100528400 : Code[20];
                             BEGIN
                               HoursExportRec.SETRANGE("Document No.",DocumentCde);
                               IF HoursExportRec.FIND('-') THEN BEGIN
                                 IF NOT CONFIRM(Text016,FALSE,DocumentCde) THEN
                                   EXIT;
                               END;
                               HoursExportRec.SETRANGE("Document No.");
                             END;
                              }

      { 1100528407;2;Field  ;
                  Name=StartDate;
                  CaptionML=ENU=From Date;
                  SourceExpr=gvStartDate;
                  Editable=StartDateEditable }

      { 1100528406;2;Field  ;
                  Name=EndDate;
                  CaptionML=ENU=To Date;
                  SourceExpr=gvEndDate;
                  Editable=EndDateEditable }

      { 1100528405;2;Field  ;
                  Name=Employer;
                  CaptionML=ENU=For Employer;
                  SourceExpr=EmployerCde;
                  OnLookup=VAR
                             EmployerPage@1100528401 : Page 11012064;
                             EmployerRec@1100528400 : Record 11012013;
                           BEGIN
                             CLEAR(EmployerPage);
                             EmployerPage.LOOKUPMODE(TRUE);
                             IF EmployerPage.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               EmployerPage.GETRECORD(EmployerRec);
                               EmployerCde := EmployerRec.Code;
                             END;
                           END;
                            }

      { 1100528404;2;Field  ;
                  Name=NotExportedLinesOnly;
                  CaptionML=ENU=Not Exported Lines only;
                  SourceExpr=NotExportedLinesOnly }

      { 1100528403;2;Field  ;
                  Name=AllCompanies;
                  CaptionML=ENU=All Companies;
                  SourceExpr=AllCompanies;
                  Editable=AllCompaniesEditable }

      { 1100528402;2;Field  ;
                  Name=DiffPeriodADP;
                  CaptionML=ENU=Different Period Multipay;
                  SourceExpr=DiffPeriodADP;
                  Editable=DiffPeriodADPEditable }

      { 1100528401;2;Field  ;
                  Name=PostPaymentADP;
                  CaptionML=ENU=Post Payments Mutipay;
                  SourceExpr=PostPaymentADP;
                  Editable=PostPaymentADPEditable }

      { 1100528400;2;Field  ;
                  Name=FileName;
                  CaptionML=ENU=Filename;
                  SourceExpr=FileName;
                  Editable=FileNameEditable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ProjSetup@1100528440 : Record 315;
      HoursExportRec@1100528438 : Record 11012041;
      PostedHourLine@1100528436 : Record 11012085;
      PathText@1100528428 : Text[100];
      FileName@1100528427 : Text[250];
      DocumentCde@1100528418 : Code[20];
      EmployerCde@1100528417 : Code[20];
      gvStartDate@1100528413 : Date;
      gvEndDate@1100528412 : Date;
      AllCompanies@1100528411 : Boolean;
      DiffPeriodADP@1100528410 : Code[2];
      NotExportedLinesOnly@1100528409 : Boolean;
      PostPaymentADP@1100528408 : Boolean;
      DocumentCdeEditable@1100528406 : Boolean INDATASET;
      StartDateEditable@1100528405 : Boolean INDATASET;
      EndDateEditable@1100528404 : Boolean INDATASET;
      AllCompaniesEditable@1100528403 : Boolean INDATASET;
      DiffPeriodADPEditable@1100528402 : Boolean INDATASET;
      PostPaymentADPEditable@1100528401 : Boolean INDATASET;
      FileNameEditable@1100528400 : Boolean INDATASET;
      Text016@1100528442 : TextConst 'ENU=Document Number %1 has been exported before, again ?';

    PROCEDURE SetSelection@10(DocumentNo@11012000 : Code[20]);
    BEGIN
      DocumentCde := DocumentNo;
      DocumentCdeEditable := TRUE;
      StartDateEditable := FALSE;
      EndDateEditable := FALSE;
      AllCompaniesEditable :=
        ProjSetup."Salary Application" = ProjSetup."Salary Application"::Payroll;
      DiffPeriodADPEditable :=
        ProjSetup."Salary Application" = ProjSetup."Salary Application"::Multipay;
      PostPaymentADPEditable :=
        ProjSetup."Salary Application" = ProjSetup."Salary Application"::Multipay;
      FileNameEditable :=
        (ProjSetup."Salary Application" = ProjSetup."Salary Application"::AFAS) OR
        (ProjSetup."Salary Application" = ProjSetup."Salary Application"::Raet);

      CASE ProjSetup."Salary Application" OF
        ProjSetup."Salary Application"::Multipay:
          BEGIN
            SetDocumentOrDate;
          END;
         ProjSetup."Salary Application"::UNIT4,
         ProjSetup."Salary Application"::Raet: //Beaufort
           BEGIN
             FileName := 'dpia100.txt';
             SetDocumentOrDate;
           END;
         ProjSetup."Salary Application"::AFAS:
           BEGIN
             ProjSetup."Selection Posting Date" := TRUE; //no choice for AFAS
             SetDocumentOrDate;
           END;
      END;
    END;

    PROCEDURE SetDocumentOrDate@1100525001();
    VAR
      lStrPos@1100525006 : Integer;
      lYearStr@1100525005 : Text[4];
      lPeriodStr@1100525003 : Text[2];
      lYear@1100525002 : Integer;
      lPeriod@1100525000 : Integer;
    BEGIN
      lStrPos := STRPOS(DocumentCde, '-');
      IF STRLEN(DocumentCde) > lStrPos THEN BEGIN
        lYearStr := COPYSTR(DocumentCde, lStrPos - 4,4);
        lPeriodStr := COPYSTR(DocumentCde, lStrPos + 1, 2);
        IF EVALUATE(lYear,lYearStr) AND EVALUATE(lPeriod,lPeriodStr) THEN BEGIN
          CASE ProjSetup."Hour Document No." OF
            ProjSetup."Hour Document No."::Week:
              BEGIN
                gvStartDate := DWY2DATE(1, lPeriod, lYear);
                gvEndDate := DWY2DATE(7, lPeriod, lYear);
              END;
            ProjSetup."Hour Document No."::"4-Weeks":
              BEGIN
                //gvEndDate := DWY2DATE(1, lPeriod, lYear);
                lPeriod := (lPeriod * 4) - 3;    //* 4wk-periodno -> first week of period
                gvStartDate := DWY2DATE(1, lPeriod, lYear);
                gvEndDate := gvStartDate + 27;
              END;
            ProjSetup."Hour Document No."::Month:
              BEGIN
                gvStartDate := DMY2DATE(1, lPeriod, lYear);
                gvEndDate := CALCDATE('<1M-1D>' ,gvStartDate);
              END;
          END;

          IF ProjSetup."Selection Posting Date" THEN BEGIN
            DocumentCde := '' ;
            DocumentCdeEditable := FALSE;
            StartDateEditable := TRUE;
            EndDateEditable := TRUE;
          END;
        END;
      END;
    END;

    BEGIN
    {
      This report is only used to run XMLport Export Hours (External) to prevent selection of the
      output file by the user.
    }
    END.
  }
  RDLDATA
  {
  }
}

