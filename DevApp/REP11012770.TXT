OBJECT Report 11012770 Update Serv. Insp. Grp. & Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Update Service Inspection Group & Lines;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 8802;    ;DataItem;                    ;
               DataItemTable=Table11071747;
               DataItemTableView=SORTING(Code);
               OnAfterGetRecord=VAR
                                  ServiceInspectionLine@1100528600 : Record 11071751;
                                BEGIN
                                  IF UpdateInspectionGroups THEN BEGIN
                                    ServiceInspectionLine.SETRANGE(Type, ServiceInspectionLine.Type::Group);
                                    ServiceInspectionLine.SETRANGE("Inspection Code", Code);
                                    ServiceInspectionLine.MODIFYALL(Description, Description, TRUE);
                                  END;
                                END;

               ReqFilterFields=Code }

    { 1885;1   ;DataItem;                    ;
               DataItemTable=Table11071748;
               DataItemTableView=SORTING(Code);
               OnAfterGetRecord=VAR
                                  ServiceInspectionLine@1100528600 : Record 11071751;
                                BEGIN
                                  IF UpdateInspectionLines THEN
                                    UpdateServiceInspectionLine("Base Service Inspection Line");
                                END;

               ReqFilterFields=Code;
               DataItemLink=Inspection Group Code=FIELD(Code) }

    { 1100525000;;DataItem;BaseServiceInspectionLineNoGrp;
               DataItemTable=Table11071748;
               DataItemTableView=SORTING(Code);
               OnPreDataItem=BEGIN
                               COPY("Base Service Inspection Line");
                               FILTERGROUP(4);
                               SETRANGE("Inspection Group Code", '');
                               FILTERGROUP(0);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF UpdateInspectionLines THEN
                                    UpdateServiceInspectionLine(BaseServiceInspectionLineNoGrp);
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100528600;;Container;
                  ContainerType=ContentArea }

      { 1100528601;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 1100528602;2;Field  ;
                  CaptionML=ENU=Update Inspection Groups;
                  SourceExpr=UpdateInspectionGroups }

      { 1100528603;2;Field  ;
                  CaptionML=ENU=Update Inspection Lines;
                  SourceExpr=UpdateInspectionLines }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      UpdateInspectionGroups@1100528600 : Boolean INDATASET;
      UpdateInspectionLines@1100528601 : Boolean INDATASET;

    PROCEDURE SetUpdateFlags@1100528603(IUpdateInspectionGroups@1100528600 : Boolean;IUpdateInspectionLines@1100528601 : Boolean);
    BEGIN
      UpdateInspectionGroups := IUpdateInspectionGroups;
      UpdateInspectionLines := IUpdateInspectionLines;
    END;

    PROCEDURE UpdateServiceInspectionLine@1100525001(IBaseServiceInspectionLine@1100525000 : Record 11071748);
    VAR
      ServiceInspectionLine@1100525001 : Record 11071751;
    BEGIN
      ServiceInspectionLine.SETRANGE(Type, ServiceInspectionLine.Type::Line);
      ServiceInspectionLine.SETRANGE("Inspection Code", IBaseServiceInspectionLine.Code);
      IF ServiceInspectionLine.FINDSET(TRUE) THEN
        REPEAT
          ServiceInspectionLine.VALIDATE(Description, IBaseServiceInspectionLine.Description);
          ServiceInspectionLine.VALIDATE("Field Type", IBaseServiceInspectionLine."Field Type");
          ServiceInspectionLine.VALIDATE("Unit Of Measure", IBaseServiceInspectionLine."Unit Of Measure");
          ServiceInspectionLine.VALIDATE(Mandatory, IBaseServiceInspectionLine.Mandatory);
          ServiceInspectionLine.MODIFY(TRUE);
        UNTIL ServiceInspectionLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

