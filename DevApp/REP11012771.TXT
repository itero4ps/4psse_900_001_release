OBJECT Report 11012771 Mail Subcontr. SO not Finished
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Mail Subcontractor Service Orders not Finished;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   NoOfDays := 7;
                 END;

  }
  DATASET
  {
    { 3182;    ;DataItem;                    ;
               DataItemTable=Table23;
               DataItemTableView=SORTING(No.)
                                 WHERE(Subcontractor=CONST(Yes));
               OnAfterGetRecord=VAR
                                  ServiceSubcontrEmailMgt@1100409000 : Codeunit 11012857;
                                  SubcontractorNo@1100528400 : Code[20];
                                BEGIN
                                  SubcontractorNo := Vendor.GetResourceNo;
                                  IF SubcontractorNo <> '' THEN
                                    IF ServiceOrderShouldBeFinished('', SubcontractorNo, NoOfDays) THEN
                                      ServiceSubcontrEmailMgt.SendSOListMail("No.", 0, NoOfDays, 3030);
                                END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1100528600;;Container;
                  ContainerType=ContentArea }

      { 1100528601;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 1100528602;2;Field  ;
                  CaptionML=ENU=Finished within x days;
                  SourceExpr=NoOfDays;
                  MinValue=0 }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      NoOfDays@1100528600 : Integer;
      Text000@1100409000 : TextConst 'ENU=Open Service Order not Finished';

    PROCEDURE ServiceOrderShouldBeFinished@1100528606(IServiceOrderNo@1100525001 : Code[20];ISubcontractorNo@1100528602 : Code[20];INoOfDays@1100528603 : Integer) : Boolean;
    VAR
      SubcontPortSetup@1100409000 : Record 11126402;
      ServiceSetup@1100528604 : Record 11012800;
      ServiceOrder@1100525000 : Record 11012823;
      ReferencePoint@1100528600 : Integer;
    BEGIN
      IF SubcontPortSetup.FINDSET THEN
        IF SubcontPortSetup."Subcontracting Portal Active" THEN
          ReferencePoint := SubcontPortSetup."Ref. Point Sending WO";
      IF ReferencePoint = 0 THEN BEGIN
        ServiceSetup.GET;
        ReferencePoint := ServiceSetup."Ref. Point SO Planned";
      END;

      IF IServiceOrderNo <> '' THEN
        ServiceOrder.SETRANGE("No.", IServiceOrderNo)
      ELSE
        ServiceOrder.SETCURRENTKEY(Subcontractor);
      ServiceOrder.SETRANGE(Subcontractor, ISubcontractorNo);
      ServiceOrder.SETFILTER("Actual Reference Point (Gantt)", '%1..', ReferencePoint);
      ServiceOrder.SETFILTER(Status, '<%1', ServiceOrder.Status::"Technical Finished");
      ServiceOrder.SETFILTER("Last Possible Ending Date", '<>%1&<=%2', 0D, TODAY + INoOfDays);
      IF NOT ServiceOrder.ISEMPTY THEN
        EXIT(TRUE);
      ServiceOrder.SETRANGE("Last Possible Ending Date");
      ServiceOrder.SETFILTER("Ending Date", '<>%1&<=%2', 0D, TODAY + INoOfDays);
      EXIT(NOT ServiceOrder.ISEMPTY);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

