OBJECT Report 11012777 Transfer Service to Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    Permissions=TableData 11012819=imd,
                TableData 11072005=imd;
    CaptionML=ENU=Transfer Service to Project;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   ServiceSetup.GET;
                   ServiceSetup.TESTFIELD("Service to Project", ServiceSetup."Service to Project"::"Transfer Cost and Revenue");
                   PostingDate := WORKDATE;
                 END;

    OnPostReport=BEGIN
                   IF LedgerEntryCount = 0 THEN
                     MESSAGE(Text000)
                   ELSE
                     MESSAGE(Text001, LedgerEntryCount);
                 END;

  }
  DATASET
  {
    { 1373;    ;DataItem;                    ;
               DataItemTable=Table11012823;
               DataItemTableView=SORTING(No.)
                                 WHERE(Status=FILTER(<Finished));
               OnAfterGetRecord=BEGIN
                                  ServiceOrderExtension.GetServOrderExtension("No.");
                                  IF ServiceOrderExtension."Master Project" = '' THEN
                                    CurrReport.SKIP;
                                  IF (ProjectFilter <> '') AND (ServiceOrderExtension."Master Project" <> ProjectFilter) THEN
                                    CurrReport.SKIP;

                                  DocumentNo := '';
                                END;

               ReqFilterFields=No.,Customer No. }

    { 4723;1   ;DataItem;                    ;
               DataItemTable=Table11012819;
               DataItemTableView=SORTING(Service Order No.,Cost Plus Line No.)
                                 WHERE(Surcharge=CONST(No),
                                       Transfer to Project=CONST(" "));
               OnAfterGetRecord=VAR
                                  GenJournalLine@1100528600 : Record 81;
                                  GeneralLedgerSetup@1100528601 : Record 98;
                                  GenJournalTemplate@1100528604 : Record 80;
                                  GenJournalBatch@1100528603 : Record 232;
                                  Job@1100528610 : Record 11072003;
                                  JobsSetup@1100528611 : Record 315;
                                  ProjectElement@1100528612 : Record 11012010;
                                  NoSeriesManagement@1100528605 : Codeunit 396;
                                BEGIN
                                  IF UseOriginalPostingDate THEN
                                    PostingDate := "Posting Date";

                                  IF DocumentNo = '' THEN BEGIN
                                    GeneralLedgerSetup.GET;
                                    ServiceSetup.TESTFIELD("Intern Charge Journal Template");
                                    ServiceSetup.TESTFIELD("Intern Charge Journal Batch");
                                    GenJournalTemplate.GET(ServiceSetup."Intern Charge Journal Template");
                                    GenJournalBatch.GET(GenJournalTemplate.Name, ServiceSetup."Intern Charge Journal Batch");
                                    GenJournalBatch.TESTFIELD("No. Series");
                                    DocumentNo := NoSeriesManagement.GetNextNo(GenJournalBatch."No. Series", PostingDate, FALSE);

                                    GenJournalLine.LOCKTABLE;
                                    GenJournalLine.SETRANGE("Journal Template Name", GenJournalTemplate.Name);
                                    GenJournalLine.SETRANGE("Journal Batch Name", GenJournalBatch.Name);
                                    IF GenJournalLine.FINDLAST THEN
                                      NextLineNo := GenJournalLine."Line No."
                                    ELSE
                                      NextLineNo := 0;
                                    GenJournalLine.SETRANGE("Document No.", DocumentNo);
                                    IF NOT GenJournalLine.ISEMPTY THEN
                                      ERROR(Text002, GenJournalBatch.Name);
                                  END;

                                  NextLineNo += 1;
                                  GenJournalLine.INIT; //mg, 06-01-2013: T004129
                                  GenJournalLine."Line No." := NextLineNo;
                                  GenJournalLine."Account Type" := GenJournalLine."Account Type"::"G/L Account";
                                  GenJournalLine.VALIDATE("Journal Template Name", ServiceSetup."Intern Charge Journal Template");
                                  GenJournalLine.VALIDATE("Journal Batch Name", ServiceSetup."Intern Charge Journal Batch");
                                  GenJournalLine.VALIDATE("Service Order No.", "Service Order No.");
                                  GenJournalLine.VALIDATE("Shortcut Dimension 2 Code", "Global Dimension 2 Code");
                                  GenJournalLine."Origin Type" := GenJournalLine."Origin Type"::Service;
                                  GenJournalLine."Source Code" := GenJournalTemplate."Source Code";
                                  GenJournalLine."Document No." := DocumentNo;
                                  GenJournalLine.Description := Description;
                                  GenJournalLine."Description 2" := "Description 2";
                                  GenJournalLine."Employee No." := "Employee No."; //C024798.n

                                  GenJournalLine."System-Created Entry" := TRUE;
                                  GenJournalLine."Posting Date" := PostingDate;
                                  IF GenJournalBatch."Bal. Account No." <> '' THEN BEGIN
                                    GenJournalLine."Bal. Account Type" := GenJournalLine."Bal. Account Type"::"G/L Account";
                                    GenJournalLine."Bal. Account No." := GenJournalBatch."Bal. Account No.";
                                  END;
                                  GenJournalLine."Gen. Prod. Posting Group" := '';
                                  GenJournalLine."Gen. Bus. Posting Group" := '';
                                  GenJournalLine."VAT Prod. Posting Group" :='';
                                  GenJournalLine."VAT Bus. Posting Group" := '';
                                  GenJournalLine."Gen. Posting Type" := 0;

                                  GenJournalLine."Interest Date" := "Posting Date";
                                  IF "Cost Type" = "Cost Type"::Revenues THEN BEGIN
                                    GenJournalLine.Quantity := Quantity;
                                    GenJournalLine.VALIDATE(Amount, "Total Revenue");
                                  END ELSE BEGIN
                                    GenJournalLine.Quantity := -Quantity;
                                    GenJournalLine.VALIDATE(Amount, -"Total Cost");
                                  END;
                                  GenJournalLine.INSERT(TRUE);

                                  Job.GET(ServiceOrderExtension."Master Project");
                                  JobsSetup.GET;
                                  IF JobsSetup."Posting Element Mandatory" THEN
                                    ProjectElement.GET(ServiceOrderExtension."Master Project", ServiceOrderExtension."Master Element"); //db, 10-09-13: C009364

                                  NextLineNo += 1;
                                  GenJournalLine.INIT; //mg, 06-01-2013: T004129
                                  GenJournalLine."Line No." := NextLineNo;
                                  GenJournalLine."Account Type" := GenJournalLine."Account Type"::"G/L Account";
                                  GenJournalLine.VALIDATE("Journal Template Name", ServiceSetup."Intern Charge Journal Template");
                                  GenJournalLine.VALIDATE("Journal Batch Name", ServiceSetup."Intern Charge Journal Batch");
                                  GenJournalLine."Service Order No." := '';
                                  GenJournalLine.VALIDATE("Job No.", ServiceOrderExtension."Master Project");
                                  GenJournalLine.Element := ServiceOrderExtension."Master Element";
                                  IF ServiceSetup."Orig.Cost Object Intern Charge" THEN
                                    GenJournalLine.VALIDATE("Shortcut Dimension 2 Code", "Global Dimension 2 Code")
                                  ELSE BEGIN
                                    IF "Cost Type" = "Cost Type"::Revenues THEN BEGIN
                                      Job.TESTFIELD("Cost Object Inter Project Rev");
                                      GenJournalLine.VALIDATE("Shortcut Dimension 2 Code", Job."Cost Object Inter Project Rev")
                                    END ELSE BEGIN
                                      Job.TESTFIELD("Cost Object Inter Project Cost");
                                      GenJournalLine.VALIDATE("Shortcut Dimension 2 Code", Job."Cost Object Inter Project Cost");
                                    END;
                                  END;
                                  GenJournalLine."Origin Type" := GenJournalLine."Origin Type"::Project;
                                  GenJournalLine."Source Code" := GenJournalTemplate."Source Code";
                                  GenJournalLine."Document No." := DocumentNo;
                                  GenJournalLine.Description := Description;
                                  GenJournalLine."Description 2" := "Description 2";
                                  GenJournalLine."Employee No." := "Employee No."; //C024798.n

                                  GenJournalLine."System-Created Entry" := TRUE;
                                  GenJournalLine."Posting Date" := PostingDate;
                                  IF GenJournalBatch."Bal. Account No." <> '' THEN BEGIN
                                    GenJournalLine."Bal. Account Type" := GenJournalLine."Bal. Account Type"::"G/L Account";
                                    GenJournalLine."Bal. Account No." := GenJournalBatch."Bal. Account No.";
                                  END;
                                  GenJournalLine."Gen. Prod. Posting Group" := '';
                                  GenJournalLine."Gen. Bus. Posting Group" := '';
                                  GenJournalLine."VAT Prod. Posting Group" :='';
                                  GenJournalLine."VAT Bus. Posting Group" := '';
                                  GenJournalLine."Gen. Posting Type" := 0;

                                  GenJournalLine."Interest Date" := "Posting Date";
                                  IF "Cost Type" = "Cost Type"::Revenues THEN BEGIN
                                    GenJournalLine.Quantity := -Quantity;
                                    GenJournalLine.VALIDATE(Amount, -"Total Revenue");
                                  END ELSE BEGIN
                                    GenJournalLine.Quantity := Quantity;
                                    GenJournalLine.VALIDATE(Amount, "Total Cost");
                                  END;
                                  GenJournalLine.INSERT(TRUE);

                                  "Transfer to Project" := "Transfer to Project"::Processed;
                                  MODIFY;

                                  LedgerEntryCount += 1;
                                END;

               OnPostDataItem=VAR
                                GenJournalLine@1100528600 : Record 81;
                                ServiceLedgerEntry@1100528601 : Record 11012819;
                                JobLedgerEntry@1100528602 : Record 11072005;
                              BEGIN
                                GenJournalLine.SETRANGE("Journal Template Name", ServiceSetup."Intern Charge Journal Template");
                                GenJournalLine.SETRANGE("Journal Batch Name", ServiceSetup."Intern Charge Journal Batch");
                                GenJournalLine.SETRANGE("Document No.", DocumentNo);
                                IF GenJournalLine.FINDSET THEN BEGIN
                                  CODEUNIT.RUN(CODEUNIT::"Gen. Jnl.-Post Batch", GenJournalLine);

                                  ServiceLedgerEntry.SETCURRENTKEY("Service Order No.");
                                  ServiceLedgerEntry.SETRANGE("Service Order No.", "Service Order No.");
                                  ServiceLedgerEntry.SETRANGE("Document No.", DocumentNo);
                                  ServiceLedgerEntry.SETRANGE("Transfer to Project", ServiceLedgerEntry."Transfer to Project"::" ");
                                  ServiceLedgerEntry.MODIFYALL("Transfer to Project", ServiceLedgerEntry."Transfer to Project"::Correction);

                                //mg.so, 30-12-2013: C011700
                                //  JobLedgerEntry.SETCURRENTKEY("Job No.");
                                //  JobLedgerEntry.SETRANGE("Job No.", ServiceOrderExtension."Master Project");
                                //  JobLedgerEntry.SETRANGE("Document No.", DocumentNo);
                                //  JobLedgerEntry.MODIFYALL(Chargeable, FALSE);
                                //mg.eo, 30-12-2013: C011700
                                END;
                              END;

               DataItemLink=Service Order No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   PostingDateEditable := NOT UseOriginalPostingDate;
                 END;

    }
    CONTROLS
    {
      { 1100528600;;Container;
                  CaptionML=ENU=Options;
                  ContainerType=ContentArea }

      { 1100528603;1;Field  ;
                  CaptionML=ENU=Use Original Posting Date;
                  SourceExpr=UseOriginalPostingDate;
                  OnValidate=BEGIN
                               PostingDateEditable := NOT UseOriginalPostingDate;
                             END;
                              }

      { 1100528601;1;Field  ;
                  CaptionML=ENU=Posting Date;
                  SourceExpr=PostingDate;
                  Editable=PostingDateEditable }

      { 1100528602;1;Field  ;
                  CaptionML=ENU=Project Filter;
                  SourceExpr=ProjectFilter;
                  TableRelation=Job }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ServiceSetup@1100528601 : Record 11012800;
      ServiceOrderExtension@1100528600 : Record 11071727;
      DocumentNo@1100528602 : Text[30];
      NextLineNo@1100528603 : Integer;
      LedgerEntryCount@1100528604 : Integer;
      Text000@1100528605 : TextConst 'ENU=No Ledger Entries transfered to Project';
      Text001@1100528606 : TextConst 'ENU=%1 Ledger Entries transfered to Project';
      Text002@1100528607 : TextConst 'ENU=Entries Exist in Batch %1';
      PostingDate@1100528608 : Date;
      ProjectFilter@1100528609 : Code[20];
      UseOriginalPostingDate@1100528610 : Boolean;
      PostingDateEditable@1100528611 : Boolean INDATASET;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

