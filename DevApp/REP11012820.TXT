OBJECT Report 11012820 Create Consignment Time
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Create Consignment Time;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6953;    ;DataItem;                    ;
               DataItemTable=Table11012864;
               DataItemTableView=SORTING(Consignment Code,Start Date,Start Time);
               OnPreDataItem=BEGIN
                               Window.OPEN(Text001 + '\#1############# #2#############');

                               BaseEndDate := CALCDATE(BasePeriod, BaseDate);
                               BaseEndDate := CALCDATE('<-1D>', BaseEndDate); // Go back 1 day for selection of a period...
                               "Consignment Time".SETFILTER("Start Date", '%1..%2', BaseDate, BaseEndDate);
                             END;

               OnAfterGetRecord=BEGIN
                                  ConsTimeTemp := "Consignment Time";
                                  ConsTimeTemp.INSERT;
                                END;

               OnPostDataItem=VAR
                                CurrentDate@1100485000 : Date;
                                DateDiff@1100485001 : Integer;
                              BEGIN
                                CurrentDate := StartDate;
                                REPEAT
                                  ConsTimeTemp.SETRANGE("Start Day of Week", DATE2DWY(CurrentDate, 1));
                                  IF ConsTimeTemp.FIND('-') THEN BEGIN
                                    REPEAT  //db, 26-03-08
                                      DateDiff := CurrentDate - ConsTimeTemp."Start Date";
                                      "Consignment Time" := ConsTimeTemp;
                                      "Consignment Time".VALIDATE("Start Date", "Consignment Time"."Start Date" + DateDiff);
                                      "Consignment Time".VALIDATE("End Date", ConsTimeTemp."End Date" + DateDiff);
                                      Window.UPDATE(1, CREATEDATETIME("Consignment Time"."Start Date", "Consignment Time"."Start Time"));
                                      Window.UPDATE(2, CREATEDATETIME("Consignment Time"."End Date", "Consignment Time"."End Time"));
                                      "Consignment Time".INSERT;

                                      ConsTimeTemp2 := ConsTimeTemp;
                                      ConsTimeTemp2.INSERT;
                                      ConsTimeTemp.DELETE;
                                    UNTIL ConsTimeTemp.NEXT = 0; //db, 26-03-08
                                    //db.sn, 26-03-08: functie verplaatst (buiten repeat-until lus)
                                    //controleer na verwijderen huidig record in te kopieren cyclus of er nog meer zijn;
                                    //zo niet, kopieer buffer terug en herhaal patroon tot opgegeven einddatum
                                    ConsTimeTemp.SETRANGE("Start Day of Week");
                                    IF NOT ConsTimeTemp.FIND('-') THEN BEGIN
                                      IF ConsTimeTemp2.FIND('-') THEN BEGIN
                                        REPEAT
                                          ConsTimeTemp := ConsTimeTemp2;
                                          ConsTimeTemp.INSERT;
                                          ConsTimeTemp2.DELETE;
                                        UNTIL ConsTimeTemp2.NEXT = 0;
                                      END;
                                    END;
                                    //db.en, 26-03-08
                                  END;
                                  CurrentDate := CALCDATE('<+1D>', CurrentDate);
                                UNTIL CurrentDate > EndDate;
                              END;

               ReqFilterFields=Consignment Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 1100525002;2;Group  ;
                  CaptionML=ENU=Source;
                  GroupType=Group }

      { 1100525003;3;Field  ;
                  CaptionML=ENU=Copy from date;
                  SourceExpr=BaseDate }

      { 1100525004;3;Field  ;
                  CaptionML=ENU=Copy period;
                  SourceExpr=BasePeriod }

      { 1100525005;2;Group  ;
                  CaptionML=ENU=Target;
                  GroupType=Group }

      { 1100525006;3;Field  ;
                  CaptionML=ENU=Start Date Times to Generate;
                  SourceExpr=StartDate }

      { 1100525007;3;Field  ;
                  CaptionML=ENU=End Date Times to Generate;
                  SourceExpr=EndDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      BaseDate@1100485000 : Date;
      BaseEndDate@1100485004 : Date;
      BasePeriod@1100485001 : DateFormula;
      StartDate@1100485002 : Date;
      EndDate@1100485003 : Date;
      Text001@1100485006 : TextConst 'ENU=Generating Consignment Times...';
      Window@1100485007 : Dialog;
      ConsTimeTemp@1100485005 : TEMPORARY Record 11012864;
      ConsTimeTemp2@1100485008 : TEMPORARY Record 11012864;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

