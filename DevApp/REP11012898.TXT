OBJECT Report 11012898 Get Lab Purchase Invoices
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Get Lab Purchase Invoices;
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  ALcontrolWebservice@1100525000 : Codeunit 11012830;
                  Response@1100525001 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60";
                  InvoiceElements@1100525003 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF82-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNodeList";
                  i@1100525004 : Integer;
                BEGIN
                  IF StartDate = 0D THEN
                    ERROR(Text000);

                  IF EndDate = 0D THEN
                    ERROR(Text001);

                  LabOrderSetup.GET;
                  LabOrderSetup.TESTFIELD("Lab Invoice Vendor No.");

                  MarketingSetup.GET;
                  MarketingSetup.TESTFIELD("Default Ext. Doc. Directory");

                  ALcontrolWebservice.GetInvoices(StartDate, EndDate, Response);

                  InvoiceElements := Response.documentElement.selectNodes('invoice[pdf=''true'']');

                  NumTotal := InvoiceElements.length;
                  NumSkipped := 0;
                  NumCreated := 0;

                  FOR i := 0 TO InvoiceElements.length - 1 DO
                    CreatePurchaseInvoice(InvoiceElements.item(i));

                  MESSAGE(Text020, NumTotal, NumCreated, NumSkipped);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  Name=a;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  CaptionML=ENU=Starting Date;
                  SourceExpr=StartDate }

      { 1100525003;2;Field  ;
                  CaptionML=ENU=Ending Date;
                  SourceExpr=EndDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      LabOrderSetup@1100525004 : Record 11071953;
      MarketingSetup@1100525013 : Record 5079;
      StartDate@1100525000 : Date;
      EndDate@1100525001 : Date;
      Text000@1100525002 : TextConst 'ENU=Please specify a starting date.';
      Text001@1100525003 : TextConst 'ENU=Please specify an ending date.';
      Text010@1100525005 : TextConst 'ENU=A value for "%1" could not be retrieved from the lab invoice.';
      Text011@1100525006 : TextConst 'ENU=Skipping invoice %1 because it contains more than one <invoiceproject> element.';
      Text012@1100525007 : TextConst 'ENU=Skipping invoice %1 because it doesn''t contain any <invoiceproject> elements.';
      Text013@1100525008 : TextConst 'ENU=Skipping invoice %1 because it already exists in the system.';
      Text020@1100525009 : TextConst 'ENU=%1 invoices received, of which %2 were created and %3 were skipped.';
      NumTotal@1100525010 : Integer;
      NumCreated@1100525011 : Integer;
      NumSkipped@1100525012 : Integer;
      Text030@1100525014 : TextConst 'ENU=Purchase Invoice %1.pdf';
      Text035@1100525015 : TextConst 'ENU=Lab Invoice for %1 %2';

    LOCAL PROCEDURE CreatePurchaseInvoice@1100525009(InvoiceElement@1100525000 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF86-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMElement");
    VAR
      ServiceOrder@1100525011 : Record 11012823;
      PurchaseHeader@1100525002 : Record 38;
      PurchaseLine@1100525007 : Record 39;
      DocumentProperties@1100525016 : Record 11012746;
      ALcontrolWebservice@1100525014 : Codeunit 11012830;
      DocumentLinkMgt@1100525019 : Codeunit 11012401;
      RecRef@1100525017 : RecordRef;
      DocLinkRID@1100525018 : ARRAY [5] OF RecordID;
      InvoiceProjectElement@1100525006 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF86-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMElement";
      InvoiceProjectElements@1100525005 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF82-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNodeList";
      ProjectDescription@1100525009 : Text[80];
      ProjectDescription2@1100525020 : Text[80];
      YourReference@1100525022 : Text[35];
      InvoiceFileName@1100525012 : Text[260];
      InvoiceFullPath@1100525013 : Text[260];
      InvoiceNo@1100525001 : Code[20];
      ServiceOrderNo@1100525008 : Code[20];
      OrderNo@1100525015 : Code[20];
      InvoiceDate@1100525003 : Date;
      InvoiceAmountExclVAT@1100525010 : Decimal;
      InvoiceAmountInclVAT@1100525004 : Decimal;
      NextLineNo@1100525021 : Integer;
    BEGIN
      YourReference := COPYSTR(
        TextValueFromLabInvoice(InvoiceElement, 'inv_number'), 1,
        MAXSTRLEN(PurchaseHeader."Your Reference"));
      InvoiceNo := COPYSTR(
        TextValueFromLabInvoice(InvoiceElement, 'inv_id'), 1,
        MAXSTRLEN(PurchaseHeader."Vendor Invoice No."));
      InvoiceDate := DateValueFromLabInvoice(InvoiceElement, 'inv_date');
      InvoiceAmountExclVAT := DecimalValueFromLabInvoice(InvoiceElement, 'inv_exclbtw');
      InvoiceAmountInclVAT := DecimalValueFromLabInvoice(InvoiceElement, 'inv_inclbtw');

      PurchaseHeader."Document Date" := InvoiceDate; //call 29942
      PurchaseHeader."Buy-from Vendor No." := LabOrderSetup."Lab Invoice Vendor No.";
      IF NOT PurchaseHeader.CheckVendorInvoiceNo(InvoiceNo, FALSE, FALSE) THEN BEGIN
        MESSAGE(Text013, InvoiceNo);
        NumSkipped := NumSkipped + 1;
        EXIT;
      END;

      InvoiceProjectElements := InvoiceElement.selectNodes('invoiceprojects/invoiceproject');

      IF InvoiceProjectElements.length > 1 THEN BEGIN
        MESSAGE(Text011, InvoiceNo);
        NumSkipped := NumSkipped + 1;
        EXIT;
      END;

      IF InvoiceProjectElements.length < 1 THEN BEGIN
        MESSAGE(Text012, InvoiceNo);
        NumSkipped := NumSkipped + 1;
        EXIT;
      END;

      InvoiceProjectElement := InvoiceProjectElements.item(0);
      ServiceOrderNo := TextValueFromLabInvoice(InvoiceProjectElement, 'project');
      OrderNo := TextValueFromLabInvoice(InvoiceProjectElement, 'ordernumber');
      ProjectDescription := COPYSTR(TextValueFromLabInvoice(InvoiceProjectElement, 'projectdescription'), 1,
        MAXSTRLEN(PurchaseLine.Description));
      ProjectDescription2 := COPYSTR(TextValueFromLabInvoice(InvoiceProjectElement, 'projectdescription'),
        MAXSTRLEN(PurchaseLine.Description) + 1, MAXSTRLEN(PurchaseLine."Description 2"));

      PurchaseHeader.INIT;
      PurchaseHeader.VALIDATE("Document Type", PurchaseHeader."Document Type"::Invoice);
      PurchaseHeader.INSERT(TRUE);
      PurchaseHeader.VALIDATE("Buy-from Vendor No.", LabOrderSetup."Lab Invoice Vendor No.");
      PurchaseHeader.VALIDATE("Posting Date", WORKDATE);
      PurchaseHeader.VALIDATE("Document Date", InvoiceDate);
      PurchaseHeader.VALIDATE("Vendor Invoice No.", InvoiceNo);
      PurchaseHeader.VALIDATE("Amount incl. VAT", InvoiceAmountInclVAT);
      PurchaseHeader.VALIDATE("Invoice Lines Input", FALSE);
      PurchaseHeader.VALIDATE("Your Reference", YourReference);
      PurchaseHeader.MODIFY(TRUE);

      PurchaseLine.SETRANGE("Document Type", PurchaseLine."Document Type"::Invoice);
      PurchaseLine.SETRANGE("Document No.", PurchaseHeader."No.");
      IF NOT PurchaseLine.FINDLAST THEN;
      NextLineNo := PurchaseLine."Line No." + 10000;

      PurchaseLine.INIT;
      PurchaseLine.VALIDATE("Document Type", PurchaseLine."Document Type"::Invoice);
      PurchaseLine.VALIDATE("Document No.", PurchaseHeader."No.");
      PurchaseLine.VALIDATE("Line No.", NextLineNo);

      // jhoek.180211.begin
      // IF ServiceOrder.GET(ServiceOrderNo) THEN
      IF (ServiceOrder.GET(ServiceOrderNo)) AND
         (ServiceOrder.Status < ServiceOrder.Status::"Administrative Finished")
      // jhoek.180211.end
      THEN
        PurchaseLine.VALIDATE("Service Order No.", ServiceOrderNo)
      ELSE BEGIN
        PurchaseLine.VALIDATE(Type, PurchaseLine.Type::"G/L Account");
        PurchaseLine.VALIDATE("No.", '44101'); // :(
      END;

      PurchaseLine.VALIDATE(Description, ProjectDescription);
      PurchaseLine.VALIDATE("Description 2", ProjectDescription2);
      PurchaseLine.VALIDATE("Cost Type", PurchaseLine."Cost Type"::Subcontracting);
      PurchaseLine.VALIDATE("Shortcut Dimension 2 Code", '310'); // :(
      PurchaseLine.VALIDATE(Quantity, 1);
      PurchaseLine.VALIDATE("Unit of Measure", 'st'); // :(
      PurchaseLine.VALIDATE("Direct Unit Cost", InvoiceAmountExclVAT);
      PurchaseLine.INSERT(TRUE);

      NumCreated := NumCreated + 1;

      InvoiceFileName := STRSUBSTNO(Text030, PurchaseHeader."No.");
      InvoiceFullPath := STRSUBSTNO('%1\%2', DELCHR(MarketingSetup."Default Ext. Doc. Directory", '>', '\'), InvoiceFileName);

      ALcontrolWebservice.GetInvoice(OrderNo,InvoiceFullPath);

      DocumentProperties.VALIDATE("External Document", TRUE);
      DocumentProperties.INSERT(TRUE);
      DocumentProperties.Description := STRSUBSTNO(Text035, ServiceOrder.TABLECAPTION, ServiceOrderNo);
      DocumentProperties.VALIDATE(File, InvoiceFileName);
      DocumentProperties.MODIFY(TRUE);

      RecRef.GETTABLE(PurchaseHeader);
      DocLinkRID[1] := RecRef.RECORDID(); //link to purchase header

      DocumentLinkMgt.CreateDocumentLink(DocumentProperties, DocLinkRID);
      DocumentProperties.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE TextValueFromLabInvoice@1100525039(InvoiceElement@1100525000 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF86-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMElement";Name@1100525001 : Text[30]) : Text[1024];
    VAR
      Element@1100525002 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF86-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMElement";
    BEGIN
      Element := InvoiceElement.selectSingleNode(Name);
      IF ISCLEAR(Element) THEN
        ERROR(Text010, Name);
      EXIT(Element.text);
    END;

    LOCAL PROCEDURE DateValueFromLabInvoice@1100525029(InvoiceElement@1100525001 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF86-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMElement";Name@1100525000 : Text[30]) : Date;
    VAR
      DateAsText@1100525002 : Text[30];
      Year@1100525003 : Integer;
      Month@1100525004 : Integer;
      Day@1100525005 : Integer;
    BEGIN
      DateAsText := TextValueFromLabInvoice(InvoiceElement, Name);

      EVALUATE(Year, COPYSTR(DateAsText, 1, 4));
      EVALUATE(Month, COPYSTR(DateAsText, 5, 2));
      EVALUATE(Day, COPYSTR(DateAsText, 7, 2));

      EXIT(DMY2DATE(Day, Month, Year));
    END;

    LOCAL PROCEDURE DecimalValueFromLabInvoice@1100525013(InvoiceElement@1100525001 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF86-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMElement";Name@1100525000 : Text[30]) Result : Decimal;
    VAR
      DecimalAsText@1100525002 : Text[30];
    BEGIN
      DecimalAsText := NormalizeDecimalSeparator(TextValueFromLabInvoice(InvoiceElement, Name));
      EVALUATE(Result, DecimalAsText);
    END;

    LOCAL PROCEDURE NormalizeDecimalSeparator@1100525014(DecimalAsText@1100525000 : Text[30]) : Text[30];
    BEGIN
      EXIT(CONVERTSTR(DecimalAsText, '.', DecimalSeparator));
    END;

    LOCAL PROCEDURE DecimalSeparator@1100525016() : Text[1];
    BEGIN
      EXIT(FORMAT(FORMAT(0.1, 0, '<Decimals>')[1]));
    END;

    BEGIN
    {
      In this report xml automation variables are still used, because it is difficult to test test this functionality
      after conversion to xml dotnet variables. The automation variables are used on the client and no events are used,
      so it should not be a problem to use automation here.
    }
    END.
  }
  RDLDATA
  {
  }
}

