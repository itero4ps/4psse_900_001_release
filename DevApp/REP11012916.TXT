OBJECT Report 11012916 Manipulate PPT as Admin
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Manipulate PPT as Admin;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF (NOT AdminRights) THEN
                    ERROR(Text000);
                  CASE Type OF
                    Type::WebLink:
                      ManipulateWebLink;
                    ELSE
                      ERROR(Text000);
                  END;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 1100525004;2;Group  ;
                  CaptionML=ENU=Replace Web Link Part;
                  GroupType=Group }

      { 1100525002;3;Field  ;
                  CaptionML=ENU=Old String Part;
                  SourceExpr=OldWebLinkPart }

      { 1100525003;3;Field  ;
                  CaptionML=ENU=New String Part;
                  SourceExpr=NewWebLinkPart }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'ENU=You must have PPT Admin Rights.';
      AdminRights@1100525001 : Boolean;
      Type@1100525002 : ',WebLink';
      OldWebLinkPart@1100525003 : Text;
      NewWebLinkPart@1100525004 : Text;
      Text001@1100525005 : TextConst 'ENU=Type %1 is not supported.';
      Text002@1100525006 : TextConst 'ENU=%1 must be filled.';
      Counter@1100525007 : Integer;

    PROCEDURE SetAdminRights@1100525000(NewValue@1100525000 : Boolean;Password@1100525001 : Text);
    VAR
      SharePointSetup4PS@1100525002 : Record 11012730;
    BEGIN
      AdminRights := FALSE;
      SharePointSetup4PS.GET;
      IF (SharePointSetup4PS."Owner Password" <> '') AND (SharePointSetup4PS."Owner Password" = Password) THEN
        AdminRights := NewValue;
    END;

    PROCEDURE SetType@1100525004(NewValue@1100525000 : Integer);
    BEGIN
      Type := NewValue;
    END;

    LOCAL PROCEDURE ManipulateWebLink@1100525005();
    VAR
      Job@1100525000 : Record 11072003;
      DocumentProperties@1100525003 : Record 11012746;
      DocumentLink@1100525002 : Record 11012747;
      RecRef@1100525004 : RecordRef;
      StringFunctions@1100525001 : Codeunit 11012273;
      DocNo@1100525005 : Code[20];
    BEGIN
      IF (OldWebLinkPart = '') THEN
        ERROR(Text002, 'OldWebLinkPart');
      IF (NewWebLinkPart = '') THEN
        ERROR(Text002, 'NewWebLinkPart');

      DocumentLink.SETCURRENTKEY("Record ID");
      DocNo := '';

      Job.SETRANGE("Project Portal Project", TRUE);
      Job.SETRANGE("SharePoint Site Generated", TRUE);
      Job.SETFILTER("SharePoint Site URL", '<>%1', '');
      IF (NOT Job.FINDSET) THEN
        EXIT;
      REPEAT
        RecRef.GETTABLE(Job);
        DocumentLink.SETRANGE("Record ID", RecRef.RECORDID);
        DocumentLink.SETRANGE("Table No.", DATABASE::Job);
        DocumentLink.SETRANGE("Internal Company", COMPANYNAME);
        IF (DocumentLink.FINDSET) THEN BEGIN
          REPEAT
            IF (DocNo <> DocumentLink."Document No.") THEN BEGIN
              DocumentProperties.GET(DocumentLink."Document No.");
              IF (DocumentProperties."Project Portal Subsite" <> '') AND (DocumentProperties."Web link" <> '') THEN BEGIN
               DocumentProperties."Web link" :=
                  StringFunctions.ReplaceString2(DocumentProperties."Web link", OldWebLinkPart, NewWebLinkPart, FALSE);
                DocumentProperties.MODIFY;
              END;
            END;
            DocNo := DocumentLink."Document No.";
          UNTIL (DocumentLink.NEXT = 0);
        END;
      UNTIL (Job.NEXT = 0);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

