OBJECT Report 11012928 Copy Employee Time Table
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Employee Time Table;
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  Employee@1100528400 : Record 5200;
                BEGIN
                  Employee.GET(OrigEmployeeNo);
                  IF FromEffectiveDate > ToEffectiveDate THEN
                    ERROR(Text000);
                END;

  }
  DATASET
  {
    { 7528;    ;DataItem;                    ;
               DataItemTable=Table5200;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               FillTempOrigEmployeeTimeTable(OrigEmployeeNo, FromEffectiveDate, ToEffectiveDate);
                             END;

               OnAfterGetRecord=VAR
                                  Resource@1100529000 : Record 11229278;
                                BEGIN
                                  //DP00465.sn
                                  Resource.SETCURRENTKEY(Type,Company,"No.");
                                  Resource.SETRANGE(Type, Resource.Type::Employee);
                                  Resource.SETRANGE(Company, COMPANYNAME);
                                  Resource.SETRANGE("Source No.", "No.");
                                  IF Resource.ISEMPTY THEN
                                    CurrReport.SKIP;
                                  //DP00465.en

                                  CopyEmployeeTimeTable("No.");
                                END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   FromEffectiveDate := 0D;
                   ValidateOrigEmployeeNo(OrigEmployeeNo, FALSE);
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1100485000;2;Group  ;
                  CaptionML=ENU=Copy From;
                  GroupType=Group }

      { 1100485001;3;Field  ;
                  CaptionML=ENU=Employee;
                  NotBlank=Yes;
                  SourceExpr=OrigEmployeeNo;
                  OnValidate=BEGIN
                               ValidateOrigEmployeeNo(OrigEmployeeNo, TRUE);
                             END;

                  OnLookup=VAR
                             Employee@1100528401 : Record 5200;
                           BEGIN
                             IF PAGE.RUNMODAL(0, Employee) = ACTION::LookupOK THEN BEGIN
                                OrigEmployeeNo := Employee."No.";
                                ValidateOrigEmployeeNo(OrigEmployeeNo, TRUE);
                             END;
                           END;
                            }

      { 1100485003;3;Field  ;
                  CaptionML=ENU=Name;
                  SourceExpr=OrigEmployeeName;
                  Editable=FALSE }

      { 1100528400;3;Field  ;
                  CaptionML=ENU=From Effective Date;
                  SourceExpr=FromEffectiveDate }

      { 1100528401;3;Field  ;
                  CaptionML=ENU=To Effective Date;
                  SourceExpr=ToEffectiveDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempOrigEmployeeTimeTable@1100528402 : TEMPORARY Record 11012923;
      OrigEmployeeNo@1100485000 : Code[20];
      OrigEmployeeName@1100485001 : Text[100];
      FromEffectiveDate@1100528400 : Date;
      ToEffectiveDate@1100528401 : Date;
      Text000@1100528403 : TextConst 'ENU="""To Effective Date"" must not be before ""From Effective Date""."';

    LOCAL PROCEDURE ValidateOrigEmployeeNo@1100485000(EmployeeNo@1100528400 : Code[20];ThrowError@1100528402 : Boolean);
    VAR
      Employee@1100528401 : Record 5200;
    BEGIN
      IF ThrowError THEN
        Employee.GET(EmployeeNo)
      ELSE
        IF NOT Employee.GET(EmployeeNo) THEN
          Employee.INIT;

      OrigEmployeeName := Employee."Full Name";
      ToEffectiveDate := GetLastEmplTimeTableEffectivDate(EmployeeNo);
    END;

    LOCAL PROCEDURE FillTempOrigEmployeeTimeTable@1100528401(OrigEmployeeNo@1100528403 : Code[20];FromEffectiveDate@1100528402 : Date;ToEffectiveDate@1100528401 : Date);
    VAR
      EmployeeTimeTable@1100528400 : Record 11012923;
    BEGIN
      CLEAR(TempOrigEmployeeTimeTable);
      EmployeeTimeTable.SETRANGE("Employee No.", OrigEmployeeNo);
      EmployeeTimeTable.SETRANGE("Effective Date", FromEffectiveDate, ToEffectiveDate);
      IF EmployeeTimeTable.FINDSET THEN
        REPEAT
          TempOrigEmployeeTimeTable := EmployeeTimeTable;
          TempOrigEmployeeTimeTable.INSERT;
        UNTIL EmployeeTimeTable.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyEmployeeTimeTable@1100528403(DestEmployeeNo@1100528401 : Code[20]);
    VAR
      EmployeeTimeTable@1100528400 : Record 11012923;
    BEGIN
      IF TempOrigEmployeeTimeTable.FINDSET THEN
        REPEAT
          IF EmployeeTimeTable.GET(DestEmployeeNo, TempOrigEmployeeTimeTable."Effective Date") THEN BEGIN
            EmployeeTimeTable.TRANSFERFIELDS(TempOrigEmployeeTimeTable, FALSE);
            EmployeeTimeTable.MODIFY;
          END ELSE BEGIN
            EmployeeTimeTable := TempOrigEmployeeTimeTable;
            EmployeeTimeTable."Employee No." := DestEmployeeNo;
            EmployeeTimeTable.INSERT;
          END;
        UNTIL TempOrigEmployeeTimeTable.NEXT = 0;
    END;

    LOCAL PROCEDURE GetLastEmplTimeTableEffectivDate@1100528400(EmployeeNo@1100528400 : Code[20]) : Date;
    VAR
      EmployeeTimeTable@1100528401 : Record 11012923;
    BEGIN
      EmployeeTimeTable.SETRANGE("Employee No.", EmployeeNo);
      IF EmployeeTimeTable.FINDLAST THEN
        EXIT(EmployeeTimeTable."Effective Date");
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

