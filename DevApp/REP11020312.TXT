OBJECT Report 11020312 Process Empoly Warehouse Order
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Process Empoli Warehouse Orders;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   IF EndDate = 0D THEN
                     EndDate := TODAY;
                 END;

    OnPreReport=BEGIN
                  IF EndDate = 0D THEN
                    ERROR(Text000);
                  IF EndDate < StartDate THEN
                    ERROR(Text001);
                END;

  }
  DATASET
  {
    { 7475;    ;DataItem;OrderHeader         ;
               DataItemTable=Table11126687;
               OnPreDataItem=BEGIN
                               SETRANGE(Status, Status::Finished);
                               SETRANGE("Order Date", StartDate, EndDate);
                               TmpOrder.DELETEALL;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF (Process = Process::"Project Delivery") OR (Process = Process::"Project Retour") THEN
                                    TESTFIELD("Project No.");
                                  IF (Process = Process::"Service Delivery") OR (Process = Process::"Service Retour") THEN
                                    TESTFIELD("Service Order No.");
                                  IF (Process = Process::"Phys. Inventory") THEN
                                    TESTFIELD("Location Code");
                                  IF (Process = Process::"Stock Transfer") THEN BEGIN
                                    TESTFIELD("Location Code");
                                    TESTFIELD("To Location Code");
                                  END;

                                  OrderLine.SETRANGE("Order No.", OrderHeader."No.");
                                  IF OrderLine.FINDSET THEN BEGIN
                                    REPEAT
                                      IF (Process <> Process::"Phys. Inventory") THEN
                                        OrderLine.TESTFIELD(Quantity);
                                      IF (Process = Process::"Project Delivery") OR
                                         (Process = Process::"Service Delivery") OR
                                         (Process = Process::"Project Retour") OR
                                         (Process = Process::"Service Retour")
                                      THEN
                                        WarehouseMgt.CreateItemJnlLineDelivery(OrderHeader,OrderLine);
                                      IF (Process = Process::"Phys. Inventory") THEN
                                        WarehouseMgt.CreateItemJnlLinePhysInventory(OrderHeader,OrderLine);
                                      IF (Process = Process::"Stock Transfer") THEN
                                        WarehouseMgt.CreateItemJnlLineStockTransfer(OrderHeader,OrderLine);
                                      IF (Process = Process::"Purch. Requisition") THEN
                                        WarehouseMgt.CreateReqLine(OrderHeader,OrderLine);
                                    UNTIL OrderLine.NEXT = 0;
                                  END;
                                  TmpOrder := OrderHeader;
                                  IF TmpOrder.INSERT THEN;
                                END;

               OnPostDataItem=BEGIN
                                IF TmpOrder.FINDSET THEN BEGIN
                                  REPEAT
                                    OrderHeader.GET(TmpOrder."No.");
                                    HistOrder.INIT;
                                    HistOrder.TRANSFERFIELDS(OrderHeader);
                                    HistOrder.INSERT;
                                    OrderLine.SETRANGE("Order No.", OrderHeader."No.");
                                    IF OrderLine.FINDSET THEN BEGIN
                                      REPEAT
                                        HistOrderLine.INIT;
                                        HistOrderLine.TRANSFERFIELDS(OrderLine);
                                        HistOrderLine.INSERT;
                                      UNTIL OrderLine.NEXT = 0;
                                    END;
                                    OrderHeader.DELETE(TRUE);
                                  UNTIL TmpOrder.NEXT = 0;
                                END;
                              END;

               ReqFilterFields=Process }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525003;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 1100525001;2;Field  ;
                  CaptionML=ENU=Starting Date;
                  SourceExpr=StartDate }

      { 1100525002;2;Field  ;
                  CaptionML=ENU=Ending Date;
                  SourceExpr=EndDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      StartDate@1210190001 : Date;
      EndDate@1210190000 : Date;
      Text000@1210190003 : TextConst 'ENU=The End Date must be filled.';
      Text001@1210190002 : TextConst 'ENU=Start Date is less than the End Date.';
      OrderLine@1100525004 : Record 11126688;
      TmpOrder@1100525003 : TEMPORARY Record 11126687;
      HistOrder@1100525002 : Record 11126689;
      HistOrderLine@1100525001 : Record 11126690;
      WarehouseMgt@1100525000 : Codeunit 11126681;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

