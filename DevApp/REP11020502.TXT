OBJECT Report 11020502 Copy Planttype to Cost Object
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Planttype to Cost Object;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   GLSetupRec.GET;
                   GLSetupRec.TESTFIELD("Global Dimension 2 Code");
                 END;

    OnPreReport=BEGIN
                  IF UpdatePrices AND (RefDate = 0D) THEN
                    ERROR(Text000);

                  Window.OPEN(Text001);
                END;

  }
  DATASET
  {
    { 8651;    ;DataItem;                    ;
               DataItemTable=Table11012551;
               DataItemTableView=SORTING(Code);
               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1, Code);

                                  IF UpdatePrices THEN BEGIN
                                    CASE "Rental Type" OF
                                      "Rental Type"::Rental,
                                      "Rental Type"::Both:
                                        PlantRate := SearchRateCU.Rate('',Code,'','',RefDate,0,0,'');
                                      "Rental Type"::"Sales/Buy Back":
                                        PlantRate := SearchRateCU.Rate('',Code,'','',RefDate,1,0,'');
                                      ELSE
                                        PlantRate := 0;
                                    END;
                                  END;

                                  IF NOT DimValRec.GET(GLSetupRec."Global Dimension 2 Code", Code) THEN BEGIN
                                    DimValRec.INIT;
                                    DimValRec."Dimension Code" := GLSetupRec."Global Dimension 2 Code";
                                    DimValRec.Code := Code;
                                    DimValRec.Name := Description;
                                    DimValRec."Dimension Value Type" := DimValRec."Dimension Value Type"::Standard;
                                    DimValRec."Global Dimension No." := 2;
                                    DimValRec."Cost Type" := DimValRec."Cost Type"::Plant;
                                    IF UpdatePrices THEN
                                      DimValRec.Price := PlantRate;
                                    DimValRec."Unit of Measure" := "Unit of Measure";
                                    DimValRec.INSERT(TRUE);
                                  END ELSE BEGIN
                                    IF (DimValRec."Dimension Value Type" = DimValRec."Dimension Value Type"::Standard) AND
                                       (DimValRec."Cost Type" = DimValRec."Cost Type"::Plant)
                                    THEN BEGIN
                                      DimValRec.Name := Description;
                                      IF UpdatePrices AND (PlantRate > 0) THEN
                                        DimValRec.Price := PlantRate;
                                      DimValRec.MODIFY(TRUE);
                                    END;
                                  END;
                                END;

               ReqFilterFields=Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   RefDate := 0D;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1100485000;2;Field  ;
                  CaptionML=ENU=Update Prices;
                  SourceExpr=UpdatePrices }

      { 1100485002;2;Field  ;
                  CaptionML=ENU=Reference Date;
                  SourceExpr=RefDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GLSetupRec@1100485001 : Record 98;
      DimValRec@1100485000 : Record 349;
      SearchRateCU@1100485002 : Codeunit 11012567;
      Window@1100485007 : Dialog;
      RefDate@1100485005 : Date;
      UpdatePrices@1100485004 : Boolean;
      PlantRate@1100485003 : Decimal;
      Text000@1100485006 : TextConst 'ENU=Reference Date must be filled.';
      Text001@1100485008 : TextConst 'ENU=Plant type #1##########';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

