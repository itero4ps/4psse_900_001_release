OBJECT Report 11020510 Correct Transport Order Date
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Correct Transport Date;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  gPostedTransOrderRec.GET(gPostedTransOrderRec."No.");
                  CheckMainTransportOrder();
                  CheckChangeOrderDateAllowed();
                  IF NewTransportDate = 0D THEN
                    ERROR(Text002);

                  IF NOT CONFIRM(Text000, FALSE, gPostedTransOrderRec."Transport Date", NewTransportDate) THEN
                    ERROR('');
                END;

  }
  DATASET
  {
    { 8514;    ;DataItem;                    ;
               DataItemTable=Table11020507;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               SETRANGE("No.", gPostedTransOrderRec."No.");
                             END;

               OnAfterGetRecord=BEGIN
                                  IF NewTransportDate <> "Transport Date" THEN BEGIN
                                    "Transport Date" := NewTransportDate;
                                    MODIFY;
                                  END;
                                END;
                                 }

    { 3168;1   ;DataItem;                    ;
               DataItemTable=Table11020515;
               DataItemTableView=SORTING(Transport Order No.,Linked Transport Order No.);
               OnAfterGetRecord=VAR
                                  lvPostedTransOrderRec@1100525000 : Record 11020507;
                                BEGIN
                                  IF lvPostedTransOrderRec.GET("Posted Linked Pl Trans. Orders"."Linked Transport Order No.") THEN BEGIN
                                    IF NewTransportDate <> lvPostedTransOrderRec."Transport Date" THEN BEGIN
                                      lvPostedTransOrderRec."Transport Date" := NewTransportDate;
                                      lvPostedTransOrderRec.MODIFY;
                                    END;
                                  END;
                                END;

               DataItemLink=Transport Order No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1100525004;2;Field  ;
                  CaptionML=ENU=Transport Order No.;
                  SourceExpr=gPostedTransOrderRec."No.";
                  Editable=FALSE }

      { 1100525000;2;Field  ;
                  CaptionML=ENU=Transport Date;
                  SourceExpr=gPostedTransOrderRec."Transport Date";
                  Editable=FALSE }

      { 1100525001;2;Field  ;
                  CaptionML=ENU=New Transport Date;
                  SourceExpr=NewTransportDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      gPostedTransOrderRec@1100525000 : Record 11020507;
      NewTransportDate@1100525001 : Date;
      Text000@1100525003 : TextConst 'ENU=Do you want to modify the Transport Date ''%1'' in ''%2''?';
      Text001@1100525002 : TextConst 'ENU=Transport Order ''%1'' is a linked order, date must be modified on the Main Transport Order ''%2''.';
      Text002@1100525004 : TextConst 'ENU=Select a new date.';

    PROCEDURE SetTransportOrder@1100525000(iTransOrderNo@1100525000 : Code[20]);
    BEGIN
      gPostedTransOrderRec.GET(iTransOrderNo);
      CheckMainTransportOrder();
      CheckChangeOrderDateAllowed();
    END;

    PROCEDURE CheckMainTransportOrder@1100525001();
    BEGIN
      gPostedTransOrderRec.CALCFIELDS("Invoice with Transport Order");
      IF gPostedTransOrderRec."Invoice with Transport Order" <> '' THEN
        ERROR(Text001, gPostedTransOrderRec."No.", gPostedTransOrderRec."Invoice with Transport Order");
    END;

    PROCEDURE CheckChangeOrderDateAllowed@1100525003();
    BEGIN
      //* Only allowed when not yet invoiced (Transport Costs: From/To Loc, Project, Internal).
      //* Note: The fields '... Invoiced' are also set to 'True' when there are no lines, so also check
      //* check if the 'Invoice No.' is filled (only filled when lines present and invoiced).

      IF (gPostedTransOrderRec."From Location Invoiced" AND (gPostedTransOrderRec."From Location Invoice No." <> '')) THEN
        gPostedTransOrderRec.TESTFIELD("From Location Invoiced", FALSE);

      IF (gPostedTransOrderRec."To Location Invoiced" AND (gPostedTransOrderRec."To Location Invoice No." <> '')) THEN
        gPostedTransOrderRec.TESTFIELD("To Location Invoiced", FALSE);

      IF (gPostedTransOrderRec."Project Invoiced"  AND (gPostedTransOrderRec."Project Invoice No." <> '')) THEN
        gPostedTransOrderRec.TESTFIELD("Project Invoiced", FALSE);

      IF (gPostedTransOrderRec."Internal Invoiced"  AND (gPostedTransOrderRec."Internal Invoice No." <> '')) THEN
        gPostedTransOrderRec.TESTFIELD("Internal Invoiced", FALSE);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

