OBJECT Report 11072018 Copy Prognosis Details
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Prognosis Details;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   IF YearFrom = 0 THEN
                     YearFrom := DATE2DMY(WORKDATE,3);

                   IF YearTo = 0 THEN
                     YearTo := YearFrom + 1;
                 END;

    OnPreReport=BEGIN
                  IF "Prognosis Detail".GETFILTER("Source Code") = '' THEN
                    ERROR(Text000)
                  ELSE
                    IF ("Prognosis Detail".GETRANGEMIN("Source Code") <> "Prognosis Detail".GETRANGEMAX("Source Code")) AND
                       (NewSourceNo <> '')
                    THEN
                      ERROR(Text000);
                END;

  }
  DATASET
  {
    { 1100528600;;DataItem;                  ;
               DataItemTable=Table11012884;
               OnPreDataItem=BEGIN
                               SETRANGE(Date, DMY2DATE(1, 1, YearFrom), DMY2DATE(31, 12, YearFrom));
                             END;

               OnAfterGetRecord=VAR
                                  PrognosisDetail@1100528600 : Record 11012884;
                                  DayNo@1100528601 : Integer;
                                  Month@1100528602 : Integer;
                                BEGIN
                                  PrognosisDetail := "Prognosis Detail";
                                  PrognosisDetail."Entry No." := 0;
                                  DayNo := DATE2DMY(Date, 1);
                                  Month := DATE2DMY(Date, 2);
                                  PrognosisDetail.Date := DMY2DATE(DayNo, Month, YearTo);
                                  IF NewSourceNo <> '' THEN
                                    CASE OccupationType OF
                                      OccupationType::Employee:
                                        PrognosisDetail."Source Code" := NewSourceNo;
                                      OccupationType::Department:
                                        PrognosisDetail."Global Dimension 1 Code" := NewSourceNo;
                                      OccupationType::Territory:
                                        PrognosisDetail."Territory Code" := NewSourceNo;
                                  END;
                                  PrognosisDetail.INSERT(TRUE);
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100528600;;Container;
                  ContainerType=ContentArea }

      { 1100528601;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 1100528602;2;Field  ;
                  CaptionML=ENU=From Year;
                  SourceExpr=YearFrom }

      { 1100528603;2;Field  ;
                  CaptionML=ENU=To Year;
                  SourceExpr=YearTo }

      { 1100528604;2;Field  ;
                  CaptionML=ENU=To Source No.;
                  SourceExpr=NewSourceNo;
                  OnLookup=VAR
                             Employee@1100528600 : Record 5200;
                             ServiceContract@1100528601 : Record 11012812;
                             DimensionValue@1100528603 : Record 349;
                             Territory@1100528604 : Record 286;
                           BEGIN
                             CASE Type OF
                               "Prognosis Detail".Type::Occupation:
                                 CASE OccupationType OF
                                   OccupationType::Employee:
                                     IF PAGE.RUNMODAL(0, Employee) = ACTION::LookupOK THEN
                                       NewSourceNo := Employee."No.";
                                   OccupationType::Department:
                                     BEGIN
                                       DimensionValue.SETRANGE("Global Dimension No.", 1);
                                       IF PAGE.RUNMODAL(0, DimensionValue) = ACTION::LookupOK THEN
                                         NewSourceNo := DimensionValue.Code;
                                     END;
                                   OccupationType::Territory:
                                     BEGIN
                                       IF PAGE.RUNMODAL(0, Territory) = ACTION::LookupOK THEN
                                         NewSourceNo := Territory.Code;
                                     END;
                                 END;
                               "Prognosis Detail".Type::Workload:
                                 IF PAGE.RUNMODAL(0, ServiceContract) = ACTION::LookupOK THEN
                                   NewSourceNo := ServiceContract."No.";
                             END;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      NewSourceNo@1100528603 : Code[20];
      YearFrom@1100528600 : Integer;
      YearTo@1100528601 : Integer;
      Type@1100528602 : 'Occupation,Workload';
      Text000@1100528604 : TextConst 'ENU=Cannot copy to different Source No., more then one original selected.';
      NextEntryNo@1100528605 : Integer;
      OccupationType@1100528606 : 'Employee,Department,Territory';

    PROCEDURE SetToYear@1100485000(IYearNo@1100485000 : Integer);
    BEGIN
      YearFrom := IYearNo;
      YearTo := YearFrom + 1;
    END;

    PROCEDURE SetType@1100485001(IType@1100485000 : Option);
    BEGIN
      Type := IType;
    END;

    PROCEDURE SetOccupationType@1100528600(IOccupationType@1100528600 : 'Employee,Department,Territory');
    BEGIN
      OccupationType := IOccupationType;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

