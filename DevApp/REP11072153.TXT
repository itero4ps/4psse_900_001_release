OBJECT Report 11072153 Copy Service Survey Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Structure Lines;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 9890;    ;DataItem;OriginalStructureLine;
               DataItemTable=Table11071806;
               DataItemTableView=SORTING(Survey No.,Location Code,Structure No.,Code,Line No.);
               OnPreDataItem=BEGIN
                               SETRANGE("Survey No.", FromSurveyNo);
                               SETRANGE("Location Code", FromLocationCode);
                               SETRANGE("Structure No.", FromStructureNo);
                               SETRANGE(Code, FromCode);
                               SETRANGE("Line No.", 0);
                             END;

               OnAfterGetRecord=BEGIN
                                  StartProcess;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525007;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 1100525001;2;Group  ;
                  CaptionML=ENU=Copy From;
                  GroupType=Group }

      { 1100525002;3;Field  ;
                  CaptionML=ENU=Survey;
                  SourceExpr=FromSurveyNo;
                  Editable=FALSE }

      { 1100525003;3;Field  ;
                  CaptionML=ENU=Survey Description;
                  SourceExpr=FromSurveyDescription;
                  Editable=FALSE }

      { 1100525008;3;Field  ;
                  CaptionML=ENU=Location;
                  SourceExpr=FromLocationCode;
                  Editable=FALSE }

      { 1100525010;3;Field  ;
                  CaptionML=ENU=Structure;
                  SourceExpr=FromStructureNo;
                  Editable=FALSE }

      { 1100525012;3;Field  ;
                  CaptionML=ENU=Code;
                  SourceExpr=FromCode;
                  Editable=FALSE }

      { 1100525014;3;Field  ;
                  CaptionML=ENU=Code Description;
                  SourceExpr=FromCodeDescription;
                  Editable=FALSE }

      { 1100525004;2;Group  ;
                  CaptionML=ENU=Copy To;
                  GroupType=Group }

      { 1100525005;3;Field  ;
                  CaptionML=ENU=Survey;
                  SourceExpr=ToSurveyNo;
                  Editable=FALSE }

      { 1100525006;3;Field  ;
                  CaptionML=ENU=Survey Description;
                  SourceExpr=ToSurveyDescription;
                  Editable=FALSE }

      { 1100525009;3;Field  ;
                  CaptionML=ENU=Location;
                  SourceExpr=ToLocationCode;
                  Editable=FALSE }

      { 1100525011;3;Field  ;
                  CaptionML=ENU=Structure;
                  SourceExpr=ToStructureNo;
                  Editable=FALSE }

      { 1100525013;3;Field  ;
                  Name=ToCode;
                  CaptionML=ENU=Code;
                  SourceExpr=ToCode;
                  OnValidate=BEGIN
                               ValidateToCode;
                             END;

                  OnLookup=VAR
                             SurveyStructureLine@1100525001 : Record 11071806;
                           BEGIN
                             SurveyStructureLine.SETRANGE("Survey No.", ToSurveyNo);
                             SurveyStructureLine.SETRANGE("Location Code", ToLocationCode);
                             SurveyStructureLine.SETRANGE("Structure No.", ToStructureNo);
                             IF PAGE.RUNMODAL(0, SurveyStructureLine) = ACTION::LookupOK THEN BEGIN
                               ToCode := SurveyStructureLine.Code;
                               ValidateToCode;
                             END;
                           END;
                            }

      { 1100525015;3;Field  ;
                  CaptionML=ENU=Code Description;
                  SourceExpr=ToCodeDescription }

      { 1100525016;3;Field  ;
                  CaptionML=ENU=Process;
                  SourceExpr=CopyMethod;
                  Editable=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      FromSurveyNo@1100528601 : Code[20];
      ToSurveyNo@1100528602 : Code[20];
      FromSurveyDescription@1100528603 : Text[50];
      ToSurveyDescription@1100528604 : Text[50];
      FromLocationCode@1100525001 : Code[20];
      ToLocationCode@1100525000 : Code[20];
      FromStructureNo@1100525002 : Integer;
      ToStructureNo@1100525003 : Integer;
      FromCode@1100525004 : Code[20];
      ToCode@1100525005 : Code[20];
      FromCodeDescription@1100525007 : Text[50];
      ToCodeDescription@1100525006 : Text[50];
      CopyObjectsOnly@1100525008 : Boolean;
      Text000@1100525014 : TextConst 'ENU=Structuur Line %1 with Level 0 can not be processed by this procedure';
      Text001@1100525009 : TextConst 'ENU=Destination Code already exists';
      Text002@1100525010 : TextConst 'ENU=Level Source Code does not match with Level Desctionation Code';
      CopyMethod@1100525011 : Text[250];
      Text003@1100525012 : TextConst 'ENU=Only Structure Lines with Object will be copied';
      Text004@1100525013 : TextConst 'ENU=Copy all structure lines';
      sp1@1100525015 : Integer;
      sp2@1100525016 : Integer;

    PROCEDURE SetDefaults@1100528604(ISurveyNo@1100528600 : Code[20];ILocationCode@1100525000 : Code[20];IStructureNo@1100525003 : Integer;ICode@1100525005 : Code[20]);
    VAR
      ServiceSurvey@1100528601 : Record 11071803;
      SurveyLocation@1100525001 : Record 11071804;
      SurveyStructure@1100525002 : Record 11071805;
      SurveyStructureLine@1100525004 : Record 11071806;
    BEGIN
      FromSurveyNo := ISurveyNo;
      ServiceSurvey.GET(FromSurveyNo);
      FromSurveyDescription := ServiceSurvey.Description;
      ToSurveyNo := FromSurveyNo;
      ToSurveyDescription := FromSurveyDescription;
      //
      FromLocationCode := ILocationCode;
      SurveyLocation.GET(ISurveyNo, ILocationCode);
      ToLocationCode := FromLocationCode;
      //
      FromStructureNo := IStructureNo;
      SurveyStructure.GET(ISurveyNo, ILocationCode, IStructureNo);
      ToStructureNo := FromStructureNo;
      //
      FromCode := ICode;
      SurveyStructureLine.SETRANGE("Survey No.", FromSurveyNo);
      SurveyStructureLine.SETRANGE("Location Code", FromLocationCode);
      SurveyStructureLine.SETRANGE("Structure No.", FromStructureNo);
      SurveyStructureLine.SETRANGE(Code, FromCode);
      SurveyStructureLine.FINDSET;
      IF SurveyStructureLine.Level = 0 THEN
        ERROR(Text000, SurveyStructureLine.Code);

      FromCodeDescription := SurveyStructureLine."Code Description";
      //ToCode := FromCode;
      ToCodeDescription := FromCodeDescription;

      CheckFormat;
    END;

    LOCAL PROCEDURE ValidateToCode@1100525000();
    VAR
      SurveyStructureLine@1100525002 : Record 11071806;
      TmpSurveyStructureLine@1100525001 : TEMPORARY Record 11071806;
      FromLevel@1100525000 : Integer;
    BEGIN
      CopyObjectsOnly := FALSE;

      IF STRLEN(ToCode) < STRLEN(FromCode) THEN BEGIN
        TmpSurveyStructureLine."Survey No." := ToSurveyNo;
        TmpSurveyStructureLine."Location Code" := ToLocationCode;
        TmpSurveyStructureLine."Structure No." := ToStructureNo;
        TmpSurveyStructureLine.VALIDATE(Code, ToCode);  //format code
        ToCode := TmpSurveyStructureLine.Code;
      END;

      SurveyStructureLine.SETRANGE("Survey No.", ToSurveyNo);
      SurveyStructureLine.SETRANGE("Location Code", ToLocationCode);
      SurveyStructureLine.SETRANGE("Structure No.", ToStructureNo);
      SurveyStructureLine.SETRANGE(Code, ToCode);
      IF SurveyStructureLine.FINDSET THEN BEGIN
        ToCodeDescription := SurveyStructureLine."Code Description";
        CopyObjectsOnly := TRUE;
        MESSAGE(Text001);
      END;

      IF CopyObjectsOnly = FALSE THEN BEGIN
        TmpSurveyStructureLine."Survey No." := FromSurveyNo;
        TmpSurveyStructureLine."Location Code" := FromLocationCode;
        TmpSurveyStructureLine."Structure No." := FromStructureNo;
        TmpSurveyStructureLine.VALIDATE(Code, FromCode);
        FromLevel := TmpSurveyStructureLine.Level;
        //
        TmpSurveyStructureLine."Survey No." := ToSurveyNo;
        TmpSurveyStructureLine."Location Code" := ToLocationCode;
        TmpSurveyStructureLine."Structure No." := ToStructureNo;
        TmpSurveyStructureLine.VALIDATE(Code, ToCode);
        IF TmpSurveyStructureLine.Level <> FromLevel THEN BEGIN
          CopyObjectsOnly := TRUE;
          MESSAGE(Text002);
        END;
      END;

      IF CopyObjectsOnly THEN
        CopyMethod := Text003
      ELSE
        CopyMethod := Text004;
    END;

    LOCAL PROCEDURE StartProcess@1100525001();
    VAR
      SurveyStructureLine@1100525008 : Record 11071806;
      TmpSurveyStructureLine@1100525007 : TEMPORARY Record 11071806;
      SurveyLocationObject@1100525006 : Record 11071813;
      TmpSurveyLocationObject@1100525005 : TEMPORARY Record 11071813;
      SurveyObjectDetail@1100525004 : Record 11071807;
      TmpSurveyObjectDetail@1100525003 : TEMPORARY Record 11071807;
      SurveyObjectAddAct@1100525002 : Record 11071814;
      TmpSurveyObjectAddAct@1100525001 : TEMPORARY Record 11071814;
      LastSeqNo@1100525000 : Integer;
      LastLineNo@1100525009 : Integer;
      NewCode@1100525010 : Code[20];
    BEGIN
      WITH OriginalStructureLine DO BEGIN
        SurveyLocationObject.SETRANGE("Survey No.", "Survey No.");
        SurveyLocationObject.SETRANGE("Location Code", "Location Code");
        IF SurveyLocationObject.FINDLAST THEN
          LastSeqNo := SurveyLocationObject."Seq. No.";
        //
        IF CopyObjectsOnly THEN BEGIN
          TmpSurveyStructureLine.SETRANGE("Survey No.", ToSurveyNo);
          TmpSurveyStructureLine.SETRANGE("Location Code", ToLocationCode);
          TmpSurveyStructureLine.SETRANGE("Structure No.", ToStructureNo);
          TmpSurveyStructureLine.SETRANGE(Code, ToCode);
          IF TmpSurveyStructureLine.FINDLAST THEN
            LastLineNo := TmpSurveyStructureLine."Line No.";
        END;
        //
        SurveyStructureLine.SETRANGE("Survey No.", "Survey No.");
        SurveyStructureLine.SETRANGE("Location Code", "Location Code");
        SurveyStructureLine.SETRANGE("Structure No.", "Structure No.");
        IF Level = 1 THEN
          SurveyStructureLine.SETRANGE("Element Level 1", Code);
        IF Level = 2 THEN
          SurveyStructureLine.SETRANGE("Element Level 2", Code);
        IF Level = 3 THEN
          SurveyStructureLine.SETRANGE(Code, Code);
        IF CopyObjectsOnly THEN
          SurveyStructureLine.SETRANGE("Object Line", TRUE);
        IF SurveyStructureLine.FINDSET THEN BEGIN
           REPEAT
             TmpSurveyStructureLine := SurveyStructureLine;
             NewCode := ToCode;
             IF NOT CopyObjectsOnly THEN BEGIN
               IF Level = 1 THEN
                 NewCode := COPYSTR(ToCode,1,sp1) + COPYSTR(SurveyStructureLine.Code,sp1+1,STRLEN(FromCode)-sp1);
               IF Level = 2 THEN
                 NewCode := COPYSTR(ToCode,1,sp2) + COPYSTR(SurveyStructureLine.Code,sp2+1,STRLEN(FromCode)-sp2);
             END;
             //TmpSurveyStructureLine.VALIDATE(Code, NewCode);
             TmpSurveyStructureLine.Code := NewCode;  //C028336 (db, 07-01-16)
             IF SurveyStructureLine."Object Line" THEN BEGIN
               TmpSurveyStructureLine."Seq. No." := LastSeqNo + 1;
               LastSeqNo := TmpSurveyStructureLine."Seq. No.";
             END ELSE BEGIN
               IF TmpSurveyStructureLine.Code = ToCode THEN
                 TmpSurveyStructureLine."Code Description":= ToCodeDescription;
             END;
             IF CopyObjectsOnly THEN BEGIN
               TmpSurveyStructureLine."Line No." := LastLineNo + 1;
               LastLineNo := TmpSurveyStructureLine."Line No.";
             END;
             TmpSurveyStructureLine.INSERT;
             //
             IF SurveyStructureLine."Object Line" THEN BEGIN
               SurveyLocationObject.SETRANGE("Survey No.", SurveyStructureLine."Survey No.");
               SurveyLocationObject.SETRANGE("Location Code", SurveyStructureLine."Location Code");
               SurveyLocationObject.SETRANGE("Seq. No.", SurveyStructureLine."Seq. No.");
               IF SurveyLocationObject.FINDSET THEN BEGIN
                 REPEAT
                   TmpSurveyLocationObject := SurveyLocationObject;
                   TmpSurveyLocationObject."Seq. No." := TmpSurveyStructureLine."Seq. No.";
                   TmpSurveyLocationObject.INSERT;
                 UNTIL SurveyLocationObject.NEXT = 0;
               END;
               //
               SurveyObjectDetail.SETRANGE("Survey No.", SurveyStructureLine."Survey No.");
               SurveyObjectDetail.SETRANGE("Location Code", SurveyStructureLine."Location Code");
               SurveyObjectDetail.SETRANGE("Seq. No.", SurveyStructureLine."Seq. No.");
               IF SurveyObjectDetail.FINDSET THEN BEGIN
                 REPEAT
                   TmpSurveyObjectDetail := SurveyObjectDetail;
                   TmpSurveyObjectDetail."Seq. No." := TmpSurveyStructureLine."Seq. No.";
                   TmpSurveyObjectDetail.INSERT;
                 UNTIL SurveyObjectDetail.NEXT = 0;
               END;
               //
               SurveyObjectAddAct.SETRANGE("Survey No.", SurveyStructureLine."Survey No.");
               SurveyObjectAddAct.SETRANGE("Location Code", SurveyStructureLine."Location Code");
               SurveyObjectAddAct.SETRANGE("Seq. No.", SurveyStructureLine."Seq. No.");
               IF SurveyObjectAddAct.FINDSET THEN BEGIN
                 REPEAT
                   TmpSurveyObjectAddAct := SurveyObjectAddAct;
                   TmpSurveyObjectAddAct."Seq. No." := TmpSurveyStructureLine."Seq. No.";
                   TmpSurveyObjectAddAct.INSERT;
                 UNTIL SurveyObjectAddAct.NEXT = 0;
               END;
             END;
           UNTIL SurveyStructureLine.NEXT = 0;
        END;
      END;

      IF TmpSurveyStructureLine.FINDSET THEN BEGIN
        REPEAT
          SurveyStructureLine := TmpSurveyStructureLine;
          SurveyStructureLine.VALIDATE(Code, TmpSurveyStructureLine.Code);  //C028336 (db, 07-01-16)
          SurveyStructureLine.INSERT;
        UNTIL TmpSurveyStructureLine.NEXT = 0;
      END;
      IF TmpSurveyLocationObject.FINDSET THEN BEGIN
        REPEAT
          SurveyLocationObject := TmpSurveyLocationObject;
          SurveyLocationObject.INSERT;
        UNTIL TmpSurveyLocationObject.NEXT = 0;
      END;
      IF TmpSurveyObjectDetail.FINDSET THEN BEGIN
        REPEAT
          SurveyObjectDetail := TmpSurveyObjectDetail;
          SurveyObjectDetail.INSERT;
        UNTIL TmpSurveyObjectDetail.NEXT = 0;
      END;
      IF TmpSurveyObjectAddAct.FINDSET THEN BEGIN
        REPEAT
          SurveyObjectAddAct := TmpSurveyObjectAddAct;
          SurveyObjectAddAct.INSERT;
        UNTIL TmpSurveyObjectAddAct.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CheckFormat@16();
    VAR
      lvText000@1100525000 : TextConst 'ENU=only 3 levels allowed';
      x@1100525001 : Integer;
      lp@1100525002 : Integer;
      TmpSurveyStructureLine@1100525003 : TEMPORARY Record 11071806;
    BEGIN
      x := 0;
      sp1 := 0;
      sp2 := 0;
      lp := STRLEN(FromCode);

      REPEAT
        x := x + 1;
        IF COPYSTR(FromCode,x,1) = '.' THEN BEGIN
          IF sp1 = 0 THEN
            sp1 := x
          ELSE
            IF sp2 = 0 THEN
              sp2 := x
            ELSE
              ERROR(lvText000);
        END;
      UNTIL x = lp;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

