OBJECT Report 11125366 IFC Object - Create Pr.Objects
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=IFC Object - Create Project Objects;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   Selected := FALSE;
                 END;

    OnPreReport=BEGIN
                  IF (CreateFor = CreateFor::Current) OR (CreateFor = CreateFor::Selection) THEN BEGIN
                    ValidateItemNo(FALSE);
                    ValidateCostObject;
                    Selected := TRUE;
                  END;
                  IF (CreateFor = CreateFor::Link) THEN
                    Selected := TRUE;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   IF CurrentLinked THEN
                     CreateFor := CreateFor::Selection;
                   SetEditable;
                 END;

      OnQueryClosePage=BEGIN
                         IF (CloseAction = ACTION::OK) AND ((CreateFor = CreateFor::Current) OR (CreateFor = CreateFor::Selection)) THEN BEGIN
                           ValidateItemNo(FALSE);
                           ValidateCostObject;
                         END;
                       END;

    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525002;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 1100525007;2;Field  ;
                  CaptionML=ENU=Create for;
                  OptionCaptionML=ENU=Current,Selection,Link;
                  SourceExpr=CreateFor;
                  Editable=CreateForEditable;
                  OnValidate=BEGIN
                               SetEditable;
                             END;
                              }

      { 1100525001;2;Field  ;
                  CaptionML=ENU=Item No.;
                  SourceExpr=ItemNo;
                  TableRelation=Item;
                  Editable=DefaultFieldsEditable;
                  OnValidate=BEGIN
                               ValidateItemNo(TRUE);
                             END;
                              }

      { 1100529000;2;Field  ;
                  CaptionML=ENU=Cost Object.;
                  SourceExpr=CostObject;
                  TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                  Editable=DefaultFieldsEditable;
                  OnValidate=BEGIN
                               ValidateCostObject;
                             END;
                              }

      { 1100525003;2;Field  ;
                  CaptionML=ENU=(Part of) Name;
                  SourceExpr=PartOfIfcName;
                  Editable=SelectionFieldsEditable }

      { 1100529001;2;Field  ;
                  CaptionML=ENU=Property Actions;
                  OptionCaptionML=ENU=None,Copy,Filter,Copy & Filter;
                  SourceExpr=PropertyActions;
                  Editable=PropertyActionsEditable;
                  OnAssistEdit=VAR
                                 TmpProperty@1100529000 : TEMPORARY Record 11020579;
                                 CopySelected@1100529001 : Boolean;
                                 FilterSelected@1100529002 : Boolean;
                               BEGIN
                                 TmpIfcObjectProperty.Bool4 := TRUE;
                                 TmpIfcObjectProperty.Bool5 := (CreateFor <> CreateFor::Current);
                                 PAGE.RUNMODAL(PAGE::"IFC Object Properties", TmpIfcObjectProperty);
                                 TmpProperty.COPY(TmpIfcObjectProperty, TRUE);
                                 TmpProperty.RESET;
                                 TmpProperty.SETRANGE(Bool1, TRUE);
                                 CopySelected := (NOT TmpProperty.ISEMPTY);
                                 TmpProperty.SETRANGE(Bool1);
                                 TmpProperty.SETRANGE(Bool2, TRUE);
                                 FilterSelected := (NOT TmpProperty.ISEMPTY);
                                 TmpProperty.SETRANGE(Bool2);
                                 PropertyActions := PropertyActions::None;
                                 IF CopySelected AND FilterSelected THEN
                                   PropertyActions := PropertyActions::"Copy&Filter"
                                 ELSE BEGIN
                                   IF CopySelected THEN
                                     PropertyActions := PropertyActions::Copy;
                                   IF FilterSelected THEN
                                     PropertyActions := PropertyActions::Filter;
                                 END;
                               END;
                                }

      { 1100525005;1;Group  ;
                  CaptionML=ENU=Current;
                  Editable=FALSE;
                  GroupType=Group }

      { 1100525004;2;Field  ;
                  CaptionML=ENU=IFC Type;
                  SourceExpr=ObjectIfcType;
                  Editable=FALSE;
                  QuickEntry=FALSE }

      { 1100525006;2;Field  ;
                  Lookup=No;
                  CaptionML=ENU=Project No.;
                  SourceExpr=ProjectNo;
                  TableRelation=Job;
                  Editable=FALSE;
                  QuickEntry=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TmpIfcObjectProperty@1100529008 : TEMPORARY Record 11020579;
      ItemNo@1100525000 : Code[20];
      CostObject@1100529002 : Code[20];
      ProjectNo@1100525004 : Code[20];
      ObjectIfcType@1100525002 : Text;
      PartOfIfcName@1100525003 : Text;
      CurrentLinked@1100529000 : Boolean;
      Selected@1100525001 : Boolean;
      CreateForEditable@1100529001 : Boolean INDATASET;
      DefaultFieldsEditable@1100529003 : Boolean INDATASET;
      SelectionFieldsEditable@1100525006 : Boolean INDATASET;
      PropertyActionsEditable@1100529004 : Boolean INDATASET;
      CreateFor@1100525005 : 'Current,Selection,Link';
      PropertyActions@1100529009 : 'None,Copy,Filter,Copy&Filter';

    PROCEDURE SetSelection@1100525000(IfcType@1100525001 : Text;IfcName@1100525000 : Text;ProjectNo2@1100529000 : Code[20];CurrentLinked2@1100529001 : Boolean;VAR TmpIfcObjectProperty2@1100529002 : TEMPORARY Record 11020579);
    BEGIN
      ObjectIfcType := IfcType;
      PartOfIfcName := IfcName;
      ProjectNo := ProjectNo2;
      CurrentLinked := CurrentLinked2;
      TmpIfcObjectProperty.COPY(TmpIfcObjectProperty2, TRUE);
    END;

    PROCEDURE GetSelection@1100525001(VAR CreateFor2@1100525000 : Integer;VAR PartOfName@1100525001 : Text;VAR ItemNo2@1100525002 : Code[20];VAR CostObject2@1100529000 : Code[20];VAR PropertyName2@1100529002 : Text;VAR PropertyValue2@1100529001 : Text);
    BEGIN
      CreateFor2 := 0;
      PartOfName := '';
      ItemNo2 := '';
      CostObject2 := '';
      PropertyName2 := '';
      PropertyValue2 := '';
      IF Selected THEN BEGIN
        CreateFor2 := CreateFor + 1;
        PartOfName := PartOfIfcName;
        ItemNo2 := ItemNo;
        CostObject2 := CostObject;
        IF CreateFor = CreateFor::Current THEN BEGIN
          TmpIfcObjectProperty.RESET;
          TmpIfcObjectProperty.MODIFYALL(Bool2, FALSE);  //Tmp table contains all properties of current IFC object, so no further filtering needed
        END ELSE BEGIN
          TmpIfcObjectProperty.RESET;
          TmpIfcObjectProperty.SETRANGE(Bool2, TRUE);
          TmpIfcObjectProperty.SETFILTER(Text5, '<>%1', '');
          IF TmpIfcObjectProperty.FINDFIRST THEN BEGIN
            PropertyName2 := TmpIfcObjectProperty.Text1;
            PropertyValue2 := TmpIfcObjectProperty.Text5;
          END ELSE BEGIN
            TmpIfcObjectProperty.SETRANGE(Text5);
            IF TmpIfcObjectProperty.FINDFIRST THEN
              PropertyName2 := TmpIfcObjectProperty.Text1;
          END;
          TmpIfcObjectProperty.RESET;
        END;
      END;
    END;

    LOCAL PROCEDURE ValidateItemNo@1100525002(FromField@1100529000 : Boolean);
    VAR
      Item@1100525000 : Record 27;
    BEGIN
      IF ItemNo <> '' THEN BEGIN
        Item.GET(ItemNo);
        IF FromField AND (Item."Global Dimension 2 Code" <> '') THEN
          CostObject := Item."Global Dimension 2 Code";
      END;
    END;

    LOCAL PROCEDURE ValidateCostObject@1100529001();
    VAR
      DimVal@1100525000 : Record 349;
      DimMgt@1100529000 : Codeunit 408;
    BEGIN
      IF CostObject <> '' THEN
        DimMgt.GetDimValueRec(2, CostObject, DimVal, TRUE, '');
    END;

    LOCAL PROCEDURE SetEditable@1100525003();
    BEGIN
      CreateForEditable := (NOT CurrentLinked);
      SelectionFieldsEditable := ((CreateFor = CreateFor::Selection) OR (CreateFor = CreateFor::Link));
      DefaultFieldsEditable := ((CreateFor = CreateFor::Current) OR (CreateFor = CreateFor::Selection));
      PropertyActionsEditable := FALSE; // Only editable via lookup if SelectionFieldsEditable
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

