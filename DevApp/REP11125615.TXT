OBJECT Report 11125615 Copy Gen. Item Forecast
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Generic Item Forecast;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 7867;    ;DataItem;                    ;
               DataItemTable=Table11125618;
               DataItemTableView=SORTING(Entry No.);
               OnPreDataItem=BEGIN
                               IF NOT CONFIRM(Text000,FALSE) THEN
                                 EXIT;

                               LOCKTABLE;

                               IF GenItemForecastEntry2.FIND('+') THEN
                                 LastEntryNo := GenItemForecastEntry2."Entry No.";

                               NextEntryNo := LastEntryNo + 1;

                               ProdForecastName.SETRANGE(Name,ToGenItemForecastEntry."Production Forecast Name");
                               IF NOT ProdForecastName.FIND('-') THEN BEGIN
                                 ProdForecastName.Name := ToGenItemForecastEntry."Production Forecast Name";
                                 ProdForecastName.INSERT;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF "Entry No." > LastEntryNo THEN
                                    CurrReport.BREAK;
                                  GenItemForecastEntry2 := "Gen. Item Forecast Entry";

                                  IF ToGenItemForecastEntry."Production Forecast Name" <> '' THEN
                                    GenItemForecastEntry2."Production Forecast Name" := ToGenItemForecastEntry."Production Forecast Name";
                                  IF ToGenItemForecastEntry."Generic Item No." <> '' THEN
                                    GenItemForecastEntry2."Generic Item No." := ToGenItemForecastEntry."Generic Item No.";
                                  IF ToGenItemForecastEntry."Location Code" <> '' THEN
                                    GenItemForecastEntry2."Location Code" := ToGenItemForecastEntry."Location Code";
                                  IF FORMAT(ChangeDateExpression) <> '' THEN
                                    GenItemForecastEntry2."Forecast Date" := CALCDATE(ChangeDateExpression,"Forecast Date");

                                  GenItemForecastEntry2."Entry No." := NextEntryNo;
                                  GenItemForecastEntry2.INSERT;
                                  NextEntryNo := NextEntryNo + 1;
                                END;

               ReqFilterFields=Production Forecast Name,Generic Item No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 21  ;2   ;Group     ;
                  CaptionML=ENU=Copy to }

      { 1   ;3   ;Field     ;
                  CaptionML=ENU=Production Forecast Name;
                  SourceExpr=ToGenItemForecastEntry."Production Forecast Name";
                  TableRelation="Production Forecast Name" }

      { 2   ;3   ;Field     ;
                  CaptionML=ENU=Item No.;
                  SourceExpr=ToGenItemForecastEntry."Generic Item No.";
                  TableRelation=Item }

      { 4   ;3   ;Field     ;
                  CaptionML=ENU=Location Code;
                  SourceExpr=ToGenItemForecastEntry."Location Code";
                  TableRelation=Location }

      { 6   ;2   ;Field     ;
                  CaptionML=ENU=Date Change Formula;
                  SourceExpr=ChangeDateExpression }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to copy the production forecast?';
      ToGenItemForecastEntry@1001 : Record 11125618;
      GenItemForecastEntry2@1002 : Record 11125618;
      ProdForecastName@1003 : Record 99000851;
      ChangeDateExpression@1004 : DateFormula;
      LastEntryNo@1005 : Integer;
      NextEntryNo@1006 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

