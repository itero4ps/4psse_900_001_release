OBJECT Report 11128078 VP Feedback CAMT054
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=PE7.01 (12048865);
  }
  PROPERTIES
  {
    CaptionML=ENU=Report back from CAMT054 (SEPA);
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   ReadFile;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1000000000;;Container;
                  Name=Cont;
                  ContainerType=ContentArea }

      { 1100570001;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 12047958;2;Field    ;
                  CaptionML=ENU=File Name;
                  SourceExpr=gFileName;
                  OnAssistEdit=BEGIN
                                 gFileName := gFileMgt.OpenFileDialog(PEB004,gFileName,PEB003);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      gFileName@12047960 : Text;
      PEB001@12047959 : TextConst 'ENU=%1, %2 is not supported';
      PEB002@12047967 : TextConst 'ENU=The amount in the file is not correct for EndToEndID %1';
      PEB003@1070000 : TextConst 'ENU=All files|*.*|TXT-files (*.txt)|*.txt|WRI-files (*.wri)|*.wri';
      PEB004@1000000008 : TextConst 'ENU=Import BG Max';
      gFileMgt@12047958 : Codeunit 419;
      gGenJournalLine@12047965 : Record 81;
      gXGenJournalLine@12047966 : Record 81;
      gDocumentNo@12047961 : Code[20];

    LOCAL PROCEDURE ReadFile@12047960();
    VAR
      XMLReader@12047958 : Codeunit 11128078;
      DocNode@12047959 : Integer;
      DocNode2@12047962 : Integer;
      DocNode3@12047972 : Integer;
      ServerFileName@12047960 : Text;
      FileMgt@12047961 : Codeunit 419;
      VPPack@12047963 : Record 11128079;
      VPPackEntry@12047964 : Record 11128063;
      FromIBAN@12047967 : Text;
      EndToEndId@12047966 : Text;
      MsgId@12047968 : Text;
      PayAmount@12047969 : Decimal;
      PayCurrency@12047971 : Code[10];
      FromAmount@12047970 : Decimal;
      FromCurrency@12047965 : Code[10];
      FromBankAmount@12047973 : Decimal;
      PostingDate@12047974 : Date;
      VendLedgEntry@12047977 : Record 25;
      TotalAmount@12047975 : Decimal;
      VPGiroType@12047976 : Record 11128061;
      VPCurrencyAccount@12047978 : Record 11128073;
      AccountType@12047980 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset';
      BankAccountNo@12047979 : Code[20];
      CurrencyFactor@12047981 : Decimal;
      TempattrValue@12047982 : Text;
      DocNodeAttrib@12047983 : Integer;
    BEGIN
      ServerFileName := FileMgt.UploadFileSilent(gFileName);

      XMLReader.SetNameSpace('x','urn:iso:std:iso:20022:tech:xsd:camt.054.001.02');

      DocNode := XMLReader.LoadFromFile(ServerFileName,'/x:Document/x:BkToCstmrDbtCdtNtfctn/x:Ntfctn');

      WHILE XMLReader.GetNextNode(DocNode) DO BEGIN
        FromIBAN := XMLReader.GetValue(DocNode, 'x:Acct/x:Id/x:IBAN');
        FromCurrency := XMLReader.GetValue(DocNode, 'x:Acct/x:Ccy');

        // Account Level
        DocNode2 := XMLReader.SelectNodes(DocNode,'x:Ntry');
        WHILE XMLReader.GetNextNode(DocNode2) DO BEGIN
          FromBankAmount := XMLReader.GetValueDec(DocNode2,'x:Amt');
          PostingDate := XMLReader.GetValueDate(DocNode2,'x:BookgDt/x:Dt');
          // Vendor Entries Level
          DocNode3 := XMLReader.SelectNodes(DocNode2,'x:NtryDtls/x:TxDtls');
          WHILE XMLReader.GetNextNode(DocNode3) DO BEGIN
            EndToEndId := XMLReader.GetValue(DocNode3, 'x:Refs/x:EndToEndId');
            MsgId := XMLReader.GetValue(DocNode3, 'x:Refs/x:MsgId');

            PayAmount := XMLReader.GetValueDec(DocNode3,'x:AmtDtls/x:InstdAmt/x:Amt');
            FromAmount := XMLReader.GetValueDec(DocNode3,'x:AmtDtls/x:TxAmt/x:Amt');
            PayCurrency := XMLReader.GetValue(DocNode3, 'x:AmtDtls/x:TxAmt/x:CcyXchg/x:TrgtCcy');
            CurrencyFactor :=  FromAmount / PayAmount;

            VPPack.SETRANGE("SEPA Reference",MsgId);
            VPPack.FINDFIRST;
            VPGiroType.GET(VPPack."Giro Type Code");
            VPGiroType.TESTFIELD("Account No. Feedback");

            VPCurrencyAccount.SETRANGE("VP Giro Type Code",VPGiroType.Code);
            VPCurrencyAccount.SETRANGE("Currency Code",FromCurrency);
            VPCurrencyAccount.SETRANGE(IBAN,FromIBAN);
            IF VPCurrencyAccount.FINDFIRST THEN BEGIN
              VPCurrencyAccount.TESTFIELD("Account No. Feedback");
              WITH VPCurrencyAccount DO BEGIN
                CASE "Account Type Feedback" OF
                  "Account Type Feedback"::"G/L Account": AccountType := AccountType::"G/L Account";
                  "Account Type Feedback"::"Bank Account": AccountType := AccountType::"Bank Account";
                  ELSE
                    ERROR(PEB001,FIELDCAPTION("Account Type Feedback"),"Account Type Feedback");
                END;
                BankAccountNo := "Account No. Feedback";
              END;
            END ELSE BEGIN
              WITH VPGiroType DO BEGIN
                CASE "Account Type Feedback" OF
                  "Account Type Feedback"::"G/L Account": AccountType := AccountType::"G/L Account";
                  "Account Type Feedback"::"Bank Account": AccountType := AccountType::"Bank Account";
                  ELSE
                    ERROR(PEB001,FIELDCAPTION("Account Type Feedback"),"Account Type Feedback");
                END;
                BankAccountNo := "Account No. Feedback";
              END;

            END;

            TotalAmount := 0;

            VPPackEntry.SETRANGE("Pack Entry No.",VPPack."Entry No.");
            VPPackEntry.SETRANGE(Type,VPPackEntry.Type::Vendor);
            VPPackEntry.SETRANGE("SEPA Line Reference",EndToEndId);
            VPPackEntry.FINDSET;
            REPEAT
              gXGenJournalLine := gGenJournalLine;
              gGenJournalLine.INIT;
              gGenJournalLine."Line No." += 10000;
              SetUpNewLine;
              gGenJournalLine.VALIDATE("Posting Date",PostingDate);

              VendLedgEntry.GET(VPPackEntry."Ledger Entry No.");
              VendLedgEntry.TESTFIELD(Open,TRUE);
              VendLedgEntry.CALCFIELDS("Remaining Amount","Remaining Amt. (LCY)");
              TotalAmount += VendLedgEntry."Remaining Amount";
              IF PayCurrency <> '' THEN
                VendLedgEntry.TESTFIELD("Currency Code",PayCurrency);

              gGenJournalLine.VALIDATE("Account Type",gGenJournalLine."Account Type"::Vendor);
              gGenJournalLine.VALIDATE("Applies-to Doc. No.",VendLedgEntry."Document No.");

              IF CurrencyFactor <> 1 THEN
                gGenJournalLine.VALIDATE("Amount (LCY)",gGenJournalLine.Amount * CurrencyFactor);

              gGenJournalLine."External Document No." :=  VendLedgEntry."External Document No.";
              gGenJournalLine.INSERT;
            UNTIL VPPackEntry.NEXT = 0;

            IF TotalAmount <> -PayAmount THEN
              ERROR(PEB002,EndToEndId);

          END;

          gXGenJournalLine := gGenJournalLine;
          gGenJournalLine.INIT;
          gGenJournalLine."Line No." += 10000;
          SetUpNewLine;
          gGenJournalLine.VALIDATE("Posting Date",PostingDate);
          gGenJournalLine.VALIDATE("Account Type",AccountType);
          gGenJournalLine.VALIDATE("Account No.",BankAccountNo);
          gGenJournalLine.VALIDATE("Document Type",gGenJournalLine."Document Type"::Payment);
          gGenJournalLine.VALIDATE(Amount,-FromBankAmount);
          gGenJournalLine.INSERT;
        END;
      END;
    END;

    PROCEDURE SetGenJournal@1070001(InGenJournalLine@1070000 : Record 81);
    BEGIN
      gGenJournalLine := InGenJournalLine;
    END;

    PROCEDURE SetUpNewLine@9();
    VAR
      GenJnlTemplate@12047958 : Record 80;
      GenJnlBatch@12047959 : Record 232;
      NoSeriesMgt@12047960 : Codeunit 396;
    BEGIN
      GenJnlTemplate.GET(gGenJournalLine."Journal Template Name");
      GenJnlBatch.GET(gGenJournalLine."Journal Template Name",gGenJournalLine."Journal Batch Name");

      IF gDocumentNo = '' THEN BEGIN
        IF GenJnlBatch."No. Series" <> '' THEN BEGIN
          CLEAR(NoSeriesMgt);
          gDocumentNo := NoSeriesMgt.GetNextNo(GenJnlBatch."No. Series",WORKDATE,FALSE);
          gGenJournalLine."Document No." := gDocumentNo;
        END;
      END ELSE
        gGenJournalLine."Document No." := gDocumentNo;

      gGenJournalLine."Source Code" := GenJnlTemplate."Source Code";
      gGenJournalLine."Reason Code" := GenJnlBatch."Reason Code";
      gGenJournalLine."Posting No. Series" := GenJnlBatch."Posting No. Series";
      gGenJournalLine."Bal. Account Type" := GenJnlBatch."Bal. Account Type";
    END;

    BEGIN
    {
      PEB *** Begin ***
      PEB0065 BG-Max
      PEB
      PEB *** End ***
    }
    END.
  }
  RDLDATA
  {
  }
}

