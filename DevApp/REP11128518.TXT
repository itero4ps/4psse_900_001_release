OBJECT Report 11128518 Create NO Elec. Credit Memos
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO7.00 (10641),4PSSE.NO;
  }
  PROPERTIES
  {
    CaptionML=ENU=Create Electronic Credit Memos;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT CurrReport.USEREQUESTPAGE THEN
                    InitLogInteraction;
                END;

  }
  DATASET
  {
    { 8098;    ;DataItem;                    ;
               DataItemTable=Table114;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               SalesCrMemoHeader@1101100000 : Record 114;
                             BEGIN
                               Counter := 0;

                               // Any electronic credit memos?
                               SalesCrMemoHeader.COPY("Sales Cr.Memo Header");
                               SalesCrMemoHeader.FILTERGROUP(6);
                               SalesCrMemoHeader.SETRANGE("E-Invoice",TRUE);
                               IF NOT SalesCrMemoHeader.FINDFIRST THEN
                                 ERROR(Text003);

                               // All electronic credit memos?
                               SalesCrMemoHeader.SETRANGE("E-Invoice",FALSE);
                               IF SalesCrMemoHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text000,TRUE) THEN
                                   CurrReport.QUIT;
                               SalesCrMemoHeader.SETRANGE("E-Invoice");

                               // Some already sent?
                               SalesCrMemoHeader.SETRANGE("E-Invoice Created",TRUE);
                               IF SalesCrMemoHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text001,TRUE) THEN
                                   CurrReport.QUIT;

                               SETRANGE("E-Invoice",TRUE);
                             END;

               OnAfterGetRecord=BEGIN
                                  CODEUNIT.RUN(CODEUNIT::"E-Invoice Exp. Sales Cr. Memo","Sales Cr.Memo Header");

                                  IF LogInteraction THEN
                                    SegManagement.LogDocument(
                                      6,"No.",0,0,DATABASE::Customer,"Sell-to Customer No.","Salesperson Code",
                                      "Campaign No.","Posting Description",'');

                                  COMMIT;

                                  Counter := Counter + 1;
                                END;

               OnPostDataItem=BEGIN
                                MESSAGE(Text002,Counter);
                              END;

               ReqFilterFields=No.,Sell-to Customer No.,Bill-to Customer No.,GLN,E-Invoice Created }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               LogInteractionEnable := TRUE;
             END;

      OnOpenPage=BEGIN
                   InitLogInteraction;
                   LogInteractionEnable := LogInteraction;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1101100000;2;Field  ;
                  Name=LogInteraction;
                  CaptionML=ENU=Log Interaction;
                  SourceExpr=LogInteraction;
                  Enabled=LogInteractionEnable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SegManagement@1080000 : Codeunit 5051;
      Counter@1080002 : Integer;
      Text000@1080004 : TextConst 'ENU=One or more credit memo documents that match your filter criteria are not electronic credit memos and will be skipped.\\Do you want to continue?';
      Text001@1080005 : TextConst 'ENU=One or more credit memo documents that match your filter criteria have been created before.\\Do you want to continue?';
      Text002@1080006 : TextConst 'ENU=Successfully created %1 electronic credit memo documents.';
      Text003@1080007 : TextConst 'ENU=Nothing to create.';
      LogInteraction@1080003 : Boolean;
      LogInteractionEnable@19003940 : Boolean INDATASET;

    PROCEDURE InitLogInteraction@1080008();
    BEGIN
      LogInteraction := SegManagement.FindInteractTmplCode(4) <> '';
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

