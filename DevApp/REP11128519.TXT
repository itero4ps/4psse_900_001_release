OBJECT Report 11128519 Create NO Electronic Reminders
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO7.00 (10642),4PSSE.NO;
  }
  PROPERTIES
  {
    CaptionML=ENU=Create Electronic Reminders;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT CurrReport.USEREQUESTPAGE THEN
                    InitLogInteraction;
                END;

  }
  DATASET
  {
    { 5612;    ;DataItem;                    ;
               DataItemTable=Table297;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               IssuedReminderHeader@1101100000 : Record 297;
                             BEGIN
                               Counter := 0;

                               // Any electronic reminders?
                               IssuedReminderHeader.COPY("Issued Reminder Header");
                               IssuedReminderHeader.FILTERGROUP(6);
                               IssuedReminderHeader.SETRANGE("E-Invoice",TRUE);
                               IF NOT IssuedReminderHeader.FINDFIRST THEN
                                 ERROR(Text003);

                               // All electronic reminders?
                               IssuedReminderHeader.SETRANGE("E-Invoice",FALSE);
                               IF IssuedReminderHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text000,TRUE) THEN
                                   CurrReport.QUIT;
                               IssuedReminderHeader.SETRANGE("E-Invoice");

                               // Some already sent?
                               IssuedReminderHeader.SETRANGE("E-Invoice Created",TRUE);
                               IF IssuedReminderHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text001,TRUE) THEN
                                   CurrReport.QUIT;

                               SETRANGE("E-Invoice",TRUE);
                             END;

               OnAfterGetRecord=BEGIN
                                  CODEUNIT.RUN(CODEUNIT::"E-Invoice Export Iss. Reminder","Issued Reminder Header");

                                  IF LogInteraction THEN
                                    SegManagement.LogDocument(
                                      8,"No.",0,0,DATABASE::Customer,"Customer No.",'','',"Posting Description",'');

                                  COMMIT;
                                  Counter := Counter + 1;
                                END;

               OnPostDataItem=BEGIN
                                MESSAGE(Text002,Counter);
                              END;

               ReqFilterFields=No.,Customer No.,Field13600,Field13601 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               LogInteractionEnable := TRUE;
             END;

      OnOpenPage=BEGIN
                   InitLogInteraction;
                   LogInteractionEnable := LogInteraction;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1101100001;2;Field  ;
                  Name=LogInteraction;
                  CaptionML=ENU=Log Interaction;
                  SourceExpr=LogInteraction;
                  Enabled=LogInteractionEnable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SegManagement@1101100001 : Codeunit 5051;
      Counter@1101100002 : Integer;
      LogInteraction@1101100000 : Boolean;
      Text000@1101100006 : TextConst 'ENU=One or more issued reminders that match your filter criteria are not electronic reminders and will be skipped.\\Do you want to continue?';
      Text001@1101100005 : TextConst 'ENU=One or more electronic reminders that match your filter criteria have been created before.\\Do you want to continue?';
      Text002@1101100004 : TextConst 'ENU=Successfully created %1 electronic reminders.';
      Text003@1101100003 : TextConst 'ENU=Nothing to create.';
      LogInteractionEnable@19003940 : Boolean INDATASET;

    PROCEDURE InitLogInteraction@1101100002();
    BEGIN
      LogInteraction := SegManagement.FindInteractTmplCode(8) <> '';
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

