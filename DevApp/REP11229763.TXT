OBJECT Report 11229763 Export Sales Cr M Digipoort
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    Permissions=TableData 114=m;
    CaptionML=ENU=Export Sales Credit Memos XML;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  Window.OPEN(Text000);
                END;

    OnPostReport=BEGIN
                   IF (NumOfExports <> 0) THEN
                     MESSAGE(Text001, NumOfExports)
                   ELSE
                     MESSAGE(Text002);
                 END;

  }
  DATASET
  {
    { 8098;    ;DataItem;                    ;
               DataItemTable=Table114;
               OnPreDataItem=BEGIN
                               IF NOT IncludeSentInvoices THEN BEGIN
                                 FILTERGROUP(10);
                                 SETFILTER("Exported on", '%1', 0D);
                                 FILTERGROUP(0);
                               END;

                               NumOfInvoices := COUNT;
                             END;

               OnAfterGetRecord=VAR
                                  CustDigipoortRelation@1100525003 : Record 11229762;
                                  TempBlob@1100525005 : Record 99008535;
                                  ThreeTierAutomationMgt@1100525004 : Codeunit 419;
                                  OutputFile@1100525001 : File;
                                  OutputFileName@1100525006 : Text[1024];
                                  Stream@1100525000 : OutStream;
                                  SalesCreditMemoHeader@1100525002 : Record 114;
                                  DigipoortXMLQueue@1100525007 : Record 11229764;
                                BEGIN
                                  NumOfRec := NumOfRec + 1;
                                  Window.UPDATE(1,100 * ROUND(NumOfRec / NumOfInvoices * 100,1));

                                  IF NOT CustDigipoortRelation.GET("Sell-to Customer No.") THEN
                                    CurrReport.SKIP;

                                  IF gFromPost AND (NOT CustDigipoortRelation."Send XML while Posting") THEN
                                    CurrReport.SKIP;

                                  CustDigipoortRelation.TESTFIELD("Export Folder XML");

                                  SalesCreditMemoHeader.SETRANGE("No.", "No.");
                                  OutputFileName := STRSUBSTNO('%1\%2.xml', DELCHR(CustDigipoortRelation."Export Folder XML", '>', '\'), "No.");

                                  TempBlob.Blob.CREATEOUTSTREAM(Stream);
                                  XMLPORT.EXPORT(XMLPORT::"Export Sales Cr M (Digipoort)", Stream, SalesCreditMemoHeader);
                                  ThreeTierAutomationMgt.BLOBExport4PS(TempBlob, OutputFileName, FALSE, TRUE);

                                  DigipoortXMLQueue.INIT;
                                  DigipoortXMLQueue."Document Type" := DigipoortXMLQueue."Document Type"::Invoice;
                                  DigipoortXMLQueue."Document No." := "No.";
                                  DigipoortXMLQueue."Line No." := NumOfExports + 1;
                                  DigipoortXMLQueue."File Type" := DigipoortXMLQueue."File Type"::UBL_Invoice;
                                  DigipoortXMLQueue."File Name" := OutputFileName;
                                  IF DigipoortXMLQueue.INSERT THEN;

                                  "Exported on" := TODAY;
                                  "Invoice Exported by XML" := TRUE;
                                  MODIFY;

                                  NumOfExports := NumOfExports + 1;
                                END;

               ReqFilterFields=No.,Exported on }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Window@1100525008 : Dialog;
      FolderName@1100525007 : Text[260];
      IncludeSentInvoices@1100525000 : Boolean;
      Text000@1100525001 : TextConst 'ENU=Invoices            @1@@@@@@@@\';
      Text001@1100525002 : TextConst 'ENU=%1 Invoices are exported.';
      Text002@1100525003 : TextConst 'ENU=Nothing exported';
      gFromPost@1100528200 : Boolean;
      NumOfInvoices@1100525004 : Integer;
      NumOfRec@1100525006 : Integer;
      NumOfExports@1100525005 : Integer;

    PROCEDURE SetReportSelection@1100525000(lFromPost@1100525000 : Boolean);
    BEGIN
      gFromPost := lFromPost;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

