OBJECT Report 12013587 Batch Create Purchase Invoices
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF400004;
  }
  PROPERTIES
  {
    CaptionML=ENU=Batch Create Purchase Documents;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4372;    ;DataItem;                    ;
               DataItemTable=Table12013587;
               DataItemTableView=SORTING(Journal Batch Name,Inbound Document No.);
               ReqFilterHeadingML=ENU=Exflow Document;
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);

                               CounterOK := 0;

                               IF GUIALLOWED THEN BEGIN
                                 CounterTotal := COUNT;
                                 Counter := 0;

                                 Window.OPEN(Text001);
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  CLEARLASTERROR;

                                  IF GUIALLOWED THEN BEGIN
                                    Window.UPDATE(1,ROUND(Counter / CounterTotal * 10000,1));
                                    Counter := Counter + 1;
                                  END;

                                  ProcessDoc := TRUE;
                                  IF RunInAutomaticMode THEN BEGIN
                                    // CALCFIELDS("Potential UOM Mismatch");
                                    // IF "Potential UOM Mismatch" THEN
                                    //  ProcessDoc := FALSE;
                                  END;

                                  IF ProcessDoc THEN BEGIN
                                    ExFPurchDocHeader.GET("Inbound Document No.");
                                    ExFlowToInvoicePreCreate.VerifyDocument(ExFPurchDocHeader);
                                    COMMIT;

                                    ExFPurchDocHeader.GET("Inbound Document No.");
                                    IF NOT ExFPurchDocHeader."Validation Error" THEN BEGIN
                                      CLEAR(ExflowToInvoice);
                                      ExflowToInvoice.SetRealCreate(TRUE);
                                      ExflowToInvoice.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                                      IF ExflowToInvoice.RUN(ExFPurchDocHeader) THEN BEGIN
                                        CounterOK := CounterOK + 1;
                                      END
                                      ELSE BEGIN
                                        ExFPurchDocHeader.GET("Inbound Document No.");
                                        ExFPurchDocHeader."Error Message" := COPYSTR(GETLASTERRORTEXT,1,MAXSTRLEN("Error Message"));
                                        ExFPurchDocHeader.MODIFY;
                                      END;

                                      COMMIT;
                                    END;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                IF GUIALLOWED THEN
                                  Window.CLOSE;
                              END;

               ReqFilterFields=Inbound Document No.,Buy-from Vendor No.,Document Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100285005;0;Container;
                  ContainerType=ContentArea }

      { 1100285004;1;Group  ;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Field     ;
                  CaptionML=ENU=Posting Date;
                  SourceExpr=PostingDateReq }

      { 2   ;2   ;Field     ;
                  CaptionML=ENU=Replace Posting Date;
                  SourceExpr=ReplacePostingDate }

      { 3   ;2   ;Field     ;
                  CaptionML=ENU=Replace Document Date;
                  SourceExpr=ReplaceDocumentDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the posting date.';
      Text001@1100285009 : TextConst 'ENU="Creating invoices  @1@@@@@@@@@@@@@ "';
      ExFPurchDocHeader@1100285011 : Record 12013587;
      ExFlowToInvoicePreCreate@1100285010 : Codeunit 12013632;
      ExflowToInvoice@1100285000 : Codeunit 12013592;
      Window@1100285007 : Dialog;
      CounterTotal@1100285004 : Integer;
      Counter@1100285001 : Integer;
      CounterOK@1100285002 : Integer;
      PostingDateReq@1008 : Date;
      ReplacePostingDate@1012 : Boolean;
      ReplaceDocumentDate@1013 : Boolean;
      RunInAutomaticMode@1100285005 : Boolean;
      ProcessDoc@1100285006 : Boolean;

    PROCEDURE SetAutomaticMode@1100285003(_RunInAutomaticMode@1100285000 : Boolean);
    BEGIN
      RunInAutomaticMode := _RunInAutomaticMode;
    END;

    PROCEDURE GetCounters@1100285002(VAR _CounterTotal@1100285002 : Integer;VAR _CounterOK@1100285000 : Integer);
    BEGIN
      _CounterTotal := CounterTotal;
      _CounterOK := CounterOK;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

