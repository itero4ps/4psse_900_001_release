OBJECT Report 12013619 Batch Post Purch Invoices ExF
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF350000,NAV2013;
  }
  PROPERTIES
  {
    CaptionML=ENU=Batch Post Purchase Invoices;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4272;    ;DataItem;                    ;
               DataItemTable=Table12013608;
               DataItemTableView=SORTING(Company Name,Document Type,Document No.)
                                 WHERE(Document Type=FILTER(Invoice|Credit Memo),
                                       Status=CONST(Ready for posting));
               ReqFilterHeadingML=ENU=Purchase Invoice;
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);

                               SETRANGE("Company Name", COMPANYNAME);

                               CounterTotal := COUNT;

                               IF GUIALLOWED THEN
                                 Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=BEGIN
                                  "Purchase Header".GET("Document Type", "Document No.");

                                  IF CalcInvDisc THEN
                                    CalculateInvoiceDiscount;

                                  Counter := Counter + 1;

                                  IF GUIALLOWED THEN BEGIN
                                    Window.UPDATE(1,"Document No.");
                                    Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                                  END;

                                  CLEAR(PurchPost);
                                  CLEARLASTERROR;
                                  PurchPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                                  IF PurchPost.RUN("Purchase Header") THEN BEGIN
                                    CounterOK := CounterOK + 1;
                                    IF MARKEDONLY THEN
                                      MARK(FALSE);
                                  END
                                  ELSE BEGIN
                                    ExDoc.GET(ID);
                                    ExDoc."Error Message" := COPYSTR(GETLASTERRORTEXT,1,MAXSTRLEN(ExDoc."Error Message"));
                                    ExDoc.MODIFY;
                                    COMMIT;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                IF GUIALLOWED THEN BEGIN
                                  Window.CLOSE;
                                  MESSAGE(Text002,CounterOK,CounterTotal);
                                END;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100285005;0;Container;
                  ContainerType=ContentArea }

      { 1100285004;1;Group  ;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Field     ;
                  CaptionML=ENU=Posting Date;
                  SourceExpr=PostingDateReq }

      { 2   ;2   ;Field     ;
                  CaptionML=ENU=Replace Posting Date;
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 3   ;2   ;Field     ;
                  CaptionML=ENU=Replace Document Date;
                  SourceExpr=ReplaceDocumentDate }

      { 5   ;2   ;Field     ;
                  CaptionML=ENU=Calc. Inv. Discount;
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               PurchSetup.GET;
                               PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the posting date.';
      Text001@1001 : TextConst 'ENU=Posting invoices   #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'ENU=%1 invoices out of a total of %2 have now been posted.';
      PurchLine@1003 : Record 39;
      PurchSetup@1004 : Record 312;
      PurchCalcDisc@1005 : Codeunit 70;
      PurchPost@1006 : Codeunit 90;
      Window@1007 : Dialog;
      PostingDateReq@1008 : Date;
      CounterTotal@1009 : Integer;
      Counter@1010 : Integer;
      CounterOK@1011 : Integer;
      ReplacePostingDate@1012 : Boolean;
      ReplaceDocumentDate@1013 : Boolean;
      CalcInvDisc@1014 : Boolean;
      Text003@1015 : TextConst 'ENU=The exchange rate associated with the new posting date on the purchase header will not apply to the purchase lines.';
      "Purchase Header"@1100285000 : Record 38;
      ExDoc@1100285001 : Record 12013608;

    PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Purchase Header"."Document Type");
      PurchLine.SETRANGE("Document No.","Purchase Header"."No.");
      IF PurchLine.FINDFIRST THEN
        IF PurchCalcDisc.RUN(PurchLine) THEN BEGIN
          "Purchase Header".GET("Purchase Header"."Document Type","Purchase Header"."No.");
          COMMIT;
        END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

