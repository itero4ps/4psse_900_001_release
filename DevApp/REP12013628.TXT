OBJECT Report 12013628 Batch Update Purch Inv. ExF
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF350000,NAV2013,4PS;
  }
  PROPERTIES
  {
    CaptionML=ENU=Batch Update Posting Date;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   InitValues;
                 END;

  }
  DATASET
  {
    { 4272;    ;DataItem;                    ;
               DataItemTable=Table12013608;
               DataItemTableView=SORTING(Company Name,Document Type,Document No.)
                                 WHERE(Document Type=FILTER(Invoice|Credit Memo));
               ReqFilterHeadingML=ENU=Purchase Invoice;
               OnPreDataItem=BEGIN
                               IF (PostingDateTo = 0D) OR (UpdateToDate = 0D) THEN
                                 ERROR(Text000);
                               SETFILTER("Posting Date", '%1..%2',PostingDateFrom,PostingDateTo);
                               CounterTotal := COUNT;
                               Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=VAR
                                  OldCurrFactor@1100285000 : Decimal;
                                  CurrExchRate@1100285001 : Record 330;
                                BEGIN
                                  "Purchase Header".GET("EX Document"."Document Type", "EX Document"."Document No.");
                                  IF CalcInvDisc THEN
                                    CalculateInvoiceDiscount;

                                  Counter := Counter + 1;
                                  Window.UPDATE(1,"Document No.");
                                  Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));

                                  "Purchase Header".SetHideValidationDialog(TRUE);
                                  "Purchase Header"."Posting Date" := UpdateToDate;

                                  IF UpdateCurrencyFactor THEN
                                    IF "Purchase Header"."Currency Code" <> '' THEN BEGIN
                                      OldCurrFactor := "Purchase Header"."Currency Factor";
                                      "Purchase Header"."Currency Factor" := CurrExchRate.ExchangeRate(0,"Purchase Header"."Job No.",UpdateToDate,"Purchase Header"."Currency Code",FALSE); // 4PS
                                      IF "Purchase Header"."Currency Factor" <> OldCurrFactor THEN
                                        "Purchase Header".VALIDATE("Currency Factor");
                                    END;

                                  IF ReplaceDocumentDate THEN
                                    "Purchase Header".VALIDATE("Document Date",UpdateToDate);

                                  IF "Purchase Header".PurchLinesExist THEN
                                    "Purchase Header".JobUpdatePurchLines(TRUE);

                                  ExFlowSEMgt.PurchHeaderUpdateInw("Purchase Header");
                                  "Purchase Header".MODIFY(TRUE);

                                  COMMIT;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                MESSAGE(Text002,CounterTotal);
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100285098;0;Container;
                  ContainerType=ContentArea }

      { 1100285099;1;Group  ;
                  CaptionML=ENU=Options }

      { 1100285000;2;Field  ;
                  CaptionML=ENU=Update Posting Dates from.;
                  SourceExpr=PostingDateFrom }

      { 1100285002;2;Field  ;
                  CaptionML=ENU=To;
                  SourceExpr=PostingDateTo }

      { 1100285005;2;Field  ;
                  CaptionML=ENU=Update To Date;
                  SourceExpr=UpdateToDate }

      { 3   ;2   ;Field     ;
                  CaptionML=ENU=Replace Document Date;
                  ToolTipML=ENU=Replacing Document Date may effect Due Date;
                  SourceExpr=ReplaceDocumentDate }

      { 5   ;2   ;Field     ;
                  CaptionML=ENU=Calc. Inv. Discount;
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               PurchSetup.GET;
                               PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

      { 1100285008;2;Field  ;
                  CaptionML=ENU=Update Currency Factor;
                  SourceExpr=UpdateCurrencyFactor }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must select an end date filter and a valid update to date.';
      Text001@1001 : TextConst 'ENU=Updating invoices   #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'ENU=%1 invoices have now been updated.';
      PurchLine@1003 : Record 39;
      PurchSetup@1004 : Record 312;
      "Purchase Header"@1100285000 : Record 38;
      PurchCalcDisc@1005 : Codeunit 70;
      ExFlowSEMgt@1100285001 : Codeunit 12013598;
      Window@1007 : Dialog;
      CounterTotal@1009 : Integer;
      Counter@1010 : Integer;
      CalcInvDisc@1014 : Boolean;
      PostingDateFrom@1100285002 : Date;
      PostingDateTo@1100285003 : Date;
      UpdateToDate@1100285004 : Date;
      ReplaceDocumentDate@1100285006 : Boolean;
      UpdateCurrencyFactor@1100285007 : Boolean;

    PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Purchase Header"."Document Type");
      PurchLine.SETRANGE("Document No.","Purchase Header"."No.");
      IF PurchLine.FIND('-') THEN
        IF PurchCalcDisc.RUN(PurchLine) THEN BEGIN
          "Purchase Header".GET("Purchase Header"."Document Type","Purchase Header"."No.");
          COMMIT;
        END;
    END;

    PROCEDURE InitValues@1100285000();
    BEGIN
      PurchSetup.GET;
      CalcInvDisc := PurchSetup."Calc. Inv. Discount";
      PostingDateFrom := 0D;
      PostingDateTo := CALCDATE('<-CM-1D>',TODAY);
      UpdateToDate := CALCDATE('<-CM>',TODAY);
    END;

    BEGIN
    {
      Report for batch updating from Form Approval Status
    }
    END.
  }
  RDLDATA
  {
  }
}

