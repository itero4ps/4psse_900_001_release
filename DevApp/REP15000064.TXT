OBJECT Report 15000064 OCR Payment - BBS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO8.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=OCR Payment - BBS;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   OCRSetup.GET;
                   SalesSetup.GET;
                   BalAccType := OCRSetup."Bal. Account Type";
                   BalAccNo := OCRSetup."Bal. Account No.";
                 END;

    OnPreReport=BEGIN
                  IF FileName = '' THEN
                    ERROR(Text10632);
                  ServerTempFile := FileMgt.UploadFileSilent(FileName);

                  IF ((OCRSetup."Journal Template Name" <> '') OR (OCRSetup."Journal Name" <> '')) AND
                     ((OCRSetup."Journal Template Name" <> JournalSelection."Journal Template Name") OR
                      (OCRSetup."Journal Name" <> JournalSelection."Journal Batch Name"))
                  THEN
                    ERROR(
                      Text10616,
                      OCRSetup."Journal Template Name",
                      OCRSetup."Journal Name");

                  GenJnlLine.SETRANGE("Journal Template Name",JournalSelection."Journal Template Name");
                  GenJnlLine.SETRANGE("Journal Batch Name",JournalSelection."Journal Batch Name");
                  IF GenJnlLine.FINDLAST THEN BEGIN
                    IF NOT CONFIRM(Text10617,FALSE) THEN
                      ERROR('');
                    NextLineNo := GenJnlLine."Line No." + 10000;
                  END ELSE
                    NextLineNo := 10000;

                  // Test journal
                  RegisterJournal.GET(JournalSelection."Journal Template Name",JournalSelection."Journal Batch Name");
                  RegisterJournal.TESTFIELD("Bal. Account No.",'');
                  RegisterJournal.TESTFIELD("No. Series");
                  // Create work file
                  TxtFile.TEXTMODE := TRUE;
                  TxtFile.OPEN(ServerTempFile);

                  BalEntrySum := 0;
                  NumberOfWarnings := 0;
                  NumberOfEntries := 0;
                  NewDocumentNo := FALSE;
                  COMMIT;
                  LatestBBSDate := 0D;
                  LatestPaymentRef := '';

                  WHILE TxtFile.LEN <> TxtFile.POS DO BEGIN
                    TxtFile.READ(RecordContent);
                    Recordtype := COPYSTR(RecordContent,7,2);
                    CASE Recordtype OF
                      '30':
                        RecordType30;
                      '31':
                        BEGIN
                          RecordType31;
                          CreatePayment;
                        END;
                    END;
                  END;
                END;

    OnPostReport=BEGIN
                   IF BalEntrySum <> 0 THEN
                     CreateBalEntry;

                   TxtFile.CLOSE;
                   MessageText := STRSUBSTNO(Text10618,NumberOfEntries);
                   IF NumberOfWarnings > 0 THEN
                     MESSAGE(Text10619,MessageText,NumberOfWarnings)
                   ELSE
                     MESSAGE(MessageText);

                   IF OCRSetup."Delete Return File" THEN
                     CreateFilename;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   FileName := OCRSetup.FileName;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1080000;2;Field     ;
                  Name=FileNameTextBox;
                  CaptionML=ENU=File Name;
                  SourceExpr=FileName;
                  OnAssistEdit=BEGIN
                                 FileName := FileMgt.OpenFileDialog(Text10613,FileName,Text10614);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      JournalSelection@1080002 : Record 81;
      PrevGenJnlLine@1080003 : Record 81;
      OCRSetup@1080004 : Record 15000100;
      SalesSetup@1080005 : Record 311;
      RegisterJournal@1080006 : Record 232;
      CustEntry@1080007 : Record 21;
      Reminder@1080008 : Record 297;
      GenJnlLine@1080010 : Record 81;
      FileMgt@1080012 : Codeunit 419;
      TxtFile@1101100001 : File;
      Bbsdate@1101100003 : Date;
      LatestBBSDate@1101100002 : Date;
      BalAccNo@1101100010 : Code[20];
      Recordtype@1101100009 : Code[2];
      LatestPaymentRef@1101100008 : Code[10];
      PaymentRef@1101100007 : Code[10];
      ApplyToDocumentNo@1101100006 : Code[20];
      ApplyToDocumentType@1101100005 : Code[1];
      TransType@1101100004 : Code[2];
      FileName@1101100019 : Text[250];
      RecordContent@1101100018 : Text[80];
      MessageText@1101100017 : Text[250];
      KID@1101100016 : Text[30];
      TransTypeText@1101100015 : Text[30];
      ServerTempFile@1101100013 : Text[1024];
      file1@1101100012 : Text[250];
      file2@1101100011 : Text[250];
      OCRAmount@1101100023 : Decimal;
      BalEntrySum@1101100022 : Decimal;
      DivergenceAmount@1101100021 : Decimal;
      RemainingAmount@1101100020 : Decimal;
      NextLineNo@1080015 : Integer;
      BalAccType@1080017 : Integer;
      CentralID@1080022 : Integer;
      DayCode@1080024 : Integer;
      PartialSettlementNo@1080026 : Integer;
      NumberOfWarnings@1080038 : Integer;
      NumberOfEntries@1080037 : Integer;
      NewDocumentNo@1080041 : Boolean;
      Text10600@1080046 : TextConst 'ENU=" OCR Payment"';
      Text10601@1080047 : TextConst 'ENU=" (Giro)"';
      Text10602@1080048 : TextConst 'ENU=" (Fixed payment orders)"';
      Text10603@1080049 : TextConst 'ENU=" (Direct remittance)"';
      Text10604@1080050 : TextConst 'ENU=" (BTG)"';
      Text10605@1080051 : TextConst 'ENU=" (CounterGiro)"';
      Text10606@1080052 : TextConst 'ENU=" (AgreementGiro)"';
      Text10607@1080053 : TextConst 'ENU=" (Telegiro)"';
      Text10608@1080054 : TextConst 'ENU=" (Giro paid in cash)"';
      Text10611@1080057 : TextConst '@@@=Parameter 1 - document type, 2 - document number.;ENU=Divergence, OCR-Payment (%1 %2)';
      Text10612@1080058 : TextConst 'ENU=OCR-Payment. Paymentref. %1';
      Text10613@1080059 : TextConst 'ENU=Import from OCR Payment File.';
      Text10614@1080060 : TextConst 'ENU=Text Files (*.txt)|*.txt|All Files (*.*)|*.*';
      Text10616@1080062 : TextConst 'ENU=OCR payments can only be imported into journal Journal Template Name %1, Journal Name %2.';
      Text10617@1080063 : TextConst 'ENU=Journal already contains lines.\Import?';
      Text10618@1080064 : TextConst 'ENU=%1 payments are imported.';
      Text10619@1080065 : TextConst 'ENU=%1\Number of warnings for import: %2.';
      Text10620@1080066 : TextConst 'ENU=Following error no.';
      Text10621@1080067 : TextConst 'ENU=%1\Following warning nos.: %2';
      Text10622@1080068 : TextConst 'ENU=Amount paid-in does not correspond to the remaining amount %1.';
      Text10623@1080069 : TextConst 'ENU=Error in KID "%1".';
      Text10624@1080070 : TextConst 'ENU=Customer entry is closed (entry serial no. %1).';
      Text10625@1080071 : TextConst 'ENU=Payment, reminder no. %1.\Apply to must be specified manually.';
      Text10626@1080072 : TextConst 'ENU=Divergence occured.\Amount paid-in does not correspond with remaining amount %1.';
      Text10627@1080073 : TextConst 'ENU="Document no. ""%1"" does not exist.\KID=""%2""."';
      Text10628@1080074 : TextConst 'ENU="Reminder no. ""%1"" does not exist.\KID=""%2""."';
      Text10629@1080075 : TextConst 'ENU=Error unknown.';
      Text10630@1080076 : TextConst '@@@=Parameter 1 - integer, 2 - text.;ENU=Warning %1: %2';
      Text10632@1080013 : TextConst 'ENU=Specify file name.';

    LOCAL PROCEDURE CreatePayment@1080014();
    BEGIN
      DivergenceAmount := 0;

      // Check new bal.entry
      IF LatestPaymentRef <> PaymentRef THEN BEGIN
        IF BalEntrySum <> 0 THEN
          CreateBalEntry;
        LatestBBSDate := Bbsdate;
        LatestPaymentRef := PaymentRef;
      END;

      BalEntrySum := BalEntrySum + OCRAmount;
      CLEAR(GenJnlLine);
      GenJnlLine.VALIDATE("Journal Template Name",JournalSelection."Journal Template Name");
      GenJnlLine.VALIDATE("Journal Batch Name",JournalSelection."Journal Batch Name");
      IF NewDocumentNo THEN
        GenJnlLine.SetUpNewLine(PrevGenJnlLine,PrevGenJnlLine."Balance (LCY)",TRUE)
      ELSE
        GenJnlLine.SetUpNewLine(PrevGenJnlLine,1,TRUE);
      GenJnlLine.VALIDATE("Line No.",NextLineNo);
      NextLineNo := NextLineNo + 10000;
      GenJnlLine.VALIDATE("Posting Date",Bbsdate);
      GenJnlLine.VALIDATE("Document Type",GenJnlLine."Document Type"::Payment);
      GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::Customer);
      GenJnlLine.VALIDATE("Applies-to Doc. Type",GenJnlLine."Applies-to Doc. Type"::Invoice);

      IF KID[25] = '''' THEN BEGIN  // If 25. char  = ' then the KID is wrong.
        // Cust.No. and ApplyToDocumentNo can not be set:
        KID := COPYSTR(KID,1,24);  // Delete ' in KID.
        GenJnlLine.VALIDATE(Amount,OCRAmount);
        SetWarning(2); // Wrong KID
      END ELSE BEGIN
        ApplyToDocumentNo := DeleteZeroes(ApplyToDocumentNo);
        IF ApplyToDocumentType = '3' THEN BEGIN
          // Find reminder, then the cust.no. is known:
          IF Reminder.GET(ApplyToDocumentNo) THEN BEGIN
            GenJnlLine.VALIDATE("Account No.",Reminder."Customer No.");
            SetWarning(4);
          END ELSE
            SetWarning(7); // reminder does not exist
          GenJnlLine.VALIDATE(Amount,OCRAmount);
        END ELSE BEGIN
          // Find invoice/credit memo. Then the cust. no. is known:
          CustEntry.SETCURRENTKEY("Document Type");
          CASE ApplyToDocumentType OF
            '0','1':
              CustEntry.SETRANGE("Document Type",CustEntry."Document Type"::Invoice);
            '2':
              CustEntry.SETRANGE("Document Type",CustEntry."Document Type"::"Finance Charge Memo");
          END;
          CustEntry.SETRANGE("Document No.",ApplyToDocumentNo);
          IF NOT CustEntry.FINDFIRST THEN BEGIN
            SetWarning(6); // Document No. does not exist
            // Create a temporary CustEntry and the info used in following:
            CustEntry."Customer No." := '';
            CustEntry.Open := TRUE;
            RemainingAmount := -OCRAmount;
            ApplyToDocumentNo := '';
          END ELSE BEGIN
            CustEntry.CALCFIELDS("Remaining Amount");
            RemainingAmount := CustEntry."Remaining Amount";
          END;
          GenJnlLine.VALIDATE("Account No.",CustEntry."Customer No.");
          GenJnlLine.VALIDATE(Amount,OCRAmount);
          CASE TRUE OF
            NOT CustEntry.Open:
              SetWarning(3); // CustEntry is closed
            (GenJnlLine.Amount <> -RemainingAmount) AND
            (ABS(GenJnlLine.Amount + RemainingAmount) <= OCRSetup."Max. Divergence"):
              BEGIN
                SetWarning(5); // Wrong Amount - automatic divergence post
                DivergenceAmount := GenJnlLine.Amount + RemainingAmount;
                // Apply to ledger
                GenJnlLine.VALIDATE(Amount,-RemainingAmount);
              END;
            GenJnlLine.Amount <> -RemainingAmount:
              SetWarning(1); // Wrong amount
          END;

          GenJnlLine.VALIDATE("Applies-to Doc. No.",ApplyToDocumentNo);
        END;
      END;

      CASE TransType OF
        '10':
          TransTypeText := Text10601;
        '11':
          TransTypeText := Text10602;
        '12':
          TransTypeText := Text10603;
        '13':
          TransTypeText := Text10604;
        '14':
          TransTypeText := Text10605;
        '15':
          TransTypeText := Text10606;
        '16':
          TransTypeText := Text10607;
        '17':
          TransTypeText := Text10608;
        ELSE
          TransTypeText := '';
      END;
      GenJnlLine.VALIDATE(Description,Text10600 + TransTypeText);
      GenJnlLine.INSERT(TRUE);
      PrevGenJnlLine := GenJnlLine;

      IF DivergenceAmount <> 0 THEN
        CreateDivergence(DivergenceAmount);

      NumberOfEntries := NumberOfEntries + 1;
      NewDocumentNo := FALSE;
    END;

    LOCAL PROCEDURE CreateBalEntry@1080004();
    BEGIN
      CLEAR(GenJnlLine);
      GenJnlLine.VALIDATE("Journal Template Name",JournalSelection."Journal Template Name");
      GenJnlLine.VALIDATE("Journal Batch Name",JournalSelection."Journal Batch Name");
      GenJnlLine.SetUpNewLine(PrevGenJnlLine,1,TRUE);
      GenJnlLine.VALIDATE("Line No.",NextLineNo);
      NextLineNo := NextLineNo + 10000;
      GenJnlLine.VALIDATE("Posting Date",LatestBBSDate);
      GenJnlLine.VALIDATE("Document Type",GenJnlLine."Document Type"::Payment);
      GenJnlLine.VALIDATE("Account Type",BalAccType);
      GenJnlLine.VALIDATE("Account No.",BalAccNo);
      GenJnlLine.VALIDATE(Description,STRSUBSTNO(Text10612,LatestPaymentRef));
      GenJnlLine.VALIDATE(Amount,-BalEntrySum);
      GenJnlLine.INSERT(TRUE);
      PrevGenJnlLine := GenJnlLine;

      BalEntrySum := 0;
      NewDocumentNo := TRUE;
    END;

    LOCAL PROCEDURE CreateDivergence@1080002(DivergenceAmount@1080000 : Decimal) : Boolean;
    BEGIN
      IF OCRSetup."Max. Divergence" = 0 THEN
        EXIT;
      OCRSetup.TESTFIELD("Divergence Account No.");

      CLEAR(GenJnlLine);
      GenJnlLine.VALIDATE("Journal Template Name",JournalSelection."Journal Template Name");
      GenJnlLine.VALIDATE("Journal Batch Name",JournalSelection."Journal Batch Name");
      GenJnlLine.SetUpNewLine(PrevGenJnlLine,1,TRUE);
      GenJnlLine.VALIDATE("Line No.",NextLineNo);
      NextLineNo := NextLineNo + 10000;
      GenJnlLine.VALIDATE("Posting Date",LatestBBSDate);
      GenJnlLine.VALIDATE("Document Type",GenJnlLine."Document Type"::" ");
      GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::"G/L Account");
      GenJnlLine.VALIDATE("Account No.",OCRSetup."Divergence Account No.");
      GenJnlLine.VALIDATE(Description,STRSUBSTNO(Text10611,CustEntry."Document Type",CustEntry."Document No."));
      GenJnlLine.VALIDATE(Amount,DivergenceAmount);
      GenJnlLine.INSERT(TRUE);
      PrevGenJnlLine := GenJnlLine;
    END;

    LOCAL PROCEDURE RecordType30@1080003();
    VAR
      OCRDateString@1080000 : Text[6];
    BEGIN
      // OCR Date string is always specified as DDMMYY
      OCRDateString := COPYSTR(RecordContent,16,6);
      ParseBbsDate(Bbsdate,OCRDateString);

      EVALUATE(CentralID,COPYSTR(RecordContent,22,2));
      EVALUATE(DayCode,COPYSTR(RecordContent,24,2));
      EVALUATE(PartialSettlementNo,COPYSTR(RecordContent,26,1));
      PaymentRef := COPYSTR(RecordContent,22,10);
      // Read the whole KID for later check:
      KID := COPYSTR(RecordContent,50,25);
      // Amount in LCY/100 to negativ Amount in LCY.
      EVALUATE(OCRAmount,COPYSTR(RecordContent,33,17));
      OCRAmount := -(OCRAmount / 100);

      // Customer No. is not read. Only the document no. and maybe document type are read
      ApplyToDocumentType := '';
      CASE SalesSetup."KID Setup" OF
        SalesSetup."KID Setup"::"Document No.":
          ApplyToDocumentNo := COPYSTR(KID,25 - SalesSetup."Document No. length",SalesSetup."Document No. length");
        SalesSetup."KID Setup"::"Document No.+Customer No.":
          ApplyToDocumentNo :=
            COPYSTR(KID,25 - SalesSetup."Document No. length" - SalesSetup."Customer No. length",
              SalesSetup."Document No. length");
        SalesSetup."KID Setup"::"Customer No.+Document No.":
          ApplyToDocumentNo := COPYSTR(KID,25 - SalesSetup."Document No. length",SalesSetup."Document No. length");
        SalesSetup."KID Setup"::"Document Type+Document No.":
          BEGIN
            ApplyToDocumentNo := COPYSTR(KID,25 - SalesSetup."Document No. length",SalesSetup."Document No. length");
            ApplyToDocumentType := FORMAT(KID[24 - SalesSetup."Document No. length"]);
          END;
        SalesSetup."KID Setup"::"Document No.+Document Type":
          BEGIN
            ApplyToDocumentNo := COPYSTR(KID,24 - SalesSetup."Document No. length",SalesSetup."Document No. length");
            ApplyToDocumentType := FORMAT(KID[25]);
          END;
        ELSE
          SalesSetup.FIELDERROR("KID Setup");
      END;
    END;

    LOCAL PROCEDURE RecordType31@1080008();
    BEGIN
      TransType := COPYSTR(RecordContent,5,2);
    END;

    LOCAL PROCEDURE DeleteZeroes@1080007(No@1080000 : Code[20]) : Code[20];
    VAR
      Continue@1080001 : Boolean;
    BEGIN
      // Delete leadnig zeros:
      Continue := TRUE;
      WHILE Continue AND (No <> '') DO BEGIN
        Continue := (No[1] = '0');
        IF Continue THEN
          No := COPYSTR(No,2);
      END;
      EXIT(No);
    END;

    PROCEDURE SetJournal@1080010(JournalLine@1080000 : Record 81);
    BEGIN
      JournalSelection."Journal Template Name" := JournalLine."Journal Template Name";
      JournalSelection."Journal Batch Name" := JournalLine."Journal Batch Name";
    END;

    LOCAL PROCEDURE SetWarning@1080001(WarningNo@1080000 : Integer);
    VAR
      Text@1080001 : Text[250];
    BEGIN
      NumberOfWarnings := NumberOfWarnings + 1;
      IF GenJnlLine.Warning THEN
        // An error is already registered. Specify error no. on further errors only
        IF STRPOS(GenJnlLine."Warning text",Text10620) = 0 THEN
          Text := STRSUBSTNO(Text10621,GenJnlLine."Warning text",WarningNo)
        ELSE
          Text := STRSUBSTNO('%1, %2',GenJnlLine."Warning text",WarningNo)
      ELSE BEGIN
        GenJnlLine.VALIDATE(Warning,TRUE);
        CASE WarningNo OF
          1:
            Text := STRSUBSTNO(Text10622,RemainingAmount);
          2:
            Text := STRSUBSTNO(Text10623,KID);
          3:
            Text := STRSUBSTNO(Text10624,CustEntry."Entry No.");
          4:
            Text := STRSUBSTNO(Text10625,ApplyToDocumentNo);
          5:
            Text := STRSUBSTNO(Text10626,RemainingAmount);
          6:
            Text := STRSUBSTNO(Text10627,ApplyToDocumentNo,KID);
          7:
            Text := STRSUBSTNO(Text10628,ApplyToDocumentNo,KID);
          ELSE
            Text := Text10629;
        END;
        Text := STRSUBSTNO(Text10630,WarningNo,Text);
      END;
      IF STRLEN(Text) > MAXSTRLEN(GenJnlLine."Warning text") THEN
        Text := COPYSTR(Text,1,MAXSTRLEN(GenJnlLine."Warning text") - 3) + '...';
      GenJnlLine.VALIDATE("Warning text",Text);
    END;

    PROCEDURE CreateFilename@1080021();
    BEGIN
      IF FileMgt.ClientFileExists(FileName) THEN BEGIN
        IF (FileName[STRLEN(FileName)] = '~') OR
           (FileName[STRLEN(FileName) - 1] = '~')
        THEN
          EXIT;

        file1 := COPYSTR(FileName,1,STRLEN(FileName) - 1) + '~';
        file2 := COPYSTR(FileName,1,STRLEN(FileName) - 2) + '~~';
        IF FileMgt.ClientFileExists(file2) THEN
          FileMgt.DeleteClientFile(file2);
        IF FileMgt.ClientFileExists(file1) THEN
          FileMgt.MoveFile(file1,file2);
        FileMgt.MoveFile(FileName,file1);
      END;
    END;

    LOCAL PROCEDURE ParseBbsDate@1080000(VAR BbsDate@1080000 : Date;OCRDateString@1080001 : Text[6]);
    VAR
      TypeHelper@1080002 : Codeunit 10;
      DateVariant@1080003 : Variant;
    BEGIN
      DateVariant := BbsDate;
      TypeHelper.Evaluate(DateVariant,OCRDateString,'ddMMyy','no');
      BbsDate := DateVariant;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

