OBJECT Report 298 Batch Post Sales Credit Memos
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Batch Post Sales Credit Memos;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6640;    ;DataItem;                    ;
               DataItemTable=Table36;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Credit Memo));
               ReqFilterHeadingML=ENU=Sales Credit Memo;
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);

                               //**4PS.sn
                               IF varUserID <> '' THEN
                                 SETFILTER("Inserted By",varUserID);

                               IF SalesSetup."Post only released Invoices" THEN BEGIN
                                 FILTERGROUP(9);
                                 SETRANGE(Status,Status::Released);
                                 FILTERGROUP(0);
                               END;
                               //**4PS.en

                               CounterTotal := COUNT;
                               Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=VAR
                                  ApprovalsMgmt@1000 : Codeunit 1535;
                                  Cust@1100525000 : Record 18;
                                  SalesCrMemoHeader2@1100525001 : Record 114;
                                BEGIN
                                  IF ApprovalsMgmt.IsSalesApprovalsWorkflowEnabled("Sales Header") OR (Status = Status::"Pending Approval") THEN
                                    CurrReport.SKIP;

                                  IF CalcInvDisc THEN
                                    CalculateInvoiceDiscount;

                                  Counter := Counter + 1;
                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                                  CLEAR(SalesPost);
                                  SalesPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                                  IF SalesPost.RUN("Sales Header") THEN BEGIN
                                    //**4PS.sn
                                    IF gPrintInvoices THEN BEGIN
                                      IF "Last Posting No." = '' THEN
                                        SalesCrMemoHeader."No." := "No."
                                      ELSE
                                        SalesCrMemoHeader."No." := "Last Posting No.";
                                      SalesCrMemoHeader.FIND('=');

                                      IF (SalesCrMemoHeader."Electronic Invoicing" <> SalesCrMemoHeader."Electronic Invoicing"::Print)
                                      THEN BEGIN
                                        // Send digital invoice and don't mark it for printing
                                        SalesCrMemoHeader2 := SalesCrMemoHeader;
                                        SalesCrMemoHeader2.SETRECFILTER;
                                        SalesCrMemoHeader2.SendCreditMemoElectronic(FALSE,TRUE);
                                      END ELSE
                                        SalesCrMemoHeader.MARK(TRUE);
                                    END;
                                    //**4PS.en

                                    CounterOK := CounterOK + 1;
                                    IF MARKEDONLY THEN
                                      MARK(FALSE);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                MESSAGE(Text002,CounterOK,CounterTotal);

                                //**4PS.sn
                                IF gPrintInvoices THEN BEGIN
                                  SalesCrMemoHeader.MARKEDONLY(TRUE);
                                  IF NOT SalesCrMemoHeader.ISEMPTY THEN //**4PS.n
                                    PrintReport();
                                END;
                                //**4PS.en
                              END;

               ReqFilterFields=No.,Status,Inserted By }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   SalesSetup.GET;
                   CalcInvDisc := SalesSetup."Calc. Inv. Discount";
                   //**4PS.so
                   //ReplacePostingDate := FALSE;
                   //ReplaceDocumentDate := FALSE;
                   //**4PS.eo
                   //**4PS.sn
                   ReplacePostingDate := SalesSetup."Replace Posting Date";
                   ReplaceDocumentDate := SalesSetup."Replace Document Date";
                   IF SalesSetup."Replace Posting Date" OR SalesSetup."Replace Document Date" THEN
                     PostingDateReq := WORKDATE;
                   //**4PS.en
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=ENU=Posting Date;
                  SourceExpr=PostingDateReq }

      { 2   ;2   ;Field     ;
                  Name=ReplacePostingDate;
                  CaptionML=ENU=Replace Posting Date;
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 3   ;2   ;Field     ;
                  CaptionML=ENU=Replace Document Date;
                  SourceExpr=ReplaceDocumentDate }

      { 5   ;2   ;Field     ;
                  CaptionML=ENU=Calc. Inv. Discount;
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               SalesSetup.GET;
                               SalesSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

      { 1100485000;2;Field  ;
                  CaptionML=ENU=Inserted By;
                  SourceExpr=varUserID;
                  OnLookup=BEGIN
                             LoginMgt.LookupUserID(varUserID);
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Enter the posting date.';
      Text001@1001 : TextConst 'ENU=Posting credit memos  #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'ENU=%1 credit memos out of a total of %2 have now been posted.';
      Text003@1003 : TextConst 'ENU=The exchange rate associated with the new posting date on the sales header will not apply to the sales lines.';
      SalesLine@1005 : Record 37;
      SalesSetup@1006 : Record 311;
      SalesCalcDisc@1007 : Codeunit 60;
      SalesPost@1008 : Codeunit 80;
      Window@1009 : Dialog;
      PostingDateReq@1010 : Date;
      CounterTotal@1011 : Integer;
      Counter@1012 : Integer;
      CounterOK@1013 : Integer;
      ReplacePostingDate@1016 : Boolean;
      ReplaceDocumentDate@1017 : Boolean;
      CalcInvDisc@1018 : Boolean;
      gPrintInvoices@1100525002 : Boolean;
      SalesCrMemoHeader@1100525001 : Record 114;
      varUserID@1100485001 : Code[50];
      LoginMgt@1100485000 : Codeunit 418;

    LOCAL PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      SalesLine.RESET;
      SalesLine.SETRANGE("Document Type","Sales Header"."Document Type");
      SalesLine.SETRANGE("Document No.","Sales Header"."No.");
      IF SalesLine.FINDFIRST THEN
        IF SalesCalcDisc.RUN(SalesLine) THEN BEGIN
          "Sales Header".GET("Sales Header"."Document Type","Sales Header"."No.");
          COMMIT;
        END;
    END;

    PROCEDURE PrintInvoices@1100485000();
    BEGIN
      //**4PS.n
      gPrintInvoices := TRUE;
    END;

    LOCAL PROCEDURE PrintReport@1100485001();
    VAR
      DocPrint@1100525000 : Codeunit 229;
      TempReportSelection@1100525001 : TEMPORARY Record 77;
    BEGIN
      //**4PS.n
      //ReportSelection.RESET;
      //ReportSelection.SETRANGE(Usage,ReportSelection.Usage::"S.Cr.Memo");
      DocPrint.GetReportSelection(TempReportSelection.Usage::"S.Cr.Memo",TempReportSelection,TRUE);
      TempReportSelection.FIND('-');
      REPEAT
        //ReportSelection.TESTFIELD("Report ID");
        REPORT.RUNMODAL(TempReportSelection."Report ID",TRUE,FALSE,SalesCrMemoHeader);
      UNTIL TempReportSelection.NEXT = 0;
    END;

    BEGIN
    {
      //**4PS: 01-10-2008 Possibility to print during batch posting. (see also Report 297).

      4PS, dbeekman, 23-01-2009:
        - ReportSelection changed. cod229.GetReportSelection called to select reports to be printed
    }
    END.
  }
  RDLDATA
  {
  }
}

