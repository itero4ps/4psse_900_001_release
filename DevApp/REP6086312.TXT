OBJECT Report 6086312 Batch Create Posting Sug.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW17.00.00.1.02.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Batch Create Posting Suggestion;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                    ERROR(Text000);
                END;

    OnPostReport=BEGIN
                   ExpensePost.ForcePIApproval;

                   Window.CLOSE;

                   IF CounterOK <> 0 THEN
                     MESSAGE(Text002,CounterOK,Counter);
                 END;

  }
  DATASET
  {
    { 1   ;0   ;DataItem;                    ;
               DataItemTable=Table6086320;
               DataItemTableView=SORTING(Posted,Employee No.,Status);
               OnPreDataItem=BEGIN
                               SETRANGE(Posted,FALSE);
                               SETFILTER(Status,'%1|%2',Status::Approved,Status::Rejected);
                               CounterTotal := COUNT;
                               Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=VAR
                                  Expense2@1000 : Record 6086320;
                                BEGIN
                                  IF NOT IncludeRejectedWithRefund THEN
                                    IF (Status = Status::Rejected) AND NOT "No Refund" THEN
                                      CurrReport.SKIP;
                                  Counter := Counter + 1;
                                  IF Expense.AllocationsExists THEN BEGIN
                                    CALCFIELDS("Allocated Amount (LCY)");
                                    IF "Allocated Amount (LCY)" <> "Amount (LCY)" THEN
                                      CurrReport.SKIP;
                                  END;
                                  CODEUNIT.RUN(CODEUNIT::"Expense-Validate",Expense);
                                  IF Expense.HasErrorComment THEN
                                    CurrReport.SKIP;
                                  CALCFIELDS("Employee Name");
                                  Window.UPDATE(1,STRSUBSTNO('%1 - %2 %3',"Entry No.","Employee No.","Employee Name"));
                                  Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                                  COMMIT;
                                  ExpensePost.SetPostingDate(ReplacePostingDate,PostingDateReq);
                                  Expense2 := Expense;
                                  IF ExpenseCheck.RUN(Expense2) THEN BEGIN
                                    TempExpenseToPost.COPY(Expense2);
                                    TempExpenseToPost.INSERT;
                                    IF MARKEDONLY THEN
                                      MARK(FALSE);
                                  END;
                                END;

               ReqFilterFields=Employee No.,Document Date,Entry No. }

    { 2   ;0   ;DataItem;                    ;
               DataItemTable=Table2000000026;
               OnPreDataItem=BEGIN
                               CounterTotal := TempExpenseToPost.COUNT;
                               SETRANGE(Number,1,CounterTotal);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF Number = 1 THEN
                                    TempExpenseToPost.FINDFIRST
                                  ELSE
                                    TempExpenseToPost.NEXT;
                                  Expense.GET(TempExpenseToPost."Entry No.");
                                  Expense.CALCFIELDS("Employee Name");
                                  Window.UPDATE(1,STRSUBSTNO('%1 - %2 %3',Expense."Entry No.",Expense."Employee No.",Expense."Employee Name"));
                                  Window.UPDATE(3,ROUND(Number / CounterTotal * 10000,1));
                                  ExpensePost.SetPostingDate(ReplacePostingDate,PostingDateReq);
                                  ExpensePost.RunWithoutCheck(Expense);
                                  CounterOK := CounterOK + 1;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 5   ;0   ;Container ;
                  ContainerType=ContentArea }

      { 4   ;1   ;Group     ;
                  CaptionML=ENU=Options }

      { 2   ;2   ;Field     ;
                  CaptionML=ENU=Posting Date;
                  SourceExpr=PostingDateReq }

      { 3   ;2   ;Field     ;
                  CaptionML=ENU=Replace Posting Date;
                  SourceExpr=ReplacePostingDate }

      { 1   ;2   ;Field     ;
                  CaptionML=ENU=Include Rejected with Refund;
                  SourceExpr=IncludeRejectedWithRefund }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ExpensePost@1010 : Codeunit 6086330;
      ExpenseCheck@1001 : Codeunit 6086333;
      TempExpenseToPost@1000 : TEMPORARY Record 6086320;
      Window@1009 : Dialog;
      Counter@1008 : Integer;
      CounterOK@1007 : Integer;
      CounterTotal@1006 : Integer;
      PostingDateReq@1005 : Date;
      ReplacePostingDate@1004 : Boolean;
      ReplaceDocumentDate@1003 : Boolean;
      IncludeRejectedWithRefund@1002 : Boolean;
      Text000@1013 : TextConst 'ENU=Please enter the posting date.';
      Text001@1012 : TextConst 'ENU=Creating Expense Posting Suggestion\#1##########################\Testing @2@@@@@@@@@@@@@@@@@@@@@@@@@@\Posting @3@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text002@1011 : TextConst 'ENU=Posting suggestion for %1 expenses out of a total of %2 have now been created.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

