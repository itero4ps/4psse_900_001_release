OBJECT Report 99001015 Calculate Subcontracts
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Calculate Subcontracts;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   MfgSetup.GET;
                 END;

    OnPreReport=BEGIN
                  ReqWkshTmpl.GET(ReqLine."Worksheet Template Name");
                  ReqWkShName.GET(ReqLine."Worksheet Template Name",ReqLine."Journal Batch Name");
                  ReqLine.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
                  ReqLine.SETRANGE("Journal Batch Name",ReqLine."Journal Batch Name");
                  ReqLine.LOCKTABLE;

                  IF ReqLine.FINDLAST THEN
                    ReqLine.INIT;
                  Window.OPEN(Text000 + Text001);
                END;

  }
  DATASET
  {
    { 8481;    ;DataItem;                    ;
               DataItemTable=Table99000754;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               ReqLine.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
                               ReqLine.SETRANGE("Journal Batch Name",ReqLine."Journal Batch Name");
                               ReqLine.DELETEALL;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF "Subcontractor No." = '' THEN
                                    CurrReport.SKIP;

                                  Window.UPDATE(1,"No.");
                                END;

               ReqFilterFields=No.,Subcontractor No. }

    { 4877;1   ;DataItem;                    ;
               DataItemTable=Table5409;
               DataItemTableView=SORTING(Type,No.)
                                 WHERE(Status=CONST(Released),
                                       Type=CONST(Work Center),
                                       Routing Status=FILTER(<Finished));
               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(2,"Prod. Order No.");

                                  ProdOrderLine.SETCURRENTKEY(Status,"Prod. Order No.","Routing No.","Routing Reference No.");
                                  ProdOrderLine.SETRANGE(Status,Status);
                                  ProdOrderLine.SETRANGE("Prod. Order No.","Prod. Order No.");
                                  ProdOrderLine.SETRANGE("Routing No.","Routing No.");
                                  ProdOrderLine.SETRANGE("Routing Reference No.","Routing Reference No.");
                                  IF ProdOrderLine.FIND('-') THEN BEGIN
                                    DeleteRepeatedReqLines;
                                    REPEAT
                                      BaseQtyToPurch :=
                                        CostCalcMgt.CalcQtyAdjdForRoutingScrap(
                                          CostCalcMgt.CalcQtyAdjdForBOMScrap(
                                            ProdOrderLine."Quantity (Base)",ProdOrderLine."Scrap %"),
                                          "Scrap Factor % (Accumulated)","Fixed Scrap Qty. (Accum.)") -
                                        (CostCalcMgt.CalcOutputQtyBaseOnPurchOrder(ProdOrderLine,"Prod. Order Routing Line") +
                                         CostCalcMgt.CalcActOutputQtyBase(ProdOrderLine,"Prod. Order Routing Line"));
                                      QtyToPurch := ROUND(BaseQtyToPurch / ProdOrderLine."Qty. per Unit of Measure",0.00001);
                                      IF QtyToPurch > 0 THEN
                                        InsertReqWkshLine;
                                    UNTIL ProdOrderLine.NEXT = 0;
                                  END;
                                END;

               ReqFilterFields=Prod. Order No.,Starting Date;
               DataItemLink=No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   ReqWkshEditable := (PurchOrderChoice = PurchOrderChoice::"Copy to Req. Wksh");
                   TransWkshEditable := (TransOrderChoice = TransOrderChoice::"Copy to Req. Wksh");
                 END;

    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  CaptionML=ENU=Production Order;
                  OptionCaptionML=ENU=" ,Planned,Firm Planned,Firm Planned & Print";
                  SourceExpr=ProdOrderChoice }

      { 1100525003;2;Field  ;
                  CaptionML=ENU=Purchase Order;
                  OptionCaptionML=ENU=" ,Make Purch. Orders,Make Purch. Orders & Print,Copy to Req. Wksh";
                  SourceExpr=PurchOrderChoice;
                  OnValidate=BEGIN
                               ReqWkshEditable := (PurchOrderChoice = PurchOrderChoice::"Copy to Req. Wksh");
                             END;
                              }

      { 1100525004;2;Group  ;
                  CaptionML=ENU=Copy to Req. Worksheet;
                  GroupType=Group }

      { 1100525005;3;Field  ;
                  CaptionML=ENU=Template Name;
                  SourceExpr=ReqWkshTemp;
                  TableRelation="Req. Wksh. Template";
                  Enabled=ReqWkshEditable;
                  OnValidate=BEGIN
                               ReqWksh := '';
                             END;

                  OnLookup=BEGIN
                             IF PAGE.RUNMODAL(PAGE::"Req. Worksheet Templates",ReqWkshTmpl) = ACTION::LookupOK THEN BEGIN
                               Text := ReqWkshTmpl.Name;
                               EXIT(TRUE);
                             END ELSE
                               EXIT(FALSE);
                           END;
                            }

      { 1100525006;3;Field  ;
                  CaptionML=ENU=Worksheet Name;
                  SourceExpr=ReqWksh;
                  TableRelation="Requisition Wksh. Name".Name;
                  Editable=ReqWkshEditable;
                  OnLookup=BEGIN
                             ReqWkShName.SETFILTER("Worksheet Template Name",ReqWkshTemp);
                             IF PAGE.RUNMODAL(PAGE::"Req. Wksh. Names",ReqWkShName) = ACTION::LookupOK THEN BEGIN
                               Text := ReqWkShName.Name;
                               EXIT(TRUE);
                             END ELSE
                               EXIT(FALSE);
                           END;
                            }

      { 1100525007;2;Field  ;
                  CaptionML=ENU=Transfer Order;
                  SourceExpr=TransOrderChoice;
                  OnValidate=BEGIN
                               TransWkshEditable := (TransOrderChoice = TransOrderChoice::"Copy to Req. Wksh");
                             END;
                              }

      { 1100525008;2;Group  ;
                  CaptionML=ENU=Copy to Req. Worksheet;
                  GroupType=Group }

      { 1100525009;3;Field  ;
                  CaptionML=ENU=Template Name;
                  SourceExpr=TransWkshTemp;
                  TableRelation="Req. Wksh. Template";
                  Enabled=TransWkshEditable;
                  OnValidate=BEGIN
                               TransWkshName := '';
                             END;

                  OnLookup=BEGIN
                             IF PAGE.RUNMODAL(PAGE::"Req. Worksheet Templates",ReqWkshTmpl) = ACTION::LookupOK THEN BEGIN
                               Text := ReqWkshTmpl.Name;
                               EXIT(TRUE);
                             END ELSE
                               EXIT(FALSE);
                           END;
                            }

      { 1100525010;3;Field  ;
                  CaptionML=ENU=Worksheet Name;
                  SourceExpr=TransWkshName;
                  Enabled=TransWkshEditable;
                  OnLookup=BEGIN
                             ReqWkShName.SETFILTER("Worksheet Template Name",TransWkshTemp);
                             IF PAGE.RUNMODAL(PAGE::"Req. Wksh. Names",ReqWkShName) = ACTION::LookupOK THEN BEGIN
                               Text := ReqWkShName.Name;
                               EXIT(TRUE);
                             END ELSE
                               EXIT(FALSE);
                           END;
                            }

      { 1100525011;2;Field  ;
                  CaptionML=ENU=Stop and Show First Error;
                  SourceExpr=NoPlanningResiliency }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing Work Centers   #1##########\';
      Text001@1001 : TextConst 'ENU="Processing Orders         #2########## "';
      MfgSetup@1002 : Record 99000765;
      ReqWkshTmpl@1003 : Record 244;
      ReqWkShName@1004 : Record 245;
      ReqLine@1005 : Record 246;
      ProdOrderLine@1006 : Record 5406;
      GLSetup@1013 : Record 98;
      PurchLine@1014 : Record 39;
      CostCalcMgt@1007 : Codeunit 5836;
      Window@1008 : Dialog;
      BaseQtyToPurch@1010 : Decimal;
      QtyToPurch@1011 : Decimal;
      GLSetupRead@1012 : Boolean;
      ReqWkshTemp@1100525007 : Code[10];
      ReqWksh@1100525006 : Code[10];
      TransWkshTemp@1100525005 : Code[10];
      TransWkshName@1100525004 : Code[10];
      ProdOrderChoice@1100525003 : ' ,Planned,Firm Planned,Firm Planned & Print,Copy to Req. Wksh';
      PurchOrderChoice@1100525002 : ' ,Make Purch. Orders,Make Purch. Orders & Print,Copy to Req. Wksh';
      TransOrderChoice@1100525001 : ' ,Make Trans. Orders,Make Trans. Orders & Print,Copy to Req. Wksh';
      NoPlanningResiliency@1100525000 : Boolean;
      ReqWkshEditable@1100525008 : Boolean INDATASET;
      TransWkshEditable@1100525009 : Boolean INDATASET;

    PROCEDURE SetWkShLine@1(NewReqLine@1000 : Record 246);
    BEGIN
      ReqLine := NewReqLine;
    END;

    LOCAL PROCEDURE InsertReqWkshLine@3();
    BEGIN
      ProdOrderLine.CALCFIELDS("Total Exp. Oper. Output (Qty.)");

      WITH ReqLine DO BEGIN
        SetSubcontracting(TRUE);
        BlockDynamicTracking(TRUE);

        INIT;
        "Line No." := "Line No." + 10000;
        VALIDATE(Type,Type::Item);
        VALIDATE("No.",ProdOrderLine."Item No.");
        VALIDATE("Variant Code",ProdOrderLine."Variant Code");
        VALIDATE("Unit of Measure Code",ProdOrderLine."Unit of Measure Code");
        VALIDATE(Quantity,QtyToPurch);
        GetGLSetup;
        IF Quantity <> 0 THEN BEGIN
          IF "Work Center"."Unit Cost Calculation" = "Work Center"."Unit Cost Calculation"::Units THEN
            VALIDATE(
              "Direct Unit Cost",
              ROUND(
                "Prod. Order Routing Line"."Direct Unit Cost" *
                ProdOrderLine."Qty. per Unit of Measure",
                GLSetup."Unit-Amount Rounding Precision"))
          ELSE
            VALIDATE(
              "Direct Unit Cost",
              ROUND(
                ("Prod. Order Routing Line"."Expected Operation Cost Amt." -
                 "Prod. Order Routing Line"."Expected Capacity Ovhd. Cost") /
                ProdOrderLine."Total Exp. Oper. Output (Qty.)",
                GLSetup."Unit-Amount Rounding Precision"));
        END ELSE
          VALIDATE("Direct Unit Cost",0);
        "Qty. per Unit of Measure" := 0;
        "Quantity (Base)" := 0;
        "Prod. Order No." := ProdOrderLine."Prod. Order No.";
        "Prod. Order Line No." := ProdOrderLine."Line No.";
        "Due Date" := "Prod. Order Routing Line"."Ending Date";
        "Requester ID" := USERID;
        "Location Code" := ProdOrderLine."Location Code";
        "Bin Code" := ProdOrderLine."Bin Code";
        "Routing Reference No." := "Prod. Order Routing Line"."Routing Reference No.";
        "Routing No." := "Prod. Order Routing Line"."Routing No.";
        "Operation No." := "Prod. Order Routing Line"."Operation No.";
        "Work Center No." := "Prod. Order Routing Line"."Work Center No.";
        VALIDATE("Vendor No.","Work Center"."Subcontractor No.");
        Description := "Prod. Order Routing Line".Description;

        // If purchase order already exist we will change this if possible
        PurchLine.RESET;
        PurchLine.SETCURRENTKEY("Document Type",Type,"Prod. Order No.","Prod. Order Line No.","Routing No.","Operation No.");
        PurchLine.SETRANGE("Document Type",PurchLine."Document Type"::Order);
        PurchLine.SETRANGE(Type,PurchLine.Type::Item);
        PurchLine.SETRANGE("Prod. Order No.",ProdOrderLine."Prod. Order No.");
        PurchLine.SETRANGE("Prod. Order Line No.",ProdOrderLine."Line No.");
        PurchLine.SETRANGE("Routing No.","Prod. Order Routing Line"."Routing No.");
        PurchLine.SETRANGE("Operation No.","Prod. Order Routing Line"."Operation No.");
        PurchLine.SETRANGE("Planning Flexibility",PurchLine."Planning Flexibility"::Unlimited);
        PurchLine.SETRANGE("Quantity Received",0);
        IF PurchLine.FINDFIRST THEN BEGIN
          VALIDATE(Quantity,Quantity + PurchLine."Outstanding Quantity");
          "Quantity (Base)" := 0;
          "Replenishment System" := "Replenishment System"::Purchase;
          "Ref. Order No." := PurchLine."Document No.";
          "Ref. Order Type" := "Ref. Order Type"::Purchase;
          "Ref. Line No." := PurchLine."Line No.";
          IF PurchLine."Expected Receipt Date" = "Due Date" THEN
            "Action Message" := "Action Message"::"Change Qty."
          ELSE
            "Action Message" := "Action Message"::"Resched. & Chg. Qty.";
          //"Accept Action Message" := TRUE; //**4PS.o
        END ELSE BEGIN
          "Ref. Order No." := ProdOrderLine."Prod. Order No.";
          "Ref. Order Type" := "Ref. Order Type"::"Prod. Order";
          "Ref. Order Status" := ProdOrderLine.Status;
          "Ref. Line No." := ProdOrderLine."Line No.";
          "Action Message" := "Action Message"::New;
          //"Accept Action Message" := TRUE; //**4PS.o
        END;

        //**4PS.sn
        MfgSetup.GET;
        "Accept Action Message" := NOT MfgSetup."Manual Set Action Message";
        //**4PS.en

        IF "Ref. Order No." <> '' THEN
          GetDimFromRefOrderLine(TRUE);

        INSERT;
      END;
    END;

    LOCAL PROCEDURE GetGLSetup@14();
    BEGIN
      IF NOT GLSetupRead THEN
        GLSetup.GET;
      GLSetupRead := TRUE;
    END;

    LOCAL PROCEDURE DeleteRepeatedReqLines@2();
    VAR
      RequisitionLine@1000 : Record 246;
    BEGIN
      WITH RequisitionLine DO BEGIN
        SETRANGE(Type,Type::Item);
        SETRANGE("No.",ProdOrderLine."Item No.");
        SETRANGE("Prod. Order No.",ProdOrderLine."Prod. Order No.");
        SETRANGE("Operation No.","Prod. Order Routing Line"."Operation No.");
        DELETEALL(TRUE);
      END;
    END;

    BEGIN
    {
      4PS HBK 07-07-09: Changed assigment "Accept Action Message"
    }
    END.
  }
  RDLDATA
  {
  }
}

