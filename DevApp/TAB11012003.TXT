OBJECT Table 11012003 Budget Adjustment
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Project No.,No.;
    OnInsert=BEGIN
               "Version Date" := WORKDATE;
               "Input by" := USERID;
               "Input Date" := TODAY;

               IF "No." = '' THEN
                 GetDefaultAdjustmentNo;
             END;

    OnModify=BEGIN
               FillFocussedFields();

               IF "Version Date" <> xRec."Version Date" THEN BEGIN
                 BudgetRec.RESET;
                 BudgetRec.SETRANGE("Project No.","Project No.");
                 BudgetRec.SETRANGE(Adjustment,"No.");
                 BudgetRec.SETRANGE(Option,'');
                 BudgetRec.SETRANGE("Extension Contract",'');
                 BudgetRec.SETFILTER("Version Date",'<> %1',"Version Date");
                 IF BudgetRec.FINDSET(TRUE, FALSE) THEN
                   REPEAT
                     BudgetRec."Version Date" := "Version Date";
                     BudgetRec.MODIFY;
                   UNTIL BudgetRec.NEXT = 0;
               END;
             END;

    OnDelete=BEGIN
               //kkleermaker.sn, 110923, M19415
               IF NOT DeleteCompleteProject THEN
                 TESTFIELD("Budget Correction Fixed", FALSE);
               //kkleermaker.en, 110923, M19415
               DELETE;  //db, 25-09-07 (melding C-011060)
               BudgetRec.RESET;
               BudgetRec.SETRANGE("Project No.","Project No.");
               BudgetRec.SETRANGE(Adjustment,"No.");
               //mg.sn, 08-03-11: M25528
               IF BudgetRec.FINDSET THEN
                 REPEAT
                   FillChangedSummarySheet(BudgetRec);
                 UNTIL BudgetRec.NEXT = 0;
               //mg.en, 08-03-11: M25528
               BudgetRec.DELETEALL(TRUE);  //update maintable cause of sql-problem
               INSERT;  //db, 25-09-07 (melding C-011060)

               RecalcSummarySheet(TRUE); //mg, 08-03-11: M25528  //Summary Adjustment
             END;

    OnRename=BEGIN
               FillFocussedFields();
               IF "Project No." <> xRec."Project No." THEN
                 ERROR(Text000);
             END;

    CaptionML=ENU=Budget Adjustment;
    LookupPageID=Page11012012;
    DrillDownPageID=Page11012012;
  }
  FIELDS
  {
    { 1   ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   OnValidate=BEGIN
                                                                GetProject();
                                                                IF NOT ProjRec."Budget Fixed" THEN
                                                                  ERROR(Text001,"Project No.");
                                                              END;

                                                   CaptionML=ENU=Project No.;
                                                   NotBlank=Yes;
                                                   Numeric=No }
    { 2   ;   ;No.                 ;Code10        ;CaptionML=ENU=No.;
                                                   SQL Data Type=Integer;
                                                   NotBlank=Yes;
                                                   Numeric=Yes }
    { 3   ;   ;Description         ;Text30        ;CaptionML=ENU=Description }
    { 4   ;   ;Modified by         ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Modified by;
                                                   Editable=No }
    { 5   ;   ;Last Date Modified  ;Date          ;CaptionML=ENU=Last Date Modified;
                                                   Editable=No }
    { 6   ;   ;Project Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=ENU=Project Description;
                                                   Editable=No }
    { 7   ;   ;Hours               ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Budget Line".Hours WHERE (Project No.=FIELD(Project No.),
                                                                                              Adjustment=FIELD(No.)));
                                                   CaptionML=ENU=Hours;
                                                   Editable=No }
    { 8   ;   ;Labor               ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Budget Line".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                               Adjustment=FIELD(No.),
                                                                                               Cost Type=CONST(Labor)));
                                                   CaptionML=ENU=Labor;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 9   ;   ;Material            ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Budget Line".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                               Adjustment=FIELD(No.),
                                                                                               Cost Type=CONST(Material)));
                                                   CaptionML=ENU=Material;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 10  ;   ;Subcontracting      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Budget Line".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                               Adjustment=FIELD(No.),
                                                                                               Cost Type=CONST(Subcontracting)));
                                                   CaptionML=ENU=Subcontracting;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 11  ;   ;Plant               ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Budget Line".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                               Adjustment=FIELD(No.),
                                                                                               Cost Type=CONST(Plant)));
                                                   CaptionML=ENU=Plant;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 12  ;   ;Sundry              ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Budget Line".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                               Adjustment=FIELD(No.),
                                                                                               Cost Type=CONST(Sundry)));
                                                   CaptionML=ENU=Sundry;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 13  ;   ;Total               ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Budget Line".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                               Adjustment=FIELD(No.)));
                                                   CaptionML=ENU=Total;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 14  ;   ;Version Date        ;Date          ;CaptionML=ENU=Version Date }
    { 20  ;   ;Input by            ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Input by;
                                                   Editable=No }
    { 21  ;   ;Input Date          ;Date          ;CaptionML=ENU=Input Date;
                                                   Editable=No }
    { 30  ;   ;Text                ;Text250       ;CaptionML=ENU=Text }
    { 40  ;   ;Budget Correction Fixed;Boolean    ;OnValidate=VAR
                                                                lUserSetup@1100485000 : Record 91;
                                                              BEGIN
                                                                IF "Budget Correction Fixed" <> xRec."Budget Correction Fixed" THEN
                                                                  IF lUserSetup.GET(USERID) THEN
                                                                    IF lUserSetup."No Access Budget Adjustm Fixed" THEN
                                                                      ERROR(Text002,FIELDCAPTION("Budget Correction Fixed"));
                                                                IF (NOT "Budget Correction Fixed") AND "Correction Fixed" THEN
                                                                  TESTFIELD("Correction Fixed", FALSE);
                                                              END;

                                                   CaptionML=ENU=Budget Correction Fixed }
    { 45  ;   ;Correction Fixed    ;Boolean       ;OnValidate=BEGIN
                                                                //*31928
                                                                IF "Correction Fixed" THEN BEGIN
                                                                  IF NOT "Budget Correction Fixed" THEN
                                                                    VALIDATE("Budget Correction Fixed", TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Correction Fixed }
    { 50  ;   ;Created from Purchase Action;Code20;TableRelation="Project Purchase Action".Code WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=ENU=Created from Purchase Action }
    { 60  ;   ;Budget Adjustment Type;Code10      ;TableRelation="Budget Adjustment Type".Code;
                                                   CaptionML=ENU=Adjustment Type }
    { 70  ;   ;Budget Neutral      ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Budget Adjustment Type"."Budget Neutral" WHERE (Code=FIELD(Budget Adjustment Type)));
                                                   CaptionML=ENU=Budget Neutral;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Project No.,No.                         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@11012004 : TextConst 'ENU=Modify Project not Allowed';
      Text001@11012005 : TextConst 'ENU=Project %1 not fixed, update the original budget.';
      ProjectSetup@1100525001 : Record 315;
      ProjRec@11012000 : Record 11072003;
      BudgetRec@11012002 : Record 11012001;
      Text002@1100485000 : TextConst 'ENU=You have no permission to change %1';
      Text003@1100525000 : TextConst 'ENU=Warning !  The %1 of %2 %3 of %4 %5 unequals zero.';
      Text004@1210190000 : TextConst 'ENU=Update Summary #1###############';
      TmpBudgetSummarySheet@1210190001 : TEMPORARY Record 11012064;
      DeleteCompleteProject@1210190002 : Boolean;

    PROCEDURE FillFocussedFields@3();
    BEGIN
      "Modified by" := USERID;
      "Last Date Modified" := TODAY;
    END;

    PROCEDURE GetProject@1();
    BEGIN
      ProjRec.SETRANGE("No.","Project No.");
      IF ProjRec.FINDFIRST THEN;
    END;

    PROCEDURE CheckAdjustmentBalance@1100525000(lAdjRec@11012000 : Record 11012003);
    BEGIN
      IF lAdjRec."Project No." = '' THEN
        EXIT;

      ProjectSetup.GET;
      IF NOT ProjectSetup."Adjustment Balance Warning" THEN
        EXIT;

      IF lAdjRec."No." <> '' THEN BEGIN //mg.c, 08-03-11: M25528
        BudgetRec.RESET;
        BudgetRec.SETRANGE("Project No.",lAdjRec."Project No.");
        BudgetRec.SETRANGE(Adjustment,lAdjRec."No.");
        BudgetRec.SETRANGE("Adjustment Filter",lAdjRec."No.");
        BudgetRec.CALCSUMS(Amount);
        IF BudgetRec.Amount <> 0 THEN
          MESSAGE(
            Text003,
            BudgetRec.FIELDCAPTION("Total Amount"),
            lAdjRec.TABLECAPTION,
            lAdjRec."No.",
            FIELDCAPTION("Project No."),
            lAdjRec."Project No.");
      END;
    END;

    PROCEDURE FillChangedSummarySheet@1100485002(IBudgetLineRec@1100485001 : Record 11012001);
    VAR
      BudgetSummarySheet@1100485000 : Record 11012064;
      lBudgetAdjustment@1210190000 : Record 11012003;
    BEGIN
      //mg, 08-03-11: M25528
      WITH IBudgetLineRec DO BEGIN

        BudgetSummarySheet.SETRANGE("Project No.", "Project No.");
        BudgetSummarySheet.SETRANGE("Contract No.", "Extension Contract");
        BudgetSummarySheet.SETRANGE("House Model", '');
        BudgetSummarySheet.SETRANGE("Main Group", '');
        BudgetSummarySheet.SETRANGE(Group, '');
        BudgetSummarySheet.SETRANGE("Sub Group", '');
        BudgetSummarySheet.SETRANGE(Option, '');
        IF NOT BudgetSummarySheet.FIND('-') THEN
        //No summary sheet present.
          EXIT;

        TmpBudgetSummarySheet.RESET;
        TmpBudgetSummarySheet.SETRANGE("Project No.", "Project No.");
        TmpBudgetSummarySheet.SETRANGE("Contract No.", "Extension Contract");
        TmpBudgetSummarySheet.SETRANGE("Row No.", "No." ); //Summary Adjustment
        IF NOT TmpBudgetSummarySheet.FIND('-') THEN BEGIN
          TmpBudgetSummarySheet.INIT;
          TmpBudgetSummarySheet."Project No." := "Project No.";
          TmpBudgetSummarySheet."Contract No." := "Extension Contract";
          TmpBudgetSummarySheet."Row No." := "No.";    //Summary Adjustment
          TmpBudgetSummarySheet.INSERT;
        END;

        //Summary Adjustment
        //More recent adjustments are re-calculated.
        lBudgetAdjustment.RESET;
        lBudgetAdjustment.SETRANGE("Project No.", "Project No.");
        lBudgetAdjustment.SETFILTER("No.", '%1..', "No.");
        IF lBudgetAdjustment.FINDSET THEN BEGIN
          IF lBudgetAdjustment."No."  <> "No." THEN BEGIN
            TmpBudgetSummarySheet.RESET;
            TmpBudgetSummarySheet.SETRANGE("Project No.", "Project No.");
            TmpBudgetSummarySheet.SETRANGE("Contract No.", "Extension Contract");
            TmpBudgetSummarySheet.SETRANGE("Row No.", lBudgetAdjustment."No." );
            IF NOT TmpBudgetSummarySheet.FIND('-') THEN BEGIN
              TmpBudgetSummarySheet.INIT;
              TmpBudgetSummarySheet."Project No." := "Project No.";
              TmpBudgetSummarySheet."Contract No." := "Extension Contract";
              TmpBudgetSummarySheet."Row No." := lBudgetAdjustment."No.";
              TmpBudgetSummarySheet.INSERT;
            END;
           END;
        END;
        //Summary Adjustment

        TmpBudgetSummarySheet.RESET;
      END;
    END;

    PROCEDURE RecalcSummarySheet@1100485006(lvToDelete@1210190002 : Boolean) : Boolean;
    VAR
      BudgetSummarySheet@1210190001 : Record 11012064;
      CalcSummaryWindow@1210190000 : Dialog;
    BEGIN
      //mg, 08-03-11: M25528
      IF NOT TmpBudgetSummarySheet.FIND('-') THEN
        EXIT(FALSE);

      CalcSummaryWindow.OPEN(Text004);

      REPEAT
        CalcSummaryWindow.UPDATE(1, "Project No.");

        BudgetSummarySheet.SETRANGE("Project No.", TmpBudgetSummarySheet."Project No.");
        BudgetSummarySheet.SETRANGE("Contract No.", TmpBudgetSummarySheet."Contract No.");
        BudgetSummarySheet.SETRANGE("House Model", '');
        BudgetSummarySheet.SETRANGE("Main Group", '');
        BudgetSummarySheet.SETRANGE(Group, '');
        BudgetSummarySheet.SETRANGE("Sub Group", '');
        BudgetSummarySheet.SETRANGE(Option, '');
        IF BudgetSummarySheet.FIND('-') THEN BEGIN
          //BudgetSummarySheet.ReCalcSummaryLines();                              //Summary Adjustment
          BudgetSummarySheet.ReCalcSummaryLines(TmpBudgetSummarySheet."Row No."); //Summary Adjustment
          IF NOT lvToDelete THEN //Summary Adjustment
            BudgetSummarySheet.UpdateBudgetLines(FALSE, TmpBudgetSummarySheet."Row No.");
        END;
        TmpBudgetSummarySheet.DELETE;
      UNTIL TmpBudgetSummarySheet.NEXT = 0;

      CalcSummaryWindow.CLOSE;

      EXIT(TRUE);
    END;

    PROCEDURE SetDeleteCompleteProject@1210190000();
    BEGIN
      //kkleermaker, 110923, M19415
      DeleteCompleteProject := TRUE;
    END;

    PROCEDURE GetDefaultAdjustmentNo@1100525001();
    VAR
      BudgetAdjustment@1100525000 : Record 11012003;
    BEGIN
      BudgetAdjustment.SETRANGE("Project No.", "Project No.");
      IF BudgetAdjustment.FINDLAST THEN
        "No." := INCSTR(BudgetAdjustment."No.")
      ELSE
        "No." := '1';
    END;

    BEGIN
    END.
  }
}

