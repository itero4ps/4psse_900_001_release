OBJECT Table 11012012 Cost Component
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ExchangeFSA('I');
             END;

    OnModify=BEGIN
               ExchangeFSA('M');
             END;

    OnDelete=BEGIN
               ExchangeFSA('D');
             END;

    OnRename=BEGIN
               ExchangeFSA('R');
             END;

    CaptionML=ENU=Cost Component;
    LookupPageID=Page11012055;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 20  ;   ;Description         ;Text30        ;CaptionML=ENU=Description }
    { 30  ;   ;Cost Component (Add.Cost);Code10   ;TableRelation="Cost Component".Code WHERE (Additional Cost (Service)=CONST(Yes));
                                                   OnValidate=BEGIN
                                                                IF "Cost Component (Add.Cost)" <> '' THEN
                                                                  IF "Cost Component (Add.Cost)" = Code THEN
                                                                    ERROR(Text007);
                                                              END;

                                                   CaptionML=ENU=Cost Component (Additional Cost) }
    { 50  ;   ;Additional Cost (Service);Boolean  ;CaptionML=ENU=Additional Cost (Service) }
    { 11012990;;Export to FSA      ;Boolean       ;CaptionML=ENU=Export to Field Service;
                                                   Description=Indication new/changed. BDA= Buitendienstadmin. (Field Service Admin.), provisional this is only field Vision. }
    { 11012991;;Date/Time Export to FSA;DateTime  ;CaptionML=ENU=Date/Time Export to Field Service }
    { 11128270;;Used for surcharge ;Boolean       ;CaptionML=ENU=Used for surcharge;
                                                   Description=IME066 }
    { 11128271;;Cost Type          ;Option        ;CaptionML=ENU=Cost Type;
                                                   OptionCaptionML=ENU=" ,Labor,Material,Subcontracting,Plant,Sundry,Revenue";
                                                   OptionString=[ ,Labor,Material,Subcontracting,Plant,Sundry,Revenue];
                                                   Description=ENH011 }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Description                              }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Description                         }
  }
  CODE
  {
    VAR
      Text007@1100525000 : TextConst 'ENU=Recursion';

    PROCEDURE ExchangeFSA@1100485000(IMode@1100485003 : Code[1]);
    VAR
      lvProjSetupRec@1100485001 : Record 315;
      lvFieldVisionExchangeMgtCU@1100485000 : Codeunit 11012091;
    BEGIN
      //**4PS.n
      IF IMode = 'I' THEN BEGIN
        "Export to FSA" := FALSE;
        "Date/Time Export to FSA" := 0DT;
      END;
      lvProjSetupRec.GET;
      IF lvProjSetupRec."Field Service Active" THEN BEGIN
        IF IMode <> 'D' THEN BEGIN
          "Export to FSA" := TRUE;
          IF IMode = 'R' THEN
            lvFieldVisionExchangeMgtCU.StoreDeletedCostComponent(xRec);
        END ELSE BEGIN
          lvFieldVisionExchangeMgtCU.StoreDeletedCostComponent(Rec);
        END;
      END;
    END;

    PROCEDURE CheckDefaultCostComponent@1100525002(iServOrder@1100525000 : Code[20];VAR ioCostComp@1100525004 : Code[20];iCompName@1100525005 : Text[50];iCostType@1100525006 : 'Labor,Material,Subcontracting,Plant,Sundry,Revenue';iAdditionalCost@1100525010 : Boolean);
    VAR
      lvServOrderRec@1100525001 : Record 11012823;
      lvOrderTypeRec@1100525002 : Record 11012837;
      lvCostCompRec@1100525008 : Record 11012012;
      lvCostComp@1100525003 : Code[20];
    BEGIN
      //db, 10-11-08: substitute of old function in table 37, 39, 81, 83, 246, 11012032/039/055/825/875, codeunit 11012152, 11125670

      IF iServOrder = '' THEN EXIT;

      IF iCompName <> '' THEN BEGIN
        lvServOrderRec.CHANGECOMPANY(iCompName);
        lvOrderTypeRec.CHANGECOMPANY(iCompName);
        lvCostCompRec.CHANGECOMPANY(iCompName);  //db, 24-02-10
      END;

      IF NOT lvServOrderRec.GET(iServOrder) THEN EXIT;

      IF lvOrderTypeRec.GET(lvOrderTypeRec.Indication::Type, lvServOrderRec."Order Type") THEN BEGIN
        IF lvOrderTypeRec."Cost Component" <> '' THEN
          lvCostComp := lvOrderTypeRec."Cost Component";
      END;

      IF lvOrderTypeRec.GET(lvOrderTypeRec.Indication::Kind, lvServOrderRec."Order Kind") THEN BEGIN
        IF lvOrderTypeRec."Cost Component" <> '' THEN
          lvCostComp := lvOrderTypeRec."Cost Component";
      END;

      IF lvCostComp <> '' THEN BEGIN
        //db.sn, 24-02-10
        IF lvCostCompRec.GET(lvCostComp) THEN BEGIN
          IF lvCostCompRec."Cost Component (Add.Cost)" <> '' THEN
            IF iAdditionalCost THEN
              lvCostComp := lvCostCompRec."Cost Component (Add.Cost)";
        END;
        //db.en, 24-02-10
        ioCostComp := lvCostComp;
      END;
    END;

    BEGIN
    {
      131121, IME066, added "Used for surcharge"

      4PSSE, 140128, ENH-011#2, new field
    }
    END.
  }
}

