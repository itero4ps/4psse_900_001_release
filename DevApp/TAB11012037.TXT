OBJECT Table 11012037 Progress Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ProjectRec.GET("Project No.");
               "Progress Registry" := ProjectRec."Progress Registry";
               "Main Project No." := ProjectRec."Main Project";           //**4PS01.n

               UpdateProgressModified();
               UpdateLatestProgress(FALSE);
             END;

    OnModify=BEGIN
               UpdateProgressModified();
               UpdateLatestProgress(FALSE);
               CalculateAllowed;
               DimMgt.GetDimValueRec(2, "Cost Object", DimValRec, TRUE,'');
               "Cost Type" := DimValRec."Cost Type";
             END;

    OnDelete=VAR
               ProgressRec@1100525000 : Record 11012036;
             BEGIN
               IF NOT DeleteFromProgressHdr THEN BEGIN
                 IF ProgressRec.GET("Project No.", "Progress Date") THEN
                   IF ProgressRec."Progress Fixed" THEN
                     ERROR(Text003);

                 UpdateProgressModified;
               END;
               UpdateLatestProgress(TRUE);
             END;

    CaptionML=ENU=Progress Line;
    DrillDownPageID=Page11012080;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   CaptionML=ENU=Project No.;
                                                   NotBlank=Yes }
    { 20  ;   ;Progress Date       ;Date          ;TableRelation="Progress Level"."Progress Date" WHERE (Project No.=FIELD(Project No.));
                                                   OnValidate=BEGIN
                                                                //"Cumulation Filter" := '..'+FORMAT("Progress Date");  //C017374.o
                                                                //PrevProgrDateFilter := '<'+FORMAT("Progress Date");  //C017374.o
                                                              END;

                                                   CaptionML=ENU=Progress Date;
                                                   NotBlank=Yes }
    { 30  ;   ;Element             ;Code20        ;TableRelation="Project Element".Element WHERE (Project No.=FIELD(Project No.));
                                                   OnValidate=BEGIN
                                                                ElementFormat.FormatElem(Element,2,'');
                                                                ProjElemRec.GET("Project No.",Element);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Element;
                                                   Editable=No }
    { 40  ;   ;Element Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Element".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                           Element=FIELD(Element)));
                                                   CaptionML=ENU=Element Description;
                                                   Editable=No }
    { 50  ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2),
                                                                                               Project Filter=FIELD(Project No.));
                                                   CaptionML=ENU=Cost Object;
                                                   Editable=No }
    { 60  ;   ;Cost Object Description;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value".Name WHERE (Code=FIELD(Cost Object),
                                                                                                    Global Dimension No.=CONST(2)));
                                                   CaptionML=ENU=Cost Object Description;
                                                   Editable=No }
    { 61  ;   ;Project Related Name;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project - Cost Object".Name WHERE (Project No.=FIELD(Project No.),
                                                                                                          Cost Object=FIELD(Cost Object)));
                                                   CaptionML=ENU=Project Related Name;
                                                   Description=Name Cost Object;
                                                   Editable=No }
    { 70  ;   ;Progress Quantity El/Cost Obj.;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Budget Quantity".Quantity WHERE (Project No.=FIELD(Project No.),
                                                                                                        Element=FIELD(Element),
                                                                                                        Cost Object=FIELD(Cost Object),
                                                                                                        Extension Contract=FIELD(Extension Contract)));
                                                   CaptionML=ENU=Progress Quantity El/Cost Obj.;
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 75  ;   ;Progress Quantity Cost Obj.;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Budget Quantity".Quantity WHERE (Project No.=FIELD(Project No.),
                                                                                                        Cost Object=FIELD(Cost Object),
                                                                                                        Extension Contract=FIELD(Extension Contract)));
                                                   CaptionML=ENU=Progress Quantity Cost Obj.;
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 80  ;   ;Progress Unit El/Cost Obj.;Code10  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Budget Quantity"."Unit of Measure" WHERE (Project No.=FIELD(Project No.),
                                                                                                                 Element=FIELD(Element),
                                                                                                                 Cost Object=FIELD(Cost Object),
                                                                                                                 Extension Contract=FIELD(Extension Contract)));
                                                   CaptionML=ENU=Progress Unit El/Cost Obj.;
                                                   Editable=No }
    { 85  ;   ;Progress Unit Cost Obj.;Code10     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Budget Quantity"."Unit of Measure" WHERE (Project No.=FIELD(Project No.),
                                                                                                                 Cost Object=FIELD(Cost Object),
                                                                                                                 Extension Contract=FIELD(Extension Contract)));
                                                   CaptionML=ENU=Progress Unit Cost Obj.;
                                                   Editable=No }
    { 100 ;   ;Cumulative Progress ;Decimal       ;OnValidate=BEGIN
                                                                CheckProgress;

                                                                SetFlowFiltersDates; //C017374.n
                                                                CALCFIELDS("Previous Posture");
                                                                "Progress in Period" := "Cumulative Progress" - "Previous Posture";

                                                                CalculateProgressPercentages;
                                                              END;

                                                   CaptionML=ENU=Cumulative Progress;
                                                   DecimalPlaces=2:5 }
    { 110 ;   ;Cost Type           ;Option        ;FieldClass=Normal;
                                                   CaptionML=ENU=Cost Type;
                                                   OptionCaptionML=ENU=Labor,Material,Subcontracting,Plant,Sundry;
                                                   OptionString=Labor,Material,Subcontracting,Plant,Sundry;
                                                   Editable=No }
    { 120 ;   ;Allowed Amount      ;Decimal       ;CaptionML=ENU=Allowed Amount;
                                                   AutoFormatType=1 }
    { 130 ;   ;Allowed Hours       ;Decimal       ;CaptionML=ENU=Allowed Hours }
    { 140 ;   ;Budget Amount       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Budget Line".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                               Element=FIELD(Element),
                                                                                               Cost Object=FIELD(Cost Object),
                                                                                               Extension Contract=FIELD(Extension Contract Filter),
                                                                                               Version Date=FIELD(FILTER(Budget Version Date Filter)),
                                                                                               Extension Contract Status=FILTER(Not Applicable|(>=Order&<Expired))));
                                                   CaptionML=ENU=Budget Amount;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 150 ;   ;Budgetted Hours     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Budget Line".Hours WHERE (Project No.=FIELD(Project No.),
                                                                                              Element=FIELD(Element),
                                                                                              Cost Object=FIELD(Cost Object),
                                                                                              Extension Contract=FIELD(Extension Contract Filter),
                                                                                              Version Date=FIELD(FILTER(Budget Version Date Filter)),
                                                                                              Extension Contract Status=FILTER(Not Applicable|(>=Order&<Expired))));
                                                   CaptionML=ENU=Budgetted Hours;
                                                   Editable=No }
    { 155 ;   ;Budget Amount Cost Obj.;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Budget Line".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                               Cost Object=FIELD(Cost Object),
                                                                                               Extension Contract=FIELD(Extension Contract Filter),
                                                                                               Version Date=FIELD(FILTER(Budget Version Date Filter)),
                                                                                               Extension Contract Status=FILTER(Not Applicable|(>=Order&<Expired))));
                                                   CaptionML=ENU=Budget Amount Cost Object;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 157 ;   ;Budgetted Hours Cost Obj.;Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Budget Line".Hours WHERE (Project No.=FIELD(Project No.),
                                                                                              Cost Object=FIELD(Cost Object),
                                                                                              Extension Contract=FIELD(Extension Contract Filter),
                                                                                              Version Date=FIELD(FILTER(Budget Version Date Filter)),
                                                                                              Extension Contract Status=FILTER(Not Applicable|(>=Order&<Expired))));
                                                   CaptionML=ENU=Budgetted Hours Cost Object;
                                                   Editable=No }
    { 160 ;   ;Progress in Period  ;Decimal       ;OnValidate=BEGIN
                                                                SetFlowFiltersDates; //C017374.n
                                                                CALCFIELDS("Previous Posture");
                                                                "Cumulative Progress" := "Previous Posture" + "Progress in Period";

                                                                CheckProgress;

                                                                CalculateProgressPercentages;
                                                              END;

                                                   CaptionML=ENU=Progress in Period;
                                                   DecimalPlaces=2:5 }
    { 170 ;   ;Allowed Amount Period;Decimal      ;CaptionML=ENU=Allowed Amount Period;
                                                   AutoFormatType=1 }
    { 180 ;   ;Allowed Hours Period;Decimal       ;CaptionML=ENU=Allowed Hours Period }
    { 200 ;   ;Adjustment          ;Code10        ;TableRelation="Budget Adjustment".No. WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=ENU=Adjustment }
    { 210 ;   ;Extension Contract  ;Code10        ;TableRelation="Extension Contract"."Contract No." WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=ENU=Extension Contract }
    { 220 ;   ;Option              ;Code20        ;TableRelation=Option.Option WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=ENU=Option }
    { 230 ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 240 ;   ;Budget Line Description;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Budget Line".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                       Adjustment=FIELD(Adjustment),
                                                                                                       Extension Contract=FIELD(Extension Contract),
                                                                                                       Option=FIELD(Option),
                                                                                                       Line No.=FIELD(Line No.)));
                                                   CaptionML=ENU=Budget Line Description;
                                                   Editable=No }
    { 250 ;   ;Progress Quantity Budget Line;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Budget Quantity".Quantity WHERE (Project No.=FIELD(Project No.),
                                                                                                        Adjustment=FIELD(Adjustment),
                                                                                                        Extension Contract=FIELD(Extension Contract),
                                                                                                        Option=FIELD(Option),
                                                                                                        Line No.=FIELD(Line No.)));
                                                   CaptionML=ENU=Progress Quantity Budget Line;
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 260 ;   ;Progress Unit Budget Line;Code10   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Budget Quantity"."Unit of Measure" WHERE (Project No.=FIELD(Project No.),
                                                                                                                 Adjustment=FIELD(Adjustment),
                                                                                                                 Extension Contract=FIELD(Extension Contract),
                                                                                                                 Option=FIELD(Option),
                                                                                                                 Line No.=FIELD(Line No.)));
                                                   CaptionML=ENU=Progress Unit Budget Line;
                                                   Editable=No }
    { 270 ;   ;Present Element-Cost Object;Boolean;FieldClass=FlowField;
                                                   CalcFormula=Exist("Budget Line" WHERE (Project No.=FIELD(Project No.),
                                                                                          Element=FIELD(Element),
                                                                                          Cost Object=FIELD(Cost Object)));
                                                   CaptionML=ENU=Present Element-Cost Object;
                                                   Editable=No }
    { 275 ;   ;Present Cost Object ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Budget Line" WHERE (Project No.=FIELD(Project No.),
                                                                                          Cost Object=FIELD(Cost Object)));
                                                   CaptionML=ENU=Present Cost Object;
                                                   Editable=No }
    { 280 ;   ;Budget Line Present ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Budget Line" WHERE (Project No.=FIELD(Project No.),
                                                                                          Adjustment=FIELD(Adjustment),
                                                                                          Extension Contract=FIELD(Extension Contract),
                                                                                          Option=FIELD(Option),
                                                                                          Line No.=FIELD(Line No.),
                                                                                          Element=FIELD(Element),
                                                                                          Cost Object=FIELD(Cost Object)));
                                                   CaptionML=ENU=Budget Line Present;
                                                   Editable=No }
    { 290 ;   ;Progress Method     ;Option        ;CaptionML=ENU=Progress Method;
                                                   OptionCaptionML=ENU=Element - Cost Object,Budget Line,Cost Object;
                                                   OptionString=Element - Cost Object,Budget Line,Cost Object;
                                                   Editable=No }
    { 295 ;   ;Progress Registry   ;Option        ;CaptionML=ENU=Progress Registry;
                                                   OptionCaptionML=ENU=Quantities,Percentages;
                                                   OptionString=Quantities,Percentages;
                                                   Editable=No }
    { 300 ;   ;Budget Amount Budg.Line;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Budget Line".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                               Adjustment=FIELD(Adjustment),
                                                                                               Extension Contract=FIELD(Extension Contract),
                                                                                               Option=FIELD(Option),
                                                                                               Line No.=FIELD(Line No.),
                                                                                               Version Date=FIELD(FILTER(Budget Version Date Filter))));
                                                   CaptionML=ENU=Budget Amount Budg.Line;
                                                   Editable=No }
    { 310 ;   ;Budget Hours Budg.Line;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Budget Line".Hours WHERE (Project No.=FIELD(Project No.),
                                                                                              Adjustment=FIELD(Adjustment),
                                                                                              Extension Contract=FIELD(Extension Contract),
                                                                                              Option=FIELD(Option),
                                                                                              Line No.=FIELD(Line No.),
                                                                                              Version Date=FIELD(FILTER(Budget Version Date Filter))));
                                                   CaptionML=ENU=Budget Hours Budg.Line;
                                                   Editable=No }
    { 320 ;   ;Cumulative Progress %;Decimal      ;OnValidate=BEGIN
                                                                CheckProgress;

                                                                SetFlowFiltersDates; //C017374.n
                                                                CALCFIELDS("Prev.Cumulative Progr.%");
                                                                "Period Progress %" := "Cumulative Progress %" - "Prev.Cumulative Progr.%";

                                                                CalculateProgress;
                                                              END;

                                                   CaptionML=ENU=Cumulative Progress % }
    { 330 ;   ;Period Progress %   ;Decimal       ;OnValidate=BEGIN
                                                                SetFlowFiltersDates; //C017374.n
                                                                CALCFIELDS("Prev.Cumulative Progr.%");
                                                                "Cumulative Progress %" := "Prev.Cumulative Progr.%" + "Period Progress %";

                                                                VALIDATE("Cumulative Progress %");
                                                              END;

                                                   CaptionML=ENU=Period Progress % }
    { 336 ;   ;Budget Version Date Filter;Date    ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Budget Version Date Filter;
                                                   Description=C017374;
                                                   Editable=No }
    { 350 ;   ;Previous Date       ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Max("Progress Line"."Progress Date" WHERE (Project No.=FIELD(Project No.),
                                                                                                          Adjustment=FIELD(Adjustment),
                                                                                                          Extension Contract=FIELD(Extension Contract),
                                                                                                          Option=FIELD(Option),
                                                                                                          Line No.=FIELD(Line No.),
                                                                                                          Element=FIELD(Element),
                                                                                                          Cost Object=FIELD(Cost Object),
                                                                                                          Progress Date=FIELD(UPPERLIMIT(FILTER(Previous Date Filter)))));
                                                   CaptionML=ENU=Previous Date;
                                                   Editable=No }
    { 351 ;   ;Previous Date Filter;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Previous Date Flter;
                                                   Description=C017374 }
    { 360 ;   ;Previous Posture    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Progress Line"."Cumulative Progress" WHERE (Project No.=FIELD(Project No.),
                                                                                                                   Adjustment=FIELD(Adjustment),
                                                                                                                   Extension Contract=FIELD(Extension Contract),
                                                                                                                   Option=FIELD(Option),
                                                                                                                   Line No.=FIELD(Line No.),
                                                                                                                   Element=FIELD(Element),
                                                                                                                   Cost Object=FIELD(Cost Object),
                                                                                                                   Progress Date=FIELD(Previous Date)));
                                                   CaptionML=ENU=Previous Posture;
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 370 ;   ;Prev.Cumulative Progr.%;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Progress Line"."Cumulative Progress %" WHERE (Project No.=FIELD(Project No.),
                                                                                                                     Adjustment=FIELD(Adjustment),
                                                                                                                     Extension Contract=FIELD(Extension Contract),
                                                                                                                     Option=FIELD(Option),
                                                                                                                     Line No.=FIELD(Line No.),
                                                                                                                     Element=FIELD(Element),
                                                                                                                     Cost Object=FIELD(Cost Object),
                                                                                                                     Progress Date=FIELD(Previous Date)));
                                                   CaptionML=ENU=Prev.Cumulative Progr.%;
                                                   Editable=No }
    { 400 ;   ;Record Progress     ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value"."Record Progress" WHERE (Code=FIELD(Cost Object),
                                                                                                                 Global Dimension No.=CONST(2)));
                                                   CaptionML=ENU=Record Progress;
                                                   Editable=No }
    { 410 ;   ;Actual Amount       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job Ledger Entry"."Total Cost (LCY)" WHERE (Job No.=FIELD(Project No.),
                                                                                                                Element=FIELD(Element),
                                                                                                                Global Dimension 2 Code=FIELD(Cost Object)));
                                                   CaptionML=ENU=Actual Amount;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 411 ;   ;Actual Hours        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job Ledger Entry".Quantity WHERE (Job No.=FIELD(Project No.),
                                                                                                      Element=FIELD(Element),
                                                                                                      Global Dimension 2 Code=FIELD(Cost Object),
                                                                                                      Cost Type=CONST(Labor)));
                                                   CaptionML=ENU=Actual Hours;
                                                   Editable=No }
    { 420 ;   ;Extension Contract Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Extension Contract".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                              Contract No.=FIELD(Extension Contract)));
                                                   CaptionML=ENU=Extension Contract Description;
                                                   Editable=No }
    { 430 ;   ;Extension Contract Filter;Code10   ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Extension Contract Filter }
    { 440 ;   ;Cost Component      ;Code20        ;TableRelation="Cost Component".Code;
                                                   CaptionML=ENU=Cost Component;
                                                   Editable=No }
    { 450 ;   ;Main Project No.    ;Code20        ;TableRelation=Job;
                                                   CaptionML=ENU=Main Project No. }
    { 11012000;;Comment            ;Text100       ;CaptionML=ENU=Comment }
  }
  KEYS
  {
    {    ;Project No.,Progress Date,Adjustment,Extension Contract,Option,Line No.,Element,Cost Object;
                                                   Clustered=Yes }
    {    ;Main Project No.,Project No.,Progress Date,Cost Object;
                                                   SumIndexFields=Allowed Hours,Allowed Amount,Allowed Hours Period,Allowed Amount Period }
    {    ;Project No.,Cost Type,Progress Date     ;SumIndexFields=Allowed Amount,Allowed Amount Period }
    {    ;Progress Date                            }
    {    ;Project No.,Progress Date,Element,Cost Object }
    {    ;Project No.,Progress Date,Cost Type,Element,Cost Object,Adjustment,Extension Contract,Option,Line No. }
    {    ;Project No.,Progress Date,Cost Type,Cost Object,Element }
    {    ;Main Project No.,Progress Date           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ProjectSetup@11012000 : Record 315;
      DimValRec@11012001 : Record 349;
      ProjElemRec@11012002 : Record 11012010;
      ProgrLine1@11012004 : Record 11012037;
      ProgLine2@11012005 : Record 11012037;
      ProjectRec@11012006 : Record 11072003;
      ElementFormat@11012007 : Codeunit 11012022;
      DimMgt@11012009 : Codeunit 408;
      Text001@1100485001 : TextConst 'ENU=%1 may not exceed 100%';
      Text002@1100485002 : TextConst 'ENU=%1 may not exceed Progress Quantity (%2)';
      Text003@1100525000 : TextConst 'ENU=Progress is fixed, Changes not allowed.';
      DeleteFromProgressHdr@1100525001 : Boolean;

    PROCEDURE CalculateAllowed@1();
    BEGIN
      ProjectSetup.GET;
      IF ProjectSetup."Production on Extensions" THEN
        SETRANGE("Extension Contract Filter", "Extension Contract")
      ELSE
        SETRANGE("Extension Contract Filter");
      SetFlowFiltersDates; //C017374.n
      CALCFIELDS("Record Progress");

      IF "Record Progress" THEN BEGIN
        CASE "Progress Method" OF
          "Progress Method"::"Element - Cost Object":
            BEGIN
              CALCFIELDS("Progress Quantity El/Cost Obj.");
              IF ("Progress Quantity El/Cost Obj." <> 0) THEN BEGIN
                CALCFIELDS("Budget Amount","Budgetted Hours");
                "Allowed Amount" := "Budget Amount" * ("Cumulative Progress"/"Progress Quantity El/Cost Obj.");
                "Allowed Hours" := "Budgetted Hours" * ("Cumulative Progress"/"Progress Quantity El/Cost Obj.");
                "Allowed Amount Period" := "Budget Amount" * ("Progress in Period"/"Progress Quantity El/Cost Obj.");
                "Allowed Hours Period" := "Budgetted Hours" * ("Progress in Period"/"Progress Quantity El/Cost Obj.");
              END ELSE BEGIN
                "Allowed Amount" := 0;
                "Allowed Hours" := 0;
                "Allowed Amount Period" := 0;
                "Allowed Hours Period" := 0;
              END;
            END;
          "Progress Method"::"Cost Object":
            BEGIN
              CALCFIELDS("Progress Quantity Cost Obj.");
              IF ("Progress Quantity Cost Obj." <> 0) THEN BEGIN
                CALCFIELDS("Budget Amount Cost Obj.","Budgetted Hours Cost Obj.");
                "Allowed Amount" := "Budget Amount Cost Obj." * ("Cumulative Progress"/"Progress Quantity Cost Obj.");
                "Allowed Hours" := "Budgetted Hours Cost Obj." * ("Cumulative Progress"/"Progress Quantity Cost Obj.");
                "Allowed Amount Period" := "Budget Amount Cost Obj." * ("Progress in Period"/"Progress Quantity Cost Obj.");
                "Allowed Hours Period" := "Budgetted Hours Cost Obj." * ("Progress in Period"/"Progress Quantity Cost Obj.");
              END ELSE BEGIN
                "Allowed Amount" := 0;
                "Allowed Hours" := 0;
                "Allowed Amount Period" := 0;
                "Allowed Hours Period" := 0;
              END;
            END;
          "Progress Method"::"Budget Line":
            BEGIN
              CALCFIELDS("Budget Amount Budg.Line","Budget Hours Budg.Line","Progress Quantity Budget Line");
              IF ("Progress Quantity Budget Line" <> 0) THEN BEGIN
                "Allowed Amount" := "Budget Amount Budg.Line" * ("Cumulative Progress"/"Progress Quantity Budget Line");
                "Allowed Hours" := "Budget Hours Budg.Line" * ("Cumulative Progress"/"Progress Quantity Budget Line");
                "Allowed Amount Period" := "Budget Amount Budg.Line" * ("Progress in Period"/"Progress Quantity Budget Line");
                "Allowed Hours Period" := "Budget Hours Budg.Line" * ("Progress in Period"/"Progress Quantity Budget Line");
              END ELSE BEGIN
                "Allowed Amount" := 0;
                "Allowed Hours" := 0;
                "Allowed Amount Period" := 0;
                "Allowed Hours Period" := 0;
              END;
            END;
        END;
      END ELSE BEGIN
        CALCFIELDS("Actual Amount","Actual Hours");
        IF "Actual Amount" < "Budget Amount" THEN
          "Allowed Amount" := "Actual Amount"
        ELSE
          "Allowed Amount" := "Budget Amount";
        IF "Actual Hours" < "Budgetted Hours" THEN
          "Allowed Hours" := "Actual Hours"
        ELSE
          "Allowed Hours" := "Budgetted Hours";
      END;
    END;

    PROCEDURE UpdateLatestProgress@2(LineRemoved@11012000 : Boolean);
    BEGIN
      IF LineRemoved THEN BEGIN
        ProgrLine1.SETRANGE("Project No.","Project No.");
        ProgrLine1.SETFILTER("Progress Date",'<%1',"Progress Date");
        ProgrLine1.SETRANGE("Progress Method","Progress Method");
        CASE "Progress Method" OF
          "Progress Method"::"Budget Line":
            BEGIN
              ProgrLine1.SETRANGE(Adjustment,Adjustment);
              ProgrLine1.SETRANGE("Extension Contract","Extension Contract");
              ProgrLine1.SETRANGE(Option,Option);
              ProgrLine1.SETRANGE("Line No.","Line No.");
            END;
          "Progress Method"::"Element - Cost Object":
            BEGIN
              ProgrLine1.SETRANGE(Element,Element);
              ProgrLine1.SETRANGE("Cost Object","Cost Object");
            END;
          "Progress Method"::"Cost Object":
            BEGIN
              ProgrLine1.SETRANGE("Cost Object","Cost Object");
            END;
        END;
        IF NOT ProgrLine1.FINDLAST THEN
          ProgrLine1."Cumulative Progress" := 0;
      END ELSE BEGIN
        ProgrLine1."Cumulative Progress" := "Cumulative Progress";
      END;

      ProgLine2.SETRANGE("Project No.","Project No.");
      ProgLine2.SETFILTER("Progress Date",'>%1',"Progress Date");
      ProgLine2.SETRANGE("Progress Method","Progress Method");
      CASE "Progress Method" OF
        "Progress Method"::"Budget Line":
        BEGIN
          ProgLine2.SETRANGE(Adjustment,Adjustment);
          ProgLine2.SETRANGE("Extension Contract","Extension Contract");
          ProgLine2.SETRANGE(Option,Option);
          ProgLine2.SETRANGE("Line No.","Line No.");
        END;
        "Progress Method"::"Element - Cost Object":
        BEGIN
          ProgLine2.SETRANGE(Element,Element);
          ProgLine2.SETRANGE("Cost Object","Cost Object");
        END;
        "Progress Method"::"Cost Object":
        BEGIN
          ProgLine2.SETRANGE("Cost Object","Cost Object");
        END;
      END;
      IF ProgLine2.FINDFIRST THEN
        IF ProgLine2."Progress in Period" <>
          (ProgLine2."Cumulative Progress" - ProgrLine1."Cumulative Progress")
        THEN BEGIN
          ProgLine2."Progress in Period" :=
            ProgLine2."Cumulative Progress" - ProgrLine1."Cumulative Progress";
          ProgLine2.MODIFY;
        END;
    END;

    PROCEDURE DesctLineOfCostObj@3() : Text[50];
    BEGIN
      CASE "Progress Method"OF
        "Progress Method"::"Budget Line":
          BEGIN
            CALCFIELDS("Budget Line Description");
            EXIT("Budget Line Description");
          END;
        ELSE
          BEGIN
            CALCFIELDS("Cost Object Description", "Project Related Name");
            IF "Project Related Name" <> '' THEN
              "Cost Object Description" := "Project Related Name";
            EXIT("Cost Object Description");
          END;
      END;
    END;

    PROCEDURE ProgressQuantity@4() : Decimal;
    BEGIN
      CASE "Progress Method"OF
        "Progress Method"::"Budget Line":
          BEGIN
            CALCFIELDS("Progress Quantity Budget Line");
            EXIT("Progress Quantity Budget Line");
          END;
        "Progress Method"::"Element - Cost Object":
          BEGIN
            CALCFIELDS("Progress Quantity El/Cost Obj.");
            EXIT("Progress Quantity El/Cost Obj.");
          END;
        "Progress Method"::"Cost Object":
          BEGIN
            CALCFIELDS("Progress Quantity Cost Obj.");
            EXIT("Progress Quantity Cost Obj.");
          END;
      END;
    END;

    PROCEDURE ProgressUnit@5() : Code[10];
    BEGIN
      CASE "Progress Method"OF
        "Progress Method"::"Budget Line":
          BEGIN
            CALCFIELDS("Progress Unit Budget Line");
            EXIT("Progress Unit Budget Line");
          END;
        "Progress Method"::"Element - Cost Object":
          BEGIN
            CALCFIELDS("Progress Unit El/Cost Obj.");
            EXIT("Progress Unit El/Cost Obj.");
          END;
        "Progress Method"::"Cost Object":
          BEGIN
            CALCFIELDS("Progress Unit Cost Obj.");
            EXIT("Progress Unit Cost Obj.");
          END;
      END;
    END;

    PROCEDURE HeaderTextStandCum@6() : Text[30];
    BEGIN
      IF "Progress Registry" = "Progress Registry"::Quantities THEN
        EXIT(FIELDCAPTION("Cumulative Progress"))
      ELSE
        EXIT(FIELDCAPTION("Cumulative Progress %"));
    END;

    PROCEDURE HeaderTextProgressPeriod@7() : Text[30];
    BEGIN
      IF "Progress Registry" = "Progress Registry"::Quantities THEN
        EXIT(FIELDCAPTION("Progress in Period"))
      ELSE
        EXIT(FIELDCAPTION("Period Progress %"));
    END;

    PROCEDURE CalculateProgressPercentages@10();
    BEGIN
      IF (ProgressQuantity = 0) THEN BEGIN
        "Cumulative Progress %" := 0;
        "Period Progress %" := 0;
      END ELSE BEGIN
        "Cumulative Progress %" := "Cumulative Progress" / ProgressQuantity * 100;
        "Period Progress %" := "Progress in Period" / ProgressQuantity * 100;
      END;
    END;

    PROCEDURE CalculateProgress@9();
    BEGIN
      "Cumulative Progress" := "Cumulative Progress %" / 100 * ProgressQuantity;
      "Progress in Period" := "Period Progress %" / 100 * ProgressQuantity;
    END;

    PROCEDURE CheckProgress@11();
    BEGIN
      IF CurrFieldNo = 0 THEN
        EXIT;

      ProjectSetup.GET;
      IF ProjectSetup."Progress Range" > ProjectSetup."Progress Range"::"Max 100 %" THEN
        EXIT;

      SetFlowFiltersDates; //C017374.n
      IF "Progress Registry" = "Progress Registry"::Quantities THEN BEGIN
        IF "Cumulative Progress" > ProgressQuantity THEN
          ERROR(Text002, FIELDCAPTION("Cumulative Progress"), ProgressQuantity);
      END ELSE
        IF "Cumulative Progress %" > 100 THEN
          ERROR(Text001, FIELDCAPTION("Cumulative Progress %"));
    END;

    PROCEDURE BudgetHours@1100525000() : Decimal;
    BEGIN
      ProjectSetup.GET;
      IF ProjectSetup."Production on Extensions" THEN
        SETRANGE("Extension Contract Filter", "Extension Contract")
      ELSE
        SETRANGE("Extension Contract Filter");
      SetFlowFiltersDates; //C017374.n
      CASE "Progress Method"OF
        "Progress Method"::"Budget Line":
          BEGIN
            CALCFIELDS("Budget Hours Budg.Line");
            EXIT("Budget Hours Budg.Line");
          END;
        "Progress Method"::"Element - Cost Object":
          BEGIN
            CALCFIELDS("Budgetted Hours");
            EXIT("Budgetted Hours");
          END;
        "Progress Method"::"Cost Object":
          BEGIN
            CALCFIELDS("Budgetted Hours Cost Obj.");
            EXIT("Budgetted Hours Cost Obj.");
          END;
      END;
    END;

    PROCEDURE SetFlowFiltersDates@1100528000();
    VAR
      Progress@1100528500 : Record 11012036;
    BEGIN
      //C017374
      SETRANGE("Previous Date Filter",0D);
      SETRANGE("Budget Version Date Filter",0D);

      IF Progress.GET("Project No.","Progress Date") THEN BEGIN
        SETFILTER("Budget Version Date Filter",'..'+FORMAT("Progress Date"));
        Progress.SETRANGE("Project No.", "Project No.");
        IF Progress.NEXT(-1) <> 0 THEN
          SETFILTER("Previous Date Filter",FORMAT(Progress."Progress Date"));
      END;
    END;

    PROCEDURE UpdateProgressModified@1100525001();
    VAR
      ProgressLevel@1100525000 : Record 11012036;
    BEGIN
      IF ProgressLevel.GET("Project No.", "Progress Date") THEN BEGIN
        IF (ProgressLevel."Modified on" <> TODAY) OR (ProgressLevel."Modified by" <> USERID) THEN BEGIN
          ProgressLevel.FillModified();
          ProgressLevel.MODIFY(FALSE);
        END;
      END;
    END;

    PROCEDURE SetDeleteFromProgressHdr@1100525002();
    BEGIN
      DeleteFromProgressHdr := TRUE;
    END;

    BEGIN
    {
      4PS01 JD, 21 feb 2007, Project Cost Control forms and reports must give overview of the costs of a mainproject as well.
    }
    END.
  }
}

