OBJECT Table 11012057 Intercompany Relation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnRename=BEGIN
               ERROR('');
             END;

    CaptionML=ENU=Intercompany Relations;
    LookupPageID=Page11012112;
  }
  FIELDS
  {
    { 1   ;   ;Supplying Company   ;Text30        ;TableRelation=Company.Name;
                                                   CaptionML=ENU=Supplying Company }
    { 2   ;   ;Receiving Company   ;Text30        ;TableRelation=Company.Name;
                                                   CaptionML=ENU=Receiving Company }
    { 3   ;   ;Supplying Company IC Account;Code20;OnValidate=BEGIN
                                                                ValidateICAccount("Supplying Company", "Supplying Company IC Account");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupICAccount("Supplying Company", "Supplying Company IC Account");
                                                              ValidateICAccount("Supplying Company", "Supplying Company IC Account");
                                                            END;

                                                   CaptionML=ENU=Supplying Company IC Account }
    { 5   ;   ;Receiving Company IC Account;Code20;OnValidate=BEGIN
                                                                ValidateICAccount("Receiving Company", "Receiving Company IC Account");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupICAccount("Receiving Company", "Receiving Company IC Account");
                                                              ValidateICAccount("Receiving Company", "Receiving Company IC Account");
                                                            END;

                                                   CaptionML=ENU=Receiving Company IC Account }
    { 13  ;   ;Source Code         ;Code10        ;OnValidate=BEGIN
                                                                SourceCodeRec.CHANGECOMPANY("Receiving Company");
                                                                SourceCodeRec.GET("Source Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              SourceCodeRec.CHANGECOMPANY("Receiving Company");
                                                              //SourceCodeRec.Code := "Source Code";
                                                              IF PAGE.RUNMODAL(0, SourceCodeRec) = ACTION::LookupOK THEN
                                                                VALIDATE("Source Code", SourceCodeRec.Code);
                                                            END;

                                                   CaptionML=ENU=Source Code }
    { 20  ;   ;Vendor Posting Group;Code10        ;TableRelation="Vendor Posting Group";
                                                   OnLookup=BEGIN
                                                              CheckCompanyIC;
                                                              VendorPostingGroupRec.Code := "Vendor Posting Group";
                                                              IF PAGE.RUNMODAL(0, VendorPostingGroupRec) = ACTION::LookupOK THEN
                                                                  VALIDATE("Vendor Posting Group", VendorPostingGroupRec.Code);
                                                            END;

                                                   CaptionML=ENU=Vendor Posting Group }
    { 30  ;   ;Gen. Bus. Posting Group;Code10     ;TableRelation="Gen. Business Posting Group";
                                                   OnLookup=BEGIN
                                                              CheckCompanyIC;
                                                              GenBusinessPostingGroup.Code := "Gen. Bus. Posting Group";
                                                              IF PAGE.RUNMODAL(0, GenBusinessPostingGroup) = ACTION::LookupOK THEN
                                                                  VALIDATE("Gen. Bus. Posting Group", GenBusinessPostingGroup.Code);
                                                            END;

                                                   CaptionML=ENU=Gen. Bus. Posting Group }
  }
  KEYS
  {
    {    ;Supplying Company,Receiving Company     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@11012001 : TextConst 'ENU=%1 %2 does not exist in %3 %4.';
      SourceCodeRec@1210190003 : Record 230;
      Text001@1100485000 : TextConst 'ENU=%1 of %2 %3 must be No in %4 %5.';
      VendorPostingGroupRec@1100525000 : Record 93;
      GenBusinessPostingGroup@1100528300 : Record 250;

    PROCEDURE CheckCompanyIC@32();
    BEGIN
      VendorPostingGroupRec.CHANGECOMPANY("Receiving Company");
      GenBusinessPostingGroup.CHANGECOMPANY("Receiving Company");
    END;

    LOCAL PROCEDURE ValidateICAccount@1100528400(CompanyName2@1100528400 : Text[30];AccountNo@1100528401 : Code[20]);
    VAR
      GLAccount@1100528402 : Record 15;
      Company@1100528403 : Record 2000000006;
    BEGIN
      IF AccountNo = '' THEN
        EXIT;

      GLAccount.CHANGECOMPANY(CompanyName2);
      IF NOT GLAccount.GET(AccountNo) THEN
        ERROR(
          Text000, GLAccount.TABLECAPTION, AccountNo, Company.TABLECAPTION, CompanyName2);
      IF GLAccount."Direct Posting" THEN
        ERROR(
          Text001, GLAccount.FIELDCAPTION("Direct Posting"), GLAccount.TABLECAPTION,
          AccountNo, Company.TABLECAPTION, CompanyName2);
    END;

    LOCAL PROCEDURE LookupICAccount@1100528410(CompanyName2@1100528400 : Text[30];VAR AccountNo@1100528401 : Code[20]);
    VAR
      GLAccount@1100528402 : Record 15;
      Company2@1100528403 : Record 2000000006;
    BEGIN
      GLAccount.CHANGECOMPANY(CompanyName2);
      GLAccount."No." := AccountNo;
      IF PAGE.RUNMODAL(0, GLAccount) = ACTION::LookupOK THEN
        AccountNo := GLAccount."No.";
    END;

    PROCEDURE GetICAccountOfCurrentCompany@1100528402() ICAccount : Code[20];
    BEGIN
      CASE COMPANYNAME OF
        "Supplying Company":
          ICAccount := "Supplying Company IC Account";
        "Receiving Company":
          ICAccount := "Receiving Company IC Account";
      END;
    END;

    BEGIN
    END.
  }
}

