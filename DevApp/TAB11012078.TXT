OBJECT Table 11012078 Hour Line Template Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF ("Receiving Company" = '') THEN
                 "Receiving Company" := COMPANYNAME;
             END;

    OnModify=BEGIN
               GetObjectNoDescription;
               CheckWageComponents;
             END;

    CaptionML=ENU=Hour Line Template;
    LookupPageID=Page11012000;
  }
  FIELDS
  {
    { 10  ;   ;Source              ;Option        ;CaptionML=ENU=Source;
                                                   OptionCaptionML=ENU=Hour Accounting,Site Mngmnt. Hours;
                                                   OptionString=Hour Accounting,Site Mngmnt. Hours }
    { 20  ;   ;Receiving Company   ;Text30        ;TableRelation="Site Mngmnt. Hours Companies";
                                                   CaptionML=ENU=Receiving Company }
    { 30  ;   ;Template Code       ;Code20        ;TableRelation="Hour Line Template".Code WHERE (Source=FIELD(Source),
                                                                                                  Receiving Company=FIELD(Receiving Company));
                                                   CaptionML=ENU=Template Code;
                                                   NotBlank=Yes }
    { 40  ;   ;Start Date          ;Date          ;OnValidate=BEGIN
                                                                IF (DATE2DWY("Start Date", 1) <> 1) THEN BEGIN
                                                                  "Start Date" := DWY2DATE(1, DATE2DWY("Start Date", 2), DATE2DWY("Start Date", 3));
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Start Date;
                                                   NotBlank=Yes }
    { 50  ;   ;Type                ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Hour Line Template".Type WHERE (Source=FIELD(Source),
                                                                                                       Receiving Company=FIELD(Receiving Company),
                                                                                                       Code=FIELD(Template Code)));
                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Project,Indirect,Service,Plant,Unmanned Plant;
                                                   OptionString=Project,Indirect,Service,Plant,Unmanned Plant;
                                                   Editable=No }
    { 60  ;   ;Object No.          ;Code20        ;OnValidate=BEGIN
                                                                ChangeIC;
                                                                CALCFIELDS(Type);
                                                                CASE Type OF
                                                                  Type::Project,
                                                                  Type::Plant,
                                                                  Type::"Unmanned Plant":
                                                                    Job.GET("Object No.");
                                                                  Type::Indirect:
                                                                    CostCode.GET("Object No.");
                                                                  Type::Service:
                                                                    ServiceOrder.GET("Object No.");
                                                                END;
                                                                GetObjectNoDescription;
                                                              END;

                                                   OnLookup=BEGIN
                                                              ChangeIC;
                                                              CALCFIELDS(Type);
                                                              CASE Type OF
                                                                Type::Project,
                                                                Type::Plant,
                                                                Type::"Unmanned Plant": BEGIN
                                                                  IF (Job.GET("Object No.")) THEN;
                                                                  IF (PAGE.RUNMODAL(0, Job) = ACTION::LookupOK) THEN
                                                                    VALIDATE("Object No.", Job."No.");
                                                                END;
                                                                Type::Indirect: BEGIN
                                                                  IF (CostCode.GET("Object No.")) THEN;
                                                                  IF (PAGE.RUNMODAL(0, CostCode) = ACTION::LookupOK) THEN
                                                                    VALIDATE("Object No.", CostCode.Code);
                                                                END;
                                                                Type::Service: BEGIN
                                                                  IF (ServiceOrder.GET("Object No.")) THEN;
                                                                  IF (PAGE.RUNMODAL(0, ServiceOrder) = ACTION::LookupOK) THEN
                                                                    VALIDATE("Object No.", ServiceOrder."No.");
                                                                END;
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Object No. }
    { 61  ;   ;Object Description  ;Text50        ;CaptionML=ENU=Object Description;
                                                   Editable=No }
    { 70  ;   ;Department Code     ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Department Description");
                                                              END;

                                                   CaptionML=ENU=Department Code }
    { 71  ;   ;Department Description;Text50      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value".Name WHERE (Global Dimension No.=CONST(1),
                                                                                                    Code=FIELD(Department Code)));
                                                   CaptionML=ENU=Department Description;
                                                   Editable=No }
    { 80  ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2),
                                                                                               Cost Type=FILTER(Labor|Plant|Sundry|Subcontracting));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Cost Object Description");
                                                              END;

                                                   CaptionML=ENU=Cost Object }
    { 81  ;   ;Cost Object Description;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value".Name WHERE (Global Dimension No.=CONST(2),
                                                                                                    Code=FIELD(Cost Object)));
                                                   CaptionML=ENU=Cost Object Description;
                                                   Editable=No }
    { 90  ;   ;Wage Component      ;Code20        ;TableRelation="Wage Component" WHERE (Component Type=FILTER(<>Days),
                                                                                         Kilometer Expense=CONST(No));
                                                   OnValidate=BEGIN
                                                                CheckWageComponents;
                                                                CALCFIELDS("Wage Component Description");
                                                              END;

                                                   CaptionML=ENU=Wage Component }
    { 91  ;   ;Wage Component Description;Text50  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Wage Component".Description WHERE (Code=FIELD(Wage Component)));
                                                   CaptionML=ENU=Wage Component Description;
                                                   Editable=No }
    { 100 ;   ;Element             ;Code10        ;OnValidate=BEGIN
                                                                ChangeIC;
                                                                CALCFIELDS(Type);
                                                                CASE Type OF
                                                                  Type::Project:
                                                                    ProjectElement.GET("Object No.", Element);
                                                                END;
                                                                GetElementDescription;
                                                              END;

                                                   OnLookup=BEGIN
                                                              ChangeIC;
                                                              CALCFIELDS(Type);
                                                              CASE Type OF
                                                                Type::Project: BEGIN
                                                                  ProjectElement.SETRANGE("Project No.", "Object No.");
                                                                  IF (ProjectElement.GET("Object No.", Element)) THEN;
                                                                  IF (PAGE.RUNMODAL(0, ProjectElement) = ACTION::LookupOK) THEN
                                                                    VALIDATE(Element, ProjectElement.Element);
                                                                END;
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Element }
    { 101 ;   ;Element Description ;Text50        ;CaptionML=ENU=Element Description;
                                                   Editable=No }
    { 110 ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity;
                                                   BlankZero=Yes }
    { 120 ;   ;Amount              ;Decimal       ;CaptionML=ENU=Amount;
                                                   BlankZero=Yes }
    { 130 ;   ;Wage Component (Expense);Code20    ;TableRelation="Wage Component" WHERE (Component Type=CONST(Expenses),
                                                                                         Kilometer Expense=CONST(No));
                                                   OnValidate=BEGIN
                                                                CheckWageComponents;
                                                                CALCFIELDS("Wage Comp (Expense) Descr");
                                                              END;

                                                   CaptionML=ENU=Wage Component (Expense) }
    { 131 ;   ;Wage Comp (Expense) Descr;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Wage Component".Description WHERE (Code=FIELD("Wage Component (Expense)")));
                                                   OnValidate=BEGIN
                                                                CheckWageComponents;
                                                              END;

                                                   CaptionML=ENU=Wage Component (Expense) Description;
                                                   Editable=No }
    { 150 ;   ;Wage Component (Km) ;Code20        ;TableRelation="Wage Component" WHERE (Component Type=CONST(Expenses),
                                                                                         Kilometer Expense=CONST(Yes));
                                                   OnValidate=BEGIN
                                                                CheckWageComponents;
                                                                CALCFIELDS("Wage Comp (Km) Descr");
                                                              END;

                                                   CaptionML=ENU=Wage Component (Travel) }
    { 151 ;   ;Wage Comp (Km) Descr;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Wage Component".Description WHERE (Code=FIELD("Wage Component (Km)")));
                                                   OnValidate=BEGIN
                                                                CheckWageComponents;
                                                                CALCFIELDS("Wage Comp (Km) Descr");
                                                              END;

                                                   CaptionML=ENU=Wage Component (Travel) Description;
                                                   Editable=No }
    { 160 ;   ;Cost Object (Km)    ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2),
                                                                                               Cost Type=FILTER(Labor|Plant|Sundry|Subcontracting));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Cost Object (Km) Descr");
                                                              END;

                                                   CaptionML=ENU=Cost Object (Travel) }
    { 161 ;   ;Cost Object (Km) Descr;Text50      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value".Name WHERE (Global Dimension No.=CONST(2),
                                                                                                    Code=FIELD("Cost Object (Km)")));
                                                   CaptionML=ENU=Cost Object (Travel) Description;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Source,Receiving Company,Template Code,Start Date;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Job@1100528302 : Record 11072003;
      ServiceOrder@1100528301 : Record 11012823;
      CostCode@1100528300 : Record 11012083;
      ProjectElement@1100528303 : Record 11012010;

    PROCEDURE GetObjectNoDescription@1100528300();
    BEGIN
      ChangeIC;
      "Object Description" := '';
      CALCFIELDS(Type);
      CASE Type OF
        Type::Project,
        Type::Plant,
        Type::"Unmanned Plant": BEGIN
          IF (Job.GET("Object No.")) THEN
             "Object Description" := Job.Description;
        END;
        Type::Indirect: BEGIN
          IF (CostCode.GET("Object No.")) THEN
             "Object Description" := CostCode.Description;
        END;
        Type::Service: BEGIN
          IF (ServiceOrder.GET("Object No.")) THEN
             "Object Description" := ServiceOrder.Description;
        END;
      END;
    END;

    PROCEDURE GetElementDescription@1100528301();
    BEGIN
      ChangeIC;
      "Element Description" := '';
      IF (ProjectElement.GET("Object No.", Element)) THEN
         "Element Description" := ProjectElement.Description;
    END;

    PROCEDURE ChangeIC@1100528313();
    BEGIN
      Job.CHANGECOMPANY("Receiving Company");
      ServiceOrder.CHANGECOMPANY("Receiving Company");
      ProjectElement.CHANGECOMPANY("Receiving Company");
    END;

    PROCEDURE CheckWageComponents@1100528302();
    VAR
      WageComponent@1100528300 : Record 11012014;
    BEGIN
      IF ("Wage Component" <> '') THEN BEGIN
        WageComponent.GET("Wage Component");
        IF (WageComponent."Component Type" = WageComponent."Component Type"::Expenses) THEN BEGIN
            "Wage Component (Expense)" := '';
            "Wage Component (Km)" := '';
        END;
      END;
    END;

    BEGIN
    END.
  }
}

