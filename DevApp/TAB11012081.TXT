OBJECT Table 11012081 Appointments By Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Appointment Type" = "Appointment Type"::Execution THEN
                 TESTFIELD("Action by");
             END;

    OnDelete=VAR
               lvPurchHeadRec@1100485000 : Record 38;
             BEGIN
               CALCFIELDS("Purchase Order");
               IF ("Line No." <> 0) AND ("Purchase Order" <> '') THEN BEGIN
                 IF lvPurchHeadRec.GET(lvPurchHeadRec."Document Type"::Order, "Purchase Order") THEN BEGIN
                   lvPurchHeadRec."Project Appointment" := 0;
                   lvPurchHeadRec.MODIFY;
                 END;
               END;
             END;

    CaptionML=ENU=Appointments By Project;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   CaptionML=ENU=Project No.;
                                                   NotBlank=Yes }
    { 12  ;   ;Appointment Type    ;Option        ;CaptionML=ENU=Appointment Type;
                                                   OptionCaptionML=ENU=General,Execution,Purchase;
                                                   OptionString=General,Execution,Purchase }
    { 15  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 20  ;   ;Starting Date       ;Date          ;CaptionML=ENU=Date }
    { 22  ;   ;Internal/External Action;Option    ;OnValidate=BEGIN
                                                                IF "Internal/External Action" <> xRec."Internal/External Action" THEN
                                                                  "Action by" := '';
                                                              END;

                                                   CaptionML=ENU=Internal/External Action;
                                                   OptionCaptionML=ENU=Internal,External;
                                                   OptionString=Internal,External }
    { 25  ;   ;Ending Date         ;Date          ;CaptionML=ENU=Ending Date }
    { 30  ;   ;Start Time          ;Time          ;CaptionML=ENU=Time }
    { 35  ;   ;End Time            ;Time          ;CaptionML=ENU=Ending Time }
    { 40  ;   ;Type                ;Code20        ;TableRelation="Appointment Type".Code;
                                                   OnValidate=BEGIN
                                                                IF AppTypeRec.GET(Type) THEN
                                                                  Description := AppTypeRec.Description;
                                                              END;

                                                   CaptionML=ENU=Type }
    { 50  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 60  ;   ;Action by           ;Code20        ;TableRelation=IF (Internal/External Action=CONST(Internal)) Employee.No.
                                                                 ELSE IF (Internal/External Action=CONST(External)) Vendor.No.;
                                                   CaptionML=ENU=Action by }
    { 70  ;   ;Contact Company No. ;Code20        ;TableRelation=Contact.No. WHERE (Type=CONST(Company));
                                                   OnValidate=VAR
                                                                ContBusRelationRec@1100485000 : Record 5054;
                                                              BEGIN
                                                                CALCFIELDS("Contact Company Name");
                                                                IF "Appointment Type" <> "Appointment Type"::Purchase THEN
                                                                  EXIT;

                                                                ContBusRelationRec.SETCURRENTKEY("Link to Table","Contact No.");
                                                                ContBusRelationRec.SETRANGE("Link to Table",ContBusRelationRec."Link to Table"::Vendor);
                                                                ContBusRelationRec.SETRANGE("Contact No.","Contact Company No.");
                                                                IF ContBusRelationRec.FIND('-') THEN
                                                                  "Vendor No." := ContBusRelationRec."No.";
                                                              END;

                                                   CaptionML=ENU=Contact Company No. }
    { 75  ;   ;Contact Company Name;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact Company No.)));
                                                   CaptionML=ENU=Contact Company Name;
                                                   Editable=No }
    { 80  ;   ;Contact Person No.  ;Code20        ;TableRelation=Contact.No. WHERE (Company No.=FIELD(Contact Company No.),
                                                                                    Type=CONST(Person));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Contact Person Name");
                                                              END;

                                                   CaptionML=ENU=Contact Person No. }
    { 85  ;   ;Contact Person Name ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact Person No.)));
                                                   CaptionML=ENU=Contact Person Name;
                                                   Editable=No }
    { 90  ;   ;Finished            ;Boolean       ;CaptionML=ENU=Finished }
    { 100 ;   ;Amount              ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Purchase Line".Amount WHERE (Document Type=CONST(Order),
                                                                                                 Document No.=FIELD(Purchase Order)));
                                                   CaptionML=ENU=Amount;
                                                   Editable=No }
    { 120 ;   ;Appointment Type Description;Text50;CaptionML=ENU=Appointment Type Description;
                                                   Editable=No }
    { 130 ;   ;Print on Worksheet  ;Boolean       ;CaptionML=ENU=Print on Worksheet }
    { 140 ;   ;Purchase Order      ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Header".No. WHERE (Job No.=FIELD(Project No.),
                                                                                                   Project Appointment Type=FIELD(Appointment Type),
                                                                                                   Project Appointment=FIELD(Line No.),
                                                                                                   Document Type=CONST(Order)));
                                                   CaptionML=ENU=Purchase Order;
                                                   Editable=No }
    { 150 ;   ;Order Lead Time     ;DateFormula   ;OnValidate=BEGIN
                                                                "Receipt Date" := CALCDATE("Order Lead Time","Starting Date");
                                                                VALIDATE("Receipt Date");
                                                              END;

                                                   CaptionML=ENU=Order Lead Time }
    { 160 ;   ;Receipt Date        ;Date          ;OnValidate=VAR
                                                                lvDayNo@1210190000 : Integer;
                                                              BEGIN
                                                                lvDayNo := DATE2DWY("Receipt Date", 1);
                                                                CASE lvDayNo OF
                                                                  6:BEGIN
                                                                      IF NOT CONFIRM(Text001, FALSE, "Receipt Date") THEN
                                                                        "Receipt Date" := "Receipt Date" + 2;
                                                                    END;
                                                                  7:BEGIN
                                                                      IF NOT CONFIRM(Text002, FALSE, "Receipt Date") THEN
                                                                        "Receipt Date" := "Receipt Date" + 1;
                                                                    END;
                                                                END;
                                                                JobRec.GET("Project No.");
                                                                IF (JobRec."Starting Date" <> 0D) AND (JobRec."Ending Date" <> 0D) THEN
                                                                  IF ("Receipt Date" < JobRec."Starting Date") OR ("Receipt Date" > JobRec."Ending Date") THEN
                                                                    MESSAGE(Text003,"Receipt Date", JobRec."Starting Date", JobRec."Ending Date");
                                                              END;

                                                   CaptionML=ENU=Receipt Date }
    { 170 ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=VAR
                                                                ContBusRelationRec@1100485000 : Record 5054;
                                                              BEGIN
                                                                IF "Appointment Type" <> "Appointment Type"::Purchase THEN
                                                                  EXIT;

                                                                ContBusRelationRec.SETCURRENTKEY("Link to Table","No.");
                                                                ContBusRelationRec.SETRANGE("Link to Table",ContBusRelationRec."Link to Table"::Vendor);
                                                                ContBusRelationRec.SETRANGE("No.","Vendor No.");

                                                                IF ContBusRelationRec.FIND('-') THEN
                                                                  "Contact Company No." := ContBusRelationRec."Contact No.";
                                                              END;

                                                   CaptionML=ENU=Vendor No. }
    { 180 ;   ;Urgency Code        ;Code10        ;CaptionML=ENU=Urgency Code }
  }
  KEYS
  {
    {    ;Project No.,Appointment Type,Line No.   ;Clustered=Yes }
    {    ;Project No.,Appointment Type,Starting Date }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1210190000 : TextConst 'ENU=Do you want to create a Purchase Order for Project ''%1'' , %2 ''%3'' ?';
      Text001@1210190001 : TextConst 'ENU=Receipt Date %1 is a saturday, continue ?';
      Text002@1210190002 : TextConst 'ENU=Receipt Date %1 is a sunday, continue ?';
      Text003@1210190003 : TextConst 'ENU=Receipt date %1 does not occur in production period from %2 until %3';
      AppTypeRec@1210190004 : Record 11012082;
      JobRec@1210190005 : Record 11072003;
      NoUserInteractionBln@1100485000 : Boolean;
      PurchaseNo@1100485001 : Code[20];

    PROCEDURE CreatePurchaseOrder@1210190000();
    VAR
      lvPurchHeadRec@1210190000 : Record 38;
      lvPurchSetupRec@1210190001 : Record 312;
      lvNoSeriesMngmntCU@1210190003 : Codeunit 396;
    BEGIN
      TESTFIELD("Starting Date");
      lvPurchSetupRec.GET;
      lvPurchSetupRec.TESTFIELD("Order Nos.");
      IF NOT NoUserInteractionBln THEN
        IF NOT CONFIRM(Text000, FALSE, "Project No.", FIELDCAPTION("Vendor No."), "Vendor No.") THEN
          EXIT;

      WITH lvPurchHeadRec DO BEGIN
        INIT;
        "Document Type" := "Document Type"::Order;
        "No." := lvNoSeriesMngmntCU.GetNextNo(lvPurchSetupRec."Order Nos.", WORKDATE, TRUE);
        "No. Series" := lvPurchSetupRec."Quote Nos.";
        INSERT(TRUE);
        VALIDATE("Job No.","Project No.");
        VALIDATE("Document Date",TODAY);
        "Order Date" := "Starting Date";
        "Lead Time Calculation" := "Order Lead Time";
        "Requested Receipt Date" := "Receipt Date";
        "Project Appointment Type" := "Appointment Type"; //needed for flowfield "Purchase Order".
        "Project Appointment" := "Line No.";
        VALIDATE("Buy-from Vendor No.","Vendor No.");
        MODIFY;
        IF NOT NoUserInteractionBln THEN BEGIN
          COMMIT;
          OpenPurchaseHeaderForm("No.");
        END ELSE
          PurchaseNo := "No.";
      END;
    END;

    PROCEDURE OpenPurchaseHeaderForm@1210190002(IOrderNo@1210190000 : Code[20]);
    VAR
      lvPurchHeadRec@1210190001 : Record 38;
      lvPurchOrderFrm@1210190002 : Page 50;
    BEGIN
      WITH lvPurchHeadRec DO BEGIN
        SETRANGE("Document Type", "Document Type"::Order);
        SETRANGE("No.", IOrderNo);
        GET("Document Type"::Order, IOrderNo);
        lvPurchOrderFrm.SETTABLEVIEW(lvPurchHeadRec);
        lvPurchOrderFrm.RUNMODAL;
      END;
    END;

    PROCEDURE InitRecord@1100485000();
    BEGIN
      IF "Appointment Type" = "Appointment Type"::Execution THEN BEGIN
        IF JobRec.GET("Project No.") THEN BEGIN
          "Internal/External Action" := JobRec."Internal/External Action";
          "Action by" := JobRec."Action by";
          "Starting Date" := JobRec."Starting Date";
          "Ending Date" := JobRec."Ending Date";
        END;
      END;
    END;

    PROCEDURE SetNoUserInteraction@1100485001(iNoUserInteractionBln@1100485000 : Boolean);
    BEGIN
      NoUserInteractionBln := iNoUserInteractionBln;
    END;

    PROCEDURE GetPurchaseNo@1100485002() : Code[20];
    BEGIN
      EXIT(PurchaseNo);
    END;

    PROCEDURE ActionByName@1100485003() : Text[100];
    VAR
      lEmplRec@1100485000 : Record 5200;
      lVendorRec@1100485001 : Record 23;
    BEGIN
      IF ("Action by" = '') THEN
        EXIT('');

      CASE "Internal/External Action" OF
        "Internal/External Action"::Internal:
          IF lEmplRec.GET("Action by") THEN
            EXIT(lEmplRec."Full Name");
        "Internal/External Action"::External:
          IF lVendorRec.GET("Action by") THEN
            EXIT(lVendorRec.Name);
      END;
    END;

    BEGIN
    END.
  }
}

