OBJECT Table 11012101 Delivered Object
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 ComplaintSetup.GET;
                 ComplaintSetup.TESTFIELD("Delivered Object Nos.");
                 NoSeriesMgt.InitSeries(ComplaintSetup."Delivered Object Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    OnModify=BEGIN
               CheckUpdateComplaint();
             END;

    CaptionML=ENU=Delivered Object;
    LookupPageID=Page11012362;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  ComplaintSetup.GET;
                                                                  NoSeriesMgt.TestManual(ComplaintSetup."Delivered Object Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 20  ;   ;Delivered Project   ;Code20        ;TableRelation="Delivered Project";
                                                   OnValidate=VAR
                                                                lDelProjRec@1100485000 : Record 11012100;
                                                              BEGIN
                                                                IF lDelProjRec.GET("Delivered Project") THEN BEGIN
                                                                  Address := lDelProjRec.Address;
                                                                  "Address 2" := lDelProjRec."Address 2";
                                                                  "Post Code" := lDelProjRec."Postal Code" ;
                                                                  City := lDelProjRec.City ;
                                                                  VALIDATE("Project Manager", lDelProjRec."Project Manager");
                                                                  VALIDATE(Supervisor, lDelProjRec.Supervisor);
                                                                  VALIDATE("Project Engineer", lDelProjRec."Project Engineer");
                                                                  VALIDATE("Site Manager", lDelProjRec."Site Manager");
                                                                  VALIDATE("Responsible Person", lDelProjRec."Responsible Person");
                                                                  VALIDATE("Original Project", lDelProjRec."Original Project No.");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Delivered Project;
                                                   Editable=Yes }
    { 30  ;   ;Principal           ;Code20        ;TableRelation="Delivered Object Principal".Principal WHERE (Delivered Object=FIELD(No.));
                                                   OnValidate=BEGIN
                                                                FillContactFields;
                                                              END;

                                                   CaptionML=ENU=Principal }
    { 50  ;   ;Address             ;Text50        ;CaptionML=ENU=Address }
    { 60  ;   ;Address 2           ;Text50        ;CaptionML=ENU=Address 2 }
    { 70  ;   ;Post Code           ;Code20        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code"
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code" WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Postal Code }
    { 80  ;   ;City                ;Text30        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code".City
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=City }
    { 90  ;   ;County              ;Text30        ;CaptionML=ENU=County }
    { 100 ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=ENU=Country/Region Code }
    { 150 ;   ;Creation Date       ;Date          ;CaptionML=ENU=Creation Date;
                                                   Editable=No }
    { 160 ;   ;Original Project    ;Code20        ;FieldClass=Normal;
                                                   TableRelation=Job;
                                                   OnValidate=VAR
                                                                lJobRec@1100485000 : Record 11072003;
                                                                lDelObjectPrincipal@1100485001 : Record 11012109;
                                                                DeliveredProject@1100529000 : Record 11012100;
                                                              BEGIN
                                                                IF "Original Project" = '' THEN BEGIN
                                                                  IF (xRec."Original Project" <> '') AND ("Original Project Description" <> '') THEN
                                                                    "Original Project Description" := '';
                                                                END ELSE BEGIN
                                                                  IF lJobRec.GET("Original Project") THEN BEGIN
                                                                    GenerateDelObjects.CopyPrincipals("Original Project","No.");
                                                                    IF lDelObjectPrincipal.GET("No.", lJobRec."Bill-to Customer No.") THEN
                                                                      VALIDATE(Principal,GenerateDelObjects.GetProjContact(lJobRec."Bill-to Customer No."));
                                                                    "Original Project Description" := lJobRec.Description;
                                                                    Address := lJobRec.Address;
                                                                    "Address 2" := lJobRec."Address 2";
                                                                    "Post Code" := lJobRec."Post Code" ;
                                                                    City := lJobRec.City ;
                                                                    VALIDATE("Project Manager", lJobRec."Project Manager");
                                                                    VALIDATE(Supervisor, lJobRec.Supervisor);
                                                                    VALIDATE("Project Engineer", lJobRec."Project Engineer");
                                                                    VALIDATE("Site Manager", lJobRec."Site Manager");
                                                                    VALIDATE("Responsible Person", lJobRec."Person Responsible");
                                                                  END ELSE BEGIN
                                                                    IF "Delivered Project" <> '' THEN BEGIN
                                                                      IF DeliveredProject.GET("Delivered Project") THEN BEGIN
                                                                        IF DeliveredProject."Original Project No." = "Original Project" THEN
                                                                          "Original Project Description" := DeliveredProject."Original Project Description";
                                                                      END;
                                                                    END;
                                                                  END;
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Original Project }
    { 165 ;   ;Original Project Description;Text50;FieldClass=Normal;
                                                   CaptionML=ENU=Original Project Description;
                                                   Editable=Yes }
    { 170 ;   ;Original Plot no.   ;Code10        ;TableRelation=Plot."Plot No." WHERE (Project No.=FIELD(Original Project));
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Original Plot no. }
    { 200 ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=No }
    { 210 ;   ;Contact Name        ;Text50        ;FieldClass=Normal;
                                                   CaptionML=ENU=Contact Name }
    { 220 ;   ;Contact Name 2      ;Text50        ;FieldClass=Normal;
                                                   CaptionML=ENU=Contact Name 2 }
    { 230 ;   ;Contact Phone       ;Text30        ;FieldClass=Normal;
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Contact Phone }
    { 240 ;   ;Contact Phone 2     ;Text30        ;FieldClass=Normal;
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Contact Phone 2 }
    { 250 ;   ;Contact Mobile Phone;Text30        ;FieldClass=Normal;
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Contact Mobile Phone }
    { 260 ;   ;Contact E-Mail      ;Text80        ;FieldClass=Normal;
                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=ENU=Contact E-Mail }
    { 270 ;   ;Contact Mobile Phone 2;Text30      ;FieldClass=Normal;
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Contact Mobile Phone 2 }
    { 280 ;   ;Contact Phone Work  ;Text30        ;FieldClass=Normal;
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Contact Phone Work }
    { 290 ;   ;Contact Phone Work 2;Text30        ;FieldClass=Normal;
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Contact Phone Work 2 }
    { 300 ;   ;Project Manager     ;Code20        ;TableRelation=Employee;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Name Project Manager");
                                                              END;

                                                   CaptionML=ENU=Project Manager }
    { 305 ;   ;Name Project Manager;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Project Manager)));
                                                   CaptionML=ENU=Name Project Manager;
                                                   Editable=No }
    { 310 ;   ;Supervisor          ;Code20        ;TableRelation=Contact.No. WHERE (Type=CONST(Person));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Name SuperVisor");
                                                              END;

                                                   CaptionML=ENU=Supervisor }
    { 315 ;   ;Name SuperVisor     ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Supervisor)));
                                                   CaptionML=ENU=Name SuperVisor;
                                                   Editable=No }
    { 320 ;   ;Project Engineer    ;Code20        ;TableRelation=Employee;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Name Project Engineer");
                                                              END;

                                                   CaptionML=ENU=Project Engineer }
    { 325 ;   ;Name Project Engineer;Text100      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Project Engineer)));
                                                   CaptionML=ENU=Name Project Engineer;
                                                   Editable=No }
    { 330 ;   ;Site Manager        ;Code20        ;TableRelation=Employee;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Name Site Manager");
                                                              END;

                                                   CaptionML=ENU=Site Manager }
    { 335 ;   ;Name Site Manager   ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Site Manager)));
                                                   CaptionML=ENU=Name Site Manager;
                                                   Editable=No }
    { 336 ;   ;Responsible Person  ;Code20        ;TableRelation=Employee;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Name Responsible Person");
                                                              END;

                                                   CaptionML=ENU=Responsible Person }
    { 337 ;   ;Name Responsible Person;Text100    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Responsible Person)));
                                                   CaptionML=ENU=Name Responsible Person;
                                                   Editable=No }
    { 340 ;   ;End Guarantee Period;Date          ;CaptionML=ENU=End Guarantee Period }
    { 350 ;   ;End Service Period  ;Date          ;CaptionML=ENU=End Service Period }
    { 360 ;   ;Date Delivered      ;Date          ;OnValidate=BEGIN
                                                                //kzwerver, 111202, c, #31017
                                                                "End Guarantee Period" := 0D;
                                                                "End Service Period" := 0D;
                                                                IF ("Date Delivered" <> 0D) THEN
                                                                  CalcGaranteeAndServicePeriod;
                                                              END;

                                                   CaptionML=ENU=Date Delivered }
    { 370 ;   ;Buyer               ;Code20        ;TableRelation=Contact;
                                                   OnValidate=BEGIN
                                                                FillContactFields;
                                                              END;

                                                   CaptionML=ENU=Buyer }
    { 400 ;   ;Principal Name      ;Text50        ;CaptionML=ENU=Principal Name }
    { 410 ;   ;Principal Name 2    ;Text50        ;CaptionML=ENU=Principal Name 2 }
    { 420 ;   ;Principal Address   ;Text50        ;CaptionML=ENU=Principal Address }
    { 430 ;   ;Principal Address 2 ;Text50        ;CaptionML=ENU=Principal Address 2 }
    { 440 ;   ;Principal Post Code ;Code20        ;TableRelation="Post Code";
                                                   OnValidate=VAR
                                                                PostcodeRec@1210190000 : Record 225;
                                                              BEGIN
                                                                PostcodeRec.SETRANGE(Code, "Principal Address");
                                                                IF PostcodeRec.FIND('-') THEN
                                                                  "Principal Address 2" := PostcodeRec.City
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Principal Post Code }
    { 450 ;   ;Principal City      ;Text30        ;CaptionML=ENU=Principal City }
    { 470 ;   ;Buy/Rent            ;Option        ;CaptionML=ENU=Buy/Rent;
                                                   OptionCaptionML=ENU=Buy,Rent;
                                                   OptionString=Sale,Rent }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Delivered Project,No.                    }
    {    ;Original Project,Address,Address 2       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ComplaintSetup@1210190000 : Record 11012108;
      PostCode@1100409001 : Record 225;
      NoSeriesMgt@1210190001 : Codeunit 396;
      GenerateDelObjects@1100485000 : Codeunit 11012050;

    PROCEDURE AssistEdit@2(OldDeliveredObjRec@1210190001 : Record 11012101) : Boolean;
    VAR
      lDeliveredObjRec@1210190000 : Record 11012101;
    BEGIN
      WITH lDeliveredObjRec DO BEGIN
        lDeliveredObjRec := Rec;
        ComplaintSetup.GET;
        ComplaintSetup.TESTFIELD("Delivered Object Nos.");
        IF NoSeriesMgt.SelectSeries(ComplaintSetup."Delivered Object Nos.",
                                    OldDeliveredObjRec."No. Series","No. Series") THEN BEGIN
          ComplaintSetup.GET;
          ComplaintSetup.TESTFIELD("Delivered Object Nos.");
          NoSeriesMgt.SetSeries("No.");
          Rec := lDeliveredObjRec;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE FillContactFields@1100485000();
    VAR
      lContactRec@1100485001 : Record 5050;
      ContactNo@1100485000 : Code[20];
    BEGIN
      IF Buyer <> '' THEN
        ContactNo := Buyer
      ELSE
        ContactNo := Principal;

      IF lContactRec.GET(ContactNo) THEN BEGIN
        "Contact Name" := lContactRec.Name;
        "Contact Name 2" := lContactRec."Name 2" ;
        "Contact Phone" := lContactRec."Phone No." ;
        "Contact Phone 2" := lContactRec."Phone No. 2" ;
        "Contact Mobile Phone" := lContactRec."Mobile Phone No."  ;
        "Contact E-Mail" := lContactRec."E-Mail" ;
        "Contact Mobile Phone 2" :=  lContactRec."Mobile Phone No. 2" ;
        "Contact Phone Work" := lContactRec."Phone No. Work" ;
        "Contact Phone Work 2" := lContactRec."Phone No. Work 2";
      END;
    END;

    PROCEDURE CheckUpdateComplaint@1100525000();
    VAR
      lvOldRec@1100525001 : Record 11012101;
      lvComplaintRec@1100525002 : Record 11012450;
      Proj@1100529001 : Record 11072003;
      NewProjNo@1100529000 : Code[20];
    BEGIN
      IF NOT lvOldRec.GET("No.") THEN
        EXIT;
      IF ("Original Project" = lvOldRec."Original Project") AND ("Original Plot no." = lvOldRec."Original Plot no.") THEN
        EXIT;

      lvComplaintRec.SETCURRENTKEY("Delivered Object");
      lvComplaintRec.SETRANGE("Delivered Object", "No.");
      IF lvComplaintRec.FINDSET(TRUE, FALSE) THEN BEGIN
        REPEAT
          NewProjNo := '';
          IF Proj.GET("Original Project") THEN
            NewProjNo := "Original Project";
          //lvComplaintRec."Project No." := "Original Project";
          lvComplaintRec."Project No." := NewProjNo;
          lvComplaintRec."Plot No." := "Original Plot no.";
          lvComplaintRec.MODIFY;
        UNTIL lvComplaintRec.NEXT = 0;
      END;
    END;

    PROCEDURE CalcGaranteeAndServicePeriod@1210190000();
    VAR
      Project@1210190000 : Record 11072003;
      DeliveredProject@1210190001 : Record 11012100;
    BEGIN
      //kzwerver, 111202, n, #31017
      IF ("Delivered Project" <> '') THEN BEGIN
        DeliveredProject.GET("Delivered Project");
        IF (DeliveredProject."Original Project No." <> '') THEN BEGIN
          IF Project.GET(DeliveredProject."Original Project No.") THEN BEGIN
            IF (Project."Days End Service Period" <> 0) THEN
              "End Service Period" := "Date Delivered" + Project."Days End Service Period";
            IF (FORMAT(Project."Guarantee Period") <> '') THEN
              "End Guarantee Period" := CALCDATE(Project."Guarantee Period", "End Service Period");
          END;
        END;
      END;
    END;

    PROCEDURE EditDocuments@1100528702(ContactNo@1210190004 : Code[20];ContactPersonNo@1210190005 : Code[20];SendMail@1210190002 : Boolean;Recipient@1100528700 : Text[120]);
    VAR
      DocumentTemplate@1210190001 : Record 11012401;
      DocumentMailing@1210190000 : Codeunit 11012278;
      RecRef@1100525000 : RecordRef;
      DocumentLinkRIDs@1100525001 : ARRAY [5] OF RecordID;
    BEGIN
      RecRef.GETTABLE(Rec);
      DocumentLinkRIDs[1] := RecRef.RECORDID;

      DocumentMailing.EditDocuments(
        STRSUBSTNO('%1', DocumentTemplate.Type::"Delivered Object"), DocumentLinkRIDs,
        '', ContactNo, ContactPersonNo, SendMail, Recipient);
    END;

    BEGIN
    END.
  }
}

