OBJECT Table 11012169 Estimate Base Part
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               PartLineRec.SETRANGE("Part Group", "Part Group");
               PartLineRec.SETRANGE(Part, Code);
               PartLineRec.DELETEALL(TRUE);

               //db.sn, 28-11-11: M30268
               TextRec.SETRANGE("Table Name", TextRec."Table Name"::Estimate);
               TextRec.SETFILTER(Group, '%1', '');
               TextRec.SETFILTER("No.", '%1', '');
               TextRec.SETFILTER("Estimate No.", '%1', '');
               TextRec.SETFILTER("Sub-Estimate No.", '%1', '');
               TextRec.SETRANGE("Part Group", "Part Group");
               TextRec.SETRANGE(Part, Code);
               TextRec.SETRANGE("Estimate Line No.", 0);
               TextRec.DELETEALL;
               //db.en, 28-11-11: M30268
             END;

    OnRename=BEGIN
               ERROR(Text000);
             END;

    CaptionML=ENU=Estimate Base Part;
    LookupPageID=Page11012181;
  }
  FIELDS
  {
    { 10  ;   ;Part Group          ;Code10        ;TableRelation="Estimate Group".No. WHERE (Type=CONST(Part));
                                                   CaptionML=ENU=Part Group }
    { 20  ;   ;Code                ;Code20        ;AltSearchField=Search Name;
                                                   OnValidate=BEGIN
                                                                ElementFormat.CreateStandardPartEst("Part Group",Code,Chapter,Paragraph,Level);
                                                                IF CurrFieldNo = FIELDNO(Code) THEN
                                                                  IF (xRec.Code <> '') AND (xRec.Code <> Code) THEN
                                                                    ERROR(Text000);  //db, 28-08-14: C017149
                                                              END;

                                                   CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 30  ;   ;Search Name         ;Code50        ;CaptionML=ENU=Search Name }
    { 40  ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                IF ("Search Name" = UPPERCASE(xRec.Description)) OR ("Search Name" = '') THEN
                                                                  "Search Name" := Description;
                                                              END;

                                                   CaptionML=ENU=Description }
    { 41  ;   ;Description 2       ;Text50        ;CaptionML=ENU=Description 2;
                                                   Description=DP00686 }
    { 50  ;   ;Level               ;Integer       ;CaptionML=ENU=Level }
    { 60  ;   ;Chapter             ;Code20        ;CaptionML=ENU=Chapter }
    { 70  ;   ;Paragraph           ;Code20        ;CaptionML=ENU=Paragraph }
    { 80  ;   ;Quantity            ;Decimal       ;InitValue=1;
                                                   CaptionML=ENU=Quantity }
    { 90  ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure".Code;
                                                   CaptionML=ENU=Unit of Measure }
    { 95  ;   ;Feature Group       ;Code10        ;TableRelation="Estimate Group".No.;
                                                   CaptionML=ENU=Feature Group }
    { 100 ;   ;Feature Cluster     ;Code10        ;TableRelation="Estimate Feature Cluster"."Feature Cluster";
                                                   CaptionML=ENU=Feature Cluster }
    { 110 ;   ;Element             ;Code20        ;TableRelation="Base Element".Code;
                                                   OnValidate=BEGIN
                                                                ElementFormat.FormatElem(Element,2,'');
                                                                IF Element <> '' THEN
                                                                  ElemRec.GET(Element);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Element }
    { 120 ;   ;Excel Estimate Template;Text250    ;OnValidate=BEGIN
                                                                IF CheckValidExcelTemplate(TRUE, FIELDNO("Excel Estimate Template")) THEN;
                                                              END;

                                                   OnLookup=VAR
                                                              EstSetup@1100485002 : Record 11012150;
                                                              FileMgt@1100525000 : Codeunit 419;
                                                              lvExcelTemplate@1100485000 : Text[250];
                                                              DirectoryName@1100525001 : Text;
                                                              FileAbsPathName@1100525002 : Text;
                                                            BEGIN
                                                              EstSetup.GET;
                                                              DirectoryName := EstSetup."Pathname Excel Est. Template";
                                                              IF FileMgt.SelectFile('xlt', DirectoryName, lvExcelTemplate, FileAbsPathName) THEN
                                                                VALIDATE("Excel Estimate Template",lvExcelTemplate);
                                                            END;

                                                   CaptionML=ENU=Excel Estimate Template }
    { 130 ;   ;Object Group        ;Code10        ;TableRelation="Service Group".No.;
                                                   OnValidate=BEGIN
                                                                CheckObjectGroup;
                                                              END;

                                                   CaptionML=ENU=Object Group }
    { 141 ;   ;Part Line Exists    ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Estimate Part Line NEW" WHERE (Part Group=FIELD(Part Group),
                                                                                                     Part=FIELD(Code)));
                                                   CaptionML=ENU=Part Line Exists;
                                                   Description=DP00381;
                                                   Editable=No }
    { 150 ;   ;Theoretical Life Span;DateFormula  ;CaptionML=ENU=Theoretical Life Span }
    { 151 ;   ;Expected Life Span  ;DateFormula   ;CaptionML=ENU=Expected Life Span }
    { 160 ;   ;Transfer to Contract;Boolean       ;CaptionML=ENU=Transfer to Contract }
    { 161 ;   ;Mark for Object     ;Boolean       ;CaptionML=ENU=Mark for Object }
    { 170 ;   ;Project Plann. Base Act. Code;Code10;
                                                   TableRelation="Project Planning Base Activity";
                                                   OnValidate=VAR
                                                                ProjectPlanningBaseActivity@1100528401 : Record 11012434;
                                                                FormatManagement@1100528400 : Codeunit 11012022;
                                                              BEGIN
                                                                FormatManagement.FormatElem("Project Plann. Base Act. Code", 20, '');
                                                                IF "Project Plann. Base Act. Code" <> '' THEN
                                                                  ProjectPlanningBaseActivity.GET("Project Plann. Base Act. Code");
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Project Plann. Base Act. Code }
    { 180 ;   ;Unit of Qty per Unit;Code20        ;TableRelation="Unit of Measure".Code;
                                                   CaptionML=ENU=Unit of Qty per Unit;
                                                   Description=DP00686 }
  }
  KEYS
  {
    {    ;Part Group,Code                         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PartLineRec@1210190002 : Record 11072076;
      TextRec@1210190000 : Record 11020595;
      ElemRec@1100485000 : Record 11012060;
      ElementFormat@1100485001 : Codeunit 11012022;
      Text000@1100409000 : TextConst 'ENU=Rename not allowed.';
      Text001@1100485004 : TextConst 'ENU=%1 %2 doesn''t exist.';
      Text003@1100485007 : TextConst 'ENU="A %1 on a lower %2 is present. "';
      Text004@1100485006 : TextConst 'ENU="A %1 on a higher %2 is present. "';
      FileMgt@1100525500 : Codeunit 419;

    PROCEDURE CheckValidExcelTemplate@1100485003(iUserInteraction@1100485004 : Boolean;CalledBy@1100525000 : Integer) : Boolean;
    VAR
      lvPartbyPartGroup@1100485001 : Record 11012169;
      lvExcelEstimateMgt@1100485002 : Codeunit 11020203;
      lvLevel@1100485000 : Integer;
    BEGIN
      IF (CalledBy = FIELDNO("Excel Estimate Template")) AND ("Excel Estimate Template" = '') THEN
        EXIT(TRUE);

      IF NOT FileMgt.ClientFileExists(lvExcelEstimateMgt.GetTemplPathFile2(Rec)) THEN
        IF iUserInteraction THEN
          ERROR(Text001,FIELDCAPTION("Excel Estimate Template"),lvExcelEstimateMgt.GetTemplPathFile2(Rec))
        ELSE
          EXIT(FALSE);

      lvPartbyPartGroup.SETRANGE("Part Group","Part Group");

      CASE Level OF
        //0: No Filters
        1: lvPartbyPartGroup.SETRANGE(Chapter,Chapter);
        2: lvPartbyPartGroup.SETRANGE(Paragraph,Paragraph);
        3: lvPartbyPartGroup.SETRANGE(Code,Code);
      END;

      lvPartbyPartGroup.SETFILTER(Level,'>%1',Level);
      IF CalledBy = FIELDNO("Excel Estimate Template") THEN
        lvPartbyPartGroup.SETFILTER("Excel Estimate Template",'<>%1','');
      FILTERGROUP(10);
      lvPartbyPartGroup.SETFILTER(Code,'<>%1',Code);
      FILTERGROUP(0);
      IF lvPartbyPartGroup.FIND('-') THEN BEGIN
        IF iUserInteraction THEN BEGIN
          IF CalledBy = FIELDNO("Excel Estimate Template") THEN
            ERROR(Text003,FIELDCAPTION("Excel Estimate Template"),FIELDCAPTION(Level));
        END ELSE BEGIN
          EXIT(FALSE);
        END;
      END;

      //Check overlying parts.
      lvPartbyPartGroup.SETRANGE(Chapter);
      lvPartbyPartGroup.SETRANGE(Paragraph);
      lvPartbyPartGroup.SETRANGE(Code);
      IF CalledBy = FIELDNO("Excel Estimate Template") THEN
        lvPartbyPartGroup.SETFILTER("Excel Estimate Template",'<>%1','');
      lvLevel := Level;
      WHILE lvLevel > 0 DO
        BEGIN
          lvLevel -= 1;
          lvPartbyPartGroup.SETRANGE(Level,lvLevel);
          CASE lvLevel OF
            0:
              BEGIN
                lvPartbyPartGroup.SETFILTER(Chapter,'%1','');
                lvPartbyPartGroup.SETFILTER(Paragraph,'%1','');
              END;
            1:
              BEGIN
                lvPartbyPartGroup.SETRANGE(Chapter,Chapter);
                lvPartbyPartGroup.SETFILTER(Paragraph,'%1','');
              END;
            2:
              BEGIN
                lvPartbyPartGroup.SETRANGE(Chapter,Chapter);
                lvPartbyPartGroup.SETRANGE(Paragraph,Paragraph);
              END;
          END;
          IF lvPartbyPartGroup.FIND('-') THEN BEGIN
            IF iUserInteraction THEN BEGIN
              IF CalledBy = FIELDNO("Excel Estimate Template") THEN
                ERROR(Text004,FIELDCAPTION("Excel Estimate Template"),FIELDCAPTION(Level));
            END ELSE BEGIN
              EXIT(FALSE);
            END;
          END;
        END;

      EXIT(TRUE);
    END;

    PROCEDURE CheckExcelTemplateFilled@1100485005();
    VAR
      EstSetup@1100485000 : Record 11012150;
    BEGIN
      EstSetup.GET;
      IF "Excel Estimate Template" = '' THEN BEGIN
        "Excel Estimate Template" := EstSetup."Excel Estimate Template";
        IF CheckValidExcelTemplate(FALSE, FIELDNO("Excel Estimate Template")) THEN
          VALIDATE("Excel Estimate Template",EstSetup."Excel Estimate Template")
        ELSE
          "Excel Estimate Template" := '';
      END;
    END;

    PROCEDURE CheckObjectGroup@1100525000();
    VAR
      lvPartRec@1100525000 : Record 11012169;
      lvText000@1100525001 : TextConst 'ENU=Object Group already assigned to Part %1';
    BEGIN
      IF "Object Group" = '' THEN EXIT;

      lvPartRec.SETRANGE("Part Group", "Part Group");
      lvPartRec.SETFILTER(Code, '<>%1', Code);
      lvPartRec.SETRANGE("Object Group", "Object Group");
      IF lvPartRec.FINDFIRST THEN
        ERROR(lvText000, lvPartRec.Code);
    END;

    PROCEDURE GetDataCaption@1100528000() : Text[250];
    BEGIN
      EXIT(STRSUBSTNO('%1 - %2 - %3', "Part Group", Code, Description));  //db, 16-02-15
    END;

    BEGIN
    END.
  }
}

