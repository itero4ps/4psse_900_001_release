OBJECT Table 11012227 Consent Rule
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Consent Level");
             END;

    OnModify=BEGIN
               TESTFIELD("Consent Level");
             END;

    OnDelete=VAR
               ConsentRuleFilter@1100528400 : Record 11229793;
             BEGIN
               ConsentRuleFilter.SETRANGE("Consent Rule Line No.", "Line No.");
               ConsentRuleFilter.DELETEALL;
             END;

    CaptionML=ENU=Consent Rule;
  }
  FIELDS
  {
    { 10  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 20  ;   ;Consent Level       ;Integer       ;CaptionML=ENU=Consent Level }
    { 25  ;   ;Process             ;Option        ;OnValidate=BEGIN
                                                                IF NOT (Process IN [Process::"Purchase Order", Process::"Purchase Quote"]) THEN BEGIN
                                                                  "Min. Deviation From Budget %" := 0;
                                                                  "Min. Deviation From Budget" := 0;
                                                                END;
                                                                IF Process = Process::"Advice Order" THEN
                                                                  Level := Level::Line;
                                                                IF Process = Process::"Purchase Action" THEN BEGIN
                                                                  Level := Level::Header;
                                                                  Type := Type::Project;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Process;
                                                   OptionCaptionML=ENU=Invoice,Invoice Approval,Advice Order,Purchase Order,Purchase Action,Purchase Quote,Credit Memo,Credit Memo Approval;
                                                   OptionString=Invoice,Invoice Approval,Advice Order,Purchase Order,Purchase Action,Purchase Quote,Credit Memo,Credit Memo Approval }
    { 27  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Type <> Type::Project THEN BEGIN
                                                                  "Project Role" := 0;
                                                                  IF Process = Process::"Purchase Action" THEN
                                                                    FIELDERROR(Process);
                                                                END;
                                                                IF Type <> Type::Service THEN
                                                                  "Service Role" := 0;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=,Project,Service,Other,All;
                                                   OptionString=,Project,Service,Other,All }
    { 30  ;   ;Level               ;Option        ;OnValidate=BEGIN
                                                                IF (Level = Level::Line) AND
                                                                   (Process = Process::"Purchase Action")
                                                                THEN FIELDERROR(Process);

                                                                IF (Level = Level::Header) AND
                                                                   (Process = Process::"Advice Order")
                                                                THEN FIELDERROR(Process);

                                                                IF Level = Level::Header THEN BEGIN
                                                                  "Min. Deviation From Rcpt. %" := 0;
                                                                  "Min. Deviation From Rcpt." := 0;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Level;
                                                   OptionCaptionML=ENU=Header,Line;
                                                   OptionString=Header,Line }
    { 50  ;   ;Job No. OLD         ;Boolean       ;CaptionML=ENU=Job No. OLD }
    { 60  ;   ;Service Order OLD   ;Boolean       ;CaptionML=ENU=Service Order OLD }
    { 70  ;   ;G/L Account Filter OLD;Code100     ;TableRelation="G/L Account".No.;
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=G/L Account Filter OLD }
    { 80  ;   ;Shortcut Dimension 1 Code OLD;Code20;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=ENU=Shortcut Dimension 1 Code OLD }
    { 90  ;   ;Vendor OLD          ;Code20        ;TableRelation=Vendor.No.;
                                                   CaptionML=ENU=Vendor OLD }
    { 100 ;   ;Shortcut Dimension 2 Code OLD;Code20;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2),
                                                                                               Cost Type=FILTER(<Revenue));
                                                   CaptionML=ENU=Shortcut Dimension 2 Code OLD }
    { 110 ;   ;Shortcut Dimension 3 Code OLD;Code20;
                                                   CaptionML=ENU=Shortcut Dimension 3 Code OLD;
                                                   CaptionClass='1,2,3' }
    { 120 ;   ;Shortcut Dimension 4 Code OLD;Code20;
                                                   CaptionML=ENU=Shortcut Dimension 4 Code OLD;
                                                   CaptionClass='1,2,4' }
    { 130 ;   ;Shortcut Dimension 5 Code OLD;Code20;
                                                   CaptionML=ENU=Shortcut Dimension 5 Code OLD;
                                                   CaptionClass='1,2,5' }
    { 140 ;   ;Shortcut Dimension 6 Code OLD;Code20;
                                                   CaptionML=ENU=Shortcut Dimension 6 Code OLD;
                                                   CaptionClass='1,2,6' }
    { 150 ;   ;Shortcut Dimension 7 Code OLD;Code20;
                                                   CaptionML=ENU=Shortcut Dimension 7 Code OLD;
                                                   CaptionClass='1,2,7' }
    { 160 ;   ;Shortcut Dimension 8 Code OLD;Code20;
                                                   CaptionML=ENU=Shortcut Dimension 8 Code OLD;
                                                   CaptionClass='1,2,8' }
    { 170 ;   ;Min. Deviation From Rcpt. %;Decimal;OnValidate=BEGIN
                                                                IF "Min. Deviation From Rcpt. %" <> 0 THEN
                                                                  TESTFIELD(Level, Level::Line);
                                                              END;

                                                   CaptionML=ENU=Min. Deviation From Rcpt. % }
    { 180 ;   ;Min. Deviation From Rcpt.;Decimal  ;OnValidate=BEGIN
                                                                IF "Min. Deviation From Rcpt." <> 0 THEN
                                                                  TESTFIELD(Level, Level::Line);
                                                              END;

                                                   CaptionML=ENU=Min. Deviation From Rcpt. }
    { 185 ;   ;Consent Deviation if No Rcpt.;Boolean;
                                                   CaptionML=ENU=Consent Deviation if No Rcpt. }
    { 190 ;   ;Minimum Amount      ;Decimal       ;CaptionML=ENU=Minimum Amount }
    { 200 ;   ;Maximum Amount      ;Decimal       ;CaptionML=ENU=Maximum Amount }
    { 210 ;   ;Consenter           ;Code50        ;TableRelation=Consenter."User ID";
                                                   OnValidate=BEGIN
                                                                IF Consenter <> '' THEN BEGIN
                                                                  TESTFIELD("Project Role", 0);
                                                                  TESTFIELD("Service Role", 0);
                                                                  TESTFIELD("Consenter Group Code", '');
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Consenter }
    { 220 ;   ;Super Consenter     ;Code50        ;TableRelation=Consenter."User ID" WHERE (Super Consenter=CONST(Yes));
                                                   CaptionML=ENU=Super Consenter }
    { 230 ;   ;Location OLD        ;Code10        ;TableRelation=Location.Code;
                                                   CaptionML=ENU=Location OLD }
    { 240 ;   ;Project Role        ;Option        ;OnValidate=BEGIN
                                                                IF "Project Role" > 0 THEN BEGIN
                                                                  TESTFIELD(Consenter, '');
                                                                  TESTFIELD("Consenter Group Code", '');
                                                                  TESTFIELD("Service Role", 0);
                                                                  TESTFIELD(Type, Type::Project);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Project Role;
                                                   OptionCaptionML=ENU=" ,Project Manager,Project Engineer,Purchaser,Site Manager,Administrator,Estimator";
                                                   OptionString=[ ,Project Manager,Project Engineer,Purchaser,Site Manager,Administrator,Estimator];
                                                   BlankZero=Yes }
    { 245 ;   ;Service Role        ;Option        ;OnValidate=BEGIN
                                                                IF "Service Role" > 0 THEN BEGIN
                                                                  TESTFIELD(Consenter, '');
                                                                  TESTFIELD("Consenter Group Code", '');
                                                                  TESTFIELD("Project Role", 0);
                                                                  TESTFIELD(Type, Type::Service);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Service Role;
                                                   OptionCaptionML=ENU=" ,Order Manager,Assigned to,Employee,Location Manager,Contract Manager";
                                                   OptionString=[ ,Order Manager,Assigned to,Employee,Location Manager,Contract Manager];
                                                   BlankZero=Yes }
    { 250 ;   ;Filters Active      ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Consent Rule Filter" WHERE (Consent Rule Line No.=FIELD(Line No.)));
                                                   CaptionML=ENU=Filters Active;
                                                   Editable=No }
    { 260 ;   ;Consenter Group Code;Code10        ;TableRelation="Consenter Group".Code;
                                                   OnValidate=BEGIN
                                                                IF "Consenter Group Code" <> '' THEN BEGIN
                                                                  TESTFIELD("Project Role", 0);
                                                                  TESTFIELD("Service Role", 0);
                                                                  TESTFIELD(Consenter, '');
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Consenter Group Code }
    { 270 ;   ;No Consent          ;Boolean       ;CaptionML=ENU=No Consent }
    { 280 ;   ;Autom. Consent Own Lines;Boolean   ;CaptionML=ENU=Autom. Consent Own Lines }
    { 290 ;   ;Min. Deviation From Budget %;Decimal;
                                                   OnValidate=BEGIN
                                                                IF ("Min. Deviation From Budget %" <> 0) AND
                                                                   NOT (Process IN [Process::"Purchase Order", Process::"Purchase Quote"])
                                                                THEN
                                                                  FIELDERROR(Process);
                                                              END;

                                                   CaptionML=ENU=Min. Deviation From Budget % }
    { 300 ;   ;Min. Deviation From Budget;Decimal ;OnValidate=BEGIN
                                                                IF ("Min. Deviation From Budget" <> 0) AND
                                                                   NOT (Process IN [Process::"Purchase Order", Process::"Purchase Quote"])
                                                                THEN
                                                                  FIELDERROR(Process);
                                                              END;

                                                   CaptionML=ENU=Min. Deviation From Budget }
    { 310 ;   ;Consent Deviation if No Budget;Boolean;
                                                   CaptionML=ENU=Consent Deviation if No Budget }
  }
  KEYS
  {
    {    ;Line No.                                ;Clustered=Yes }
    {    ;Consent Level                            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetSuperConsenter@1100528405() SuperConsenter : Code[50];
    VAR
      ConsentRule@1100528401 : Record 11012227;
      PurchasesPayablesSetup@1100528402 : Record 312;
    BEGIN
      IF "Super Consenter" <> '' THEN
        SuperConsenter := "Super Consenter"
      ELSE BEGIN
        PurchasesPayablesSetup.GET;
        PurchasesPayablesSetup.TESTFIELD("Super Consenter Denied Lines");
        SuperConsenter := PurchasesPayablesSetup."Super Consenter Denied Lines";
      END;
    END;

    PROCEDURE GetLastLineNo@1100528400() : Integer;
    VAR
      ConsentRule@1100528400 : Record 11012227;
    BEGIN
      IF ConsentRule.FINDLAST THEN
        EXIT(ConsentRule."Line No.");
    END;

    PROCEDURE ShowConsentRuleFilters@1100528401(ReadOnly@1100528400 : Boolean);
    VAR
      ConsentRuleFilter@1100528401 : Record 11229793;
      ConsentRuleFilters@1100528402 : Page 11126513;
    BEGIN
      ConsentRuleFilter.SETRANGE("Consent Rule Line No.", "Line No.");
      IF ReadOnly THEN
        ConsentRuleFilters.EDITABLE(FALSE);
      ConsentRuleFilters.SETTABLEVIEW(ConsentRuleFilter);
      ConsentRuleFilters.RUNMODAL;
    END;

    BEGIN
    END.
  }
}

