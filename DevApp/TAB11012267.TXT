OBJECT Table 11012267 Progress Level (PTP)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ProgressLinePTE@1100525000 : Record 11012268;
               ProgressLevel@1210190000 : Record 11012036;
             BEGIN
               //kzwerver, 110530, sn, #C-028879
               IF (ProgressLevel.GET("Project No.", "Progress Date")) THEN
                 ProgressLevel.DELETE(TRUE);
               //kzwerver, 110530, en, #C-028879
               ProgressLinePTE.SETRANGE("Project No.","Project No.");
               ProgressLinePTE.SETRANGE("Progress Date","Progress Date");
               ProgressLinePTE.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=ENU=Progress Level - Project Tender Paragraph;
    LookupPageID=Page11020561;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job WHERE (Progress on base of Tender=CONST(Yes));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Project Description");
                                                              END;

                                                   CaptionML=ENU=Project No.;
                                                   NotBlank=Yes }
    { 20  ;   ;Progress Date       ;Date          ;CaptionML=ENU=Progress Date;
                                                   NotBlank=Yes }
    { 30  ;   ;Comment             ;Text50        ;CaptionML=ENU=Comment }
    { 40  ;   ;Project Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=ENU=Project Description;
                                                   Editable=No }
    { 50  ;   ;Text                ;Text250       ;CaptionML=ENU=Text }
    { 60  ;   ;Technical Finished  ;Boolean       ;CaptionML=ENU=Technical Finished }
  }
  KEYS
  {
    {    ;Project No.,Progress Date               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@11012002 : TextConst 'ENU=Renaming %1 not possible. Copy %1 and delete the old %1.';

    PROCEDURE CreateLines@1100525000();
    VAR
      ProjectTenderParagraph@1100525001 : Record 11012263;
      ProgressLinePTP@1100525000 : Record 11012268;
    BEGIN
      ProjectTenderParagraph.SETRANGE("Project No.","Project No.");
      ProjectTenderParagraph.SETRANGE(Level,ProjectTenderParagraph.Level::"Tender Paragraph");
      IF ProjectTenderParagraph.FINDSET THEN
        REPEAT
          ProgressLinePTP.SETRANGE("Project No.","Project No.");
          ProgressLinePTP.SETRANGE("Progress Date","Progress Date");
          ProgressLinePTP.SETRANGE("Tender Paragraph",ProjectTenderParagraph."Tender Paragraph");
          IF ProgressLinePTP.ISEMPTY THEN BEGIN
            ProgressLinePTP.INIT;
            ProgressLinePTP."Project No." := "Project No.";
            ProgressLinePTP.VALIDATE("Progress Date","Progress Date");
            ProgressLinePTP."Tender Paragraph" := ProjectTenderParagraph."Tender Paragraph";
            ProgressLinePTP.VALIDATE(Description,ProjectTenderParagraph.Description);
            ProgressLinePTP."Description 2" := ProjectTenderParagraph."Description 2";
            ProgressLinePTP.INSERT;
          END;
        UNTIL ProjectTenderParagraph.NEXT = 0;
    END;

    PROCEDURE CreateProgress@1100525001();
    VAR
      ProgressLevel@1100525000 : Record 11012036;
    BEGIN
      ProgressLevel.INIT;
      ProgressLevel."Project No." := "Project No.";
      ProgressLevel."Progress Date" := "Progress Date";
      ProgressLevel.Comment := Comment;
      ProgressLevel.Text := Text;
      ProgressLevel."Technical Finished" := "Technical Finished";
      ProgressLevel."Project Description" := "Project Description";
      ProgressLevel.FillCreated();
      ProgressLevel.INSERT;
      CODEUNIT.RUN(CODEUNIT::"Create Progress Lines",ProgressLevel);
    END;

    PROCEDURE CreateLinesFromInstallMotLines@1100525002(VAR InstallmentMotivationLine@1100525000 : Record 11012472);
    VAR
      ProgressLinePTE@1100525001 : Record 11012268;
      ProjectTenderElement@1100525002 : Record 11012263;
      Text001@1100525003 : TextConst 'ENU=Project Tender Paragraph %1 for job %2 does not exists in table %3. You can only create progress lines if there are corresponding Project Tender Paragraphs in table %3.';
      Text002@1100525004 : TextConst 'ENU=Quantity and/or unit of measure in Project Tender Paragraph %1 for job %2 does correspondent with Quantity and/or unit of measure in table %3. You can only create progress lines if there are corresponding Project Tender Paragraphs in table %3 has the same quantity and unit of measure.';
    BEGIN
      //check related project tender paragraphs
      IF InstallmentMotivationLine.FINDSET THEN
        REPEAT
          IF NOT ProjectTenderElement.GET("Project No.",InstallmentMotivationLine."Tender Paragraph No.") THEN BEGIN
            MESSAGE(STRSUBSTNO(Text001,
              InstallmentMotivationLine."Tender Paragraph No.",
              "Project No.",
              ProjectTenderElement.TABLECAPTION));
            EXIT;
          END ELSE BEGIN
            InstallmentMotivationLine.CALCFIELDS("Element Quantity",Unit);
            IF (ProjectTenderElement.Quantity <> InstallmentMotivationLine."Element Quantity") OR
              (ProjectTenderElement."Unit of Measure Code" <> InstallmentMotivationLine.Unit)
            THEN BEGIN
              MESSAGE(STRSUBSTNO(Text002,
                InstallmentMotivationLine."Tender Paragraph No.",
                "Project No.",
                ProjectTenderElement.TABLECAPTION));
              EXIT;
            END;
          END;
        UNTIL InstallmentMotivationLine.NEXT = 0;

      //if all related project tender paragraphs are correct insert progress lines
      IF InstallmentMotivationLine.FINDSET THEN
        REPEAT
          ProgressLinePTE.INIT;
          ProgressLinePTE."Project No." := "Project No.";
          ProgressLinePTE."Progress Date" := "Progress Date";
          ProgressLinePTE."Tender Paragraph" := InstallmentMotivationLine."Tender Paragraph No.";
          ProgressLinePTE.VALIDATE("Progress in Period",InstallmentMotivationLine.Production);
          ProgressLinePTE.Description := InstallmentMotivationLine.Description;
          IF ProgressLinePTE.INSERT(TRUE) THEN;
        UNTIL InstallmentMotivationLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

