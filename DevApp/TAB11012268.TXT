OBJECT Table 11012268 Progress Line (PTP)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               Job@1100525000 : Record 11072003;
             BEGIN
               Job.GET("Project No.");
               "Progress Registry" := Job."Progress Registry (PTE)";
               "Main Project No." := Job."Main Project";
             END;

    CaptionML=ENU=Progress Line - Project Tender Paragraph;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   CaptionML=ENU=Project No.;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Progress Date       ;Date          ;TableRelation="Progress Level (PTP)"."Progress Date" WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=ENU=Progress Date;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 30  ;   ;Tender Paragraph    ;Text10        ;TableRelation="Standard Tender Paragraph";
                                                   AltSearchField=Search Name;
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Tender Paragraph;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 40  ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                IF ("Search Name" = UPPERCASE(COPYSTR(xRec.Description, 1, MAXSTRLEN("Search Name")))) OR ("Search Name" = '') THEN
                                                                  "Search Name" := COPYSTR(Description, 1, MAXSTRLEN("Search Name"));
                                                              END;

                                                   CaptionML=ENU=Description;
                                                   Editable=No }
    { 45  ;   ;Description 2       ;Text50        ;CaptionML=ENU=Description 2;
                                                   Editable=No }
    { 50  ;   ;Search Name         ;Code30        ;CaptionML=ENU=Search Name;
                                                   Editable=No }
    { 70  ;   ;Progress Quantity   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Tender Paragraph".Quantity WHERE (Project No.=FIELD(Project No.),
                                                                                                                 Tender Paragraph=FIELD(Tender Paragraph)));
                                                   CaptionML=ENU=Progress Quantity;
                                                   Editable=No }
    { 75  ;   ;Unit of Measure Code;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Tender Paragraph"."Unit of Measure Code" WHERE (Project No.=FIELD(Project No.),
                                                                                                                               Tender Paragraph=FIELD(Tender Paragraph)));
                                                   CaptionML=ENU=Unit of Measure Code;
                                                   Editable=No }
    { 100 ;   ;Previous Posture    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Progress Line (PTP)"."Progress in Period" WHERE (Project No.=FIELD(Project No.),
                                                                                                                     Progress Date=FIELD(Prev. Progress Date Filter),
                                                                                                                     Tender Paragraph=FIELD(Tender Paragraph)));
                                                   CaptionML=ENU=Previous Posture;
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 101 ;   ;Cumulative Progress ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Progress Line (PTP)"."Progress in Period" WHERE (Project No.=FIELD(Project No.),
                                                                                                                     Progress Date=FIELD(Date Filter),
                                                                                                                     Tender Paragraph=FIELD(Tender Paragraph)));
                                                   CaptionML=ENU=Cumulative Progress;
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 102 ;   ;Progress in Period  ;Decimal       ;OnValidate=BEGIN
                                                                CALCFIELDS("Previous Posture");
                                                                "Progress till Period" := "Progress in Period" + "Previous Posture";
                                                                CheckProgress(FIELDCAPTION("Progress in Period"));
                                                                CalculateProgressPercentages;
                                                              END;

                                                   CaptionML=ENU=Progress in Period;
                                                   DecimalPlaces=2:5 }
    { 103 ;   ;Progress till Period;Decimal       ;OnValidate=BEGIN
                                                                CALCFIELDS("Previous Posture");
                                                                "Progress in Period" := "Progress till Period" - "Previous Posture";
                                                                CheckProgress(FIELDCAPTION("Progress till Period"));
                                                                CalculateProgressPercentages;
                                                              END;

                                                   CaptionML=ENU=Progress till Period }
    { 110 ;   ;Prev. Cumulative Progr.%;Decimal   ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Progress Line (PTP)"."Progress % in Period" WHERE (Project No.=FIELD(Project No.),
                                                                                                                       Progress Date=FIELD(Prev. Progress Date Filter),
                                                                                                                       Tender Paragraph=FIELD(Tender Paragraph)));
                                                   CaptionML=ENU=Prev.Cumulative Progr.%;
                                                   Editable=No }
    { 111 ;   ;Cumulative Progress %;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Progress Line (PTP)"."Progress % in Period" WHERE (Project No.=FIELD(Project No.),
                                                                                                                       Progress Date=FIELD(Date Filter),
                                                                                                                       Tender Paragraph=FIELD(Tender Paragraph)));
                                                   CaptionML=ENU=Cumulative Progress %;
                                                   Editable=No }
    { 112 ;   ;Progress % in Period;Decimal       ;OnValidate=BEGIN
                                                                CALCFIELDS("Prev. Cumulative Progr.%");
                                                                "Progress % till Period" := "Progress % in Period" + "Prev. Cumulative Progr.%";
                                                                CheckProgress(FIELDCAPTION("Progress % in Period"));
                                                                CalculateProgress;
                                                              END;

                                                   CaptionML=ENU=Progress % in Period }
    { 113 ;   ;Progress % till Period;Decimal     ;OnValidate=BEGIN
                                                                CALCFIELDS("Prev. Cumulative Progr.%");
                                                                "Progress % in Period" := "Progress % till Period" - "Prev. Cumulative Progr.%";
                                                                CheckProgress(FIELDCAPTION("Progress % till Period"));
                                                                CalculateProgress;
                                                              END;

                                                   CaptionML=ENU=Progress % till Period }
    { 130 ;   ;Progress Registry   ;Option        ;CaptionML=ENU=Progress Registry;
                                                   OptionCaptionML=ENU=Quantities,Percentages;
                                                   OptionString=Quantities,Percentages;
                                                   Editable=No }
    { 140 ;   ;Previous Date       ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Max("Progress Line (PTP)"."Progress Date" WHERE (Project No.=FIELD(Project No.),
                                                                                                                Progress Date=FIELD(UPPERLIMIT(FILTER(Prev. Progress Date Filter))),
                                                                                                                Tender Paragraph=FIELD(Tender Paragraph)));
                                                   CaptionML=ENU=Previous Date;
                                                   Editable=No }
    { 150 ;   ;Main Project No.    ;Code20        ;TableRelation=Job;
                                                   CaptionML=ENU=Main Project No. }
    { 200 ;   ;Prev. Progress Date Filter;Date    ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Prev. Progress Date Filter }
    { 201 ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter }
  }
  KEYS
  {
    {    ;Project No.,Progress Date,Tender Paragraph;
                                                   SumIndexFields=Progress % in Period,Progress in Period;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE ProgressQuantity@4() : Decimal;
    BEGIN
      CALCFIELDS("Progress Quantity");
      EXIT("Progress Quantity");
    END;

    PROCEDURE CalculateProgressPercentages@10();
    BEGIN
      IF ProgressQuantity <> 0 THEN
        "Progress % in Period" := ("Progress in Period" / ProgressQuantity) * 100
      ELSE
        "Progress % in Period" := 0;

      CALCFIELDS("Prev. Cumulative Progr.%");
      "Progress % till Period" := "Progress % in Period" + "Prev. Cumulative Progr.%";
    END;

    PROCEDURE CalculateProgress@9();
    BEGIN
      "Progress in Period" := ("Progress % in Period" / 100) * ProgressQuantity;
      CALCFIELDS("Previous Posture");
      "Progress till Period" := "Progress in Period" + "Previous Posture";
    END;

    PROCEDURE CheckProgress@11(FieldName@1100525001 : Text[100]);
    VAR
      JobsSetup@1100525000 : Record 315;
      Text001@1100525003 : TextConst 'ENU=%1 may not exceed 100%';
      Text002@1100525002 : TextConst 'ENU=%1 may not exceed Progress Quantity (%2)';
      Text003@1100525004 : TextConst 'ENU=%1 may not exceed Progress Quantity (%2)';
    BEGIN
      IF CurrFieldNo = 0 THEN
        EXIT;

      JobsSetup.GET;
      IF JobsSetup."Progress Range" = JobsSetup."Progress Range"::"No Progress Max. Allowed Max 100 %" THEN
        EXIT;

      IF JobsSetup."Progress Range" = JobsSetup."Progress Range"::"No Progress or Allowed Max." THEN //mg, 21-07-10, C-025906
        EXIT;

      CALCFIELDS("Previous Posture");
      CALCFIELDS("Prev. Cumulative Progr.%");
      CASE FieldName OF
        FIELDCAPTION("Progress in Period"):
          IF ("Progress in Period" + "Previous Posture") > ProgressQuantity THEN
            ERROR(Text003,FIELDCAPTION("Progress in Period"),ProgressQuantity - "Previous Posture");
        FIELDCAPTION("Progress till Period"):
          IF "Progress till Period" > ProgressQuantity THEN
            ERROR(Text002,FIELDCAPTION("Progress till Period"),ProgressQuantity);
        FIELDCAPTION("Progress % in Period"):
          IF ("Progress % in Period" + "Prev. Cumulative Progr.%") > 100 THEN
            ERROR(Text003,FIELDCAPTION("Progress % in Period"),100 - "Prev. Cumulative Progr.%");
        FIELDCAPTION("Progress % till Period"):
          IF "Progress % till Period" > 100 THEN
            ERROR(Text001,FIELDCAPTION("Progress % till Period"));
      END;
    END;

    PROCEDURE CopyPreviousProgress@1100525001();
    VAR
      ProgressLinePTE@1100525000 : Record 11012268;
    BEGIN
      ProgressLinePTE.SETRANGE("Project No.","Project No.");
      ProgressLinePTE.SETRANGE("Progress Date","Progress Date");
      IF ProgressLinePTE.FINDSET(TRUE,FALSE) THEN
        REPEAT
          IF ProgressLinePTE."Progress Registry" = ProgressLinePTE."Progress Registry"::Quantities THEN BEGIN
            ProgressLinePTE.CALCFIELDS("Previous Posture");
            ProgressLinePTE.VALIDATE("Progress till Period",ProgressLinePTE."Previous Posture");
          END;
          IF ProgressLinePTE."Progress Registry" = ProgressLinePTE."Progress Registry"::Percentages THEN BEGIN
            ProgressLinePTE.CALCFIELDS("Prev. Cumulative Progr.%");
            ProgressLinePTE.VALIDATE("Progress % till Period",ProgressLinePTE."Prev. Cumulative Progr.%");
          END;
          ProgressLinePTE.MODIFY;
        UNTIL ProgressLinePTE.NEXT = 0;
    END;

    PROCEDURE CopyInstallmentMotivationLines@1100525000();
    VAR
      Job@1100525003 : Record 11072003;
      ProgressLevelPTE@1100525002 : Record 11012267;
      InstallmentMotivationLine@1100525001 : Record 11012472;
      InstallmentMotivationLines@1100525000 : Page 11020562;
    BEGIN
      Job.GET("Project No.");
      Job.TESTFIELD("Progress from Instal. Progress");
      ProgressLevelPTE.GET("Project No.","Progress Date");
      InstallmentMotivationLines.SetProgressLevelPTE(ProgressLevelPTE);
      InstallmentMotivationLine.SETRANGE("Project No.","Project No.");
      InstallmentMotivationLine.SETFILTER("Production Date",'..%1',"Progress Date");
      IF InstallmentMotivationLine.FINDLAST THEN
        InstallmentMotivationLine.SETRANGE("Production Date",InstallmentMotivationLine."Production Date")
      ELSE BEGIN
        InstallmentMotivationLine.SETFILTER("Production Date",'>%1',"Progress Date");
        IF InstallmentMotivationLine.FINDFIRST THEN
          InstallmentMotivationLine.SETRANGE("Production Date",InstallmentMotivationLine."Production Date");
      END;
      InstallmentMotivationLines.SETTABLEVIEW(InstallmentMotivationLine);
      InstallmentMotivationLines.LOOKUPMODE(TRUE);
      InstallmentMotivationLines.RUNMODAL;
    END;

    BEGIN
    END.
  }
}

