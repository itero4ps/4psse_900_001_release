OBJECT Table 11012312 Discount Term History
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ValidateDiscountType;

               IF CompRelationRec.READPERMISSION THEN BEGIN
                 IF "Discount Level" = "Discount Level"::Company THEN
                   CentrManagementCU.EditDiscHist(Rec,1,xRec);
                 IF "Discount Level" = "Discount Level"::Customer THEN BEGIN
                   InvtSetup.GET;
                   IF InvtSetup."Discount Term Groups" THEN
                     CentrManagementCU.EditDiscHist(Rec,1,xRec);
                 END;
               END;
             END;

    OnModify=BEGIN
               ValidateDiscountType;

               IF CompRelationRec.READPERMISSION THEN BEGIN
                 IF "Discount Level" = "Discount Level"::Company THEN
                   CentrManagementCU.EditDiscHist(Rec,2,xRec);
                 IF "Discount Level" = "Discount Level"::Customer THEN BEGIN
                   InvtSetup.GET;
                   IF InvtSetup."Discount Term Groups" THEN
                     CentrManagementCU.EditDiscHist(Rec,2,xRec);
                 END;
               END;
             END;

    OnDelete=BEGIN
               IF CompRelationRec.READPERMISSION THEN BEGIN
                 IF "Discount Level" = "Discount Level"::Company THEN
                   CentrManagementCU.EditDiscHist(Rec,4,xRec);
                 IF "Discount Level" = "Discount Level"::Customer THEN BEGIN
                   InvtSetup.GET;
                   IF InvtSetup."Discount Term Groups" THEN
                     CentrManagementCU.EditDiscHist(Rec,4,xRec);
                 END;
               END;
             END;

    OnRename=BEGIN
               ValidateDiscountType;
             END;

    CaptionML=ENU=Discount Term History;
    LookupPageID=Page11020421;
  }
  FIELDS
  {
    { 1   ;   ;Vendor              ;Code15        ;TableRelation="ETIM Vendor (Central Mgt)".Code;
                                                   CaptionML=ENU=Vendor }
    { 2   ;   ;Discount Group      ;Code35        ;OnValidate=VAR
                                                                DiscHistRec@1100525000 : Record 11012312;
                                                              BEGIN
                                                                IF xRec."Discount Group" <> "Discount Group" THEN BEGIN  //db, 09-10-09: M16469
                                                                  DiscHistRec := xRec;
                                                                  IF DiscHistRec.FIND THEN
                                                                    CentrManagementCU.EditDiscHist(Rec,3,xRec);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Discount Group }
    { 3   ;   ;Description         ;Text100       ;CaptionML=ENU=Description }
    { 4   ;   ;Discount Percentage 1;Decimal      ;CaptionML=ENU=Discount Percentage 1;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 5   ;   ;Discount Percentage 2;Decimal      ;CaptionML=ENU=Discount Percentage 2;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 6   ;   ;Discount Percentage 3;Decimal      ;CaptionML=ENU=Discount Percentage 3;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 7   ;   ;Item Code           ;Code20        ;TableRelation="Trade Item"."Item Code" WHERE (Vendor=FIELD(Vendor));
                                                   OnValidate=VAR
                                                                DiscHistRec@1100525000 : Record 11012312;
                                                              BEGIN
                                                                IF xRec."Item Code" <> "Item Code" THEN BEGIN  //db, 09-10-09: M16469
                                                                  DiscHistRec := xRec;
                                                                  IF DiscHistRec.FIND THEN
                                                                    CentrManagementCU.EditDiscHist(Rec,3,xRec);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Item Code }
    { 8   ;   ;Purchase Price      ;Decimal       ;CaptionML=ENU=Purchase Price;
                                                   AutoFormatType=2 }
    { 9   ;   ;Vendor Name         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ETIM Vendor (Central Mgt)".Name WHERE (Code=FIELD(Vendor)));
                                                   CaptionML=ENU=Vendor Name;
                                                   Editable=No }
    { 10  ;   ;Currency            ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=ENU=Currency }
    { 11  ;   ;Sales Discount Group;Code20        ;TableRelation="Item Discount Group";
                                                   CaptionML=ENU=Sales Discount Group }
    { 12  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD("Starting Date");
                                                              END;

                                                   CaptionML=ENU=Starting Date }
    { 15  ;   ;Ending Date         ;Date          ;CaptionML=ENU=Ending Date }
    { 20  ;   ;Maximum % Price Difference;Decimal ;CaptionML=ENU=Maximum % Price Difference }
    { 100 ;   ;Discount Level      ;Option        ;CaptionML=ENU=Discount Level;
                                                   OptionCaptionML=ENU=Company,Customer,Job;
                                                   OptionString=Company,Customer,Job }
    { 110 ;   ;Discount Reference  ;Code20        ;TableRelation="Discount Term Group".Code WHERE (Level=FIELD(Discount Level));
                                                   CaptionML=ENU=Discount Reference }
    { 120 ;   ;Sales Discount Percentage;Decimal  ;CaptionML=ENU=Sales Discount Percentage;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 130 ;   ;Sales Price         ;Decimal       ;CaptionML=ENU=Sales Price;
                                                   AutoFormatType=2 }
    { 140 ;   ;Delivery Group Present;Boolean     ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Delivery Group" WHERE (Vendor=FIELD(Vendor),
                                                                                             Code=FIELD(Discount Group)));
                                                   CaptionML=ENU=Delivery Group Present;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Vendor,Discount Group,Item Code,Discount Level,Discount Reference,Starting Date;
                                                   Clustered=Yes }
    {    ;Discount Level,Discount Reference,Vendor,Discount Group,Item Code,Starting Date }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CentrManagementCU@1100485001 : Codeunit 11012010;
      CompRelationRec@1100485000 : Record 11012059;
      InvtSetup@1100485002 : Record 313;

    PROCEDURE GetPreviousData@1100485001(VAR DiscHistRec@1100485000 : Record 11012312;StartDate@1100485002 : Date);
    VAR
      DiscHistRec2@1100485001 : Record 11012312;
    BEGIN
      //db, 10-07-08:
      //used in dataport to get default (as cost object, sales discount group) from previous import
      WITH DiscHistRec DO BEGIN
        DiscHistRec2.COPY(DiscHistRec);
        DiscHistRec2.SETFILTER("Starting Date", '<%1', StartDate);
        IF DiscHistRec2.FINDLAST THEN
          DiscHistRec := DiscHistRec2
        ELSE
          DiscHistRec.INIT;
      END;
    END;

    PROCEDURE GetPurchaseDiscount@1100525000() PurchDisc : Decimal;
    BEGIN
      PurchDisc := "Discount Percentage 1";
      IF "Discount Percentage 2" <> 0 THEN
        PurchDisc := PurchDisc + ((100 - PurchDisc) * ("Discount Percentage 2" / 100));
      IF "Discount Percentage 3" <> 0 THEN
        PurchDisc := PurchDisc + ((100 - PurchDisc) * ("Discount Percentage 3" / 100));

      EXIT(PurchDisc)
    END;

    PROCEDURE ValidateDiscountType@1100525001();
    BEGIN
      //db, 10-08-09: M10666
      IF "Discount Group" <> '' THEN
        TESTFIELD("Item Code", '');
      IF "Item Code" <> '' THEN
        TESTFIELD("Discount Group", '');

      TESTFIELD("Starting Date");

      IF "Discount Level" <> "Discount Level"::Company THEN BEGIN
        //DP00065.so
        //TESTFIELD("Cost Object", '');
        //TESTFIELD("Cost Object (Removal)", '');
        //DP00065.eo
        TESTFIELD("Sales Discount Group", '');
        TESTFIELD("Maximum % Price Difference", 0);
      END;
    END;

    PROCEDURE CheckChangeAllowed@1100525004();
    VAR
      ChangeAllowed@1100525001 : Boolean;
      lvText001@1100525002 : TextConst 'ENU=Insert, change or delete not allowed due to central management.';
      LeadingCompany@1210190000 : Text[50];
      CompRelationRec@1210190001 : Record 11012059;
    BEGIN
      //db, 25-02-10
      ChangeAllowed := TRUE;
      InvtSetup.GET;
      //X016.sn
      LeadingCompany := CompRelationRec.GetLeadingCompany(DATABASE::"Inventory Setup",InvtSetup."Central Management");
      IF LeadingCompany <> COMPANYNAME THEN BEGIN
      //X016.en
        CASE "Discount Level" OF
          "Discount Level"::Company: ChangeAllowed := (InvtSetup."Discount Terms" = FALSE);
          "Discount Level"::Customer: ChangeAllowed := (InvtSetup."Discount Term Groups" = FALSE);
        END;
      END;
      IF ChangeAllowed = FALSE THEN
        ERROR(lvText001);
    END;

    BEGIN
    {
      X016: Multiple central management companies for 1 operating company (db, 22-08-11)

      db, 26-09-14: C016571 (MinValue DiscountPercentage disabled; allowed as surcharge)
    }
    END.
  }
}

