OBJECT Table 11012341 INSBOU Despatch Advice
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               INSBOUDespatchAdviceLine@1100528300 : Record 11012342;
               INSBOUDespatchAdviceItem@1100528301 : Record 11012343;
             BEGIN
               IF ("Process Status" = "Process Status"::Processing) THEN
                 FIELDERROR("Process Status");

               INSBOUDespatchAdviceLine.SETRANGE("Vendor No.", "Vendor No.");
               INSBOUDespatchAdviceLine.SETRANGE(MessageNumber, MessageNumber);
               INSBOUDespatchAdviceLine.DELETEALL(TRUE);

               INSBOUDespatchAdviceItem.SETRANGE("Vendor No.", "Vendor No.");
               INSBOUDespatchAdviceItem.SETRANGE(MessageNumber, MessageNumber);
               INSBOUDespatchAdviceItem.DELETEALL(TRUE);
             END;

    CaptionML=ENU=INSBOU Despatch Advice;
  }
  FIELDS
  {
    { 10  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=ENU=Vendor No.;
                                                   NotBlank=Yes }
    { 11  ;   ;Vendor Name         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=ENU=Vendor Name;
                                                   Editable=No }
    { 20  ;   ;MessageNumber       ;Code17        ;CaptionML=ENU=Message Number;
                                                   NotBlank=Yes }
    { 30  ;   ;MessageDate         ;Date          ;CaptionML=ENU=Message Date;
                                                   NotBlank=Yes }
    { 40  ;   ;MessageStructure    ;Code17        ;CaptionML=ENU=Message Structure }
    { 50  ;   ;EndCustomerOrderNumber;Code20      ;CaptionML=ENU=End Customer Order Number }
    { 60  ;   ;FreeText            ;Text70        ;CaptionML=ENU=FreeText }
    { 70  ;   ;BuyersOrderNumber   ;Code17        ;CaptionML=ENU=Buyers Order Number }
    { 80  ;   ;ProjectNumber       ;Code17        ;CaptionML=ENU=Project Number }
    { 90  ;   ;OrderResponseNumber ;Code17        ;CaptionML=ENU=Order Response Number }
    { 110 ;   ;ScheduledDeliveryDate;Date         ;CaptionML=ENU=Scheduled Delivery Date }
    { 120 ;   ;ScheduledDeliveryTime;Time         ;CaptionML=ENU=Scheduled Delivery Time }
    { 130 ;   ;DeliveryDateEarliest;Date          ;CaptionML=ENU=Delivery Date Earliest }
    { 140 ;   ;DeliveryTimeEarliest;Time          ;CaptionML=ENU=Delivery Time Earliest }
    { 150 ;   ;DeliveryDateLatest  ;Date          ;CaptionML=ENU=Delivery Date Latest }
    { 160 ;   ;DeliveryTimeLatest  ;Time          ;CaptionML=ENU=Delivery Time Latest }
    { 180 ;   ;SupplierGLN         ;Code13        ;CaptionML=ENU=GLN }
    { 190 ;   ;SupplierName        ;Text50        ;CaptionML=ENU=Name }
    { 200 ;   ;SupplierName2       ;Text50        ;CaptionML=ENU=Name2 }
    { 210 ;   ;SupplierStreetAndNumber;Text35     ;CaptionML=ENU=Street And Number }
    { 220 ;   ;SupplierCity        ;Text35        ;CaptionML=ENU=City }
    { 230 ;   ;SupplierPostalCode  ;Code9         ;CaptionML=ENU=Postal Code }
    { 240 ;   ;SupplierCountry     ;Code2         ;CaptionML=ENU=Country }
    { 260 ;   ;SupplierContactPersonName;Text35   ;CaptionML=ENU=Contact Person Name }
    { 270 ;   ;SupplierPhoneNumber ;Code20        ;CaptionML=ENU=Phone Number }
    { 290 ;   ;SupplierCertificateName;Text35     ;CaptionML=ENU=Certificate Name }
    { 300 ;   ;SupplierCertificateNumber;Code35   ;CaptionML=ENU=Certificate Number }
    { 310 ;   ;SupplierCertificateIssuer;Text35   ;CaptionML=ENU=Certificate Issuer }
    { 320 ;   ;SupplierCertificateSubNumber;Code35;CaptionML=ENU=Certificate Sub Number }
    { 330 ;   ;SupplierCertificateText;Text250    ;CaptionML=ENU=Certificate Text }
    { 340 ;   ;SupplierCertificateDate;Date       ;CaptionML=ENU=Certificate Date }
    { 350 ;   ;SupplCertificateExpirationDate;Date;CaptionML=ENU=Certificate Expiration Date }
    { 370 ;   ;BuyerGLN            ;Code13        ;CaptionML=ENU=GLN }
    { 380 ;   ;BuyerName           ;Text50        ;CaptionML=ENU=Name }
    { 390 ;   ;BuyerName2          ;Text50        ;CaptionML=ENU=Name2 }
    { 400 ;   ;BuyerStreetAndNumber;Text35        ;CaptionML=ENU=Street And Number }
    { 410 ;   ;BuyerCity           ;Text35        ;CaptionML=ENU=City }
    { 420 ;   ;BuyerPostalCode     ;Code9         ;CaptionML=ENU=Postal Code }
    { 430 ;   ;BuyerCountry        ;Code2         ;CaptionML=ENU=Country }
    { 450 ;   ;BuyerCertificateName;Text35        ;CaptionML=ENU=Certificate Name }
    { 460 ;   ;BuyerCertificateNumber;Code35      ;CaptionML=ENU=Certificate Number }
    { 470 ;   ;BuyerCertificateIssuer;Text35      ;CaptionML=ENU=Certificate Issuer }
    { 480 ;   ;BuyerCertificateSubNumber;Code35   ;CaptionML=ENU=Certificate Sub Number }
    { 490 ;   ;BuyerCertificateText;Text250       ;CaptionML=ENU=Certificate Text }
    { 500 ;   ;BuyerCertificateDate;Date          ;CaptionML=ENU=Certificate Date }
    { 510 ;   ;BuyerCertificateExpirationDate;Date;CaptionML=ENU=Certificate Expiration Date }
    { 530 ;   ;ShipFromGLN         ;Code13        ;CaptionML=ENU=GLN }
    { 540 ;   ;ShipFromName        ;Text50        ;CaptionML=ENU=Name }
    { 550 ;   ;ShipFromName2       ;Text50        ;CaptionML=ENU=Name2 }
    { 560 ;   ;ShipFromStreetAndNumber;Text35     ;CaptionML=ENU=Street And Number }
    { 570 ;   ;ShipFromCity        ;Text35        ;CaptionML=ENU=City }
    { 580 ;   ;ShipFromPostalCode  ;Code9         ;CaptionML=ENU=Postal Code }
    { 590 ;   ;ShipFromCountry     ;Code2         ;CaptionML=ENU=Country }
    { 610 ;   ;DeliveryPartyGLN    ;Code13        ;CaptionML=ENU=GLN }
    { 620 ;   ;DeliveryPartyName   ;Text50        ;CaptionML=ENU=Name }
    { 630 ;   ;DeliveryPartyName2  ;Text50        ;CaptionML=ENU=Name2 }
    { 640 ;   ;DeliveryPartyStreetAndNumber;Text35;CaptionML=ENU=Street And Number }
    { 650 ;   ;DeliveryPartyCity   ;Text35        ;CaptionML=ENU=City }
    { 660 ;   ;DeliveryPartyPostalCode;Code9      ;CaptionML=ENU=Postal Code }
    { 670 ;   ;DeliveryPartyCountry;Code2         ;CaptionML=ENU=Country }
    { 690 ;   ;UltimateConsigneeGLN;Code13        ;CaptionML=ENU=GLN }
    { 700 ;   ;UltimateConsigneeName;Text50       ;CaptionML=ENU=Name }
    { 710 ;   ;UltimateConsigneeName2;Text50      ;CaptionML=ENU=Name2 }
    { 720 ;   ;UltimateConsigStreetAndNumber;Text35;
                                                   CaptionML=ENU=Street And Number }
    { 730 ;   ;UltimateConsigneeCity;Text35       ;CaptionML=ENU=City }
    { 740 ;   ;UltimateConsigneePostalCode;Code9  ;CaptionML=ENU=Postal Code }
    { 750 ;   ;UltimateConsigneeCountry;Code2     ;CaptionML=ENU=Country }
    { 1000;   ;Process Status      ;Option        ;CaptionML=ENU=Process Status;
                                                   OptionCaptionML=ENU=Received,,,Processing,,,Processed;
                                                   OptionString=Received,,,Processing,,,Processed;
                                                   Editable=No }
    { 1010;   ;Message Id          ;Text50        ;CaptionML=ENU=Message Id;
                                                   Editable=No }
    { 1020;   ;Error Message       ;Text250       ;CaptionML=ENU=Error Message;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Vendor No.,MessageNumber                ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528301 : TextConst 'ENU=%1 %2 already exsists.';
      Text001@1100528300 : TextConst 'ENU=There are no %1s or more than 1 %1s found for %2 %3.';

    PROCEDURE ProcessDespatchAdvices@1100528300(VAR INSBOUDespatchAdvice@1100528300 : Record 11012341);
    VAR
      INSBOUOrderResponse@1100528301 : Codeunit 11072078;
    BEGIN
      //RFFU kz
      //  INSBOUDespatchAdviceCU.RUN(INSBOUDespatchAdvice);
      ERROR('Not implemented, Contact your Application Administrator');
    END;

    PROCEDURE CreatePurchaseOrder@1100528301(VAR INSBOUDespatchAdvice@1100528301 : Record 11012341);
    VAR
      PurchaseHeader@1100528300 : Record 38;
      PurchasesPayablesSetup@1100525000 : Record 312;
      NoSeriesManagement@1100525001 : Codeunit 396;
    BEGIN
      IF (NOT INSBOUDespatchAdvice.FINDSET) THEN
        EXIT;

      REPEAT
        IF (INSBOUDespatchAdvice.BuyersOrderNumber = '') THEN BEGIN
          PurchasesPayablesSetup.GET;
          INSBOUDespatchAdvice.BuyersOrderNumber := NoSeriesManagement.GetNextNo(PurchasesPayablesSetup."Order Nos.", TODAY, TRUE);
          INSBOUDespatchAdvice.MODIFY;
        END;
        INSBOUDespatchAdvice.TESTFIELD(SupplierGLN);
        IF (PurchaseHeader.GET(INSBOUDespatchAdvice.DetermineDocumentType, INSBOUDespatchAdvice.BuyersOrderNumber)) THEN
          ERROR(Text000, PurchaseHeader.TABLECAPTION, INSBOUDespatchAdvice.BuyersOrderNumber);

        PurchaseHeader.INIT;
        PurchaseHeader."Document Type" := INSBOUDespatchAdvice.DetermineDocumentType;
        PurchaseHeader."No." := INSBOUDespatchAdvice.BuyersOrderNumber;
        PurchaseHeader.VALIDATE("Buy-from Vendor No.", GetVendorNo(INSBOUDespatchAdvice.SupplierGLN));
        PurchaseHeader.InitRecord;
        PurchaseHeader.INSERT(TRUE);

        PurchaseHeader."Job No." := INSBOUDespatchAdvice.ProjectNumber;
      //  PurchaseHeader."Vendor Order No." := INSBOUDespatchAdvice.OrderResponsNumber;
      //  PurchaseHeader."Vendor Shipment No." := INSBOUDespatchAdvice.OrderResponsNumber;

        PurchaseHeader."Ship-to Name" := INSBOUDespatchAdvice.DeliveryPartyName;
        PurchaseHeader."Ship-to Name 2" := INSBOUDespatchAdvice.DeliveryPartyName2;
        PurchaseHeader."Ship-to Address" := INSBOUDespatchAdvice.DeliveryPartyStreetAndNumber;
        PurchaseHeader."Ship-to City" := INSBOUDespatchAdvice.DeliveryPartyCity;

      //  PurchaseHeader."Order Date" := INSBOUDespatchAdvice.OrderResponseDate;
      //  PurchaseHeader."Expected Receipt Date" := INSBOUDespatchAdvice.OrderResponseDate;
      //  PurchaseHeader."Due Date" := INSBOUDespatchAdvice.OrderResponseDate;
      //  PurchaseHeader."Document Date" := INSBOUDespatchAdvice.OrderResponseDate;
      //  PurchaseHeader."Posting Date" := INSBOUPurchaseOrderResponse.OrderResponseDate;
        PurchaseHeader.MODIFY(TRUE);

        IF (INSBOUDespatchAdvice.FreeText <> '') THEN
          CreateTextLines(INSBOUDespatchAdvice);

        CreatePurchaseLines(INSBOUDespatchAdvice, PurchaseHeader);
      UNTIL (INSBOUDespatchAdvice.NEXT = 0);
    END;

    LOCAL PROCEDURE CreatePurchaseLines@1100528306(VAR INSBOUDespatchAdvice@1100528306 : Record 11012341;PurchaseHeader@1100528300 : Record 38);
    VAR
      PurchaseLine@1100528301 : Record 39;
      Vendor@1100528304 : Record 23;
      GeneralLedgerSetup@1100528305 : Record 98;
      INSBOUDespatchAdviceLine@1100525000 : Record 11012342;
      INSBOUDespatchAdviceItem@1100525001 : Record 11012343;
    BEGIN
      INSBOUDespatchAdviceItem.SETRANGE("Vendor No.", INSBOUDespatchAdvice."Vendor No.");
      INSBOUDespatchAdviceItem.SETRANGE(MessageNumber, INSBOUDespatchAdvice.MessageNumber);
      IF (NOT INSBOUDespatchAdviceItem.FINDSET) THEN
        EXIT;

      Vendor.GET(PurchaseHeader."Buy-from Vendor No.");
      GeneralLedgerSetup.GET;
      REPEAT
        INSBOUDespatchAdviceItem.TESTFIELD(OrderLineIdentification);
        INSBOUDespatchAdviceItem.TESTFIELD(TradeItemGTIN);
        INSBOUDespatchAdviceLine.GET(PurchaseHeader."Buy-from Vendor No.", INSBOUDespatchAdviceItem.MessageNumber,
          INSBOUDespatchAdviceItem.LineNumber);

        PurchaseLine.INIT;
        PurchaseLine."Document Type" := PurchaseHeader."Document Type";
        PurchaseLine."Document No." := PurchaseHeader."No.";
        PurchaseLine."Line No." := INSBOUDespatchAdviceItem.OrderLineIdentification;
        PurchaseLine.INSERT(TRUE);

        PurchaseLine.VALIDATE("Job No.", PurchaseHeader."Job No.");
        PurchaseLine.VALIDATE("Vendor (Trade Item)", Vendor."Vendor (Trade Item)");
        PurchaseLine.VALIDATE("Trade Item", GetTradeItem(Vendor."Vendor (Trade Item)", INSBOUDespatchAdviceItem));
        PurchaseLine.VALIDATE(Quantity, INSBOUDespatchAdviceLine.NumberOfDespatchUnits);
        PurchaseLine.VALIDATE("Unit of Measure", INSBOUDespatchAdviceLine.GTINDespatchUnitType);

        PurchaseLine.MODIFY(TRUE);
      UNTIL (INSBOUDespatchAdviceItem.NEXT = 0);
    END;

    LOCAL PROCEDURE CreateTextLines@1100528308(INSBOUDespatchAdvice@1100528301 : Record 11012341);
    VAR
      PurchaseText@1100528300 : Record 11012067;
      LastLineNo@1100528302 : Integer;
    BEGIN
      LastLineNo := 0;
      PurchaseText.SETRANGE("Line Type", PurchaseText."Line Type"::"Purchase Order");
      PurchaseText.SETRANGE("No.", INSBOUDespatchAdvice.BuyersOrderNumber);
      PurchaseText.SETRANGE("Purchase Action", '');
      IF (PurchaseText.FINDLAST) THEN
        LastLineNo := PurchaseText."Line No.";

      PurchaseText.RESET;
      PurchaseText.INIT;
      PurchaseText."Line Type" := PurchaseText."Line Type"::"Purchase Order";
      PurchaseText."No." := INSBOUDespatchAdvice.BuyersOrderNumber;
      PurchaseText."Line No." := LastLineNo + 10000;
      PurchaseText.Text := COPYSTR(INSBOUDespatchAdvice.FreeText, 1, MAXSTRLEN(PurchaseText.Text));
      PurchaseText."Line Break" := TRUE;
      PurchaseText.INSERT(TRUE);
    END;

    LOCAL PROCEDURE GetVendorNo@1100528302(GLN@1100528300 : Code[13]) : Code[20];
    VAR
      Vendor@1100528301 : Record 23;
    BEGIN
      Vendor.SETRANGE("GLN Code", GLN);
      IF (Vendor.COUNT <> 1) THEN
        ERROR(Text001, Vendor.TABLECAPTION, Vendor."GLN Code", GLN);
      Vendor.FINDFIRST;
      Vendor.TESTFIELD("Vendor (Trade Item)");
      EXIT(Vendor."No.");
    END;

    LOCAL PROCEDURE GetTradeItem@1100528309(VendorNo@1100528302 : Code[20];INSBOUDespatchAdviceItem@1100528300 : Record 11012343) : Code[20];
    VAR
      TradeItem@1100528301 : Record 11012317;
    BEGIN
      TradeItem.SETRANGE(Vendor, VendorNo);
      TradeItem.SETRANGE("GTIN Code (Item)", INSBOUDespatchAdviceItem.TradeItemGTIN);
      IF (TradeItem.COUNT = 0) THEN
        CreateTradeItem(VendorNo, INSBOUDespatchAdviceItem);
      IF (TradeItem.COUNT <> 1) THEN
        ERROR(Text001, TradeItem.TABLECAPTION, TradeItem."GTIN Code (Item)", INSBOUDespatchAdviceItem.TradeItemGTIN);
      TradeItem.FINDFIRST;
      EXIT(TradeItem."Item Code");
    END;

    PROCEDURE PurchaseHeaderPresent@1100528303() : Boolean;
    VAR
      PurchaseHeader@1100528300 : Record 38;
    BEGIN
      EXIT(PurchaseHeader.GET(DetermineDocumentType, BuyersOrderNumber));
    END;

    PROCEDURE DetermineDocumentType@1100528305() : Integer;
    VAR
      PurchaseHeader@1100528300 : Record 38;
    BEGIN
      TESTFIELD(BuyersOrderNumber);
      PurchaseHeader.SETRANGE("No.", BuyersOrderNumber);
      IF (PurchaseHeader.COUNT <> 1) THEN BEGIN
        EXIT(PurchaseHeader."Document Type"::Order);
      END;
      PurchaseHeader.FINDFIRST;
      EXIT(PurchaseHeader."Document Type");
    END;

    LOCAL PROCEDURE CreateTradeItem@1100525000(VendorNo@1100525001 : Code[20];INSBOUDespatchAdviceItem@1100525002 : Record 11012343);
    VAR
      ItemSetup@1100525003 : Record 11012300;
      TradeItem@1100525000 : Record 11012317;
      "2baManagement"@1100525004 : Codeunit 11012306;
    BEGIN
      IF (TradeItem.GET(VendorNo, INSBOUDespatchAdviceItem.SupplierTradeItemIdentificat)) THEN
        EXIT;

      ItemSetup.GET;
      TradeItem.INIT;
      TradeItem.Vendor := VendorNo;
      TradeItem."Item Code" := INSBOUDespatchAdviceItem.SupplierTradeItemIdentificat;
      TradeItem.INSERT;

      TradeItem."GTIN Code (Item)" := INSBOUDespatchAdviceItem.TradeItemGTIN;
      TradeItem."Item Description" := COPYSTR(INSBOUDespatchAdviceItem.TradeItemDescription, 1, MAXSTRLEN(TradeItem."Item Description"));
      CASE ItemSetup."Adjust Item Description" OF
        ItemSetup."Adjust Item Description"::ToUpper: BEGIN
          TradeItem."Item Description" := UPPERCASE(TradeItem."Item Description");
        END;
        ItemSetup."Adjust Item Description"::ToLower: BEGIN
          TradeItem."Item Description" := LOWERCASE(TradeItem."Item Description");
        END;
      END;
      IF TradeItem."Search Argument" = '' THEN
        TradeItem."Search Argument" := COPYSTR(TradeItem."Item Description", 1, MAXSTRLEN(TradeItem."Search Argument"));

      TradeItem."Application Unit Quantity" := 1;
      TradeItem."Starting Date Price" := TODAY;
      TradeItem."Gross Price" := 0;
      TradeItem."Quantity Per Price" := 1;

      IF (TradeItem."Cost Object" = '') OR (NOT TradeItem."Cost Object changed Manually") THEN
        TradeItem."Cost Object" := TradeItem.GetDefaultCostObject(FALSE);
      TradeItem.Expired := FALSE;
      TradeItem.MODIFY;

      "2baManagement".UpdatePriceHistory(TradeItem);
    END;

    BEGIN
    END.
  }
}

