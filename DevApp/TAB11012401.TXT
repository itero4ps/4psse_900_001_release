OBJECT Table 11012401 Document Template
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lDocTemplRec@1210190000 : Record 11012401;
               DocumentDataItem@1199197401 : Record 11012140;
               DocumentDataItemLink@1199197400 : Record 11012141;
               FilterText@1100485000 : Text[50];
             BEGIN

               FilterText := '@';
               FilterText := FilterText + Rec."Prefix File Name";
               lDocTemplRec.SETFILTER("Prefix File Name",FilterText);

               lDocTemplRec.SETRANGE("Document Group","Document Group");
               lDocTemplRec.SETFILTER("Template Code",'<>%1',"Template Code");
               lDocTemplRec.SETRANGE(Type,Type);
               IF lDocTemplRec.FIND('-') THEN
                 ERROR(Text002,UPPERCASE("Prefix File Name"),Type);
               lDocTemplRec.SETRANGE(Type);
               IF lDocTemplRec.FIND('-') THEN
                 MESSAGE(Text001,UPPERCASE("Prefix File Name"));

               //create the basic tables (call 21883)
               DocumentDataItem."Document Group" := "Document Group";
               DocumentDataItem.Document := "Template Code";
               DocumentDataItem."Line No." := 10000;
               DocumentDataItem."Table No." := DATABASE::"Document Properties";
               DocumentDataItem.INSERT(TRUE);
               DocumentDataItem."Line No." := 15000;
               DocumentDataItem."Table No." := DATABASE::"Document Link";
               DocumentDataItem.INSERT(TRUE);

               DocumentDataItemLink."Document Group" := "Document Group";
               DocumentDataItemLink.Document := "Template Code";
               DocumentDataItemLink."Data Item Line No." := 15000;
               DocumentDataItemLink."From Table No." := DATABASE::"Document Link";
               DocumentDataItemLink."To Table No." := DATABASE::"Document Properties";
               DocumentDataItemLink."Field No." := 10;
               DocumentDataItemLink."Reference Field No." := 1;
               DocumentDataItemLink.INSERT(TRUE);
             END;

    OnModify=BEGIN
               "Modified by" := USERID;
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               PermissionRec@1210190000 : Record 11012404;
               lDocTableRec@1210190004 : Record 11012140;
               lDocLinkRec@1210190003 : Record 11012141;
               lDocFilterRec@1210190002 : Record 11012143;
               lDocFieldRec@1210190001 : Record 11012142;
             BEGIN
               PermissionRec.SETRANGE("Document Group","Document Group");
               PermissionRec.SETRANGE("Template Code","Template Code");
               PermissionRec.DELETEALL;

               lDocFieldRec.SETRANGE("Document Group", "Document Group");
               lDocFieldRec.SETRANGE(Document, "Template Code");
               lDocFieldRec.DELETEALL;

               lDocFilterRec.SETRANGE("Document Group", "Document Group");
               lDocFilterRec.SETRANGE(Document, "Template Code");
               lDocFilterRec.DELETEALL;

               lDocLinkRec.SETRANGE("Document Group", "Document Group");
               lDocLinkRec.SETRANGE(Document, "Template Code");
               lDocLinkRec.DELETEALL;

               lDocTableRec.SETRANGE("Document Group", "Document Group");
               lDocTableRec.SETRANGE(Document, "Template Code");
               lDocTableRec.DELETEALL;
             END;

    OnRename=VAR
               DocumentProperties@1100525000 : Record 11012746;
             BEGIN
               //**4PS01.n
               DocumentProperties.HandleRenameofDocumentTemplate(COMPANYNAME,xRec."Document Group",
                 xRec."Template Code","Document Group","Template Code");

               "Modified by" := USERID;
               "Last Date Modified" := TODAY;
             END;

    CaptionML=ENU=Document Template;
    LookupPageID=Page11012401;
    DrillDownPageID=Page11012401;
  }
  FIELDS
  {
    { 1   ;   ;Document Group      ;Code10        ;TableRelation="Document Group";
                                                   CaptionML=ENU=Document Group;
                                                   NotBlank=Yes }
    { 2   ;   ;Template Code       ;Code20        ;CaptionML=ENU=Template Code;
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 4   ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Congratulation Letter,Invitation Meeting,Extension Contract,Delivery Form,Invoice,Option List,Miscellaneous,Purchase Order,Credit Memo,Try-out Quote,Purchase Quote,KOMO Inspection Form,KOMO Finished Form,Blanket Order,Plant,Service Contract,Sales Quote,Sales Blanket Order,Sales Order,Try-Out,Service Order,Option Quote,Complaint,Assignment,Service Location,Estimate,Installment,Tender Application,Delivered Object,Job,Contact,LT. Maint. Plan,Rental Package,Plant Quote;
                                                   OptionString=Congratulation Letter,Invitation Meeting,Extension Contract,Delivery Form,Invoice,Option List,Miscellaneous,Purchase Order,Credit Memo,Try-out Quote,Purchase Quote,KOMO Inspection,KOMO Ready,Blanket Order,Plant,Service Contract,S-Quote,S-Blanket Order,S-Order,Try-Out,Service Order,Option Quote,Complaint,Assignment,Service Location,Estimate,Installment,TenderApplication,Delivered Object,Job,Contact,LT. Maint. Plan,Rental Package,Plant Quote }
    { 5   ;   ;Word Template       ;Text250       ;CaptionML=ENU=Word Template }
    { 6   ;   ;Prefix File Name    ;Text20        ;OnValidate=VAR
                                                                lDocTemplRec@1210190000 : Record 11012401;
                                                                FilterText@1100485000 : Text[50];
                                                              BEGIN
                                                                FilterText := '@';
                                                                FilterText := FilterText + Rec."Prefix File Name";
                                                                lDocTemplRec.SETFILTER("Prefix File Name",FilterText);

                                                                lDocTemplRec.SETRANGE("Document Group","Document Group");
                                                                lDocTemplRec.SETFILTER("Template Code",'<>%1',"Template Code");
                                                                lDocTemplRec.SETRANGE(Type,Type);
                                                                IF lDocTemplRec.FIND('-') THEN
                                                                  ERROR(Text002,UPPERCASE("Prefix File Name"),Type);
                                                                lDocTemplRec.SETRANGE(Type);
                                                                IF lDocTemplRec.FIND('-') THEN
                                                                  MESSAGE(Text001,UPPERCASE("Prefix File Name"));
                                                              END;

                                                   CaptionML=ENU=Prefix File Name }
    { 15  ;   ;Document Type       ;Code10        ;TableRelation=IF (Project Portal Subsite=FILTER(<>'')) "Subsite-Doc Type (PPT)"."Document Type" WHERE (Project Portal Subsite=FIELD(Project Portal Subsite))
                                                                 ELSE "Document Type".Type;
                                                   CaptionML=ENU=Document Type }
    { 16  ;   ;Document Type Description;Text30   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=ENU=Document Type Description;
                                                   Editable=No }
    { 18  ;   ;Subject             ;Text100       ;CaptionML=ENU=Subject }
    { 20  ;   ;Use for             ;Option        ;OnValidate=BEGIN
                                                                IF ("Use for" <> xRec."Use for") AND (xRec."Use for" = "Use for"::Document) THEN
                                                                  VALIDATE("Attach to E-Mail as", "Attach to E-Mail as"::Attachment)
                                                                ELSE
                                                                  VALIDATE("Attach to E-Mail as");
                                                              END;

                                                   CaptionML=ENU=Use for;
                                                   OptionCaptionML=ENU=Document,E-Mail,Document and E-Mail;
                                                   OptionString=Document,E-Mail,Document and E-Mail }
    { 25  ;   ;Attach to E-Mail as ;Option        ;OnValidate=BEGIN
                                                                IF "Use for" = "Use for"::Document THEN
                                                                  "Attach to E-Mail as" := "Attach to E-Mail as"::" "
                                                                ELSE
                                                                  TESTFIELD("Attach to E-Mail as");
                                                              END;

                                                   CaptionML=ENU=Attach to E-Mail as;
                                                   OptionCaptionML=ENU=" ,Attachment,Body";
                                                   OptionString=[ ,Attachment,Body] }
    { 26  ;   ;E-Mail Subject      ;Text50        ;CaptionML=ENU=E-Mail Subject }
    { 30  ;   ;Secured Document    ;Option        ;CaptionML=ENU=Secured Document;
                                                   OptionCaptionML=ENU=Not,Always,Both;
                                                   OptionString=Not,Always,Both }
    { 40  ;   ;Secured Document Directory;Text250 ;OnValidate=VAR
                                                                FileMgt@1100525000 : Codeunit 419;
                                                              BEGIN
                                                                FileMgt.AddBackSlashToDirectoryName("Secured Document Directory");
                                                              END;

                                                   CaptionML=ENU=Secured Document Directory }
    { 50  ;   ;Search Directory    ;BLOB          ;CaptionML=ENU=Search Directory;
                                                   SubType=User-Defined }
    { 60  ;   ;Project Mandatory   ;Option        ;CaptionML=ENU=Project Mandatory;
                                                   OptionCaptionML=ENU=" ,Mandatory,Warning";
                                                   OptionString=[ ,Mandatory,Warning] }
    { 70  ;   ;Modified by         ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Modified by;
                                                   Editable=No }
    { 80  ;   ;Last Date Modified  ;Date          ;CaptionML=ENU=Last Date Modified;
                                                   Editable=No }
    { 90  ;   ;Comment             ;Text100       ;CaptionML=ENU=Comment }
    { 185 ;   ;Library Code        ;Code10        ;TableRelation="SharePoint Library";
                                                   CaptionML=ENU=Library Code }
    { 186 ;   ;Library Name        ;Text20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("SharePoint Library".Name WHERE (Code=FIELD(Library Code)));
                                                   CaptionML=ENU=Library Name;
                                                   Editable=No }
    { 190 ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  MarketingSetup.GET;
                                                                  NoSeriesRelationship.GET(MarketingSetup."Document Nos.","No. Series")
                                                                END;
                                                              END;

                                                   CaptionML=ENU=No. Series }
    { 300 ;   ;Publish on Service Portal;Boolean  ;CaptionML=ENU=Publish on Service Portal }
    { 310 ;   ;Publish on Subcont.Portal;Boolean  ;CaptionML=ENU=Publish on  Subcontractor Portal }
    { 320 ;   ;Available in Field Service;Boolean ;CaptionML=ENU=Available in Field Service }
    { 325 ;   ;Project Portal Subsite Type;Option ;CaptionML=ENU=Document Portal Subsite Type;
                                                   OptionCaptionML=ENU=Project,Service Location;
                                                   OptionString=Project,ServLoc }
    { 330 ;   ;Project Portal Subsite;Code20      ;TableRelation="Project Portal Subsite".Code WHERE (Type=FIELD(Project Portal Subsite Type));
                                                   CaptionML=ENU=Document Portal Subsite }
    { 331 ;   ;Project Portal Subsite Descr;Text30;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Portal Subsite".Description WHERE (Code=FIELD(Project Portal Subsite)));
                                                   CaptionML=ENU=Document Portal Subsite Description;
                                                   Editable=No }
    { 340 ;   ;Private Company Document;Boolean   ;CaptionML=ENU=Private Company Document }
    { 500 ;   ;D3 Document Type    ;Text30        ;CaptionML=ENU=D3 Document Type }
    { 501 ;   ;D3 Document Link Name ID;Integer   ;CaptionML=ENU=D3 Document Link Name ID }
    { 502 ;   ;D3 Document Link Key ID;Integer    ;CaptionML=ENU=D3 Document Link Key ID }
  }
  KEYS
  {
    {    ;Document Group,Template Code            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1210190000 : TextConst 'ENU=Usage of the same Prefix can lead to loss of documents.';
      Text002@1100485000 : TextConst 'ENU=Identical Prefix (%1) not allowed within same Document Type (%2)';
      MarketingSetup@1100525000 : Record 5079;
      NoSeriesRelationship@1100525003 : Record 310;

    BEGIN
    {
      **4PS01 Jan ter Harmsel, Call 2094  At Rename also update references in documents.
      That table is on database level and will not be handled automatically.

      4PS02 Jan ter Harmsel, Call 2782  subject field added, this will be transferred to a document from form: 11012405.
    }
    END.
  }
}

