OBJECT Table 11012501 House Model
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ProjectSiteMgt.LogHouseModel(1, Rec); //**4PS.n
             END;

    OnModify=BEGIN
               ProjectSiteMgt.LogHouseModel(2, Rec); //**4PS.n
             END;

    OnDelete=VAR
               lvPlotRec@1100485000 : Record 11012500;
               lvOptionRec@1210190001 : Record 11012502;
               lvBuyerMgtTextRec@1210190000 : Record 11012511;
             BEGIN
               IF ("Project No." <> '') AND (Model <> '') THEN BEGIN
                 lvPlotRec.SETRANGE("Project No.", "Project No.");
                 lvPlotRec.SETRANGE("House Model", Model);
                 IF lvPlotRec.FINDFIRST THEN
                   ERROR(Text004,Model,"Project No.",lvPlotRec."Plot No.")
               END;

               lvOptionRec.SETRANGE("Project No.", "Project No.");
               lvOptionRec.SETRANGE("House Model", Model);
               IF lvOptionRec.FINDFIRST THEN BEGIN
                 IF "Project No." = '' THEN BEGIN
                   ERROR(Text002,Model);
                 END ELSE BEGIN
                   IF lvOptionRec."Plot No." = '' THEN BEGIN
                     ERROR(Text001,Model,"Project No.")
                   END ELSE BEGIN
                     ERROR(Text003,Model,"Project No.",lvOptionRec."Plot No.")
                   END;
                 END;
               END;

               lvBuyerMgtTextRec.SETRANGE(Table, lvBuyerMgtTextRec.Table::"House Model");
               lvBuyerMgtTextRec.SETRANGE("Project No.", "Project No.");
               lvBuyerMgtTextRec.SETRANGE("House Model", Model);
               lvBuyerMgtTextRec.DELETEALL;

               ProjectSiteMgt.LogHouseModel(3, Rec); //**4PS.n
             END;

    CaptionML=ENU=House Model;
    LookupPageID=Page11012501;
  }
  FIELDS
  {
    { 1   ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   OnValidate=VAR
                                                                lProjectRec@1100485000 : Record 11072003;
                                                                lvProjPrinRec@1100485001 : Record 11012005;
                                                              BEGIN
                                                                IF ("Project No." = '') THEN
                                                                  EXIT;

                                                                lProjectRec.GET("Project No.");
                                                                IF lProjectRec."Bill-to Customer No." <> '' THEN BEGIN
                                                                  IF lvProjPrinRec.GET("Project No.", lProjectRec."Bill-to Customer No.") THEN BEGIN
                                                                    IF lvProjPrinRec."VAT Bus. Posting Group" <> '' THEN BEGIN
                                                                      "VAT Bus. Posting Group" := lvProjPrinRec."VAT Bus. Posting Group";
                                                                      EXIT;
                                                                    END;
                                                                  END;
                                                                END;

                                                                "VAT Bus. Posting Group" := lProjectRec."VAT Bus. Posting Group";
                                                              END;

                                                   CaptionML=ENU=Project No. }
    { 10  ;   ;Model               ;Code20        ;TableRelation="House Model".Model WHERE (Project No.=FIELD(Project No.));
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Model;
                                                   NotBlank=Yes }
    { 20  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 30  ;   ;Total Surface       ;Decimal       ;CaptionML=ENU=Total Surface }
    { 40  ;   ;Total Content       ;Decimal       ;CaptionML=ENU=Total Content }
    { 50  ;   ;No. of Bedrooms     ;Integer       ;CaptionML=ENU=No. of Bedrooms }
    { 60  ;   ;No. of Bathrooms    ;Integer       ;CaptionML=ENU=No. of Bathrooms }
    { 70  ;   ;Surface Living      ;Decimal       ;CaptionML=ENU=Surface Living }
    { 80  ;   ;Garage              ;Boolean       ;CaptionML=ENU=Garage }
    { 95  ;   ;House Type          ;Code20        ;TableRelation="House Type";
                                                   CaptionML=ENU=House Type }
    { 100 ;   ;Picture             ;BLOB          ;CaptionML=ENU=Picture;
                                                   SubType=Bitmap }
    { 110 ;   ;Price               ;Decimal       ;CaptionML=ENU=Price;
                                                   AutoFormatType=1 }
    { 120 ;   ;Roof Gradient       ;Decimal       ;CaptionML=ENU=Roof Gradient }
    { 130 ;   ;Main Building Dimensions;Text30    ;CaptionML=ENU=Main Building Dimensions }
    { 140 ;   ;Sub Building Dimensions;Text30     ;CaptionML=ENU=Sub Building Dimensions }
    { 150 ;   ;Detailed Description;Text250       ;CaptionML=ENU=Detailed Description }
    { 160 ;   ;Installment Scheme  ;Code10        ;TableRelation="Installment Scheme".Code;
                                                   CaptionML=ENU=Installment Scheme }
    { 170 ;   ;Number in Project   ;Decimal       ;CaptionML=ENU=Number in Project }
    { 180 ;   ;VAT Bus. Posting Group;Code10      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=ENU=VAT Bus. Posting Group }
    { 190 ;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=ENU=VAT Prod. Posting Group }
    { 200 ;   ;Energy Label        ;Code10        ;TableRelation="Energy Label";
                                                   CaptionML=ENU=Energy Label }
  }
  KEYS
  {
    {    ;Project No.,Model                       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100485000 : TextConst 'ENU=Canot remove House Model %1 from Project %2';
      Text002@1100485001 : TextConst 'ENU=Canot remove Standard House Model %1';
      Text003@1100485002 : TextConst 'ENU=Canot remove House Model %1 from Project %2, Options present at Plot %3';
      Text004@1100485003 : TextConst 'ENU=Delete House Model %1 of Project %2 not allowed, is still present on Plot %3 of the Project.';
      ProjectSiteMgt@1100528300 : Codeunit 11012282;

    PROCEDURE CopyPictureFromStdHouseModel@1100528400();
    VAR
      HouseModel@1100528400 : Record 11012501;
    BEGIN
      TESTFIELD("Project No.");
      HouseModel.SETRANGE("Project No.", '');
      HouseModel.SETRANGE(Model, Model);
      IF HouseModel.FINDFIRST THEN BEGIN
        HouseModel.CALCFIELDS(Picture);
        Picture := HouseModel.Picture;
        MODIFY;
      END;
    END;

    BEGIN
    END.
  }
}

