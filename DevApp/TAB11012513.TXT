OBJECT Table 11012513 Option Quote Header
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 JobSetupRec.GET;
                 JobSetupRec.TESTFIELD("Option Quote Nos.");
                 NoSeriesMgt.InitSeries(JobSetupRec."Option Quote Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    OnModify=BEGIN
               IF ("Project No." <> xRec."Project No.") OR ("Plot No." <> xRec."Plot No.") THEN
                 CheckOptQuoteLinesPresent();
             END;

    OnDelete=VAR
               lvOptionQuoteLinesRec@1210190000 : Record 11012514;
             BEGIN
               lvOptionQuoteLinesRec.SETRANGE("Option Quote No.", "No.");
               lvOptionQuoteLinesRec.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Option Quote Header;
    LookupPageID=Page11012546;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  JobSetupRec.GET;
                                                                  NoSeriesMgt.TestManual(JobSetupRec."Option Quote Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 20  ;   ;Project No.         ;Code20        ;TableRelation=Job.No. WHERE (Housebuilding Project=CONST(Yes));
                                                   OnValidate=BEGIN
                                                                IF ("Project No." <> '') THEN BEGIN
                                                                  ProjRec.GET("Project No.");
                                                                  ProjRec.CheckProjectStatus(0);
                                                                END;
                                                                IF ("Project No." <> xRec."Project No.") THEN
                                                                  CheckOptQuoteLinesPresent();
                                                              END;

                                                   CaptionML=ENU=Project No.;
                                                   NotBlank=Yes }
    { 30  ;   ;Project Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=ENU=Project Description;
                                                   Editable=No }
    { 40  ;   ;Plot No.            ;Code20        ;TableRelation=Plot."Plot No." WHERE (Project No.=FIELD(Project No.));
                                                   OnValidate=BEGIN
                                                                IF ("Plot No." <> xRec."Plot No.") THEN
                                                                  CheckOptQuoteLinesPresent();

                                                                CALCFIELDS("House Model", "Plot Address", "Plot Post Code", "Plot City");
                                                                IF ("Plot No." <> '') AND ("House Model" = '') THEN
                                                                  ERROR(Text001, FIELDCAPTION("House Model"), FIELDCAPTION("Plot No."), "Plot No.");

                                                                IF "Plot No." <> xRec."Plot No." THEN
                                                                  GetPlotData();
                                                              END;

                                                   CaptionML=ENU=Plot No.;
                                                   NotBlank=Yes }
    { 50  ;   ;House Model         ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Plot."House Model" WHERE (Project No.=FIELD(Project No.),
                                                                                                Plot No.=FIELD(Plot No.)));
                                                   CaptionML=ENU=House Model;
                                                   Editable=No }
    { 70  ;   ;Plot Address        ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Plot.Address WHERE (Project No.=FIELD(Project No.),
                                                                                          Plot No.=FIELD(Plot No.)));
                                                   CaptionML=ENU=Plot Address;
                                                   Editable=No }
    { 80  ;   ;Plot Post Code      ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Plot."Post Code" WHERE (Project No.=FIELD(Project No.)));
                                                   TableRelation="Post Code";
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Plot Post Code;
                                                   Editable=No }
    { 90  ;   ;Plot City           ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Plot.City WHERE (Project No.=FIELD(Project No.),
                                                                                       Plot No.=FIELD(Plot No.)));
                                                   CaptionML=ENU=Plot City;
                                                   Editable=No }
    { 100 ;   ;Buyer               ;Code20        ;TableRelation=Contact.No.;
                                                   OnValidate=BEGIN
                                                                IF Buyer <> xRec.Buyer THEN
                                                                  GetContactData();
                                                              END;

                                                   CaptionML=ENU=Buyer }
    { 110 ;   ;Buyer Name          ;Text50        ;CaptionML=ENU=Buyer Name }
    { 120 ;   ;Buyer Name 2        ;Text50        ;CaptionML=ENU=Buyer Name 2 }
    { 130 ;   ;Buyer Address       ;Text50        ;CaptionML=ENU=Buyer Address }
    { 140 ;   ;Buyer Address 2     ;Text50        ;CaptionML=ENU=Buyer Address 2 }
    { 150 ;   ;Buyer Post Code     ;Code20        ;TableRelation=IF (Buyer Country/Region Code=CONST()) "Post Code"
                                                                 ELSE IF (Buyer Country/Region Code=FILTER(<>'')) "Post Code" WHERE (Country/Region Code=FIELD(Buyer Country/Region Code));
                                                   OnValidate=BEGIN
                                                                Postcode.ValidatePostCode(
                                                                  "Buyer City","Buyer Post Code","Buyer County", "Buyer Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Buyer Post Code }
    { 160 ;   ;Buyer City          ;Text30        ;TableRelation=IF (Buyer Country/Region Code=CONST()) "Post Code".City
                                                                 ELSE IF (Buyer Country/Region Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Buyer Country/Region Code));
                                                   OnValidate=BEGIN
                                                                Postcode.ValidateCity(
                                                                  "Buyer City","Buyer Post Code","Buyer County", "Buyer Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Buyer City }
    { 170 ;   ;Buyer Country/Region Code;Code10   ;TableRelation=Country/Region;
                                                   CaptionML=ENU=Buyer Country/Region Code }
    { 175 ;   ;Buyer County        ;Text30        ;CaptionML=ENU=Buyer County }
    { 180 ;   ;Buyer Phone No.     ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Buyer Phone No. }
    { 190 ;   ;Buyer Phone No. 2   ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Buyer Phone No. 2 }
    { 200 ;   ;Buyer Mobile Phone No.;Text30      ;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Buyer Mobile Phone No. }
    { 210 ;   ;Buyer E-Mail        ;Text80        ;CaptionML=ENU=Buyer E-Mail }
    { 220 ;   ;Buyer Mobile Phone No. 2;Text30    ;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Buyer Mobile Phone No. 2 }
    { 230 ;   ;Buyer Phone Work    ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Buyer Phone Work }
    { 240 ;   ;Buyer Phone Work 2  ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Buyer Phone Work 2 }
    { 250 ;   ;Buyer E-Mail 2      ;Text80        ;ExtendedDatatype=E-Mail;
                                                   CaptionML=ENU=Buyer E-Mail 2 }
    { 260 ;   ;Due Date            ;Date          ;CaptionML=ENU=Due Date }
    { 270 ;   ;Document Date       ;Date          ;CaptionML=ENU=Document Date }
    { 280 ;   ;Print Date          ;Date          ;CaptionML=ENU=Print Date }
    { 285 ;   ;Confirmation Date   ;Date          ;CaptionML=ENU=Confirmation Date }
    { 290 ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=No }
    { 300 ;   ;Quote Amount incl. VAT;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Option Quote Line"."Sales Amount incl. VAT" WHERE (Option Quote No.=FIELD(No.),
                                                                                                                       Status=FILTER(<>Expired)));
                                                   CaptionML=ENU=Quote Amount incl. VAT;
                                                   Editable=No }
    { 310 ;   ;Quote Amount excl. VAT;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Option Quote Line"."Sales Amount excl. VAT" WHERE (Option Quote No.=FIELD(No.),
                                                                                                                       Status=FILTER(<>Expired)));
                                                   CaptionML=ENU=Quote Amount excl. VAT;
                                                   Editable=No }
    { 320 ;   ;Comment             ;Boolean       ;CaptionML=ENU=Comment }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Project No.,Plot No.,No.                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      JobSetupRec@1210190001 : Record 315;
      OptionQuoteRec@1210190000 : Record 11012513;
      ProjRec@1100525000 : Record 11072003;
      Postcode@1210190004 : Record 225;
      NoSeriesMgt@1210190002 : Codeunit 396;
      Text000@1100485000 : TextConst 'ENU=Not allowed, there are already quote lines present.';
      Text001@1100485001 : TextConst 'ENU=%1 must be filled for %2 ''%3''.';

    PROCEDURE InitRecord@1210190001();
    BEGIN
      JobSetupRec.GET;
      "Document Date" := TODAY;
      "Due Date" := CALCDATE(JobSetupRec."Default Quote Lead Time", "Document Date");
    END;

    PROCEDURE AssistEdit@2(OldOptionQuoteRec@1210190000 : Record 11012513) : Boolean;
    BEGIN
      WITH OptionQuoteRec DO BEGIN
        OptionQuoteRec := Rec;
        JobSetupRec.GET;
        JobSetupRec.TESTFIELD("Option Quote Nos.");
        IF NoSeriesMgt.SelectSeries(JobSetupRec."Option Quote Nos.",OldOptionQuoteRec."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := OptionQuoteRec;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE CheckOptQuoteLinesPresent@1100485000();
    VAR
      lvOptionQuoteLinesRec@1100485000 : Record 11012514;
    BEGIN
      lvOptionQuoteLinesRec.SETRANGE("Option Quote No.", "No.");
      IF lvOptionQuoteLinesRec.FIND('-') THEN
        ERROR(Text000);
    END;

    PROCEDURE GetPlotData@1210190000();
    VAR
      lvPlotRec@1210190000 : Record 11012500;
    BEGIN
      IF ("Project No." = '') OR ("Plot No." = '') THEN
        EXIT;

      IF NOT lvPlotRec.GET("Project No.","Plot No.") THEN
        EXIT;

      IF lvPlotRec.Buyer <> '' THEN BEGIN
        Buyer := lvPlotRec.Buyer;
        GetContactData();
      END;
    END;

    PROCEDURE GetContactData@1210190002();
    VAR
      lvContRec@1210190000 : Record 5050;
    BEGIN
      IF Buyer = '' THEN
        EXIT;

      IF NOT lvContRec.GET(Buyer) THEN
        EXIT;

      "Buyer Name" := lvContRec.Name;
      "Buyer Name 2" := lvContRec."Name 2";
      "Buyer Address" := lvContRec.Address;
      "Buyer Address 2" := lvContRec."Address 2";
      "Buyer Post Code" := lvContRec."Post Code";
      "Buyer City" := lvContRec.City;
      "Buyer Country/Region Code" := lvContRec."Country/Region Code";
      "Buyer County" := lvContRec.County;
      "Buyer Phone No." := lvContRec."Phone No.";
      "Buyer Phone No. 2" := lvContRec."Phone No. 2";
      "Buyer Mobile Phone No." := lvContRec."Mobile Phone No.";
      "Buyer Mobile Phone No. 2" := lvContRec."Mobile Phone No. 2";
      "Buyer Phone Work" := lvContRec."Phone No. Work";
      "Buyer Phone Work 2" := lvContRec."Phone No. Work 2";
      "Buyer E-Mail" := lvContRec."E-Mail";
      "Buyer E-Mail 2" := lvContRec."E-Mail 2";
    END;

    PROCEDURE EditDocuments@1100528702(ContactNo@1210190003 : Code[20];ContactPersonNo@1210190004 : Code[20];SendMail@1210190002 : Boolean;Recipient@1100528700 : Text[120]);
    VAR
      Job@1210190005 : Record 11072003;
      Plot@1100528701 : Record 11012500;
      DocumentTemplate@1210190001 : Record 11012401;
      DocumentMailing@1210190000 : Codeunit 11012278;
      RecRef@1100525000 : RecordRef;
      DocumentLinkRIDs@1100525001 : ARRAY [5] OF RecordID;
    BEGIN
      RecRef.GETTABLE(Rec);
      DocumentLinkRIDs[1] := RecRef.RECORDID;

      IF Job.GET("Project No.") THEN BEGIN
        RecRef.GETTABLE(Job);
        DocumentLinkRIDs[2] := RecRef.RECORDID;
      END;

      IF Plot.GET("Project No.", "Plot No.") THEN BEGIN
        RecRef.GETTABLE(Plot);
        DocumentLinkRIDs[3] := RecRef.RECORDID;
      END;

      DocumentMailing.EditDocuments(
        STRSUBSTNO('%1', DocumentTemplate.Type::"Option Quote"), DocumentLinkRIDs,
        '', ContactNo, ContactPersonNo, SendMail, Recipient);
    END;

    BEGIN
    END.
  }
}

