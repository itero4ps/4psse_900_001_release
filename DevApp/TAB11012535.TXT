OBJECT Table 11012535 Plant Rent Entry
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Plant Rent Entry;
    LookupPageID=Page11020584;
    DrillDownPageID=Page11020584;
  }
  FIELDS
  {
    { 10  ;   ;Removed             ;Boolean       ;CaptionML=ENU=Removed;
                                                   Editable=No }
    { 20  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=ENU=Vendor No.;
                                                   Editable=No }
    { 30  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   CaptionML=ENU=Plant Type;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 40  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   CaptionML=ENU=Plant No.;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 50  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No.;
                                                   Editable=No }
    { 60  ;   ;Vendor Name         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=ENU=Vendor Name;
                                                   Editable=No }
    { 65  ;   ;Plant No.Description;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number".Description WHERE (Plant Type=FIELD(Plant Type),
                                                                                                        No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Plant Description;
                                                   Editable=No }
    { 70  ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 80  ;   ;Present from        ;Date          ;CaptionML=ENU=Present from;
                                                   Editable=No }
    { 90  ;   ;Removed on          ;Date          ;CaptionML=ENU=Removed on;
                                                   Editable=No }
    { 100 ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter }
    { 110 ;   ;Cum. Qty. Vendor-Plant Type;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Rent Entry".Quantity WHERE (Removed=CONST(No),
                                                                                                      Vendor No.=FIELD(Vendor No.),
                                                                                                      Plant Type=FIELD(Plant Type),
                                                                                                      Present from=FIELD(Date Filter)));
                                                   CaptionML=ENU=Cum. Qty. Vendor-Plant Type;
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 120 ;   ;Cum. Qty. Vendor-Plant No.;Decimal ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Rent Entry".Quantity WHERE (Removed=CONST(No),
                                                                                                      Vendor No.=FIELD(Vendor No.),
                                                                                                      Plant Type=FIELD(Plant Type),
                                                                                                      Plant No.=FIELD(Plant No.),
                                                                                                      Present from=FIELD(Date Filter)));
                                                   CaptionML=ENU=Cum. Qty. Vendor-Plant No.;
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 130 ;   ;Arrival Type        ;Option        ;CaptionML=ENU=Arrival Type;
                                                   OptionCaptionML=ENU=Post Plant,Receipt;
                                                   OptionString=Post Plant,Receipt;
                                                   Editable=No }
    { 131 ;   ;Arrival Document    ;Code20        ;TableRelation=IF (Arrival Type=CONST(Receipt)) "Purch. Rcpt. Header";
                                                   CaptionML=ENU=Arrival Document;
                                                   Editable=No }
    { 132 ;   ;Arrival Line No.    ;Integer       ;TableRelation=IF (Arrival Type=CONST(Receipt)) "Purch. Rcpt. Line"."Line No." WHERE (Document No.=FIELD(Arrival Document));
                                                   CaptionML=ENU=Arrival Line No.;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 140 ;   ;Removal Type        ;Option        ;CaptionML=ENU=Removal Order Type;
                                                   OptionCaptionML=ENU=,Exit Order;
                                                   OptionString=,Exit Order;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 141 ;   ;Removal Document    ;Code20        ;TableRelation=IF (Removal Type=CONST(Exit Order)) "Exit Order";
                                                   CaptionML=ENU=Removal Document;
                                                   Editable=No }
    { 142 ;   ;Removal Line No.    ;Integer       ;TableRelation=IF (Removal Type=CONST(Exit Order)) "Exit Order Line"."Line No." WHERE (Exit-order No.=FIELD(Removal Document));
                                                   CaptionML=ENU=Removal Line No.;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 150 ;   ;Expected Ending Date;Date          ;OnValidate=BEGIN
                                                                IF "Expected Ending Date" = 0D THEN
                                                                  EXIT;

                                                                TESTFIELD(Removed, FALSE);
                                                                IF "Expected Ending Date" < "Present from" THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=ENU=Expected Ending Date }
  }
  KEYS
  {
    {    ;Removed,Vendor No.,Plant Type,Plant No.,Line No.;
                                                   SumIndexFields=Quantity;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Removed,Plant Type,Plant No.,Vendor No.,Line No.;
                                                   SumIndexFields=Quantity }
    {    ;Removed,Plant Type,Plant No.,Vendor No.,Present from;
                                                   SumIndexFields=Quantity }
    {    ;Vendor No.,Plant Type,Plant No.          }
    {    ;Plant Type,Plant No.,Vendor No.          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100485000 : TextConst 'ENU=Expected Ending Date may not be less than ''Present from''.';

    PROCEDURE GetExtRentPeriod@1100485005(IVendorNo@1100485001 : Code[20];IPlantType@1100485000 : Code[20];IPlantNo@1100485002 : Code[10];VAR StartingDate@1100525000 : Date;VAR EndingDate@1100525001 : Date) : Decimal;
    VAR
      lvRentEntryRec@1100485006 : Record 11012535;
    BEGIN
      WITH lvRentEntryRec DO BEGIN
        StartingDate := 0D;
        EndingDate := 0D;

        SETRANGE(Removed, FALSE);
        SETRANGE("Vendor No.", IVendorNo);
        SETRANGE("Plant Type", IPlantType);
        SETRANGE("Plant No.", IPlantNo);
        SETFILTER(Quantity, '>%1', 0);
        IF FIND('-') THEN BEGIN
          StartingDate := "Present from";
          EXIT;  //* Still (partly) present, so do not search further (so EndingDate empty)
        END;

        SETRANGE(Removed, TRUE);
        IF FIND('-') THEN BEGIN
          StartingDate := "Present from";
          EndingDate := "Removed on";
          REPEAT
            IF ("Present from" <> 0D) AND ("Present from" < StartingDate) THEN
              StartingDate := "Present from";
            IF ("Removed on" <> 0D) AND ("Removed on" > EndingDate) THEN
              EndingDate := "Removed on";
          UNTIL NEXT = 0;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

