OBJECT Table 11012544 Rental Discount
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckAll();
               DetermineDiscountLevel();
               IF Level = 0 THEN
                 ERROR(Text000);
             END;

    OnRename=BEGIN
               CheckAll();
               DetermineDiscountLevel();
               IF Level = 0 THEN
                 ERROR(Text000);
             END;

    CaptionML=ENU=Rental Discount;
  }
  FIELDS
  {
    { 10  ;   ;Customer Discount Group;Code20     ;TableRelation="Customer Discount Group";
                                                   OnValidate=BEGIN
                                                                CheckCustomerDicsGroup();
                                                                DetermineDiscountLevel();
                                                              END;

                                                   CaptionML=ENU=Customer Discount Group }
    { 20  ;   ;Location            ;Code20        ;TableRelation="Plant Location";
                                                   OnValidate=BEGIN
                                                                CheckLocation();
                                                                DetermineDiscountLevel();
                                                              END;

                                                   CaptionML=ENU=Location }
    { 30  ;   ;Group Level         ;Code20        ;TableRelation="Grouping Level";
                                                   OnValidate=BEGIN
                                                                CheckPlantGroupLevel();
                                                                DetermineDiscountLevel();
                                                              END;

                                                   CaptionML=ENU=Group Level }
    { 40  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   OnValidate=VAR
                                                                lvPlantNoRec@1100485000 : Record 11012552;
                                                              BEGIN
                                                                CheckPlantType();
                                                                IF "Plant No." <> '' THEN BEGIN
                                                                  IF ("Plant Type" = '') OR (("Plant Type" <> xRec."Plant Type") AND (NOT lvPlantNoRec.GET("Plant Type","Plant No."))) THEN
                                                                    "Plant No." := '';
                                                                END;
                                                                DetermineDiscountLevel();
                                                              END;

                                                   CaptionML=ENU=Plant Type }
    { 50  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   OnValidate=BEGIN
                                                                CheckPlantNo();
                                                                DetermineDiscountLevel();
                                                              END;

                                                   CaptionML=ENU=Plant No. }
    { 60  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                CheckStartEndDate;
                                                              END;

                                                   CaptionML=ENU=Starting Date }
    { 70  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                CheckStartEndDate;
                                                              END;

                                                   CaptionML=ENU=Ending Date }
    { 90  ;   ;Level               ;Option        ;CaptionML=ENU=Level;
                                                   OptionCaptionML=ENU=" ,Group Level,Plant Type,Plant No.,Customer Discount Group,Customer-Group Level,Customer-Plant Type,Customer-Plant No.,Location,Location-Group Level,Location-Plant Type,Location-Plant No.";
                                                   OptionString=[ ,PlantGroup,PlantType,PlantNo,Customer,CustomerPlantGroup,CustomerPlantType,CustomerPlantNo,Location,LocationPlantGroup,LocationPlantType,LocationPlantNo];
                                                   Editable=No }
    { 100 ;   ;Discount %          ;Decimal       ;CaptionML=ENU=Discount %;
                                                   DecimalPlaces=0:5;
                                                   MaxValue=100 }
  }
  KEYS
  {
    {    ;Customer Discount Group,Location,Group Level,Plant Type,Plant No.,Starting Date;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100485000 : TextConst 'ENU=Unknown Discount Level.';
      Text001@1100485001 : TextConst 'ENU=''%1'' and ''%2'' may not be filled both.';
      Text002@1100525000 : TextConst 'ENU=%1 may not lie before the %2';

    PROCEDURE CheckAll@1100485000();
    BEGIN
      CheckCustomerDicsGroup();
      CheckLocation();
      CheckPlantGroupLevel();
      CheckPlantType();
      CheckPlantNo();
    END;

    PROCEDURE CheckCustomerDicsGroup@1100485004();
    BEGIN
      IF ("Customer Discount Group" <> '') AND (Location <> '') THEN
        ERROR(Text001, FIELDCAPTION("Customer Discount Group"), FIELDCAPTION(Location));
    END;

    PROCEDURE CheckLocation@1100485005();
    BEGIN
      IF (Location <> '') AND ("Customer Discount Group" <> '') THEN
        ERROR(Text001, FIELDCAPTION(Location), FIELDCAPTION("Customer Discount Group"));
    END;

    PROCEDURE CheckPlantGroupLevel@1100485006();
    BEGIN
      IF ("Group Level" <> '') AND ("Plant Type" <> '') THEN
        ERROR(Text001, FIELDCAPTION("Group Level"), FIELDCAPTION("Plant Type"));
    END;

    PROCEDURE CheckPlantType@1100485007();
    BEGIN
      IF ("Plant Type" <> '') AND ("Group Level" <> '') THEN
        ERROR(Text001, FIELDCAPTION("Plant Type"), FIELDCAPTION("Group Level"));
    END;

    PROCEDURE CheckPlantNo@1100485008();
    BEGIN
      IF "Plant No." <> '' THEN
        TESTFIELD("Plant Type");
    END;

    PROCEDURE DetermineDiscountLevel@1100485003();
    BEGIN
      CASE TRUE OF
        (Location <> '') AND ("Plant Type" <> '') AND ("Plant No." <> ''):
          Level := Level::LocationPlantNo;
        (Location <> '') AND ("Plant Type" <> '') AND ("Plant No." = ''):
          Level := Level::LocationPlantType;
        (Location <> '') AND ("Group Level" <> ''):
          Level := Level::LocationPlantGroup;
        (Location <> '') :
          Level := Level::Location;
        ("Customer Discount Group" <> '') AND ( "Plant Type" <> '') AND ( "Plant No." <> ''):
          Level := Level::CustomerPlantNo;
        ("Customer Discount Group" <> '') AND ( "Plant Type" <> '') AND ( "Plant No." = ''):
          Level := Level::CustomerPlantType;
        ("Customer Discount Group" <> '') AND ( "Group Level" <> ''):
          Level := Level::CustomerPlantGroup;
        ("Customer Discount Group" <> '') :
          Level := Level::Customer;
        ("Plant Type" <> '') AND ("Plant No." <> ''):
          Level := Level::PlantNo;
        ("Plant Type" <> ''):
          Level := Level::PlantType;
        ("Group Level" <> ''):
          Level := Level::PlantGroup;
        ELSE
          Level := 0;
      END;
    END;

    PROCEDURE CheckStartEndDate@1100525000();
    BEGIN
      IF ("Ending Date" <> 0D) AND ("Ending Date" < "Starting Date") THEN
        ERROR(Text002, FIELDCAPTION("Ending Date"), FIELDCAPTION("Starting Date"));
    END;

    BEGIN
    {
      //* Provisionally not per date
    }
    END.
  }
}

