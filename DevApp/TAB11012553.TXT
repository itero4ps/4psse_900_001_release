OBJECT Table 11012553 Plant Posting Group
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               lvPlantCostCompPostSetupRec@1100485000 : Record 11012570;
               lvPlantPostingSetupRec@1100485001 : Record 11012586;
             BEGIN
               lvPlantCostCompPostSetupRec.SETRANGE("Plant Posting Group Code", Code);
               lvPlantCostCompPostSetupRec.DELETEALL;

               lvPlantPostingSetupRec.SETRANGE("Plant Posting Group", Code);
               lvPlantPostingSetupRec.DELETEALL;
             END;

    CaptionML=ENU=Plant Posting Group;
    LookupPageID=Page11130061;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 11  ;   ;Vendor Posting Group No.;Code20    ;TableRelation="Vendor Posting Group".Code;
                                                   CaptionML=ENU=Vendor Posting Group }
    { 15  ;   ;Description         ;Text30        ;CaptionML=ENU=Description }
    { 70  ;   ;Def. FA Posting Group;Code10       ;TableRelation="FA Posting Group";
                                                   CaptionML=ENU=Def. FA Posting Group }
    { 80  ;   ;PLE Account No.     ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                IF ("PLE Account No." <> '') THEN BEGIN
                                                                  PlantSetupRec.GET;
                                                                  PlantSetupRec.TESTFIELD("PLE Journal Template");

                                                                  IF ("PLE Account No." = "PLE Balance Account No.") THEN
                                                                    ERROR(Text001, FIELDCAPTION("PLE Account No."), FIELDCAPTION("PLE Balance Account No."));
                                                                END;
                                                              END;

                                                   CaptionML=ENU=PLE Account No.;
                                                   Description=PLE=Preceding Ledger Entry / VJP=Voorafgaande JournaalPost }
    { 85  ;   ;PLE Balance Account No.;Code20     ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                IF ("PLE Balance Account No." <> '') THEN BEGIN
                                                                  PlantSetupRec.GET;
                                                                  PlantSetupRec.TESTFIELD("PLE Journal Template");

                                                                  IF ("PLE Balance Account No." = "PLE Account No.") THEN
                                                                    ERROR(Text001, FIELDCAPTION("PLE Balance Account No."), FIELDCAPTION("PLE Account No."));
                                                                END;
                                                              END;

                                                   CaptionML=ENU=PLE Balance Account No. }
  }
  KEYS
  {
    {    ;Code,Vendor Posting Group No.           ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100485000 : TextConst 'ENU=%1 may not be equal to %2';
      PlantSetupRec@1100485001 : Record 11012550;

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@11012000 : Code[20]);
    VAR
      GLAcc@11012001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
      END;
    END;

    PROCEDURE ReadCostAccount@1210190000(CostComponent@1210190000 : Code[10]) : Code[20];
    VAR
      PlantCostCompPostingSetup@1210190001 : Record 11012570;
    BEGIN
      IF PlantCostCompPostingSetup.GET(Code, "Vendor Posting Group No.", CostComponent) THEN
        EXIT(PlantCostCompPostingSetup."Plant Cost Account");
    END;

    PROCEDURE WriteCostAccount@1210190001(CostComponent@1210190000 : Code[10];PlantCostAccount@1210190001 : Code[20]);
    VAR
      PlantCostCompPostingSetup@1210190002 : Record 11012570;
    BEGIN
      IF PlantCostAccount = '' THEN BEGIN
        IF PlantCostCompPostingSetup.GET(Code, "Vendor Posting Group No.", CostComponent) THEN
          PlantCostCompPostingSetup.DELETE;
        EXIT;
      END;

      IF PlantCostCompPostingSetup.GET(Code, "Vendor Posting Group No.", CostComponent) THEN BEGIN
        PlantCostCompPostingSetup.VALIDATE("Plant Cost Account", PlantCostAccount);
        PlantCostCompPostingSetup.MODIFY;
      END ELSE BEGIN
        PlantCostCompPostingSetup."Plant Posting Group Code" := Code;
        PlantCostCompPostingSetup."Vendor Posting Group No." := "Vendor Posting Group No.";
        PlantCostCompPostingSetup."Cost Component" := CostComponent;
        PlantCostCompPostingSetup.VALIDATE("Plant Cost Account", PlantCostAccount);
        PlantCostCompPostingSetup.INSERT;
      END;
    END;

    BEGIN
    END.
  }
}

