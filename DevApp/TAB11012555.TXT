OBJECT Table 11012555 Plant Inventory
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF LocationRec.GET(Location) THEN
                 Depot := LocationRec.Depot;
             END;

    CaptionML=ENU=Plant Inventory;
    PasteIsValid=No;
    LookupPageID=Page11012559;
    DrillDownPageID=Page11012559;
  }
  FIELDS
  {
    { 10  ;   ;Removed             ;Boolean       ;CaptionML=ENU=Removed;
                                                   Editable=No }
    { 20  ;   ;Location            ;Code20        ;TableRelation="Plant Location".Code;
                                                   CaptionML=ENU=Location;
                                                   Editable=No }
    { 30  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   CaptionML=ENU=Plant Type;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 40  ;   ;Plant No.           ;Code10        ;TableRelation=IF (Bulk=CONST(No)) "Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS(Bulk);
                                                                IF Bulk AND ("Plant No." <> '') THEN BEGIN
                                                                  PlantNoRec.SETRANGE("Plant Type","Plant Type");
                                                                  PlantNoRec.SETRANGE("Stock No.","Plant No.");
                                                                  IF NOT PlantNoRec.FIND('-') THEN
                                                                    FIELDERROR("Plant No.");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Plant No.;
                                                   Editable=No }
    { 50  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No.;
                                                   Editable=No }
    { 60  ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 70  ;   ;Present from        ;Date          ;CaptionML=ENU=Present from;
                                                   Editable=No }
    { 75  ;   ;Rental Start Date   ;Date          ;CaptionML=ENU=Rental Start Date;
                                                   Editable=No }
    { 79  ;   ;Expected return on  ;Date          ;OnValidate=BEGIN
                                                                IF ("Expected return on" <> 0D) AND ("Expected return on" < "Present from") THEN
                                                                  "Expected return on" := "Present from";
                                                              END;

                                                   CaptionML=ENU=Expected return on }
    { 80  ;   ;Removed on          ;Date          ;CaptionML=ENU=Removed on;
                                                   Editable=No }
    { 85  ;   ;Rental End Date     ;Date          ;CaptionML=ENU=Rental End Date;
                                                   Editable=No }
    { 90  ;   ;Depot               ;Boolean       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF LocationRec.GET(Location) THEN
                                                                  Depot := LocationRec.Depot;
                                                              END;

                                                   CaptionML=ENU=Depot;
                                                   Editable=No }
    { 95  ;   ;Employee Location   ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Plant Location" WHERE (Code=FIELD(Location),
                                                                                             Employee No.=FILTER(<>'')));
                                                   CaptionML=ENU=Employee Location;
                                                   Editable=No }
    { 100 ;   ;Bulk                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Bulk WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=ENU=Bulk;
                                                   Editable=No }
    { 110 ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter;
                                                   Description=Present from Filter }
    { 120 ;   ;Sold                ;Boolean       ;CaptionML=ENU=Sold;
                                                   Editable=No }
    { 130 ;   ;Sold Filter         ;Boolean       ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Sold Filter }
    { 135 ;   ;Quantity Filter     ;Decimal       ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Quantity Filter }
    { 140 ;   ;Arrival Order Type  ;Option        ;CaptionML=ENU=Arrival Order Type;
                                                   OptionCaptionML=ENU=Receipt,Plant Order,Exit Order,Returned Exit;
                                                   OptionString=Receipt,Plant Order,Exit Order,ReturnedExit;
                                                   Editable=No }
    { 150 ;   ;Arrival Order       ;Code20        ;TableRelation=IF (Arrival Order Type=CONST(Plant Order)) "Plant Order"
                                                                 ELSE IF (Arrival Order Type=CONST(Exit Order)) "Exit Order"
                                                                 ELSE IF (Arrival Order Type=CONST(ReturnedExit)) "Returned Loss Order";
                                                   OnLookup=BEGIN
                                                              LookupArrivalOrder();
                                                            END;

                                                   CaptionML=ENU=Arrival Order;
                                                   Editable=No }
    { 151 ;   ;Arrival Order Line  ;Integer       ;TableRelation=IF (Arrival Order Type=CONST(Plant Order)) "Plant Order Line"."Line No." WHERE (Plant Order No.=FIELD(Arrival Order))
                                                                 ELSE IF (Arrival Order Type=CONST(Exit Order)) "Exit Order Line"."Line No." WHERE (Exit-order No.=FIELD(Arrival Order))
                                                                 ELSE IF (Arrival Order Type=CONST(ReturnedExit)) "Returned Loss Order Line"."Line No." WHERE (Returned Loss Order No.=FIELD(Arrival Order));
                                                   CaptionML=ENU=Arrival Order Line;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 152 ;   ;Arrival Order Line Descr. 1;Text50 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Order Line".Description WHERE (Plant Order No.=FIELD(Arrival Order),
                                                                                                            Line No.=FIELD(Arrival Order Line)));
                                                   CaptionML=ENU=Arrival Order Line Descr. 1;
                                                   Description=(Only Plant Order);
                                                   Editable=No }
    { 153 ;   ;Arrival Order Line Descr. 2;Text50 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Order Line"."Description 2" WHERE (Plant Order No.=FIELD(Arrival Order),
                                                                                                                Line No.=FIELD(Arrival Order Line)));
                                                   CaptionML=ENU=Arrival Order Line Descr. 2;
                                                   Description=(Only Plant Order);
                                                   Editable=No }
    { 155 ;   ;Removal Order Type  ;Option        ;CaptionML=ENU=Removal Order Type;
                                                   OptionCaptionML=ENU=,Plant Order,Exit Order;
                                                   OptionString=,Plant Order,Exit Order;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 160 ;   ;Removal Order       ;Code20        ;TableRelation=IF (Removal Order Type=CONST(Plant Order)) "Plant Order"
                                                                 ELSE IF (Removal Order Type=CONST(Exit Order)) "Exit Order";
                                                   OnLookup=BEGIN
                                                              LookupRemovalOrder();
                                                            END;

                                                   CaptionML=ENU=Removal Order;
                                                   Editable=No }
    { 161 ;   ;Removal Order Line  ;Integer       ;TableRelation=IF (Removal Order Type=CONST(Plant Order)) "Plant Order Line"."Line No." WHERE (Plant Order No.=FIELD(Removal Order))
                                                                 ELSE IF (Removal Order Type=CONST(Exit Order)) "Exit Order Line"."Line No." WHERE (Exit-order No.=FIELD(Removal Order));
                                                   CaptionML=ENU=Removal Order Line;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 170 ;   ;Removed to Location ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Order"."To Location" WHERE (No.=FIELD(Removal Order),
                                                                                                         Posted=CONST(Yes)));
                                                   CaptionML=ENU=Removed to Location;
                                                   Editable=No }
    { 180 ;   ;Original Stock Line No.;Integer    ;CaptionML=ENU=Original Stock Line No.;
                                                   Editable=No }
    { 190 ;   ;Location Description;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Location".Description WHERE (Code=FIELD(Location)));
                                                   CaptionML=ENU=Location Description;
                                                   Editable=No }
    { 191 ;   ;Location Status     ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Location".Status WHERE (Code=FIELD(Location)));
                                                   CaptionML=ENU=Location Status;
                                                   OptionCaptionML=ENU=Production,Technical Finished,Administrative Finished,Finished,Archive,Archived;
                                                   OptionString=Production,Technical Finished,Administrative Finished,Finished,Archive,Archived;
                                                   Editable=No }
    { 200 ;   ;Plant Type Description;Text50      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Description WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=ENU=Plant Type Description;
                                                   Editable=No }
    { 205 ;   ;Plant No. Description;Text50       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number".Description WHERE (Plant Type=FIELD(Plant Type),
                                                                                                        No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Plant No. Description;
                                                   Editable=No }
    { 210 ;   ;Cum.Quantity Loc.-Plant Type;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Inventory".Quantity WHERE (Removed=CONST(No),
                                                                                                     Location=FIELD(Location),
                                                                                                     Plant Type=FIELD(Plant Type),
                                                                                                     Rate Code=FIELD(Rate Code Filter),
                                                                                                     Sold=FIELD(Sold Filter),
                                                                                                     Present from=FIELD(Date Filter),
                                                                                                     Quantity=FIELD(Quantity Filter)));
                                                   CaptionML=ENU=Cum.Quantity Loc.-Plant Type;
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 220 ;   ;Cum.Quantity Loc.-Plant No.;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Inventory".Quantity WHERE (Removed=CONST(No),
                                                                                                     Location=FIELD(Location),
                                                                                                     Plant Type=FIELD(Plant Type),
                                                                                                     Plant No.=FIELD(Plant No.),
                                                                                                     Rate Code=FIELD(Rate Code Filter),
                                                                                                     Sold=FIELD(Sold Filter),
                                                                                                     Present from=FIELD(Date Filter),
                                                                                                     Quantity=FIELD(Quantity Filter)));
                                                   CaptionML=ENU=Cum.Quantity Loc.-Plant No.;
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 225 ;   ;Cum.Quantity Loc.-Rate Code;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Inventory".Quantity WHERE (Removed=CONST(No),
                                                                                                     Location=FIELD(Location),
                                                                                                     Plant Type=FIELD(Plant Type),
                                                                                                     Plant No.=FIELD(Plant No.),
                                                                                                     Rate Code=FIELD(Rate Code),
                                                                                                     Sold=FIELD(Sold Filter),
                                                                                                     Present from=FIELD(Date Filter),
                                                                                                     Quantity=FIELD(Quantity Filter)));
                                                   CaptionML=ENU=Cum.Quantity Loc.-Plant--Rate Code;
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 230 ;   ;Invoiced until      ;Date          ;CaptionML=ENU=Invoiced until;
                                                   Editable=No }
    { 240 ;   ;Completely Invoiced ;Boolean       ;OnValidate=BEGIN
                                                                IF Removed AND
                                                                  ((LastRentalDay = "Invoiced until") OR
                                                                   ((LastRentalDay < "Rental Start Date") AND ("Invoiced until" = 0D)))
                                                                THEN
                                                                  "Completely Invoiced" := TRUE
                                                                ELSE
                                                                  "Completely Invoiced" := FALSE;
                                                              END;

                                                   CaptionML=ENU=Completely Invoiced;
                                                   Editable=No }
    { 250 ;   ;Last Invoice No.    ;Code20        ;TableRelation=IF (Invoice Status=CONST(Proposal)) "Sales Header".No. WHERE (Document Type=CONST(Invoice Proposal))
                                                                 ELSE IF (Invoice Status=CONST(Temporary)) "Sales Header".No. WHERE (Document Type=CONST(Invoice))
                                                                 ELSE IF (Invoice Status=CONST(Posted)) "Sales Invoice Header".No.;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Last Invoice No.;
                                                   Editable=No }
    { 260 ;   ;Last Invoice Line No.;Integer      ;CaptionML=ENU=Last Invoice Line No.;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 270 ;   ;Invoice Status      ;Option        ;CaptionML=ENU=Invoice Status;
                                                   OptionCaptionML=ENU=,Proposal,Temporary,Posted;
                                                   OptionString=,Proposal,Temporary,Posted;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 280 ;   ;Previous Invoice No.;Code20        ;TableRelation="Sales Invoice Header".No.;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Previous Invoice No.;
                                                   Editable=No }
    { 290 ;   ;Previous Invoice Line No.;Integer  ;CaptionML=ENU=Previous Invoice Line No.;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 300 ;   ;Previous Invoiced Until;Date       ;CaptionML=ENU=Previous Invoiced Until;
                                                   Editable=No }
    { 305 ;   ;No. of Invoiced Days;Decimal       ;CaptionML=ENU=Number of Invoiced Days;
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 306 ;   ;Previous No. of Invoiced Days;Decimal;
                                                   CaptionML=ENU=Previous Number of Invoiced Days;
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 310 ;   ;Manually Changed by ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Manually Changed by;
                                                   Editable=No }
    { 320 ;   ;Rate Code           ;Code10        ;OnLookup=VAR
                                                              PlantRateCodeRec@1100525000 : Record 11020502;
                                                            BEGIN
                                                              PlantRateCodeRec.LookupPlantRateCode(Location, "Rate Code", TRUE, '');
                                                            END;

                                                   CaptionML=ENU=Rate Code;
                                                   Editable=No }
    { 325 ;   ;Rate Code Filter    ;Code10        ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Rate Code Filter }
    { 330 ;   ;Vendor No.          ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number".Vendor WHERE (Plant Type=FIELD(Plant Type),
                                                                                                   No.=FIELD(Plant No.)));
                                                   TableRelation=Vendor;
                                                   CaptionML=ENU=Vendor No.;
                                                   Editable=No }
    { 340 ;   ;Arrival Transport Order;Code20     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Transport Plant Order Line"."Transport Order No." WHERE (Plant Order No.=FIELD(Arrival Order)));
                                                   OnLookup=BEGIN
                                                              LookupTransportOrder(TRUE);
                                                            END;

                                                   CaptionML=ENU=Arrival Transport Order;
                                                   Editable=No }
    { 345 ;   ;Removal Transport Order;Code20     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Transport Plant Order Line"."Transport Order No." WHERE (Plant Order No.=FIELD(Removal Order)));
                                                   OnLookup=BEGIN
                                                              LookupTransportOrder(FALSE);
                                                            END;

                                                   CaptionML=ENU=Removal Transport Order;
                                                   Editable=No }
    { 350 ;   ;Exit Order Location ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Exit Order".Location WHERE (No.=FIELD(Removal Order)));
                                                   TableRelation="Plant Location";
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Exit Order Location;
                                                   Description=Location of the Exit Order Header;
                                                   Editable=No }
    { 360 ;   ;Unit of Measure     ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type"."Unit of Measure" WHERE (Code=FIELD(Plant Type)));
                                                   TableRelation="Unit of Measure";
                                                   CaptionML=ENU=Unit of Measure;
                                                   Editable=No }
    { 370 ;   ;Inspection No.      ;Code30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number"."Inspection No." WHERE (Plant Type=FIELD(Plant Type),
                                                                                                             No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Inspection No.;
                                                   Editable=No }
    { 380 ;   ;Employee Company    ;Text30        ;TableRelation=Company;
                                                   OnValidate=VAR
                                                                lvPlantLocRec@1100485000 : Record 11012554;
                                                              BEGIN
                                                                IF lvPlantLocRec.GET(Location) THEN BEGIN
                                                                  IF (lvPlantLocRec."Employee No." <> '') THEN
                                                                    TESTFIELD("Employee Company", lvPlantLocRec."Company Name");
                                                                END;

                                                                VALIDATE("Employee No.");
                                                                IF "Employee Company" = COMPANYNAME THEN
                                                                  "Employee Company" := '';
                                                              END;

                                                   CaptionML=ENU=Employee Company }
    { 381 ;   ;Employee No.        ;Code20        ;TableRelation=IF (Employee Company=FILTER('')) Employee;
                                                   OnValidate=VAR
                                                                lvEmplRec@1100485000 : Record 5200;
                                                                lvPlantLocRec@1100485001 : Record 11012554;
                                                              BEGIN
                                                                IF lvPlantLocRec.GET(Location) THEN BEGIN
                                                                  IF (lvPlantLocRec."Employee No." <> '') THEN
                                                                    TESTFIELD("Employee No.", lvPlantLocRec."Employee No.");
                                                                END;

                                                                IF "Employee No." = '' THEN
                                                                  EXIT;

                                                                IF "Employee Company" <> '' THEN
                                                                  lvEmplRec.CHANGECOMPANY("Employee Company");

                                                                lvEmplRec.GET("Employee No.");
                                                              END;

                                                   CaptionML=ENU=Employee No. }
    { 390 ;   ;Element             ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Order Line".Element WHERE (Plant Order No.=FIELD(Arrival Order),
                                                                                                        Line No.=FIELD(Arrival Order Line)));
                                                   OnLookup=VAR
                                                              PlantElementMgtCU@1100525003 : Codeunit 11012582;
                                                            BEGIN
                                                              IF "Arrival Order Type" <> "Arrival Order Type"::"Plant Order" THEN
                                                                EXIT;
                                                              PlantElementMgtCU.OnLookupElement(Location, Element, '', FALSE);
                                                            END;

                                                   CaptionML=ENU=Element;
                                                   Description=PO-Line;
                                                   Editable=No }
    { 500 ;   ;CP Plant Number     ;Code30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number"."CP Plant Number" WHERE (Plant Type=FIELD(Plant Type),
                                                                                                              No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=CP Plant No.;
                                                   Description=BNBM (Att.: Flowfield can only be used for plant that exist in inventory with 'Plant No.');
                                                   Editable=No }
    { 510 ;   ;Rental Type         ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type"."Rental Type" WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=ENU=Rental Type;
                                                   OptionCaptionML=ENU=Rental,Sales/Buy Back,Both;
                                                   OptionString=Rental,Sales/Buy Back,Both;
                                                   Editable=No }
    { 520 ;   ;Order via Plant Request;Boolean    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type"."Order via Plant Request" WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=ENU=Order via Plant Request;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Removed,Location,Plant Type,Plant No.,Rate Code,Line No.;
                                                   SumIndexFields=Quantity;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Removed,Plant Type,Plant No.,Rate Code,Location }
    {    ;Removed,Plant Type,Depot,Sold           ;SumIndexFields=Quantity }
    {    ;Removed,Location,Plant Type,Plant No.,Rate Code,Sold,Present from,Quantity;
                                                   SumIndexFields=Quantity }
    {    ;Removed,Location,Plant Type,Plant No.,Rate Code,Present from }
    {    ;Location,Plant Type,Plant No.,Rate Code,Present from,Removed on }
    {    ;Location,Plant Type,Plant No.,Present from }
    {    ;Plant Type,Plant No.,Rate Code,Present from,Removed on,Location }
    {    ;Plant Type,Plant No.,Present from        }
    {    ;Completely Invoiced,Location,Plant Type,Plant No.,Rate Code,Present from }
    {    ;Invoice Status,Last Invoice No.,Last Invoice Line No. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      LocationRec@11012000 : Record 11012554;
      PlantNoRec@11012001 : Record 11012552;
      VendRec@1100525000 : Record 23;
      PlantLookupMgt@1100409000 : Codeunit 11012590;

    PROCEDURE PlantDescription@1() : Text[50];
    BEGIN
      IF ("Plant No." <> '') AND ("Plant No." <> '0') THEN BEGIN
        CALCFIELDS("Plant No. Description");
        IF "Plant No. Description" <> '' THEN
          EXIT("Plant No. Description");
      END;
      CALCFIELDS("Plant Type Description");
      EXIT("Plant Type Description");
    END;

    PROCEDURE GetLineNo@13(lInventRec@11012000 : Record 11012555) : Integer;
    VAR
      lLineNo@11012001 : Integer;
    BEGIN
      WITH lInventRec DO BEGIN
        RESET;
        SETRANGE(Removed,Removed);
        SETRANGE(Location,Location);
        SETRANGE("Plant Type","Plant Type");
        SETRANGE("Plant No.","Plant No.");
        SETRANGE("Rate Code","Rate Code");
        IF FINDLAST THEN
          lLineNo := "Line No." + 1
        ELSE
          lLineNo := 1;
      END;

      EXIT(lLineNo);
    END;

    PROCEDURE CompressedQuantity@6(Details@11012000 : 'LedgerEntry,PlantType,PlantNo,RateCode';InclNegInventory@1100409000 : Boolean) : Decimal;
    BEGIN
      IF Details <> Details::LedgerEntry THEN BEGIN
        IF InclNegInventory THEN
          SETRANGE("Quantity Filter")  //Reset
        ELSE
          SETFILTER("Quantity Filter", '>=%1', 0);  // Not applied for LedgerEntry Level
      END;
      CASE Details OF
        Details::PlantType:
          BEGIN
            SETRANGE("Sold Filter",FALSE);
            CALCFIELDS("Cum.Quantity Loc.-Plant Type");
            SETRANGE("Sold Filter");
            SETRANGE("Quantity Filter");
            EXIT("Cum.Quantity Loc.-Plant Type");
          END;
        Details::PlantNo:
          BEGIN
            SETRANGE("Sold Filter",FALSE);
            CALCFIELDS("Cum.Quantity Loc.-Plant No.");
            SETRANGE("Sold Filter");
            SETRANGE("Quantity Filter");
            EXIT("Cum.Quantity Loc.-Plant No.");
          END;
        Details::RateCode:
          BEGIN
            SETRANGE("Sold Filter",FALSE);
            CALCFIELDS("Cum.Quantity Loc.-Rate Code");
            SETRANGE("Sold Filter");
            SETRANGE("Quantity Filter");
            EXIT("Cum.Quantity Loc.-Rate Code");
          END;
        Details::LedgerEntry:
          IF Sold THEN
            EXIT(0)
          ELSE
            EXIT(Quantity);
      END;
    END;

    PROCEDURE SoldQuantity@3(Details@11012000 : 'LedgerEntry,PlantType,PlantNo,RateCode') : Decimal;
    BEGIN
      CASE Details OF
        Details::PlantType:
          BEGIN
            SETRANGE("Sold Filter",TRUE);
            CALCFIELDS("Cum.Quantity Loc.-Plant Type");
            SETRANGE("Sold Filter");
            EXIT("Cum.Quantity Loc.-Plant Type");
          END;
        Details::PlantNo:
          BEGIN
            SETRANGE("Sold Filter",TRUE);
            CALCFIELDS("Cum.Quantity Loc.-Plant No.");
            SETRANGE("Sold Filter");
            EXIT("Cum.Quantity Loc.-Plant No.");
          END;
        Details::RateCode:
          BEGIN
            SETRANGE("Sold Filter",TRUE);
            CALCFIELDS("Cum.Quantity Loc.-Rate Code");
            SETRANGE("Sold Filter");
            EXIT("Cum.Quantity Loc.-Rate Code");
          END;
        Details::LedgerEntry:
          IF Sold THEN
            EXIT(Quantity)
          ELSE
            EXIT(0);
      END;
    END;

    PROCEDURE PlantDescr@2(Details@11012000 : 'LedgerEntry,PlantType,PlantNo,RateCode') : Text[50];
    BEGIN
      IF Details = Details::PlantType THEN BEGIN
        CALCFIELDS("Plant Type Description");
        EXIT("Plant Type Description");
      END;

      EXIT(PlantDescription);
    END;

    PROCEDURE LastRentalDay@17() : Date;
    BEGIN
      IF ("Rental End Date" <> 0D) AND ("Rental End Date" < "Removed on") THEN
        EXIT("Rental End Date")
      ELSE
        EXIT("Removed on");
    END;

    PROCEDURE LookupArrivalOrder@1100525002();
    BEGIN
      PlantLookupMgt.PlantInventoryArrivalOrder(Rec);
    END;

    PROCEDURE LookupRemovalOrder@1100525003();
    BEGIN
      PlantLookupMgt.PlantInventoryRemovalOrder(Rec);
    END;

    PROCEDURE LookupTransportOrder@1100485000(IModeArrival@1100485000 : Boolean);
    BEGIN
      IF IModeArrival THEN
        PlantLookupMgt.PlantInventoryArrivalTransportOrder(Rec)
      ELSE
        PlantLookupMgt.PlantInventoryRemovalTransportOrder(Rec);
    END;

    PROCEDURE LookupEmployee@1100485011(IEditable@1100485001 : Boolean);
    VAR
      lvEmplRec@1100485000 : Record 5200;
    BEGIN
      IF "Employee Company" <> '' THEN
        lvEmplRec.CHANGECOMPANY("Employee Company");
      lvEmplRec.SETRANGE(Status, lvEmplRec.Status::Active);
      lvEmplRec."No." := "Employee No.";
      IF NOT IEditable THEN
        PAGE.RUNMODAL(0,lvEmplRec)
      ELSE BEGIN
        IF PAGE.RUNMODAL(0,lvEmplRec) = ACTION::LookupOK THEN
          VALIDATE("Employee No.",lvEmplRec."No.");
      END;
    END;

    PROCEDURE GetVendorName@1100525000() : Text[50];
    BEGIN
      CALCFIELDS("Vendor No.");
      IF "Vendor No." <> ''THEN BEGIN
        IF VendRec.GET("Vendor No.") THEN
          EXIT(VendRec.Name);
      END;
      EXIT('');
    END;

    PROCEDURE GetElementReturnedExit@1100525001() : Code[20];
    VAR
      ReturnLossOrderLineRec@1100525000 : Record 11012656;
    BEGIN
      IF "Arrival Order Type" = "Arrival Order Type"::ReturnedExit THEN BEGIN
        IF ReturnLossOrderLineRec.GET("Arrival Order", "Arrival Order Line") THEN
          EXIT(ReturnLossOrderLineRec."To Element");
      END;
      EXIT('');
    END;

    PROCEDURE ActualMeterReading@1100529005() : Decimal;
    VAR
      MeterReading@1100529000 : Record 11072671;
      OServiceOrder@1100409000 : Code[20];
    BEGIN
      //DP00614
      IF ("Plant Type" = '') OR ("Plant No." = '') THEN
        EXIT(0);
      EXIT(MeterReading.GetActualMeterReading("Plant Type", "Plant No.", ActualMeterReadingType, OServiceOrder));
    END;

    PROCEDURE ActualMeterReadingUnit@1100529009() : Code[10];
    VAR
      MeterReadingType@1100529000 : Record 11072670;
    BEGIN
      //DP00614
      IF MeterReadingType.GET(ActualMeterReadingType()) THEN BEGIN
        IF MeterReadingType.Code <> '' THEN
          EXIT(MeterReadingType."Unit of Measure");
      END;
      EXIT('');
    END;

    PROCEDURE ActualMeterReadingType@1100529008() : Code[10];
    VAR
      MeterReading@1100529000 : Record 11072671;
    BEGIN
      //DP00614
      IF ("Plant Type" = '') OR ("Plant No." = '') THEN
        EXIT('');
      EXIT(MeterReading.DefaultMeterReadingType("Plant Type", "Plant No."));
    END;

    PROCEDURE NextMeterReadingAt@1100529000() : Decimal;
    VAR
      MeterReading@1100529001 : Record 11072671;
      ActMeterReadingType@1100529000 : Code[10];
    BEGIN
      //DP00614
      IF ("Plant Type" = '') OR ("Plant No." = '') THEN
        EXIT(0);
      ActMeterReadingType := ActualMeterReadingType();
      IF ActMeterReadingType <> '' THEN
        EXIT(MeterReading.NextMeterReadingForInspections("Plant Type", "Plant No.", ActMeterReadingType));
      EXIT(0);
    END;

    PROCEDURE DrillDownMeterReading@1100529011();
    VAR
      MeterReading@1100529000 : Record 11072671;
      PlantMeterReading@1100529001 : Page 11125948;
    BEGIN
      //DP00614
      IF ("Plant Type" = '') OR ("Plant No." = '') THEN
        EXIT;

      MeterReading.FILTERGROUP(7);
      MeterReading.SETRANGE("Plant Type", "Plant Type");
      MeterReading.SETRANGE("Plant No.", "Plant No.");
      MeterReading.FILTERGROUP(0);
      MeterReading.SETRANGE("Meter Reading Type", ActualMeterReadingType());
      IF MeterReading.FINDLAST THEN
        PlantMeterReading.SETRECORD(MeterReading);
      MeterReading.SETRANGE("Meter Reading Type");  //Reset filter
      IF MeterReading."Meter Reading Type" = '' THEN
      IF MeterReading.FINDLAST THEN
        PlantMeterReading.SETRECORD(MeterReading);
      PlantMeterReading.SETTABLEVIEW(MeterReading);
      PlantMeterReading.EDITABLE(FALSE);
      PlantMeterReading.RUNMODAL;
    END;

    PROCEDURE GetPlantLocationByPlantNumber@1100528800(iPlantType@1100528800 : Code[20];iPlantNumber@1100528801 : Code[10];VAR OPlantLocation@1100528802 : Code[20]) : Boolean;
    BEGIN
      SETRANGE(Removed, FALSE);
      SETRANGE("Plant Type", iPlantType);
      SETRANGE("Plant No.", iPlantNumber);
      IF FINDFIRST THEN BEGIN
        OPlantLocation := Location;
        EXIT(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

