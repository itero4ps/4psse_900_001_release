OBJECT Table 11012574 Plant Hours Revenues Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               InitRecord;
             END;

    OnModify=BEGIN
               TESTFIELD(Status,Status::Open);
             END;

    OnDelete=BEGIN
               IF NOT DeleteAllowed THEN
                 ERROR(Text003,TABLECAPTION,"Line No.");
             END;

    CaptionML=ENU=Plant Hours Revenues Line;
  }
  FIELDS
  {
    { 10  ;   ;Year                ;Integer       ;CaptionML=ENU=Year }
    { 20  ;   ;Week                ;Integer       ;CaptionML=ENU=Week }
    { 30  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   CaptionML=ENU=Plant Type }
    { 35  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   CaptionML=ENU=Plant No. }
    { 40  ;   ;Line No.            ;Integer       ;TableRelation="Hour Accounting" WHERE (Year=FIELD(Year),
                                                                                          Week=FIELD(Week));
                                                   CaptionML=ENU=Line No. }
    { 50  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Type <> xRec.Type THEN BEGIN
                                                                  TESTFIELD(Status,Status::Open);
                                                                  IF Type <> Type::Expenses THEN BEGIN
                                                                    TESTFIELD(Amount, 0);
                                                                    "Cost Object" := '';
                                                                    Quantity := 0;
                                                                    "Unit Price" := 0;
                                                                    Amount := 0;
                                                                    IF Type = Type::"Unmanned Hours" THEN BEGIN  //*C-019470.sn
                                                                      "Employee No." := '';
                                                                      "Employee Name" := '';
                                                                    END;  //*C-019470.en
                                                                  END ELSE BEGIN
                                                                    TESTFIELD("Total Line", 0);
                                                                    "Rate Code" := '';
                                                                    Monday := 0;
                                                                    Tuesday := 0;
                                                                    Wednesday := 0;
                                                                    Thursday := 0;
                                                                    Friday := 0;
                                                                    Saturday := 0;
                                                                    Sunday := 0;
                                                                    "Total Hours";
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Hours,Expenses,Unmanned Hours;
                                                   OptionString=Hours,Expenses,Unmanned Hours }
    { 60  ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=Open,Posted;
                                                   OptionString=Open,Posted;
                                                   Editable=No }
    { 70  ;   ;Plant Location      ;Code20        ;TableRelation="Plant Location";
                                                   OnValidate=BEGIN
                                                                IF "Plant Location" <> xRec."Plant Location" THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF "Plant Location" <> '' THEN BEGIN
                                                                  PlantLocationRec.GET("Plant Location");
                                                                  PlantLocationRec.CheckBlock(2,FALSE,FALSE);
                                                                  CALCFIELDS("Location Description");
                                                                END;

                                                                IF "Plant Location" <> xRec."Plant Location" THEN BEGIN
                                                                  Element := '';
                                                                  IF "Rate Code" <> '' THEN BEGIN
                                                                    IF NOT PlantRateCodeRec.CheckPlantRateCode("Plant Location", "Rate Code", FALSE, '') THEN
                                                                      "Rate Code" := '';
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Location }
    { 80  ;   ;Rate Code           ;Code10        ;OnValidate=BEGIN
                                                                IF "Rate Code" <> xRec."Rate Code" THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF "Rate Code" <> '' THEN BEGIN
                                                                  //TESTFIELD(Type, Type::Hours);  //*C-019470.o
                                                                  IF Type = Type::Expenses THEN  //*C-019470.n
                                                                    FIELDERROR(Type);
                                                                  PlantSetupRec.GET;
                                                                  PlantSetupRec.TESTFIELD("Rate Codes");
                                                                  PlantRateCodeRec.CheckPlantRateCode("Plant Location", "Rate Code", TRUE, '');
                                                                  IF "Employee No." = '' THEN BEGIN
                                                                    //PlantRateCodeRec.GET("Rate Code");
                                                                    PlantRateCodeRec.ReadPlantRateCode("Plant Location", "Rate Code", '');
                                                                    IF PlantRateCodeRec."Hour Rate from" = PlantRateCodeRec."Hour Rate from"::Employee THEN BEGIN
                                                                      FIELDERROR("Rate Code",
                                                                        STRSUBSTNO(Text002, "Rate Code", PlantRateCodeRec.FIELDCAPTION("Hour Rate from"), PlantRateCodeRec."Hour Rate from"));
                                                                    END;
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              PlantRateCodeRec.Code := PlantRateCodeRec.LookupPlantRateCode("Plant Location", "Rate Code", TRUE, '');
                                                              IF (Status = Status::Open) AND (Type IN [Type::Hours, Type::"Unmanned Hours"]) AND  //*C-019470.c
                                                                 (PlantRateCodeRec.Code <> "Rate Code")
                                                              THEN
                                                                VALIDATE("Rate Code", PlantRateCodeRec.Code);
                                                            END;

                                                   CaptionML=ENU=Rate Code }
    { 100 ;   ;Employee No.        ;Code20        ;TableRelation=IF (Employee Company=CONST()) Employee;
                                                   OnValidate=BEGIN
                                                                IF "Employee No." <> xRec."Employee No." THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF ("Employee No." <> '') AND (Type = Type::"Unmanned Hours") THEN  //*C-019470.n
                                                                  FIELDERROR(Type);
                                                                IF (Type = Type::Expenses) AND (CurrFieldNo = FIELDNO("Employee No.")) AND ("Cost Object" <> '') THEN
                                                                 VALIDATE("Unit Price", GetUnitPriceExpenses());

                                                                CALCFIELDS("Employee Name");
                                                              END;

                                                   CaptionML=ENU=Employee No. }
    { 105 ;   ;Employee Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Employee No.)));
                                                   CaptionML=ENU=Employee Name;
                                                   Editable=No }
    { 110 ;   ;Monday              ;Decimal       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF Monday <> xRec.Monday THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Monday <> 0 THEN
                                                                  //TESTFIELD(Type, Type::Hours);  //*C-019470.o
                                                                  IF Type = Type::Expenses THEN  //*C-019470.n
                                                                    FIELDERROR(Type);

                                                                "Total Hours";
                                                              END;

                                                   CaptionML=ENU=Monday;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes }
    { 120 ;   ;Tuesday             ;Decimal       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF Tuesday <> xRec.Tuesday THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Tuesday <> 0 THEN
                                                                  //TESTFIELD(Type, Type::Hours);  //*C-019470.o
                                                                  IF Type = Type::Expenses THEN  //*C-019470.n
                                                                    FIELDERROR(Type);

                                                                "Total Hours";
                                                              END;

                                                   CaptionML=ENU=Tuesday;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes }
    { 130 ;   ;Wednesday           ;Decimal       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF Wednesday <> xRec.Wednesday THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Wednesday <> 0 THEN
                                                                  //TESTFIELD(Type, Type::Hours);  //*C-019470.o
                                                                  IF Type = Type::Expenses THEN  //*C-019470.n
                                                                    FIELDERROR(Type);

                                                                "Total Hours";
                                                              END;

                                                   CaptionML=ENU=Wednesday;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes }
    { 140 ;   ;Thursday            ;Decimal       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF Thursday <> xRec.Thursday THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Thursday <> 0 THEN
                                                                  //TESTFIELD(Type, Type::Hours);  //*C-019470.o
                                                                  IF Type = Type::Expenses THEN  //*C-019470.n
                                                                    FIELDERROR(Type);

                                                                "Total Hours";
                                                              END;

                                                   CaptionML=ENU=Thursday;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes }
    { 150 ;   ;Friday              ;Decimal       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF Friday <> xRec.Friday THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Friday <> 0 THEN
                                                                  //TESTFIELD(Type, Type::Hours);  //*C-019470.o
                                                                  IF Type = Type::Expenses THEN  //*C-019470.n
                                                                    FIELDERROR(Type);

                                                                "Total Hours";
                                                              END;

                                                   CaptionML=ENU=Friday;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes }
    { 160 ;   ;Saturday            ;Decimal       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF Saturday <> xRec.Saturday THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Saturday <> 0 THEN
                                                                  //TESTFIELD(Type, Type::Hours);  //*C-019470.o
                                                                  IF Type = Type::Expenses THEN  //*C-019470.n
                                                                    FIELDERROR(Type);

                                                                "Total Hours";
                                                              END;

                                                   CaptionML=ENU=Saturday;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes }
    { 170 ;   ;Sunday              ;Decimal       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF Sunday <> xRec.Sunday THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Sunday <> 0 THEN
                                                                  //TESTFIELD(Type, Type::Hours);  //*C-019470.o
                                                                  IF Type = Type::Expenses THEN  //*C-019470.n
                                                                    FIELDERROR(Type);

                                                                "Total Hours";
                                                              END;

                                                   CaptionML=ENU=Sunday;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes }
    { 180 ;   ;Total Line          ;Decimal       ;CaptionML=ENU=Total Line;
                                                   DecimalPlaces=0:3;
                                                   Editable=No }
    { 190 ;   ;Total Monday        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Revenues Line".Monday WHERE (Year=FIELD(Year),
                                                                                                             Week=FIELD(Week),
                                                                                                             Plant Type=FIELD(Plant Type),
                                                                                                             Plant No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Total Monday;
                                                   Editable=No }
    { 200 ;   ;Total Tuesday       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Revenues Line".Tuesday WHERE (Year=FIELD(Year),
                                                                                                              Week=FIELD(Week),
                                                                                                              Plant Type=FIELD(Plant Type),
                                                                                                              Plant No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Total Tuesday;
                                                   Editable=No }
    { 210 ;   ;Total Wednesday     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Revenues Line".Wednesday WHERE (Year=FIELD(Year),
                                                                                                                Week=FIELD(Week),
                                                                                                                Plant Type=FIELD(Plant Type),
                                                                                                                Plant No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Total Wednesday;
                                                   Editable=No }
    { 220 ;   ;Total Thursday      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Revenues Line".Thursday WHERE (Year=FIELD(Year),
                                                                                                               Week=FIELD(Week),
                                                                                                               Plant Type=FIELD(Plant Type),
                                                                                                               Plant No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Total Thursday;
                                                   Editable=No }
    { 230 ;   ;Total Friday        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Revenues Line".Friday WHERE (Year=FIELD(Year),
                                                                                                             Week=FIELD(Week),
                                                                                                             Plant Type=FIELD(Plant Type),
                                                                                                             Plant No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Total Friday;
                                                   Editable=No }
    { 240 ;   ;Total Saturday      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Revenues Line".Saturday WHERE (Year=FIELD(Year),
                                                                                                               Week=FIELD(Week),
                                                                                                               Plant Type=FIELD(Plant Type),
                                                                                                               Plant No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Total Saturday;
                                                   Editable=No }
    { 250 ;   ;Total Sunday        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Revenues Line".Sunday WHERE (Year=FIELD(Year),
                                                                                                             Week=FIELD(Week),
                                                                                                             Plant Type=FIELD(Plant Type),
                                                                                                             Plant No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Total Sunday;
                                                   Editable=No }
    { 330 ;   ;Total Normal        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Revenues Line"."Total Line" WHERE (Year=FIELD(Year),
                                                                                                                   Week=FIELD(Week),
                                                                                                                   Plant Type=FIELD(Plant Type),
                                                                                                                   Plant No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Total Normal;
                                                   Editable=No }
    { 350 ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Cost Type=FILTER(<Revenue),
                                                                                               Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                IF "Cost Object" <> xRec."Cost Object" THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF "Cost Object" = '' THEN
                                                                  EXIT;

                                                                TESTFIELD(Type, Type::Expenses);

                                                                IF (CurrFieldNo = FIELDNO("Cost Object")) THEN
                                                                 VALIDATE("Unit Price", GetUnitPriceExpenses());

                                                                CALCFIELDS("Cost Object Description");
                                                              END;

                                                   CaptionML=ENU=Cost Object;
                                                   Description=Only for expenses }
    { 351 ;   ;Cost Object Description;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value".Name WHERE (Global Dimension No.=FILTER(2),
                                                                                                    Code=FIELD(Cost Object)));
                                                   CaptionML=ENU=Cost Object Description;
                                                   Editable=No }
    { 352 ;   ;Cost Object Unit of Measure;Code10 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value"."Unit of Measure" WHERE (Global Dimension No.=FILTER(2),
                                                                                                                 Code=FIELD(Cost Object)));
                                                   CaptionML=ENU=Cost Object Unit of Measure;
                                                   Editable=No }
    { 360 ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF Quantity <> xRec.Quantity THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Quantity <> 0 THEN
                                                                  TESTFIELD(Type, Type::Expenses);

                                                                Amount := ROUND(Quantity * "Unit Price");
                                                              END;

                                                   CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes;
                                                   Description=Only for expenses }
    { 370 ;   ;Unit Price          ;Decimal       ;OnValidate=BEGIN
                                                                IF "Unit Price" <> xRec."Unit Price" THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF "Unit Price" <> 0 THEN
                                                                  TESTFIELD(Type, Type::Expenses);

                                                                Amount := ROUND(Quantity * "Unit Price");
                                                              END;

                                                   CaptionML=ENU=Unit Price;
                                                   BlankNumbers=BlankNeg;
                                                   BlankZero=Yes;
                                                   Description=Only for expenses;
                                                   AutoFormatType=2 }
    { 380 ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                IF Amount <> xRec.Amount THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Amount <> 0 THEN BEGIN
                                                                  TESTFIELD(Type, Type::Expenses);
                                                                  TESTFIELD(Quantity);
                                                                END;

                                                                IF Quantity = 0 THEN
                                                                  "Unit Price" := 0
                                                                ELSE BEGIN
                                                                  GLSetupRec.GET;
                                                                  "Unit Price" := ROUND((Amount / Quantity), GLSetupRec."Unit-Amount Rounding Precision");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Amount;
                                                   BlankZero=Yes;
                                                   Description=Only for expenses;
                                                   AutoFormatType=1 }
    { 430 ;   ;Comment             ;Text50        ;OnValidate=BEGIN
                                                                IF Comment <> xRec.Comment THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                              END;

                                                   CaptionML=ENU=Comment }
    { 440 ;   ;Location Description;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Location".Description WHERE (Code=FIELD(Plant Location)));
                                                   CaptionML=ENU=Location Description;
                                                   Editable=No }
    { 450 ;   ;Invoiced            ;Boolean       ;CaptionML=ENU=Invoiced;
                                                   Editable=No }
    { 460 ;   ;Invoice No.         ;Code20        ;TableRelation=IF (Invoice Status=CONST(Proposal)) "Sales Header".No. WHERE (Document Type=CONST(Invoice Proposal))
                                                                 ELSE IF (Invoice Status=CONST(Temporary)) "Sales Header".No. WHERE (Document Type=CONST(Invoice))
                                                                 ELSE IF (Invoice Status=CONST(Posted)) "Sales Invoice Header".No.;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Invoice No.;
                                                   Editable=No }
    { 465 ;   ;Invoice Line No.    ;Integer       ;CaptionML=ENU=Invoice Line No.;
                                                   Editable=No }
    { 470 ;   ;Invoice Status      ;Option        ;CaptionML=ENU=Invoice Status;
                                                   OptionCaptionML=ENU=,Proposal,Temporary,Posted;
                                                   OptionString=,Proposal,Temporary,Posted;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 490 ;   ;Origin              ;Option        ;CaptionML=ENU=Origin;
                                                   OptionCaptionML=ENU=Plant,HourAccounting;
                                                   OptionString=Plant,HourAcc;
                                                   Editable=No }
    { 495 ;   ;Employee Company    ;Text30        ;TableRelation=Company;
                                                   OnValidate=BEGIN
                                                                IF "Employee Company" = COMPANYNAME THEN
                                                                  "Employee Company" := '';
                                                              END;

                                                   CaptionML=ENU=Employee Company;
                                                   Description=Only allowed for hours from 'Hour Accounting', so field not editable;
                                                   Editable=No }
    { 500 ;   ;Element             ;Code20        ;OnValidate=BEGIN
                                                                IF Element <> xRec.Element THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Element = '' THEN
                                                                  EXIT;
                                                                TESTFIELD("Plant Location");
                                                                PlantElementMgtCU.OnValidateElement("Plant Location", Element, '', FALSE);
                                                              END;

                                                   OnLookup=VAR
                                                              lvNewElement@1100525000 : Code[20];
                                                            BEGIN
                                                              lvNewElement := PlantElementMgtCU.OnLookupElement("Plant Location", Element, '', Status=Status::Open);
                                                              IF (lvNewElement <> '') AND (lvNewElement <> Element) THEN
                                                                VALIDATE(Element, lvNewElement);
                                                            END;

                                                   CaptionML=ENU=Element }
    { 510 ;   ;Wage Component      ;Code10        ;TableRelation="Wage Component".Code WHERE (Component Type=FILTER(Hours));
                                                   CaptionML=ENU=Wage Component;
                                                   Description=Only allowed for hours from 'Hour Accounting', so field not editable (only Hours for Sales Hour Rate for Rate Code for Employee);
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Year,Week,Plant Type,Plant No.,Line No. ;SumIndexFields=Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday,Total Line;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Year,Week,Plant Location,Plant Type,Plant No. }
    {    ;Invoiced,Status,Plant Location,Year,Week,Plant Type,Plant No. }
    {    ;Invoice Status,Invoice No.               }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GLSetupRec@1100485004 : Record 98;
      PlantSetupRec@1100525003 : Record 11012550;
      PlantLocationRec@1210190002 : Record 11012554;
      Text002@1100525002 : TextConst 'ENU=''%1'' with ''%2 %3'' not allowed if employee is not fillerd';
      Text003@1210190003 : TextConst 'ENU=%1 %2 is not deleted.';
      Text004@1210190001 : TextConst 'ENU=%1 is not invoiced yet. Do you still want to delete the %1?';
      Text005@1210190000 : TextConst 'ENU=%1 is not invoiced completely. Do you still want to delete the %1?';
      PlantRateCodeRec@1100525001 : Record 11020502;
      DimValRec@1100485002 : Record 349;
      DimMgtCu@1100485001 : Codeunit 408;
      PlantElementMgtCU@1100525000 : Codeunit 11012582;

    PROCEDURE InitRecord@1100485001();
    BEGIN
      Status := Status::Open;
      Invoiced := FALSE;
      "Invoice No." := '';
      "Invoice Line No." := 0;
      "Invoice Status" := "Invoice Status"::"0";
      Origin := Origin::Plant;
      "Employee Company" := '';
    END;

    PROCEDURE "Total Hours"@1();
    BEGIN
      "Total Line" := Monday + Tuesday + Wednesday + Thursday + Friday + Saturday + Sunday;
    END;

    PROCEDURE DeleteAllowed@1210190000() : Boolean;
    BEGIN
      IF NOT Invoiced THEN BEGIN
        IF NOT CONFIRM(STRSUBSTNO(
          Text004,
          TABLECAPTION))
        THEN
          EXIT(FALSE);
      END ELSE
        IF "Invoice Status" IN ["Invoice Status"::Proposal,"Invoice Status"::"Temporary"] THEN
          IF NOT CONFIRM(STRSUBSTNO(
            Text005,TABLECAPTION))
          THEN
            EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE GetUnitPrice@1100485000(IDate@1100485003 : Date;IGiveMess@1100485004 : Integer) UnitPrice : Decimal;
    VAR
      lvSearchRate@1100485002 : Codeunit 11012567;
      lvFirstHrsDateWeek@1100485000 : Date;
      lvRateDate@1100485001 : Date;
    BEGIN
      UnitPrice := 0;
      IF (Year = 0) OR (Week = 0) OR ("Plant Type" = '') THEN
        EXIT;

      lvRateDate := IDate;
      IF lvRateDate <> 0D THEN BEGIN
        IF (lvRateDate < DWY2DATE(1, Week, Year)) OR (lvRateDate > DWY2DATE(7,Week,Year)) THEN
          lvRateDate := 0D;
      END;

      IF lvRateDate = 0D THEN BEGIN
        lvFirstHrsDateWeek := 0D;
        IF (lvFirstHrsDateWeek = 0D) AND (Monday <> 0) THEN lvFirstHrsDateWeek := DWY2DATE(1,Week,Year);
        IF (lvFirstHrsDateWeek = 0D) AND (Tuesday <> 0) THEN lvFirstHrsDateWeek := DWY2DATE(2,Week,Year);
        IF (lvFirstHrsDateWeek = 0D) AND (Wednesday <> 0) THEN lvFirstHrsDateWeek := DWY2DATE(3,Week,Year);
        IF (lvFirstHrsDateWeek = 0D) AND (Thursday <> 0) THEN lvFirstHrsDateWeek := DWY2DATE(4,Week,Year);
        IF (lvFirstHrsDateWeek = 0D) AND (Friday <> 0) THEN lvFirstHrsDateWeek := DWY2DATE(5,Week,Year);
        IF (lvFirstHrsDateWeek = 0D) AND (Saturday <> 0) THEN lvFirstHrsDateWeek := DWY2DATE(6,Week,Year);
        IF (lvFirstHrsDateWeek = 0D) AND (Sunday <> 0) THEN lvFirstHrsDateWeek := DWY2DATE(7,Week,Year);
        IF (lvFirstHrsDateWeek = 0D) THEN lvFirstHrsDateWeek := DWY2DATE(1, Week, Year);
        lvRateDate := lvFirstHrsDateWeek;
      END;

      lvSearchRate.SetExtraSelectionGetEmplRate("Employee Company", "Wage Component");
      UnitPrice := lvSearchRate.Rate("Plant Location","Plant Type","Plant No.","Rate Code",lvRateDate,3,IGiveMess,"Employee No.");
    END;

    PROCEDURE GetUnitPriceExpenses@1100485003() UnitPriceExp : Decimal;
    VAR
      lvEmplPriceCostObjectRec@1100485000 : Record 11020259;
    BEGIN
      UnitPriceExp := 0;

      IF "Employee No." <> '' THEN BEGIN
        IF "Employee Company" <> '' THEN
          lvEmplPriceCostObjectRec.CHANGECOMPANY("Employee Company");
        lvEmplPriceCostObjectRec.SETRANGE("Employee No.","Employee No.");
        lvEmplPriceCostObjectRec.SETRANGE("Cost Object","Cost Object");
        lvEmplPriceCostObjectRec.SETRANGE("Starting Date", 0D, DWY2DATE(7,Week,Year));
        IF lvEmplPriceCostObjectRec.FINDLAST THEN BEGIN
          UnitPriceExp := lvEmplPriceCostObjectRec."Cost Price";
          EXIT;
        END;
      END;

      DimMgtCu.GetDimValueRec(2, "Cost Object", DimValRec, FALSE, '');
      UnitPriceExp := DimValRec.Price;
    END;

    PROCEDURE GetEmployeeName@1100525000() : Text[250];
    VAR
      lvEmplRec@1100525000 : Record 5200;
    BEGIN
      IF ("Employee No." <> '') AND ("Employee Company" <> '') THEN BEGIN
        lvEmplRec.CHANGECOMPANY("Employee Company");
        IF lvEmplRec.GET("Employee No.") THEN
          EXIT(lvEmplRec."Full Name")
      END ELSE BEGIN
        CALCFIELDS("Employee Name");
        EXIT("Employee Name");
      END;
      EXIT('');
    END;

    BEGIN
    {
      07-10-2002 Cor Hofman: Copied from table "Hour Accounting Line" for "Plant Hour Accounting"
    }
    END.
  }
}

