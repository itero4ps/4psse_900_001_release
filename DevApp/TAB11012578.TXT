OBJECT Table 11012578 Plant Hours Costs Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Status := Status::Open;
               Origin := Origin::Plant;
             END;

    OnModify=BEGIN
               TESTFIELD(Status,Status::Open);
             END;

    CaptionML=ENU=Plant Hours Costs Line;
  }
  FIELDS
  {
    { 10  ;   ;Year                ;Integer       ;CaptionML=ENU=Year }
    { 20  ;   ;Week                ;Integer       ;CaptionML=ENU=Week }
    { 25  ;   ;Employee Company    ;Text30        ;TableRelation=Company;
                                                   CaptionML=ENU=Employee Company;
                                                   Description=Only allowed for hours from 'Hour Accounting', so field not editable;
                                                   Editable=No }
    { 30  ;   ;Employee No.        ;Code20        ;TableRelation=IF (Employee Company=CONST()) Employee;
                                                   CaptionML=ENU=Employee No.;
                                                   NotBlank=Yes }
    { 40  ;   ;Line No.            ;Integer       ;TableRelation="Hour Accounting" WHERE (Year=FIELD(Year),
                                                                                          Week=FIELD(Week));
                                                   CaptionML=ENU=Line No. }
    { 50  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   OnValidate=BEGIN
                                                                IF "Plant Type" <> xRec."Plant Type" THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                PlantSetup.GET;
                                                                IF ("Plant Type" <> '') AND (Status = Status::Open) AND
                                                                   ("Cost Component" = '') AND (PlantSetup."Cost Component Hour Costs" <> '') THEN
                                                                  VALIDATE("Cost Component", PlantSetup."Cost Component Hour Costs");

                                                                FillDepartmentCode;
                                                              END;

                                                   CaptionML=ENU=Plant Type }
    { 55  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   OnValidate=BEGIN
                                                                IF "Plant No." <> xRec."Plant No." THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                CALCFIELDS("Plant Description");
                                                                FillDepartmentCode;
                                                              END;

                                                   CaptionML=ENU=Plant No. }
    { 60  ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=Open,Posted;
                                                   OptionString=Open,Posted;
                                                   Editable=No }
    { 70  ;   ;Cost Component      ;Code10        ;TableRelation="Cost Component Plant";
                                                   OnValidate=BEGIN
                                                                IF "Cost Component" <> xRec."Cost Component" THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                              END;

                                                   CaptionML=ENU=Cost Component }
    { 80  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                //*C019484.n
                                                                IF Type <> xRec.Type THEN BEGIN
                                                                  TESTFIELD(Status,Status::Open);
                                                                  TESTFIELD(Origin, Origin::Plant);
                                                                  IF Type <> Type::Expenses THEN BEGIN
                                                                    TESTFIELD(Amount, 0);
                                                                    Quantity := 0;
                                                                    "Unit Rate" := 0;
                                                                    Amount := 0;
                                                                    "Cost Object" := '';
                                                                    "Wage Component Expenses" := '';
                                                                    "Posting Date (Exp.)" := 0D;  //C015176.n
                                                                  END ELSE BEGIN
                                                                    TESTFIELD("Total Line", 0);
                                                                    Monday := 0;
                                                                    Tuesday := 0;
                                                                    Wednesday := 0;
                                                                    Thursday := 0;
                                                                    Friday := 0;
                                                                    Saturday := 0;
                                                                    Sunday := 0;
                                                                    "Total Hours";
                                                                    "Unit Cost" := 0;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Hours,Expenses;
                                                   OptionString=Hours,Expenses }
    { 110 ;   ;Monday              ;Decimal       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF Monday <> xRec.Monday THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Monday <> 0 THEN  //*C019484.n
                                                                  TESTFIELD(Type, Type::Hours);
                                                                "Total Hours";
                                                              END;

                                                   CaptionML=ENU=Monday;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes }
    { 120 ;   ;Tuesday             ;Decimal       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF Tuesday <> xRec.Tuesday THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Tuesday <> 0 THEN  //*C019484.n
                                                                  TESTFIELD(Type, Type::Hours);
                                                                "Total Hours";
                                                              END;

                                                   CaptionML=ENU=Tuesday;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes }
    { 130 ;   ;Wednesday           ;Decimal       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF Wednesday <> xRec.Wednesday THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Wednesday <> 0 THEN  //*C019484.n
                                                                  TESTFIELD(Type, Type::Hours);
                                                                "Total Hours";
                                                              END;

                                                   CaptionML=ENU=Wednesday;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes }
    { 140 ;   ;Thursday            ;Decimal       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF Thursday <> xRec.Thursday THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Thursday <> 0 THEN  //*C019484.n
                                                                  TESTFIELD(Type, Type::Hours);
                                                                "Total Hours";
                                                              END;

                                                   CaptionML=ENU=Thursday;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes }
    { 150 ;   ;Friday              ;Decimal       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF Friday <> xRec.Friday THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Friday <> 0 THEN  //*C019484.n
                                                                  TESTFIELD(Type, Type::Hours);
                                                                "Total Hours";
                                                              END;

                                                   CaptionML=ENU=Friday;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes }
    { 160 ;   ;Saturday            ;Decimal       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF Saturday <> xRec.Saturday THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Saturday <> 0 THEN  //*C019484.n
                                                                  TESTFIELD(Type, Type::Hours);
                                                                "Total Hours";
                                                              END;

                                                   CaptionML=ENU=Saturday;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes }
    { 170 ;   ;Sunday              ;Decimal       ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                IF Sunday <> xRec.Sunday THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                IF Sunday <> 0 THEN  //*C019484.n
                                                                  TESTFIELD(Type, Type::Hours);
                                                                "Total Hours";
                                                              END;

                                                   CaptionML=ENU=Sunday;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes }
    { 180 ;   ;Total Line          ;Decimal       ;CaptionML=ENU=Total Line;
                                                   DecimalPlaces=0:3;
                                                   Editable=No }
    { 190 ;   ;Total Monday        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Costs Line".Monday WHERE (Year=FIELD(Year),
                                                                                                          Week=FIELD(Week),
                                                                                                          Employee Company=FIELD(Employee Company),
                                                                                                          Employee No.=FIELD(Employee No.)));
                                                   CaptionML=ENU=Total Monday;
                                                   Editable=No }
    { 200 ;   ;Total Tuesday       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Costs Line".Tuesday WHERE (Year=FIELD(Year),
                                                                                                           Week=FIELD(Week),
                                                                                                           Employee Company=FIELD(Employee Company),
                                                                                                           Employee No.=FIELD(Employee No.)));
                                                   CaptionML=ENU=Total Tuesday;
                                                   Editable=No }
    { 210 ;   ;Total Wednesday     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Costs Line".Wednesday WHERE (Year=FIELD(Year),
                                                                                                             Week=FIELD(Week),
                                                                                                             Employee Company=FIELD(Employee Company),
                                                                                                             Employee No.=FIELD(Employee No.)));
                                                   CaptionML=ENU=Total Wednesday;
                                                   Editable=No }
    { 220 ;   ;Total Thursday      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Costs Line".Thursday WHERE (Year=FIELD(Year),
                                                                                                            Week=FIELD(Week),
                                                                                                            Employee Company=FIELD(Employee Company),
                                                                                                            Employee No.=FIELD(Employee No.)));
                                                   CaptionML=ENU=Total Thursday;
                                                   Editable=No }
    { 230 ;   ;Total Friday        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Costs Line".Friday WHERE (Year=FIELD(Year),
                                                                                                          Week=FIELD(Week),
                                                                                                          Employee Company=FIELD(Employee Company),
                                                                                                          Employee No.=FIELD(Employee No.)));
                                                   CaptionML=ENU=Total Friday;
                                                   Editable=No }
    { 240 ;   ;Total Saturday      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Costs Line".Saturday WHERE (Year=FIELD(Year),
                                                                                                            Week=FIELD(Week),
                                                                                                            Employee Company=FIELD(Employee Company),
                                                                                                            Employee No.=FIELD(Employee No.)));
                                                   CaptionML=ENU=Total Saturday;
                                                   Editable=No }
    { 250 ;   ;Total Sunday        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Costs Line".Sunday WHERE (Year=FIELD(Year),
                                                                                                          Week=FIELD(Week),
                                                                                                          Employee Company=FIELD(Employee Company),
                                                                                                          Employee No.=FIELD(Employee No.)));
                                                   CaptionML=ENU=Total Sunday;
                                                   Editable=No }
    { 330 ;   ;Total Normal        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Hours Costs Line"."Total Line" WHERE (Year=FIELD(Year),
                                                                                                                Week=FIELD(Week),
                                                                                                                Employee Company=FIELD(Employee Company),
                                                                                                                Employee No.=FIELD(Employee No.)));
                                                   CaptionML=ENU=Total Normal;
                                                   Editable=No }
    { 340 ;   ;Unit Cost           ;Decimal       ;OnValidate=BEGIN
                                                                IF "Unit Cost" <> 0 THEN  //*C019484.n
                                                                  TESTFIELD(Type, Type::Hours);
                                                              END;

                                                   CaptionML=ENU=Unit Cost;
                                                   Description=Only for hours (not on form, may not be visible for the user);
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 350 ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF Quantity <> xRec. Quantity THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                //*C019484.n
                                                                IF Quantity <> 0 THEN
                                                                  TESTFIELD(Type, Type::Expenses);

                                                                Amount := ROUND(Quantity * "Unit Rate");
                                                              END;

                                                   CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:3;
                                                   BlankZero=Yes;
                                                   Description=Only for expenses }
    { 360 ;   ;Unit Rate           ;Decimal       ;OnValidate=BEGIN
                                                                IF "Unit Rate" <> xRec. "Unit Rate" THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                //*C019484.n
                                                                IF "Unit Rate" <> 0 THEN
                                                                  TESTFIELD(Type, Type::Expenses);

                                                                Amount := ROUND(Quantity * "Unit Rate");
                                                              END;

                                                   CaptionML=ENU=Unit Rate;
                                                   BlankNumbers=BlankNeg;
                                                   BlankZero=Yes;
                                                   Description=Only for expenses;
                                                   AutoFormatType=2 }
    { 370 ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                IF Amount <> xRec. Amount THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                //*C019484.n
                                                                IF Amount <> 0 THEN BEGIN
                                                                  TESTFIELD(Type, Type::Expenses);
                                                                  TESTFIELD(Quantity);
                                                                END;

                                                                IF Quantity = 0 THEN
                                                                  "Unit Rate" := 0
                                                                ELSE BEGIN
                                                                  GLSetup.GET;
                                                                  "Unit Rate" := ROUND((Amount / Quantity), GLSetup."Unit-Amount Rounding Precision");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Amount;
                                                   BlankZero=Yes;
                                                   Description=Only for expenses;
                                                   AutoFormatType=1 }
    { 375 ;   ;Posting Date (Exp.) ;Date          ;OnValidate=BEGIN
                                                                //C015176.n
                                                                IF Type <> Type::Expenses THEN
                                                                  TESTFIELD("Posting Date (Exp.)", 0D)
                                                                ELSE BEGIN
                                                                  IF "Posting Date (Exp.)" <> 0D THEN BEGIN
                                                                    IF ("Posting Date (Exp.)" < DWY2DATE(1,Week,Year)) OR ("Posting Date (Exp.)" > DWY2DATE(7,Week,Year)) THEN
                                                                      FIELDERROR("Posting Date (Exp.)", STRSUBSTNO(Text001, Year, Week, DWY2DATE(1,Week,Year), DWY2DATE(7,Week,Year)));
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Posting Date (Exp.);
                                                   Description=Only for expenses }
    { 380 ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Cost Type=FILTER(<Revenue),
                                                                                               Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                IF "Cost Object" <> xRec."Cost Object" THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                //*C019484.n
                                                                IF "Cost Object" = '' THEN
                                                                  EXIT;

                                                                TESTFIELD(Type, Type::Expenses);

                                                                IF (CurrFieldNo = FIELDNO("Cost Object")) AND ("Cost Object" <> xRec."Cost Object") AND (Origin = Origin::Plant) THEN BEGIN
                                                                  VALIDATE("Unit Rate", GetUnitPriceExpenses());
                                                                  DimMgt.GetDimValueRec(2, "Cost Object", DimVal, FALSE, '');
                                                                  IF DimVal."Wage Component" <> '' THEN BEGIN
                                                                    IF WageComp.GET(DimVal."Wage Component") THEN BEGIN
                                                                      IF WageComp."Component Type" = WageComp."Component Type"::Expenses THEN
                                                                        "Wage Component Expenses" := DimVal."Wage Component";
                                                                    END;
                                                                  END;
                                                                END;

                                                                CALCFIELDS("Cost Object Description");
                                                              END;

                                                   CaptionML=ENU=Cost Object;
                                                   Description=Only for expenses }
    { 381 ;   ;Cost Object Description;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value".Name WHERE (Global Dimension No.=FILTER(2),
                                                                                                    Code=FIELD(Cost Object)));
                                                   CaptionML=ENU=Cost Object Description;
                                                   Editable=No }
    { 382 ;   ;Cost Object Unit of Measure;Code10 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value"."Unit of Measure" WHERE (Global Dimension No.=FILTER(2),
                                                                                                                 Code=FIELD(Cost Object)));
                                                   CaptionML=ENU=Cost Object Unit of Measure;
                                                   Editable=No }
    { 390 ;   ;Wage Component Expenses;Code10     ;TableRelation="Wage Component" WHERE (Component Type=CONST(Expenses));
                                                   OnValidate=BEGIN
                                                                IF "Wage Component Expenses" <> xRec."Wage Component Expenses" THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                                //*C019484.n
                                                                IF "Wage Component Expenses" <> '' THEN
                                                                  TESTFIELD(Type, Type::Expenses);
                                                              END;

                                                   CaptionML=ENU=Wage Component Expenses;
                                                   Description=Only for expenses }
    { 430 ;   ;Comment             ;Text50        ;OnValidate=BEGIN
                                                                IF Comment <> xRec.Comment THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                              END;

                                                   CaptionML=ENU=Comment }
    { 440 ;   ;Plant Description   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number".Description WHERE (Plant Type=FIELD(Plant Type),
                                                                                                        No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Plant Description;
                                                   Editable=No }
    { 490 ;   ;Origin              ;Option        ;CaptionML=ENU=Origin;
                                                   OptionCaptionML=ENU=Plant,HourAccounting;
                                                   OptionString=Plant,HourAcc;
                                                   Editable=No }
    { 530 ;   ;Department Code     ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                IF "Department Code" <> xRec."Department Code" THEN
                                                                  TESTFIELD(Status,Status::Open);
                                                              END;

                                                   CaptionML=ENU=Department Code;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Year,Week,Employee Company,Employee No.,Line No.;
                                                   SumIndexFields=Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday,Total Line;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Year,Week,Cost Component,Employee Company,Employee No. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PlantSetup@1100485002 : Record 11012550;
      GLSetup@1210190000 : Record 98;
      WageComp@1210190003 : Record 11012014;
      DimVal@1210190002 : Record 349;
      DimMgt@1210190001 : Codeunit 408;
      Text001@1100525000 : TextConst 'ENU=should be in week %1-%2 (%3..%4)';

    PROCEDURE "Total Hours"@1();
    BEGIN
      "Total Line" := Monday + Tuesday + Wednesday + Thursday + Friday + Saturday + Sunday;
    END;

    PROCEDURE GetEmployeeName@1100525000() : Text[100];
    VAR
      lvEmplRec@1100525000 : Record 5200;
    BEGIN
      IF ("Employee No." <> '') THEN BEGIN
        IF ("Employee Company" <> '') THEN
          lvEmplRec.CHANGECOMPANY("Employee Company");
        IF lvEmplRec.GET("Employee No.") THEN
          EXIT(lvEmplRec."Full Name")
      END;
      EXIT('');
    END;

    PROCEDURE GetUnitPriceExpenses@1100485003() : Decimal;
    VAR
      EmplPriceCostObject@1100485000 : Record 11020259;
    BEGIN
      //*C019484.n
      IF "Employee No." <> '' THEN BEGIN
        IF "Employee Company" <> '' THEN
          EmplPriceCostObject.CHANGECOMPANY("Employee Company");
        EmplPriceCostObject.SETRANGE("Employee No.","Employee No.");
        EmplPriceCostObject.SETRANGE("Cost Object","Cost Object");
        EmplPriceCostObject.SETRANGE("Starting Date", 0D, DWY2DATE(7,Week,Year));
        IF EmplPriceCostObject.FINDLAST THEN BEGIN
          EXIT(EmplPriceCostObject."Cost Price");
        END;
      END;

      DimMgt.GetDimValueRec(2, "Cost Object", DimVal, FALSE, '');
      EXIT(DimVal.Price);
    END;

    PROCEDURE FillDepartmentCode@1100409000() : Code[20];
    VAR
      TableID@1100409001 : ARRAY [10] OF Integer;
      No@1100409000 : ARRAY [10] OF Code[20];
      GlobalDim1Code@1100409003 : Code[20];
      GlobalDim2Code@1100525000 : Code[20];
    BEGIN
      TableID[1] := DATABASE::"Plant Number";
      No[1] := DimMgt.MakeCombinedPlantNo("Plant Type", "Plant No.");
      TableID[2] := DATABASE::"Plant Type";
      No[2] := "Plant Type";

      DimMgt.GetDefaultDimID(
        TableID,No,'',
        GlobalDim1Code,GlobalDim2Code,
        0,0);

      VALIDATE("Department Code",  GlobalDim1Code);
    END;

    PROCEDURE ValidatePlantNo@1100525031();
    VAR
      PlantType@1100525000 : Record 11012551;
      PurchaseLine@1100525001 : Record 39;
      PlantSetup@1100525002 : Record 11012550;
    BEGIN
      //**4PS
      IF ("Plant Type" <> '') AND ("Plant No." = '') THEN BEGIN
        PlantSetup.GET;
        IF PlantSetup."Posting on Unique Plant No." THEN
          IF PlantType.GET("Plant Type") THEN
            IF NOT PlantType.Bulk THEN
              IF PlantSetup."Cost Component Acquisition" <> "Cost Component" THEN
                TESTFIELD("Plant No.");
      END;
    END;

    BEGIN
    {
      24-10-2002 Cor Hofman: Copied from table "Plant Hour Revenues Line"
    }
    END.
  }
}

