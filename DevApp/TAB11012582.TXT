OBJECT Table 11012582 Plant Profile Answer
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lvPlantQuestionListRec@1210190000 : Record 11012581;
               lvPlantQuestionListRec2@1210190001 : Record 11012581;
               lvPlantProfileAnswerRec@1210190003 : Record 11012582;
               lvLineNo@1210190002 : Integer;
             BEGIN
               lvPlantQuestionListRec.GET("Profile Code","Line No.");
               lvPlantQuestionListRec.TESTFIELD(Type,lvPlantQuestionListRec.Type::Answer);

               lvLineNo := QuestionLineNo();
               lvPlantQuestionListRec2.GET("Profile Code",lvLineNo);

               IF NOT lvPlantQuestionListRec2."Multiple Answers" THEN BEGIN
                 lvPlantQuestionListRec2.SETRANGE("Profile Code","Profile Code");
                 lvPlantQuestionListRec2.SETRANGE(Type,lvPlantQuestionListRec2.Type::Question);
                 lvPlantQuestionListRec2.SETFILTER("Line No.", '>%1',lvLineNo);
                 IF lvPlantQuestionListRec2.FIND('-') THEN
                   lvPlantProfileAnswerRec.SETRANGE("Line No.",lvLineNo,(lvPlantQuestionListRec2."Line No." - 1))
                 ELSE
                   lvPlantProfileAnswerRec.SETFILTER("Line No.", '>=%1',lvPlantQuestionListRec2."Line No.");
                 lvPlantProfileAnswerRec.SETRANGE("Plant Type", "Plant Type");
                 lvPlantProfileAnswerRec.SETRANGE("Plant No.", "Plant No.");
                 lvPlantProfileAnswerRec.SETRANGE("Profile Code","Profile Code");
                 IF lvPlantProfileAnswerRec.FIND('-') THEN
                   ERROR(Text000,lvPlantQuestionListRec.FIELDCAPTION("Multiple Answers"));
               END;

               "Question Line No." := lvLineNo;
             END;

    CaptionML=ENU=Plant Profile Answer;
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   CaptionML=ENU=Plant Type }
    { 2   ;   ;Profile Code        ;Code10        ;TableRelation="Plant Question Profile";
                                                   CaptionML=ENU=Profile Code;
                                                   NotBlank=Yes }
    { 3   ;   ;Line No.            ;Integer       ;TableRelation="Plant Question List"."Line No." WHERE (Profile Code=FIELD(Profile Code),
                                                                                                         Line No.=FIELD(Line No.),
                                                                                                         Type=CONST(Answer));
                                                   CaptionML=ENU=Line No. }
    { 4   ;   ;Answer              ;Text50        ;CaptionML=ENU=Technical Specification }
    { 5   ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   CaptionML=ENU=Plant No.;
                                                   Editable=No }
    { 10  ;   ;Feature             ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Question List".Description WHERE (Profile Code=FIELD(Profile Code),
                                                                                                               Line No.=FIELD(Line No. Question),
                                                                                                               Type=CONST(Question)));
                                                   CaptionML=ENU=Feature;
                                                   Description=Only for first answer of a question (because "Line No. Question" is only filled for first);
                                                   Editable=No }
    { 11  ;   ;Line No. Question   ;Integer       ;TableRelation="Plant Question List"."Line No." WHERE (Profile Code=FIELD(Profile Code));
                                                   CaptionML=ENU=Line No. Question;
                                                   Description=May only be filled in the first answer of a question;
                                                   Editable=No }
    { 12  ;   ;Feature (Question)  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Question List".Description WHERE (Profile Code=FIELD(Profile Code),
                                                                                                               Line No.=FIELD(Question Line No.),
                                                                                                               Type=CONST(Question)));
                                                   CaptionML=ENU=Feature (Question);
                                                   Description=For each answer of a question ("Question Line No." is filled for each);
                                                   Editable=No }
    { 13  ;   ;Question Line No.   ;Integer       ;TableRelation="Plant Question List"."Line No." WHERE (Profile Code=FIELD(Profile Code));
                                                   CaptionML=ENU=Question Line No.;
                                                   Description=Must be filled in each answer of a question;
                                                   Editable=Yes }
    { 15  ;   ;Profile Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Question Profile".Description WHERE (Code=FIELD(Profile Code)));
                                                   CaptionML=ENU=Profile Description;
                                                   Editable=No }
    { 30  ;   ;Plant Type Description;Text50      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Description WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=ENU=Plant Type Description;
                                                   Editable=No }
    { 31  ;   ;Plant No. Description;Text50       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number".Description WHERE (Plant Type=FIELD(Plant Type),
                                                                                                        No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Plant No. Description;
                                                   Editable=No }
    { 40  ;   ;Copy To Plant No.   ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("Plant No.", ''); //mg, 12-04-11: M25005
                                                              END;

                                                   CaptionML=ENU=Copy To Plant No. }
  }
  KEYS
  {
    {    ;Plant Type,Plant No.,Profile Code,Line No.;
                                                   Clustered=Yes }
    {    ;Profile Code,Line No.                    }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This Question does not allow %1.';

    PROCEDURE GetQuestion@1() : Text[50];
    VAR
      lvPlantQuestionListRec@1210190000 : Record 11012581;
    BEGIN
      IF lvPlantQuestionListRec.GET("Profile Code", QuestionLineNo()) THEN
        EXIT(lvPlantQuestionListRec.Description)
      ELSE
        EXIT('');
    END;

    PROCEDURE QuestionLineNo@3() : Integer;
    VAR
      lvPlantQuestionListRec@1210190000 : Record 11012581;
    BEGIN
      WITH lvPlantQuestionListRec DO BEGIN
        RESET;
        SETRANGE("Profile Code",Rec."Profile Code");
        SETFILTER("Line No.",'<%1',Rec."Line No.");
        SETRANGE(Type,Type::Question);
        IF FIND('+') THEN
          EXIT("Line No.");
      END;
      EXIT(-1);
    END;

    BEGIN
    END.
  }
}

