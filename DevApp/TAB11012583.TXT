OBJECT Table 11012583 Rental Rate by Location
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Rental Rate by Location;
  }
  FIELDS
  {
    { 2   ;   ;Location            ;Code20        ;TableRelation="Plant Location".Code;
                                                   CaptionML=ENU=Location;
                                                   NotBlank=Yes }
    { 5   ;   ;Rental Type         ;Option        ;OnValidate=BEGIN
                                                                IF PlantRec.GET("Plant Type") THEN BEGIN
                                                                  IF ("Rental Type" = "Rental Type"::Rental) AND (PlantRec."Rental Type" = PlantRec."Rental Type"::"Sales/Buy Back") THEN
                                                                    FIELDERROR("Rental Type");

                                                                  IF ("Rental Type" = "Rental Type"::"Sales / Buy Back") AND (PlantRec."Rental Type" = PlantRec."Rental Type"::Rental) THEN
                                                                    FIELDERROR("Rental Type");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Rental Type;
                                                   OptionCaptionML=ENU=Rental,Sales / Buy Back,Plant Hours;
                                                   OptionString=Rental,Sales / Buy Back,Plant Hours;
                                                   Editable=No }
    { 10  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Plant Description", "Plant Unit of Measure");
                                                                CalcfieldRateUnit();
                                                              END;

                                                   CaptionML=ENU=Plant Type;
                                                   NotBlank=Yes }
    { 15  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Plant No. Description");
                                                              END;

                                                   CaptionML=ENU=Plant No. }
    { 20  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                CheckStartEndDate;
                                                              END;

                                                   CaptionML=ENU=Starting Date }
    { 25  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                CheckStartEndDate;
                                                              END;

                                                   CaptionML=ENU=Ending Date }
    { 30  ;   ;Rental Rate (Days)  ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Rental Type","Rental Type"::Rental);
                                                              END;

                                                   CaptionML=ENU=Rental Rate (Day);
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 35  ;   ;Rental Rate (Hours) ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Rental Type","Rental Type"::"Plant Hours");
                                                              END;

                                                   CaptionML=ENU=Rental Rate (Days);
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 40  ;   ;Plant Description   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Description WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=ENU=Plant Type Description;
                                                   Editable=No }
    { 41  ;   ;Plant Unit of Measure;Code10       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type"."Unit of Measure" WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=ENU=Plant Unit of Measure }
    { 42  ;   ;Rate Unit of Measure;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type"."Rate Unit of Measure" WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=ENU=Rate Unit of Measure;
                                                   Editable=No }
    { 45  ;   ;Plant No. Description;Text50       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number".Description WHERE (Plant Type=FIELD(Plant Type),
                                                                                                        No.=FIELD(Plant No.)));
                                                   CaptionML=ENU=Plant No. Description;
                                                   Editable=No }
    { 50  ;   ;Sales Rate          ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Rental Type","Rental Type"::"Sales / Buy Back");
                                                                VALIDATE("% of Sales Rate");
                                                              END;

                                                   CaptionML=ENU=Sales Rate;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 60  ;   ;% of Sales Rate     ;Decimal       ;OnValidate=BEGIN
                                                                IF "% of Sales Rate" <> 0 THEN BEGIN
                                                                  TESTFIELD("Rental Type","Rental Type"::"Sales / Buy Back");
                                                                  "Buy Back Rate" := ROUND("Sales Rate" * "% of Sales Rate" / 100,PriceRoundingPrecision);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=% of Sales Rate;
                                                   BlankZero=Yes }
    { 70  ;   ;Buy Back Rate       ;Decimal       ;OnValidate=BEGIN
                                                                IF "Sales Rate" <> 0 THEN BEGIN
                                                                  TESTFIELD("Rental Type","Rental Type"::"Sales / Buy Back");
                                                                  "% of Sales Rate" := ROUND("Buy Back Rate" / "Sales Rate" * 100, 0.00001);
                                                                END ELSE
                                                                  "% of Sales Rate" := 0;
                                                              END;

                                                   CaptionML=ENU=Buy Back Rate;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 90  ;   ;Rental Rate (Month) ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Rental Type","Rental Type"::Rental);
                                                              END;

                                                   CaptionML=ENU=Rental Rate (Month);
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 100 ;   ;Rental Rate (Week)  ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Rental Type","Rental Type"::Rental);
                                                              END;

                                                   CaptionML=ENU=Rental Rate (Week);
                                                   MinValue=0;
                                                   AutoFormatType=2 }
  }
  KEYS
  {
    {    ;Location,Rental Type,Plant Type,Plant No.,Starting Date;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PlantSetupRec@1100485000 : Record 11012550;
      GLSetup@11012000 : Record 98;
      PlantRec@11012001 : Record 11012551;
      Text001@1100525000 : TextConst 'ENU=%1 may not lie before the %2';

    PROCEDURE PriceRoundingPrecision@1() : Decimal;
    BEGIN
      GLSetup.GET;
      GLSetup.TESTFIELD("Unit-Amount Rounding Precision");
      EXIT(GLSetup."Unit-Amount Rounding Precision");
    END;

    PROCEDURE CalcfieldRateUnit@1100485000() : Code[10];
    BEGIN
      CALCFIELDS("Plant Unit of Measure", "Rate Unit of Measure");
      CASE "Rental Type" OF
        "Rental Type"::Rental:
          IF ("Rate Unit of Measure" = '') THEN
            "Rate Unit of Measure" := "Plant Unit of Measure";
        "Rental Type"::"Sales / Buy Back":
          "Rate Unit of Measure" := "Plant Unit of Measure";
        "Rental Type"::"Plant Hours":
          BEGIN
            PlantSetupRec.GET;
            "Rate Unit of Measure" := PlantSetupRec."Plant Hours Unit of Measure";
          END;
        ELSE
          "Rate Unit of Measure" := '';
      END;
      EXIT("Rate Unit of Measure");
    END;

    PROCEDURE CheckStartEndDate@1100525000();
    BEGIN
      IF ("Ending Date" <> 0D) AND ("Ending Date" < "Starting Date") THEN
        ERROR(Text001, FIELDCAPTION("Ending Date"), FIELDCAPTION("Starting Date"));
    END;

    BEGIN
    {
      04-10-2002 Cor Hofman Field "Rental Rate (Hours)" added for "Plant Hour Accounting"
    }
    END.
  }
}

