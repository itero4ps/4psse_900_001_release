OBJECT Table 11012588 Plant Set Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Plant Set Line;
  }
  FIELDS
  {
    { 10  ;   ;Set Code            ;Code10        ;TableRelation="Plant Set";
                                                   CaptionML=ENU=Set }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 25  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Type = Type::Item THEN
                                                                  CheckLicenseForPlantOrderItems(TRUE);

                                                                IF Type <> xRec.Type THEN BEGIN
                                                                  "Plant Type" := '';
                                                                  "Plant No." := '';
                                                                  "Item No." := '';
                                                                  "Default Rate Code" := '';
                                                                  Quantity := 0;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Plant,Item;
                                                   OptionString=Plant,Item }
    { 30  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   OnValidate=BEGIN
                                                                IF "Plant Type" <> '' THEN
                                                                  TESTFIELD(Type, Type::Plant);

                                                                CALCFIELDS(Bulk, "Unit of Measure");

                                                                IF "Plant Type" <> xRec."Plant Type" THEN BEGIN
                                                                  IF ("Plant Type" <> '') AND ("Default Rate Code" <> '') THEN BEGIN
                                                                    PlantTypeRec.GET("Plant Type");
                                                                    IF PlantTypeRec."Rental Type" <> PlantTypeRec."Rental Type"::Rental THEN
                                                                      "Default Rate Code" := '';
                                                                  END;
                                                                END;

                                                                IF Type = Type::Plant THEN BEGIN
                                                                  IF Bulk THEN
                                                                    "Plant No." := ''
                                                                  ELSE BEGIN
                                                                    IF NOT PlantNoRec.GET("Plant Type", "Plant No.") THEN
                                                                      "Plant No." := '';
                                                                  END;
                                                                  IF "Plant No." = '' THEN
                                                                    VALIDATE(Quantity);

                                                                  GetPlantDescription;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Plant Type }
    { 32  ;   ;Bulk                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Bulk WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=ENU=Bulk;
                                                   Editable=No }
    { 33  ;   ;Unit of Measure     ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type"."Unit of Measure" WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=ENU=Unit of Measure;
                                                   Editable=No }
    { 40  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   OnValidate=BEGIN
                                                                IF "Plant No." <> '' THEN
                                                                  TESTFIELD(Type, Type::Plant);

                                                                CALCFIELDS(Bulk);

                                                                IF Type = Type::Plant THEN BEGIN
                                                                  IF Bulk THEN
                                                                    TESTFIELD("Plant No.", '');

                                                                  IF ("Plant No." <> '') AND (NOT Bulk) THEN BEGIN
                                                                    IF Quantity <> 1 THEN
                                                                      VALIDATE(Quantity, 1)
                                                                  END;

                                                                  GetPlantDescription;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 45  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF "Item No." <> '' THEN BEGIN
                                                                  TESTFIELD(Type, Type::Item);
                                                                  CheckLicenseForPlantOrderItems(TRUE);
                                                                END;

                                                                CALCFIELDS("Item Description", "Sales Unit of Measure");
                                                              END;

                                                   CaptionML=ENU=Item No. }
    { 46  ;   ;Item Description    ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item.Description WHERE (No.=FIELD(Item No.)));
                                                   CaptionML=ENU=Item Description;
                                                   Editable=No }
    { 47  ;   ;Sales Unit of Measure;Code10       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item."Sales Unit of Measure" WHERE (No.=FIELD(Item No.)));
                                                   CaptionML=ENU=Sales Unit of Measure;
                                                   Editable=No }
    { 50  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                CALCFIELDS(Bulk);

                                                                IF Type = Type::Plant THEN BEGIN
                                                                  IF ("Plant No." <> '') AND (NOT Bulk) AND (NOT (Quantity IN [0,1])) THEN
                                                                    FIELDERROR(Quantity, Text000);

                                                                  IF ("Plant No." = '') AND (NOT Bulk) AND ((Quantity MOD 1) <> 0) THEN
                                                                    Quantity := ROUND(Quantity, 1);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 60  ;   ;Default Rate Code   ;Code10        ;TableRelation="Plant Rate Code";
                                                   OnValidate=BEGIN
                                                                IF "Default Rate Code" <> '' THEN BEGIN
                                                                  TESTFIELD(Type, Type::Plant);
                                                                  TESTFIELD("Plant Type");
                                                                  PlantSetup.GET;
                                                                  IF NOT PlantSetup."Rate Codes" THEN
                                                                    TESTFIELD("Default Rate Code", '');
                                                                  PlantTypeRec.GET("Plant Type");
                                                                  IF NOT (PlantTypeRec."Rental Type" IN [PlantTypeRec."Rental Type"::Rental, PlantTypeRec."Rental Type"::Both]) THEN
                                                                    PlantTypeRec.FIELDERROR("Rental Type");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Default Rate Code }
  }
  KEYS
  {
    {    ;Set Code,Line No.                       ;Clustered=Yes }
    {    ;Plant Type,Plant No.                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PlantSetup@1210190000 : Record 11012550;
      PlantTypeRec@1210190001 : Record 11012551;
      PlantNoRec@1100485000 : Record 11012552;
      Text000@1100485001 : TextConst 'ENU=must be 0 or 1 for unique Plant';

    PROCEDURE CheckLicenseForPlantOrderItems@1100485001(IErrorMode@1100485000 : Boolean) : Boolean;
    VAR
      lvLicensePlantExtendedCU@1100485001 : Codeunit 11012579;
    BEGIN
      IF lvLicensePlantExtendedCU.LicenseTablePlantItem(IErrorMode,11012588) THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    PROCEDURE GetSetLineDescrInPlantCompany@1210190000(PlantCompany@1210190000 : Text[50]) : Text[100];
    VAR
      PlantType@1210190001 : Record 11012551;
      PlantNumber@1210190002 : Record 11012552;
      Item@1210190003 : Record 27;
    BEGIN
      IF (PlantCompany = '') OR (PlantCompany = COMPANYNAME) THEN
        EXIT(GetSetLineDescription());

      CASE Type OF
        Type::Plant:
          BEGIN
            IF "Plant No." <> '' THEN BEGIN
              PlantNumber.CHANGECOMPANY(PlantCompany);
              IF PlantNumber.GET("Plant Type", "Plant No.") THEN BEGIN
                IF PlantNumber.Description <> '' THEN
                  EXIT(PlantNumber.Description);
              END;
            END;
            IF "Plant Type" <> '' THEN BEGIN
              PlantType.CHANGECOMPANY(PlantCompany);
              IF PlantType.GET("Plant Type") THEN
                EXIT(PlantType.Description);
            END;
          END;
        Type::Item:
          BEGIN
            IF "Item No." <> '' THEN BEGIN
              Item.CHANGECOMPANY(PlantCompany);
              IF Item.GET("Item No.") THEN
                EXIT(Item.Description);
            END;
          END;
      END;
      EXIT('');
    END;

    PROCEDURE GetSetLineDescription@1100485002() RetDescription : Text[100];
    BEGIN
      RetDescription := '';
      CASE Type OF
        Type::Plant:
          RetDescription := GetPlantDescription();
        Type::Item:
          BEGIN
            CALCFIELDS("Item Description");
            RetDescription := "Item Description";
          END;
      END;
    END;

    PROCEDURE GetPlantDescription@1100485000() RetDescription : Text[100];
    BEGIN
      RetDescription := '';
      PlantNoRec.GetPlantDescription("Plant Type", "Plant No.", RetDescription);
    END;

    BEGIN
    END.
  }
}

