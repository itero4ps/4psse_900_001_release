OBJECT Table 11012596 Plant Vendor
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               lvPlantPurchPriceRec@1100485000 : Record 11012597;
               lvPlantPurchDiscRec@1100485001 : Record 11012598;
             BEGIN
               lvPlantPurchPriceRec.SETRANGE("Price Type", Type);
               lvPlantPurchPriceRec.SETRANGE("Plant Type", "Plant Type");
               lvPlantPurchPriceRec.DELETEALL();

               lvPlantPurchDiscRec.SETRANGE("Price Type", Type);
               lvPlantPurchDiscRec.SETRANGE("Plant Type", "Plant Type");
               lvPlantPurchDiscRec.DELETEALL();
             END;

    OnRename=VAR
               lvPlantPurchPriceRec@1100485001 : Record 11012597;
               lvPlantPurchDiscRec@1100485000 : Record 11012598;
             BEGIN
               lvPlantPurchPriceRec.SETRANGE("Plant Type", xRec."Plant Type");
               lvPlantPurchPriceRec.SETRANGE("Vendor No.", xRec."Vendor No.");
               lvPlantPurchPriceRec.SETRANGE("Price Type", xRec.Type);
               IF lvPlantPurchPriceRec.FIND('-') THEN
                 ERROR(Text000, lvPlantPurchPriceRec.TABLECAPTION);

               lvPlantPurchDiscRec.SETRANGE("Plant Type", xRec."Plant Type");
               lvPlantPurchDiscRec.SETRANGE("Vendor No.", xRec."Vendor No.");
               lvPlantPurchDiscRec.SETRANGE("Price Type", xRec.Type);
               IF lvPlantPurchDiscRec.FIND('-') THEN
                 ERROR(Text000, lvPlantPurchDiscRec.TABLECAPTION);
             END;

    CaptionML=ENU=Plant Vendor;
    LookupPageID=Page11020579;
  }
  FIELDS
  {
    { 10  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   CaptionML=ENU=Plant Type;
                                                   NotBlank=Yes }
    { 15  ;   ;Plant Description   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Description WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=ENU=Plant Description;
                                                   Editable=No }
    { 20  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Vendor Name");
                                                              END;

                                                   CaptionML=ENU=Vendor No.;
                                                   NotBlank=Yes }
    { 25  ;   ;Vendor Name         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=ENU=Vendor Name;
                                                   Editable=No }
    { 30  ;   ;Vendor Plant No.    ;Code20        ;CaptionML=ENU=Vendor Plant No. }
    { 40  ;   ;Preferred Vendor    ;Boolean       ;OnValidate=BEGIN
                                                                IF "Preferred Vendor" AND (("Plant Type" = '') OR ("Vendor No." = '')) THEN
                                                                  "Preferred Vendor" := FALSE;
                                                                ResetPreferedOtherPlantVendors();
                                                              END;

                                                   CaptionML=ENU=Preferred Vendor }
    { 50  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Type <> xRec.Type THEN
                                                                  "Preferred Vendor" := FALSE;

                                                                IF (Type <> Type::Rent) AND ("External Rent Calendar" <> '') THEN
                                                                  "External Rent Calendar" := '';
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Purchase,Rent;
                                                   OptionString=Purchase,Rent }
    { 60  ;   ;External Rent Calendar;Code10      ;TableRelation="Plant Calendar";
                                                   OnValidate=BEGIN
                                                                IF ("External Rent Calendar" <> '') THEN
                                                                  TESTFIELD(Type, Type::Rent);
                                                              END;

                                                   CaptionML=ENU=External Rent Calendar }
    { 70  ;   ;Reorder Quantity    ;Decimal       ;CaptionML=ENU=Reorder Quantity;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
  }
  KEYS
  {
    {    ;Plant Type,Vendor No.,Type              ;Clustered=Yes }
    {    ;Vendor No.,Vendor Plant No.,Plant Type   }
    {    ;Vendor No.,Type,Plant Type               }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100485000 : TextConst 'ENU=Not allowed, present in ''%1''.';

    PROCEDURE ResetPreferedOtherPlantVendors@1100485000();
    VAR
      lvPlantVendRec@1100485000 : Record 11012596;
    BEGIN
      IF "Preferred Vendor" THEN BEGIN
        lvPlantVendRec.SETRANGE("Plant Type", "Plant Type");
        lvPlantVendRec.SETFILTER("Vendor No.", '<>%1', "Vendor No.");
        lvPlantVendRec.SETRANGE(Type, Type);
        lvPlantVendRec.SETRANGE("Preferred Vendor", TRUE);
        lvPlantVendRec.MODIFYALL("Preferred Vendor", FALSE);
      END;
    END;

    PROCEDURE GetPreferredVendorNo@1100530000(PlantType@1100530000 : Code[20];PurchType@1100530001 : Option;OneIsPreferred@1100530002 : Boolean) : Code[20];
    VAR
      PlantVend@1100530003 : Record 11012596;
    BEGIN
      IF (PlantType = '') OR (NOT (PurchType IN [Type::Purchase, Type::Rent])) THEN
        EXIT('');

      PlantVend.SETRANGE("Plant Type", PlantType);
      PlantVend.SETFILTER("Vendor No.", '<>%1', '');
      PlantVend.SETRANGE(Type, PurchType);
      PlantVend.SETRANGE("Preferred Vendor", TRUE);
      IF PlantVend.FINDFIRST THEN
        EXIT(PlantVend."Vendor No.");

      IF OneIsPreferred THEN BEGIN
        PlantVend.SETRANGE("Preferred Vendor");  //Reset filter
        IF PlantVend.COUNT = 1 THEN BEGIN
          IF PlantVend.FINDFIRST THEN
            EXIT(PlantVend."Vendor No.");
        END;
      END;

      EXIT('');
    END;

    BEGIN
    END.
  }
}

