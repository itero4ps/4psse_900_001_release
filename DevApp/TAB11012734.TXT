OBJECT Table 11012734 OLD Default SharePoint Permiss
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00,Obsolete;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TestFields;
             END;

    OnModify=BEGIN
               TestFields;
             END;

    OnRename=BEGIN
               ERROR('');
             END;

  }
  FIELDS
  {
    { 10  ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=" ,Internal,External";
                                                   OptionString=,Internal,External;
                                                   NotBlank=Yes }
    { 20  ;   ;Responsibility      ;Code10        ;TableRelation=Responsibility;
                                                   CaptionML=ENU=Responsibility }
    { 21  ;   ;Responsibility Description;Text100 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Responsibility.Description WHERE (Code=FIELD(Responsibility)));
                                                   CaptionML=ENU=Responsibility Description;
                                                   Editable=No }
    { 30  ;   ;Authority Type      ;Code20        ;TableRelation="Authority Type".Code;
                                                   CaptionML=ENU=Authority Type }
    { 31  ;   ;Authority Type Description;Text30  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Authority Type".Description WHERE (Code=FIELD(Authority Type)));
                                                   CaptionML=ENU=Authority Type Description;
                                                   Editable=No }
    { 40  ;   ;Library Code        ;Code10        ;TableRelation="SharePoint Library" WHERE (OLD Used in Extended SP Portal=CONST(Yes));
                                                   CaptionML=ENU=Library Code }
    { 50  ;   ;SharePoint Type     ;Option        ;CaptionML=ENU=SharePoint Type;
                                                   OptionCaptionML=ENU=None,Visitor,Member,Owner;
                                                   OptionString=None,Visitor,Member,Owner }
    { 60  ;   ;Show Company Documents Only;Boolean;CaptionML=ENU=Show Company Documents Only }
  }
  KEYS
  {
    {    ;Type,Responsibility,Authority Type,Library Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE TestFields@1100528303();
    BEGIN
      CASE Type OF
        Type::Internal:
         TESTFIELD(Responsibility);
        Type::External:
          TESTFIELD("Authority Type");
      END;
    END;

    PROCEDURE InsertRespPermissions@1100528301();
    VAR
      Responsibility@1100528300 : Record 11012007;
      SharePointLibrary@1100528303 : Record 11012209;
    BEGIN
      IF (NOT Responsibility.FINDSET) THEN
        EXIT;

      REPEAT
        InsertRec(1, Responsibility.Code, '', '');
        SharePointLibrary.SETRANGE("OLD Used in Extended SP Portal", TRUE);
        IF (SharePointLibrary.FINDSET) THEN BEGIN
          REPEAT
            InsertRec(1, Responsibility.Code, '', SharePointLibrary.Code);
          UNTIL (SharePointLibrary.NEXT = 0);
        END;
      UNTIL (Responsibility.NEXT = 0);
    END;

    PROCEDURE InsertAuthPermissions@1100528302();
    VAR
      AuthorityType@1100528301 : Record 11020215;
      SharePointLibrary@1100528300 : Record 11012209;
    BEGIN
      IF (NOT AuthorityType.FINDSET) THEN
        EXIT;

      REPEAT
        InsertRec(2, '', AuthorityType.Code, '');
        SharePointLibrary.SETRANGE("OLD Used in Extended SP Portal", TRUE);
        IF (SharePointLibrary.FINDSET) THEN BEGIN
          REPEAT
            InsertRec(2, '', AuthorityType.Code, SharePointLibrary.Code);
          UNTIL (SharePointLibrary.NEXT = 0);
        END;
      UNTIL (AuthorityType.NEXT = 0);
    END;

    LOCAL PROCEDURE InsertRec@1100528300(Type@1100528301 : Option;ResponsibilityCode@1100528302 : Code[20];AuthorityCode@1100528303 : Code[20];LibraryCode@1100528304 : Code[10]);
    VAR
      DefaultSharePointPermission@1100528300 : Record 11012734;
    BEGIN
      DefaultSharePointPermission.INIT;
      DefaultSharePointPermission.Type := Type;
      DefaultSharePointPermission.Responsibility := ResponsibilityCode;
      DefaultSharePointPermission."Authority Type" := AuthorityCode;
      DefaultSharePointPermission."Library Code" := LibraryCode;
      DefaultSharePointPermission."SharePoint Type" := DefaultSharePointPermission."SharePoint Type"::Visitor;

      IF (DefaultSharePointPermission.INSERT) THEN;
    END;

    BEGIN
    END.
  }
}

