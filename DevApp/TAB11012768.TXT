OBJECT Table 11012768 E-Mail On Behalf
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=E-Mail On Behalf;
  }
  FIELDS
  {
    { 10  ;   ;Usage               ;Option        ;CaptionML=ENU=Usage;
                                                   OptionCaptionML=ENU=" ,Sales Invoice,Sales Credit Memo,Sales Order,Sales Shipment,Reminder,Sales Quote,,,,,Purchase Quote,Purchase Order,,,,Option Quote,Plot,,,,,Service Order,,,,,,,,Hour Accounting,,Plant Transport Order,Plant Exit Order,Plant Order,,,,,Document Portal,Docket,Service Portal,Subcontrator Portal,Extension Contract,WKA Data Request,,,,,,,,Consent,,,,,Plant Reports,,Open Hour Accounting,Open Hour Consent Lines,Denied Hour Consent Lines,Changed Hour Consent Lines";
                                                   OptionString=[ ,Sales Invoice,Sales Credit Memo,Sales Order,Sales Shipment,Reminder,Sales Quote,,,,,Purchase Quote,Purchase Order,,,,Option Quote,Plot,,,,,Service Order,,,,,,,,Hour Accounting,,Plant Transport Order,Plant Exit Order,Plant Order,,,,,Project Portal,Docket,Service Portal,Subcontrator Portal,Extension Contract,WKA Data Request,,,,,,,,Consent,,,,,Plant Reports,,Open Hour Accounting,Open Hour Consent Lines,Denied Hour Consent Lines,Changed Hour Consent Lines];
                                                   NotBlank=Yes }
    { 20  ;   ;Send On Behalf Of Name;Text50      ;CaptionML=ENU=Send On Behalf Of Name }
    { 30  ;   ;Send On Behalf Of E-mail;Text50    ;CaptionML=ENU=Send On Behalf Of E-mail }
  }
  KEYS
  {
    {    ;Usage                                   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetUsageNoFromSalesHeader@1100528500(SalesHeader@1100528500 : Record 36) UsageNo : Integer;
    BEGIN
      CASE SalesHeader."Document Type" OF
        SalesHeader."Document Type"::Quote:
         UsageNo := Usage::"Sales Quote";
        SalesHeader."Document Type"::Order:
         UsageNo := Usage::"Sales Order";
        SalesHeader."Document Type"::Invoice:
         UsageNo := Usage::"Sales Invoice";
        SalesHeader."Document Type"::"Credit Memo":
         UsageNo := Usage::"Sales Credit Memo";
      END;
    END;

    PROCEDURE GetUsageNoFromPurchHeader@1100528501(PurchaseHeader@1100528500 : Record 38) UsageNo : Integer;
    BEGIN
      CASE PurchaseHeader."Document Type" OF
        PurchaseHeader."Document Type"::Quote:
          UsageNo := Usage::"Purchase Quote";
        PurchaseHeader."Document Type"::Order:
         UsageNo := Usage::"Purchase Order";
      END;
    END;

    PROCEDURE CheckEMailOnBehalf@1100526202(Usage2@1100528402 : Integer);
    VAR
      OnBehalfOfName@1100528400 : Text[50];
      OnBehalfOfEMail@1100528401 : Text[80];
    BEGIN
      GetApplicableEMailOnBehalf(Usage2, TRUE, OnBehalfOfName, OnBehalfOfEMail);
    END;

    PROCEDURE GetApplicableEMailOnBehalf@1100528400(Usage2@1100528400 : Integer;ThrowError@1100528401 : Boolean;VAR OnBehalfOfName@1100528403 : Text[50];VAR OnBehalfOfEMail@1100528402 : Text[80]) : Boolean;
    VAR
      CompanyInformation@1100528405 : Record 79;
      Found@1100528406 : Boolean;
    BEGIN
      Found := GetEMailOnBehalf(Usage2, ThrowError);

      IF NOT Found THEN
        IF Usage2 IN [
          Usage::"Plant Transport Order",
          Usage::"Plant Exit Order",
          Usage::"Plant Order"]
        THEN
          Found := GetEMailOnBehalf(Usage::"Plant Reports", ThrowError);

      IF Found THEN BEGIN
        OnBehalfOfName := "Send On Behalf Of Name";
        OnBehalfOfEMail := "Send On Behalf Of E-mail";
      END ELSE BEGIN
        IF ThrowError THEN BEGIN
          CompanyInformation.GET;
          CompanyInformation.TESTFIELD(Name);
          CompanyInformation.TESTFIELD("E-Mail");
        END ELSE
          IF CompanyInformation.GET THEN;
        OnBehalfOfName := CompanyInformation.Name;
        OnBehalfOfEMail := CompanyInformation."E-Mail";
      END;

      EXIT((OnBehalfOfName <> '') AND (OnBehalfOfEMail <> ''));
    END;

    LOCAL PROCEDURE GetEMailOnBehalf@1100528401(Usage2@1100528400 : Integer;ThrowError@1100528402 : Boolean) : Boolean;
    BEGIN
      IF GET(Usage2) THEN BEGIN
        IF ThrowError THEN BEGIN
          TESTFIELD("Send On Behalf Of Name");
          TESTFIELD("Send On Behalf Of E-mail");
        END;
        EXIT(TRUE);
      END;
    END;

    BEGIN
    {
      4PS, 30-09-2014, C008067, Usage Docket added
      SESB.I035.I036 FD Message when dissaproving-correcting hours
         VKO 12.07.2013 Field 10 Usage changed - new options added ("Open Hour Accounting","Open Hour Consent Lines",
           "Denied Hour Consent Lines","Changed Hour Consent Lines")
    }
    END.
  }
}

