OBJECT Table 11012779 Provisional Work Contact
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ProvisionalWorkDocumentRec@1100485001 : Record 11012783;
             BEGIN
               ProvisionalWorkDocumentRec.SETRANGE("Provisional Work No.", "Provisional Work No.");
               ProvisionalWorkDocumentRec.SETRANGE("Feature Group", "Feature Group");
               ProvisionalWorkDocumentRec.SETRANGE("Feature Code", Feature);
               ProvisionalWorkDocumentRec.SETRANGE("Contact No.", "Contact No.");
               ProvisionalWorkDocumentRec.SETRANGE("Contact Person No.", "Contact Person No.");
               IF ProvisionalWorkDocumentRec.COUNT > 0 THEN BEGIN
                 IF NOT CONFIRM(Text001, FALSE, "Contact No.") THEN
                   ERROR('');
               END;

               ProvisionalWorkDocumentRec.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Provisional Work Contact;
    LookupPageID=Page11020645;
    DrillDownPageID=Page11020645;
  }
  FIELDS
  {
    { 10  ;   ;Provisional Work No.;Code20        ;TableRelation="Provisional Work";
                                                   CaptionML=ENU=Provisional Work No. }
    { 30  ;   ;Feature Group       ;Text50        ;TableRelation="Contact Feature Group".Description WHERE (Provisional Works=CONST(Yes));
                                                   CaptionML=ENU=Feature Group }
    { 40  ;   ;Feature             ;Code20        ;TableRelation="Contact Feature".Code WHERE (Feature Group=FIELD(Feature Group));
                                                   CaptionML=ENU=Feature }
    { 50  ;   ;Feature Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Provisional Work Feature"."Feature Description" WHERE (Provisional Work No.=FIELD(Provisional Work No.),
                                                                                                                              Feature Group=FIELD(Feature Group),
                                                                                                                              Feature=FIELD(Feature)));
                                                   CaptionML=ENU=Feature Description;
                                                   Editable=No }
    { 60  ;   ;Contact No.         ;Code20        ;TableRelation=Contact WHERE (Person No.=FILTER(''));
                                                   OnValidate=VAR
                                                                Contact@1100409000 : Record 5050;
                                                              BEGIN
                                                                IF "Contact No." <> xRec."Contact No." THEN
                                                                  "Contact Person No." := '';
                                                                IF Contact.GET("Contact No.") THEN BEGIN
                                                                  Contact.TESTFIELD(Blocked, FALSE);
                                                                  Contact.ValidateIsContact("Contact No.");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Contact No.;
                                                   NotBlank=Yes }
    { 65  ;   ;Contact Person No.  ;Code20        ;TableRelation=IF (Contact No.=FILTER(<>'')) Contact.No. WHERE (Company No.=FIELD(Contact No.),
                                                                                                                  Type=CONST(Person));
                                                   OnValidate=VAR
                                                                Contact@1100528700 : Record 5050;
                                                              BEGIN
                                                                IF "Contact Person No." <> '' THEN BEGIN
                                                                  TESTFIELD("Contact No.");
                                                                  Contact.GET("Contact Person No.");
                                                                  Contact.TESTFIELD(Blocked, FALSE);
                                                                END ELSE
                                                                  VALIDATE("Contact No.");
                                                              END;

                                                   CaptionML=ENU=Contact Person No. }
    { 70  ;   ;Contact Name        ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=ENU=Contact Name;
                                                   Editable=No }
    { 71  ;   ;Contact Post Code   ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Post Code" WHERE (No.=FIELD(Contact Filter)));
                                                   CaptionML=ENU=Post Code;
                                                   Editable=No }
    { 72  ;   ;Contact Phone No.   ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Phone No." WHERE (No.=FIELD(Contact Filter)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Phone No.;
                                                   Editable=No }
    { 75  ;   ;Contact Person Name ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact Person No.)));
                                                   CaptionML=ENU=Contact Person Name;
                                                   Editable=No }
    { 80  ;   ;Contact City        ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.City WHERE (No.=FIELD(Contact Filter)));
                                                   CaptionML=ENU=City;
                                                   Editable=No }
    { 100 ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=Request,Sent,Deliberation,Refuse,Received,Rejected,Selected,,,Market;
                                                   OptionString=Request,Sent,Deliberation,Refuse,Received,Rejected,Selected,,,Market }
    { 110 ;   ;Date Quote Received ;Date          ;CaptionML=ENU=Date Quote Received }
    { 120 ;   ;Comment             ;Text250       ;CaptionML=ENU=Comment }
    { 130 ;   ;Territory Code      ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Territory Code" WHERE (No.=FIELD(Contact Filter)));
                                                   CaptionML=ENU=Territory Code;
                                                   Editable=No }
    { 195 ;   ;Date Quote Expected ;Date          ;CaptionML=ENU=Date Quote Expected }
    { 300 ;   ;Contact Filter      ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Contact Filter;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Provisional Work No.,Feature Group,Feature,Contact No.,Contact Person No.;
                                                   Clustered=Yes }
    {    ;Provisional Work No.,Contact No.,Contact Person No. }
    {    ;Status,Contact No.,Contact Person No.    }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100485000 : TextConst 'ENU=There are related documents, remove vendor %1?';

    PROCEDURE GetProvisionalDocumentNo@1100530000(ProvisionalWorkDocTypeCode@1100525000 : Code[20]) : Code[20];
    VAR
      DocumentLink@1100530000 : Record 11012747;
      DocumentLink2@1100530002 : Record 11012747;
      ProvisionalWorkDocumentType@1100530003 : Record 11012770;
      RecRef@1100530001 : RecordRef;
      RecRef2@1100530004 : RecordRef;
    BEGIN
      // jhoek.100111

      // First, find document link records for the current provisional work contact
      RecRef.GETTABLE(Rec);
      DocumentLink.SETCURRENTKEY("Record ID");
      DocumentLink.SETFILTER("Record ID", STRSUBSTNO('''%1''', RecRef.RECORDID));

      IF DocumentLink.FINDSET THEN
        REPEAT
          // Now, return the document no. from the first document link record that
          // is *also* linked to the current provisional work document type
          ProvisionalWorkDocumentType.GET(ProvisionalWorkDocTypeCode);
          RecRef2.GETTABLE(ProvisionalWorkDocumentType);

          DocumentLink2.SETCURRENTKEY("Record ID");
          DocumentLink2.SETFILTER("Record ID", STRSUBSTNO('''%1''', RecRef2.RECORDID));
          DocumentLink2.SETRANGE("Document No.", DocumentLink."Document No.");

          IF NOT DocumentLink2.ISEMPTY THEN
            EXIT(DocumentLink."Document No.");
        UNTIL DocumentLink.NEXT = 0;
    END;

    PROCEDURE GetStandardDocumentNo@1100528700(DocumentTemplateCode@1100525000 : Code[20]) : Code[20];
    VAR
      DocumentLink@1100530000 : Record 11012747;
      DocumentLink2@1100530002 : Record 11012747;
      TempDocumentLink@1100528700 : TEMPORARY Record 11012747;
      DocumentProperties@1100528701 : Record 11012746;
      ProvisionalWork@1100530003 : Record 11012774;
      RecRef@1100530001 : RecordRef;
      RecRef2@1100530004 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DocumentLink.SETCURRENTKEY("Record ID");
      DocumentLink.SETFILTER("Record ID", STRSUBSTNO('''%1''', RecRef.RECORDID));

      IF DocumentLink.FINDSET THEN
        REPEAT
          TempDocumentLink.TRANSFERFIELDS(DocumentLink);
          TempDocumentLink.INSERT;
        UNTIL DocumentLink.NEXT = 0;

      TempDocumentLink.SETCURRENTKEY("Document No.","Seq. No.");
      IF TempDocumentLink.FINDLAST THEN // Find the last document first
        REPEAT
          ProvisionalWork.GET("Provisional Work No.");
          RecRef2.GETTABLE(ProvisionalWork);

          DocumentLink2.SETCURRENTKEY("Record ID");
          DocumentLink2.SETFILTER("Record ID", STRSUBSTNO('''%1''', RecRef2.RECORDID));
          DocumentLink2.SETRANGE("Document No.", TempDocumentLink."Document No.");

          IF DocumentLink2.FINDFIRST THEN BEGIN
            IF DocumentProperties.GET(TempDocumentLink."Document No.") THEN
              IF DocumentProperties."Document Template" = DocumentTemplateCode THEN
                EXIT(DocumentProperties."No.");
          END;
        UNTIL TempDocumentLink.NEXT(-1) = 0;
    END;

    PROCEDURE FilterContacts@1100528701();
    VAR
      FilterContactsProvWork@1100528700 : Page 11020646;
    BEGIN
      FilterContactsProvWork.SetParameters("Provisional Work No.", "Feature Group", Feature);
      FilterContactsProvWork.LOOKUPMODE(TRUE);
      FilterContactsProvWork.RUNMODAL;
    END;

    PROCEDURE SetContactFlowFilter@1100525000();
    BEGIN
      IF "Contact Person No." <> '' THEN
        SETFILTER("Contact Filter", "Contact Person No.")
      ELSE
        SETFILTER("Contact Filter", "Contact No.");
      CALCFIELDS("Contact Post Code", "Contact Phone No.", "Contact City", "Territory Code");
    END;

    BEGIN
    END.
  }
}

