OBJECT Table 11012842 Service Analysis
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Analysis Type;
    OnInsert=BEGIN
               "Last Date Modified" := TODAY;

               ExchangeFSA('I');
               ExchangeMOB('I');
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;

               ExchangeFSA('M');
               ExchangeMOB('M');
             END;

    OnDelete=VAR
               ServiceAnalysisDiscipline@1100409000 : Record 11072198;
               ServiceAnalysisRelation@1100409001 : Record 11072199;
             BEGIN
               ExchangeFSA('D');
               ExchangeMOB('D');

               // dp00662.sn
               ServiceAnalysisDiscipline.SETRANGE("Analysis Type", "Analysis Type");
               ServiceAnalysisDiscipline.SETRANGE("Analysis Code", Code);
               ServiceAnalysisDiscipline.DELETEALL;

               ServiceAnalysisRelation.SETRANGE("Analysis Type", "Analysis Type");
               ServiceAnalysisRelation.SETRANGE("Analysis Code", Code);
               ServiceAnalysisRelation.DELETEALL;

               IF ("Analysis Type" = "Analysis Type"::Problem) THEN BEGIN
                 ServiceAnalysisRelation.RESET;
                 ServiceAnalysisRelation.SETRANGE("Problem Code", Code);
                 ServiceAnalysisRelation.DELETEALL;
               END;
               // dp00662.en
             END;

    OnRename=BEGIN
               "Last Date Modified" := TODAY;

               ExchangeFSA('R');
               ExchangeMOB('R');
             END;

    CaptionML=ENU=Service Analysis;
  }
  FIELDS
  {
    { 10  ;   ;Analysis Type       ;Option        ;CaptionML=ENU=Analysis Type;
                                                   OptionCaptionML=ENU=Problem,Cause,Solution;
                                                   OptionString=Problem,Cause,Solution }
    { 20  ;   ;Code                ;Code10        ;TableRelation="Service Analysis".Code WHERE (Analysis Type=FIELD(Analysis Type));
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 40  ;   ;Expected Hours      ;Decimal       ;CaptionML=ENU=Expected Hours;
                                                   BlankNumbers=BlankZero }
    { 50  ;   ;Publish on Subcontr. Portal;Boolean;CaptionML=ENU=Publish on Subcontractor Portal }
    { 60  ;   ;Priority            ;Code10        ;TableRelation="Service Priority";
                                                   OnValidate=BEGIN
                                                                IF Priority <> '' THEN
                                                                  TESTFIELD("Analysis Type", "Analysis Type"::Problem);
                                                              END;

                                                   CaptionML=ENU=Priority }
    { 70  ;   ;Last Date Modified  ;Date          ;CaptionML=ENU=Last Date Modified }
    { 11012990;;Export to FSA      ;Boolean       ;CaptionML=ENU=Export to Field Service;
                                                   Description=Indication new/changed (Field Service Admin.) }
    { 11012991;;Date/Time Export to FSA;DateTime  ;CaptionML=ENU=Date/Time Export to Field Service }
  }
  KEYS
  {
    {    ;Analysis Type,Code                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE ExchangeFSA@1100485000(IMode@1100485003 : Code[1]);
    VAR
      lvProjSetupRec@1100485001 : Record 315;
      lvFieldVisionExchangeMgtCU@1100485000 : Codeunit 11012091;
    BEGIN
      //**4PS.n
      IF IMode = 'I' THEN BEGIN
        "Export to FSA" := FALSE;
        "Date/Time Export to FSA" := 0DT;
      END;
      lvProjSetupRec.GET;
      IF lvProjSetupRec."Field Service Active" THEN BEGIN
        IF IMode <> 'D' THEN BEGIN
          "Export to FSA" := TRUE;
          IF IMode = 'R' THEN
            lvFieldVisionExchangeMgtCU.StoreDeletedServAnalysis(xRec);
        END ELSE BEGIN
          lvFieldVisionExchangeMgtCU.StoreDeletedServAnalysis(Rec);
        END;
      END;
    END;

    LOCAL PROCEDURE ExchangeMOB@1100528703(Mode@1100528702 : Code[1]);
    VAR
      JobsSetup@1100528700 : Record 315;
      DeletedDataMgtMobile@1100528701 : Codeunit 11012440;
    BEGIN
      JobsSetup.GET;
      IF NOT JobsSetup."4PS-Mobile Active" THEN
        EXIT;

      IF (Mode = 'D') THEN
        DeletedDataMgtMobile.StoreDeletedServiceAnalysis(Rec);

      IF (Mode = 'R') THEN
        DeletedDataMgtMobile.StoreDeletedServiceAnalysis(xRec);
    END;

    BEGIN
    END.
  }
}

