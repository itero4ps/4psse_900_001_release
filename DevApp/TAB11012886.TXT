OBJECT Table 11012886 Debrief Text
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               FillModifiedBy();
             END;

    OnModify=BEGIN
               FillModifiedBy();
             END;

    CaptionML=ENU=Debrief Text;
  }
  FIELDS
  {
    { 1   ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Service Order;
                                                   OptionString=ServiceOrder }
    { 2   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(ServiceOrder)) "Service Order";
                                                   OnValidate=VAR
                                                                ServOrder@1100525000 : Record 11012823;
                                                              BEGIN
                                                                IF Type = Type::ServiceOrder THEN BEGIN
                                                                  IF "No." <> '' THEN BEGIN //mg, 31-05-11
                                                                    ServOrder.GET("No.");
                                                                    "Base Service Order No." := ServOrder."Base Service Order No.";
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 4   ;   ;Date                ;Date          ;CaptionML=ENU=Date }
    { 5   ;   ;Rubric              ;Code10        ;TableRelation="Debrief Text Rubric";
                                                   CaptionML=ENU=Rubric }
    { 6   ;   ;Comment             ;Text80        ;CaptionML=ENU=Comment }
    { 10  ;   ;Modified by Resource;Code20        ;TableRelation="Resource (WOP)" WHERE (Type=CONST(Employee));
                                                   CaptionML=ENU=Modified by Resoure;
                                                   Description=DP00465, based on Resource;
                                                   Editable=No }
    { 11  ;   ;Full Name           ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Resource (WOP)".Name WHERE (No.=FIELD(Modified by Resource)));
                                                   CaptionML=ENU=Full Name;
                                                   Editable=No }
    { 15  ;   ;Modified by User    ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Modified by User;
                                                   Editable=No }
    { 20  ;   ;Base Service Order No.;Code20      ;TableRelation="Service Order".No.;
                                                   CaptionML=ENU=Base Service Order No.;
                                                   Editable=No }
    { 30  ;   ;Finished            ;Boolean       ;CaptionML=ENU=Finished }
    { 40  ;   ;Work Order No.      ;Code20        ;TableRelation="Work Order";
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Work Order No.;
                                                   Description=dp00563;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Type,No.,Line No.                       ;Clustered=Yes }
    {    ;Base Service Order No.                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE SetUpNewLine@1(IxRec@1100485000 : Record 11012886;IBelowxRec@1100485002 : Boolean);
    VAR
      lvDebriefTextRec@1100485001 : Record 11012886;
    BEGIN
      lvDebriefTextRec.SETRANGE(Type,Type);
      lvDebriefTextRec.SETRANGE("No.","No.");
      lvDebriefTextRec.SETRANGE(Date,WORKDATE);
      IF NOT lvDebriefTextRec.FINDFIRST THEN BEGIN
        Date := WORKDATE;
        EXIT;
      END;

      IF NOT IBelowxRec THEN
        lvDebriefTextRec.SETFILTER("Line No.",'<%1',IxRec."Line No.");
      lvDebriefTextRec.SETFILTER(Date,'<>%1',0D);
      IF NOT lvDebriefTextRec.FINDLAST THEN
        Date := WORKDATE
      ELSE BEGIN
        IF lvDebriefTextRec.Date <> WORKDATE THEN
          Date := WORKDATE;
      END;
    END;

    PROCEDURE FillModifiedBy@1100485000();
    VAR
      lvUserSetupRec@1100485000 : Record 91;
      Resource@1100529000 : Record 11229278;
    BEGIN
      //DP00465.c
      "Modified by Resource" := '';
      "Modified by User" := USERID;
      IF lvUserSetupRec.GET(USERID) THEN BEGIN
        IF lvUserSetupRec."Employee No." <> '' THEN BEGIN
          Resource.SETCURRENTKEY(Type, Company, "Source No.");
          Resource.SETRANGE(Type, Resource.Type::Employee);
          Resource.SETRANGE(Company, COMPANYNAME);
          Resource.SETRANGE("Source No.", lvUserSetupRec."Employee No.");
          IF Resource.FINDFIRST THEN
            "Modified by Resource" := Resource."No.";
        END;
      END;
    END;

    BEGIN
    END.
  }
}

