OBJECT Table 11012909 Rubric Budget to Distribute
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               GetProjDevData();
               IF (ProjDevRec."Budget Level" <> ProjDevRec."Budget Level"::Model) AND ("House Model" <> '') THEN
                 FIELDERROR("House Model", STRSUBSTNO(Text000, ProjDevRec.FIELDCAPTION("Budget Level"), ProjDevRec."Budget Level"));

               InvPropRubricRec.GET("Project No.", Version, "Rubric Code");
               IF InvPropRubricRec.Type = InvPropRubricRec.Type::"Costs to Distr. as Perc. Rev." THEN BEGIN
                 IF ("House Model" <> '') THEN
                   FIELDERROR("House Model", STRSUBSTNO(Text000, InvPropRubricRec.FIELDCAPTION(Type), InvPropRubricRec.Type));
                 "Line No." := 10000;
               END;

               CheckFixed();
             END;

    OnModify=BEGIN
               GetProjDevData();
               IF (ProjDevRec."Budget Level" <> ProjDevRec."Budget Level"::Model) AND ("House Model" <> '') THEN
                 FIELDERROR("House Model", STRSUBSTNO(Text000, ProjDevRec.FIELDCAPTION("Budget Level"), ProjDevRec."Budget Level"));

               CheckFixed();
             END;

    OnDelete=BEGIN
               CheckFixed();
             END;

    OnRename=BEGIN
               GetProjDevData();
               IF (ProjDevRec."Budget Level" <> ProjDevRec."Budget Level"::Model) AND ("House Model" <> '') THEN
                 FIELDERROR("House Model", STRSUBSTNO(Text000, ProjDevRec.FIELDCAPTION("Budget Level"), ProjDevRec."Budget Level"));

               CheckFixed();
             END;

    CaptionML=ENU=Rubric Budget to Distribute;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation="Project Development";
                                                   CaptionML=ENU=Project No.;
                                                   NotBlank=Yes }
    { 20  ;   ;Version             ;Code10        ;TableRelation="Investment Proposal".Version WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=ENU=Version;
                                                   NotBlank=Yes }
    { 30  ;   ;Rubric Code         ;Code10        ;TableRelation="Investm. Proposal Rubric Line"."Rubric Code" WHERE (Project No.=FIELD(Project No.),
                                                                                                                      Version=FIELD(Version),
                                                                                                                      Type=FILTER(Costs|Revenues|Costs to Distr. as Perc. Rev.));
                                                   OnValidate=BEGIN
                                                                IF ("Rubric Code" <> '') THEN BEGIN
                                                                  InvPropRubricRec.GET("Project No.", Version, "Rubric Code");
                                                                  IF InvPropRubricRec.Type = InvPropRubricRec.Type::"Costs to Distr. as Perc. Rev." THEN BEGIN
                                                                    //* Then only 1 budget line on project level allowed
                                                                    TESTFIELD("House Model", '');
                                                                    IF "Line No." <> 0 THEN
                                                                      TESTFIELD("Line No.", 10000);
                                                                  END;
                                                                END;

                                                                CALCFIELDS("Rubric Description", "Budget Distribution");

                                                                IF ("Rubric Code" <> xRec."Rubric Code") OR (Description = '') THEN
                                                                  Description := "Rubric Description";
                                                              END;

                                                   CaptionML=ENU=Rubric Code;
                                                   NotBlank=Yes }
    { 31  ;   ;Rubric Description  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Investm. Proposal Rubric Line".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                                         Version=FIELD(Version),
                                                                                                                         Rubric Code=FIELD(Rubric Code)));
                                                   CaptionML=ENU=Rubric Description;
                                                   Editable=No }
    { 32  ;   ;Budget Distribution ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Investm. Proposal Rubric Line"."Budget Distribution" WHERE (Project No.=FIELD(Project No.),
                                                                                                                                   Version=FIELD(Version),
                                                                                                                                   Rubric Code=FIELD(Rubric Code)));
                                                   CaptionML=ENU=Budget Distribution;
                                                   OptionCaptionML=ENU=" ,Linear,Scheme";
                                                   OptionString=[ ,Linear,Scheme];
                                                   Editable=No }
    { 40  ;   ;House Model         ;Code20        ;TableRelation="Proj. Development House Model".Model WHERE (Project No.=FIELD(Project No.));
                                                   OnValidate=BEGIN
                                                                IF ("House Model" <> '') THEN BEGIN
                                                                  GetProjDevData();
                                                                  IF (ProjDevRec."Budget Level" <> ProjDevRec."Budget Level"::Model) AND ("House Model" <> '') THEN
                                                                    FIELDERROR("House Model", STRSUBSTNO(Text000, ProjDevRec.FIELDCAPTION("Budget Level"), ProjDevRec."Budget Level"));

                                                                  IF ("Rubric Code" <> '') THEN BEGIN
                                                                    InvPropRubricRec.GET("Project No.", Version, "Rubric Code");
                                                                    IF InvPropRubricRec.Type = InvPropRubricRec.Type::"Costs to Distr. as Perc. Rev." THEN
                                                                      FIELDERROR("House Model", STRSUBSTNO(Text000, InvPropRubricRec.FIELDCAPTION(Type), InvPropRubricRec.Type));
                                                                  END;
                                                                END;

                                                                CALCFIELDS("House Model Description");
                                                              END;

                                                   CaptionML=ENU=House Model }
    { 41  ;   ;House Model Description;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Proj. Development House Model".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                                         Model=FIELD(House Model)));
                                                   CaptionML=ENU=House Model Description;
                                                   Editable=No }
    { 45  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 50  ;   ;Amount              ;Decimal       ;CaptionML=ENU=Amount }
    { 60  ;   ;Scheme Budget Distr.;Code10        ;TableRelation="Budget Distribution Scheme";
                                                   OnValidate=VAR
                                                                lvInvPropRubricRec@1100485000 : Record 11012913;
                                                              BEGIN
                                                                IF ("Scheme Budget Distr." <> '') THEN BEGIN
                                                                  CALCFIELDS("Budget Distribution");
                                                                  IF ("Budget Distribution" <> "Budget Distribution"::Scheme) THEN BEGIN
                                                                    lvInvPropRubricRec."Budget Distribution" := "Budget Distribution"::Scheme;
                                                                    ERROR(Text001, FIELDCAPTION("Scheme Budget Distr."), FIELDCAPTION("Budget Distribution"),
                                                                      lvInvPropRubricRec."Budget Distribution" ,"Rubric Code");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Scheme Budget Distribution }
    { 70  ;   ;Frequency Budget Distr.;DateFormula;OnValidate=VAR
                                                                lvInvPropRubricRec@1100485000 : Record 11012913;
                                                              BEGIN
                                                                IF (FORMAT("Frequency Budget Distr.") <> '') THEN BEGIN
                                                                  CALCFIELDS("Budget Distribution");
                                                                  IF ("Budget Distribution" <> "Budget Distribution"::Linear) THEN BEGIN
                                                                    lvInvPropRubricRec."Budget Distribution" := "Budget Distribution"::Linear;
                                                                    ERROR(Text001, FIELDCAPTION("Frequency Budget Distr."), FIELDCAPTION("Budget Distribution"),
                                                                      lvInvPropRubricRec."Budget Distribution" ,"Rubric Code");
                                                                  END;

                                                                  IF (CALCDATE("Frequency Budget Distr.", TODAY) < TODAY) THEN
                                                                    FIELDERROR("Frequency Budget Distr.");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Frequency Budget Distribution }
    { 80  ;   ;Start Date Budget Distr.;Date      ;OnValidate=BEGIN
                                                                //* Both dates (start/end) or none on this level, start is determinative
                                                                IF ("Start Date Budget Distr." = 0D) THEN BEGIN
                                                                  IF ("End Date Budget Distr." <> 0D) THEN
                                                                    "End Date Budget Distr." := 0D;
                                                                END ELSE BEGIN
                                                                  IF ("End Date Budget Distr." < "Start Date Budget Distr.") THEN
                                                                    "End Date Budget Distr." := "Start Date Budget Distr.";
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Starting Date Budget Distribution }
    { 81  ;   ;End Date Budget Distr.;Date        ;OnValidate=BEGIN
                                                                IF ("End Date Budget Distr." <> 0D) THEN BEGIN
                                                                  TESTFIELD("Start Date Budget Distr.");
                                                                  IF ("End Date Budget Distr." < "Start Date Budget Distr.") THEN
                                                                    FIELDERROR("End Date Budget Distr.",
                                                                      STRSUBSTNO(Text002, "End Date Budget Distr.", FIELDCAPTION("Start Date Budget Distr.") ,"Start Date Budget Distr."));
                                                                END ELSE BEGIN
                                                                  IF ("Start Date Budget Distr." <> 0D) THEN
                                                                    FIELDERROR("End Date Budget Distr.",
                                                                      STRSUBSTNO(Text002, "End Date Budget Distr.", FIELDCAPTION("Start Date Budget Distr.") ,"Start Date Budget Distr."));
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Ending Date Budget Distribution }
    { 90  ;   ;Fixed               ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Investment Proposal".Fixed WHERE (Project No.=FIELD(Project No.),
                                                                                                         Version=FIELD(Version)));
                                                   CaptionML=ENU=Fixed;
                                                   Editable=No }
    { 100 ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
  }
  KEYS
  {
    {    ;Project No.,Version,Rubric Code,House Model,Line No.;
                                                   SumIndexFields=Amount;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Project No.,Version,House Model,Rubric Code }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ProjDevRec@1100485000 : Record 11012915;
      Text000@1100485001 : TextConst 'ENU=may not be filled because %1 is ''%2''';
      Text001@1100485002 : TextConst 'ENU=%1 only allowed if %2 is ''%3'' in Rubric ''%4''.';
      Text002@1100485003 : TextConst 'ENU=(%1) may not be less than %2 (%3)';
      InvPropRubricRec@1100485004 : Record 11012913;

    PROCEDURE GetProjDevData@1100485000();
    BEGIN
      IF "Project No." <> ProjDevRec."Project No." THEN
        ProjDevRec.GET("Project No.");
    END;

    PROCEDURE CheckFixed@1100485001();
    BEGIN
      CALCFIELDS(Fixed);
      TESTFIELD(Fixed, FALSE);
    END;

    BEGIN
    END.
  }
}

