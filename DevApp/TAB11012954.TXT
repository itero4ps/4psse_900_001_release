OBJECT Table 11012954 Project Process Control Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckFrozen();
             END;

    OnModify=BEGIN
               CheckFrozen();
             END;

    OnDelete=BEGIN
               CheckFrozen();

               IF ("Main Feature" <> '') THEN BEGIN
                 IF (Feature = '') THEN BEGIN
                   IF (MainFeatureRec.GET("Main Feature")) AND (MainFeatureRec.Mandatory) THEN
                     ERROR(Text001);
                 END ELSE BEGIN
                   IF (FeatureRec.GET("Main Feature",Feature)) AND (FeatureRec.Mandatory) THEN
                     ERROR(Text001);
                 END;
               END;

               DeleteLineComment();
             END;

    OnRename=BEGIN
               CheckFrozen();

               IF (xRec."Main Feature" <> '') THEN BEGIN
                 IF (xRec.Feature = '') THEN BEGIN
                   IF (MainFeatureRec.GET(xRec."Main Feature")) AND (MainFeatureRec.Mandatory) THEN
                     ERROR(Text002);
                 END ELSE BEGIN
                   IF (FeatureRec.GET(xRec."Main Feature",xRec.Feature)) AND (FeatureRec.Mandatory) THEN
                     ERROR(Text002);
                 END;
               END;

               IF (Rec."Main Feature" <> xRec."Main Feature") OR (Rec.Feature <> xRec.Feature) THEN
                 DeleteLineComment();
             END;

    CaptionML=ENU=Process Control Line;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=ENU=Project No.;
                                                   NotBlank=Yes }
    { 20  ;   ;Version             ;Integer       ;TableRelation="Project Process Control Header".Version WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=ENU=Version }
    { 30  ;   ;Main Feature        ;Code10        ;TableRelation="Process Main Feature";
                                                   OnValidate=BEGIN
                                                                VALIDATE(Feature);
                                                                CALCFIELDS("Main Feature Description");
                                                              END;

                                                   CaptionML=ENU=Main Feature;
                                                   NotBlank=Yes }
    { 40  ;   ;Feature             ;Code10        ;TableRelation="Process Feature".Feature WHERE (Main Feature=FIELD(Main Feature));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Feature Description");
                                                              END;

                                                   CaptionML=ENU=Feature }
    { 50  ;   ;Main Feature Description;Text50    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Process Main Feature".Description WHERE (Main Feature=FIELD(Main Feature)));
                                                   CaptionML=ENU=Main Feature Description;
                                                   Editable=No }
    { 60  ;   ;Feature Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Process Feature".Description WHERE (Main Feature=FIELD(Main Feature),
                                                                                                           Feature=FIELD(Feature)));
                                                   CaptionML=ENU=Feature Description;
                                                   Editable=No }
    { 70  ;   ;Choice              ;Code10        ;TableRelation="Process Feature Choice".Choice WHERE (Main Feature=FIELD(Main Feature),
                                                                                                        Feature=FIELD(Feature));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Choice Description");
                                                              END;

                                                   CaptionML=ENU=Choice }
    { 80  ;   ;Choice Description  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Process Feature Choice".Description WHERE (Main Feature=FIELD(Main Feature),
                                                                                                                  Feature=FIELD(Feature),
                                                                                                                  Choice=FIELD(Choice)));
                                                   CaptionML=ENU=Choice Description;
                                                   Editable=No }
    { 90  ;   ;Quantity            ;Integer       ;CaptionML=ENU=Quantity;
                                                   BlankZero=Yes }
    { 100 ;   ;Room Numbers        ;Text50        ;CaptionML=ENU=Room Nos. }
    { 110 ;   ;Selected            ;Boolean       ;InitValue=Yes;
                                                   CaptionML=ENU=Selected }
  }
  KEYS
  {
    {    ;Project No.,Version,Main Feature,Feature;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1210190000 : TextConst 'ENU=This version is frozen';
      ProcessControlHeader@1210190001 : Record 11012953;
      ProcContrCommentLine@1210190005 : Record 11012955;
      Text001@1210190002 : TextConst 'ENU=Mandatory Feature,  deleting not allowed !';
      MainFeatureRec@1210190003 : Record 11012950;
      FeatureRec@1210190004 : Record 11012951;
      Text002@1210190006 : TextConst 'ENU=Mandatory Feature,  rename not allowed !';

    PROCEDURE CheckFrozen@1210190000();
    BEGIN
      IF ProcessControlHeader.GET("Project No.", Version) THEN
        IF ProcessControlHeader.Frozen THEN
          ERROR(Text000);
    END;

    PROCEDURE DeleteLineComment@1210190001();
    BEGIN
      ProcContrCommentLine.RESET;
      ProcContrCommentLine.SETRANGE("Project No.","Project No.");
      ProcContrCommentLine.SETRANGE(Version,Version);
      ProcContrCommentLine.SETRANGE("Main Feature","Main Feature");
      ProcContrCommentLine.SETRANGE(Feature,Feature);
      ProcContrCommentLine.DELETEALL;
    END;

    BEGIN
    END.
  }
}

