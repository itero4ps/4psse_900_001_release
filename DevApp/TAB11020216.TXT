OBJECT Table 11020216 Project Authority
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "SharePoint Status" := "SharePoint Status"::New;

               CheckDeliveredObject;

               BuildRelationalStructure('INSERT');
               ProjectSiteMgt.LogProjectAuthority(1, Rec); //**4PS.n
             END;

    OnModify=BEGIN
               IF "SharePoint Status" = "SharePoint Status"::Updated THEN
                 "SharePoint Status" := "SharePoint Status"::Modified;

               CheckDeliveredObject;

               BuildRelationalStructure('MODIFY');
               ProjectSiteMgt.LogProjectAuthority(2, Rec); //**4PS.n
             END;

    OnDelete=VAR
               lJobRec@1100485000 : Record 11072003;
               AuthorityPermPPT@1100528300 : Record 11229813;
             BEGIN
               IF NOT GlobalDelete THEN BEGIN
                 IF lJobRec.GET("Project No.") THEN BEGIN
                   IF lJobRec."SharePoint Status" = lJobRec."SharePoint Status"::Updated THEN BEGIN
                     lJobRec."SharePoint Status" :=  lJobRec."SharePoint Status"::Modified;
                     lJobRec.MODIFY;
                   END;
                 END;
                 CheckDeliveredObject;
               END;

               BuildRelationalStructure('DELETE');

               AuthorityPermPPT.SETRANGE("Project No.", "Project No.");
               AuthorityPermPPT.SETRANGE("Authority Type", "Authority Type");
               AuthorityPermPPT.SETRANGE("Contact No.", "Contact No.");
               AuthorityPermPPT.SETRANGE("Contact Person No.", "Contact Person No.");
               IF (NOT AuthorityPermPPT.ISEMPTY) THEN
                 AuthorityPermPPT.DELETEALL(TRUE);

               ProjectSiteMgt.LogProjectAuthority(3, Rec); //**4PS.n
             END;

    OnRename=VAR
               Job@1100528301 : Record 11072003;
               AuthorityPermPPT@1100409000 : Record 11229813;
               CreateAuthorityPermPPT@1100409001 : Report 11012196;
             BEGIN
               CheckDeliveredObject;
               BuildRelationalStructure('MODIFY');  //*27412

               Job.GET("Project No.");
               IF (Job.IsJobInProjectPortal) THEN BEGIN
                 AuthorityPermPPT.SETRANGE("Project No.", xRec."Project No.");
                 AuthorityPermPPT.SETRANGE("Authority Type", xRec."Authority Type");
                 AuthorityPermPPT.SETRANGE("Contact No.", xRec."Contact No.");
                 AuthorityPermPPT.SETRANGE("Contact Person No.", xRec."Contact Person No.");
                 AuthorityPermPPT.DELETEALL;
                 CreateAuthorityPermPPT.ProcessProjectAuthority(Rec);
               END;
               ProjectSiteMgt.LogProjectAuthority(3, xRec); //**4PS.n
               ProjectSiteMgt.LogProjectAuthority(1, Rec); //**4PS.n
             END;

    CaptionML=ENU=Project Authority;
    LookupPageID=Page11020230;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=ENU=Project No. }
    { 20  ;   ;Authority Type      ;Code20        ;TableRelation="Authority Type".Code;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Authority Type Description");
                                                              END;

                                                   CaptionML=ENU=Authority Type;
                                                   NotBlank=Yes }
    { 30  ;   ;Authority Type Description;Text30  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Authority Type".Description WHERE (Code=FIELD(Authority Type)));
                                                   CaptionML=ENU=Authority Type Description;
                                                   Editable=No }
    { 50  ;   ;Contact No.         ;Code20        ;TableRelation=Contact;
                                                   OnValidate=VAR
                                                                Contact@1100528700 : Record 5050;
                                                              BEGIN
                                                                Contact.ValidateIsContact("Contact No.");
                                                                IF "Contact No." <> xRec."Contact No." THEN
                                                                  VALIDATE("Contact Person No.", '');

                                                                CALCFIELDS("Contact Name");
                                                                CALCFIELDS(Address);   //Call C022971
                                                                CALCFIELDS("Post Code");
                                                                CALCFIELDS(City);
                                                                CALCFIELDS(Phone);
                                                                CALCFIELDS("Mobile Phone");
                                                                CALCFIELDS(Fax);
                                                                CALCFIELDS("E-Mail");
                                                              END;

                                                   CaptionML=ENU=Contact No. }
    { 51  ;   ;Contact Person No.  ;Code20        ;TableRelation=Contact.No. WHERE (Type=CONST(Person),
                                                                                    Company No.=FIELD(Contact No.));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Contact Person Name");
                                                                CALCFIELDS("Phone 2");//Call C022971
                                                              END;

                                                   CaptionML=ENU=Contact Person No. }
    { 60  ;   ;Contact Name        ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=ENU=Contact Name;
                                                   Editable=No }
    { 61  ;   ;Contact Person Name ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact Person No.)));
                                                   CaptionML=ENU=Contact Person Name;
                                                   Editable=No }
    { 66  ;   ;Address             ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Address WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=ENU=Address;
                                                   Editable=No }
    { 67  ;   ;Post Code           ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Post Code" WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=ENU=Post Code;
                                                   Editable=No }
    { 68  ;   ;City                ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.City WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=ENU=City;
                                                   Editable=No }
    { 70  ;   ;Comment             ;Text50        ;CaptionML=ENU=Comment }
    { 90  ;   ;Phone               ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Phone No." WHERE (No.=FIELD(Contact No.)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Phone;
                                                   Editable=No }
    { 100 ;   ;Phone 2             ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Phone No." WHERE (No.=FIELD(Contact Person No.)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Phone 2;
                                                   Editable=No }
    { 110 ;   ;Mobile Phone        ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Mobile Phone No." WHERE (No.=FIELD(Contact No.)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Mobile Phone;
                                                   Editable=No }
    { 120 ;   ;Fax                 ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Fax No." WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=ENU=Fax;
                                                   Editable=No }
    { 130 ;   ;E-Mail              ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.E-Mail WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=ENU=E-Mail;
                                                   Editable=No }
    { 140 ;   ;SharePoint Status   ;Option        ;CaptionML=ENU=SharePoint Status;
                                                   OptionCaptionML=ENU=New,Modified,Updated;
                                                   OptionString=New,Modified,Updated;
                                                   Editable=Yes }
    { 150 ;   ;Completed           ;Date          ;CaptionML=ENU=Completed }
    { 160 ;   ;Principal           ;Boolean       ;CaptionML=ENU=Principal }
    { 200 ;   ;Task                ;Text30        ;CaptionML=ENU=Task }
    { 210 ;   ;Portal Login E-Mail Send On;DateTime;
                                                   CaptionML=ENU=Portal Login E-Mail Send On;
                                                   Editable=No }
    { 220 ;   ;Use as Subc. on Serv. Location;Boolean;
                                                   CaptionML=ENU=Use as Subcontractor on Service Location }
  }
  KEYS
  {
    {    ;Project No.,Authority Type,Contact No.,Contact Person No.;
                                                   Clustered=Yes }
    {    ;Contact No.                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GlobalDelete@1100485000 : Boolean;
      Text001@1100485001 : TextConst 'ENU=Project %1 is already Delivered. Changes should be made at %2 %3.';
      ProjectSiteMgt@1100528300 : Codeunit 11012282;

    PROCEDURE SetGlobalDelete@1100485000();
    BEGIN
      GlobalDelete := TRUE;
    END;

    PROCEDURE CheckDeliveredObject@1100485001();
    VAR
      DeliveredProject@1100485001 : Record 11012100;
    BEGIN
      DeliveredProject.SETCURRENTKEY("Original Project No.");
      DeliveredProject.SETRANGE("Original Project No.", "Project No.");
      IF DeliveredProject.FINDFIRST THEN
        MESSAGE(Text001, "Project No.", DeliveredProject.TABLECAPTION, DeliveredProject."No.");
    END;

    PROCEDURE BuildRelationalStructure@1100525001(Action@1100525001 : Text[30]);
    VAR
      RelationalStructureMgt@1100525000 : Codeunit 11012238;
    BEGIN
      RelationalStructureMgt.BuildFromProjAuth(Rec, xRec, Action);
    END;

    PROCEDURE ByCompany@1100528700() : Boolean;
    VAR
      MarketingSetup@1100528703 : Record 5079;
      Contact@1100528701 : Record 5050;
    BEGIN
      MarketingSetup.GET;

      TESTFIELD("Contact No.");
      Contact.GET("Contact No.");

      IF NOT MarketingSetup."Central Management" OR
        ((NOT MarketingSetup.Companies) AND (Contact.Type = Contact.Type::Company)) OR
        ((NOT MarketingSetup.Persons) AND (Contact.Type = Contact.Type::Person)) THEN
        EXIT(TRUE);
    END;

    PROCEDURE EditDocuments@1100528702(DocumentTemplateTypeFilter@1210190003 : Text[250];ContactNo@1210190004 : Code[20];ContactPersonNo@1210190005 : Code[20];SendMail@1210190002 : Boolean;Recipient@1100528700 : Text[120]);
    VAR
      Job@1210190006 : Record 11072003;
      DocumentMailing@1210190000 : Codeunit 11012278;
      RecRef@1100525000 : RecordRef;
      DocumentLinkRIDs@1100525001 : ARRAY [5] OF RecordID;
    BEGIN
      RecRef.GETTABLE(Rec);
      DocumentLinkRIDs[1] := RecRef.RECORDID;

      IF Job.GET("Project No.") THEN BEGIN
        RecRef.GETTABLE(Job);
        DocumentLinkRIDs[2] := RecRef.RECORDID;
      END;

      DocumentMailing.EditDocuments(
        DocumentTemplateTypeFilter, DocumentLinkRIDs, '', ContactNo, ContactPersonNo, SendMail, Recipient);
    END;

    PROCEDURE AddProjectportalPermissions@1100528300();
    VAR
      ProjectAuthority@1100528300 : Record 11020216;
      CreateAuthorityPermPPT@1100528301 : Report 11012196;
    BEGIN
      ProjectAuthority := Rec;
      ProjectAuthority.SETRANGE("Project No.", "Project No.");
      CreateAuthorityPermPPT.SetSubsiteType(0); //Project
      CreateAuthorityPermPPT.SETTABLEVIEW(ProjectAuthority);
      CreateAuthorityPermPPT.RUNMODAL
    END;

    PROCEDURE UpdateProjectPortal@1100528301();
    VAR
      Job@1100528301 : Record 11072003;
      DocumentPortalProject@1100528300 : Codeunit 11012948;
    BEGIN
      Job.GET("Project No.");
      DocumentPortalProject.AddAuthorities(Job);
    END;

    BEGIN
    END.
  }
}

