OBJECT Table 11020234 Leave Journal Line (4PS)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Leave Journal Line (4PS);
  }
  FIELDS
  {
    { 10  ;   ;Journal Template Name;Code10       ;TableRelation="Gen. Journal Template";
                                                   CaptionML=ENU=Journal Template Name }
    { 20  ;   ;Journal Batch Name  ;Code10        ;TableRelation="Gen. Journal Batch".Name WHERE (Journal Template Name=FIELD(Journal Template Name));
                                                   CaptionML=ENU=Journal Batch Name }
    { 30  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 40  ;   ;Posting Date        ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Document Date","Posting Date");
                                                                VALIDATE(Rate, GetRate);
                                                              END;

                                                   CaptionML=ENU=Posting Date }
    { 50  ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No. }
    { 60  ;   ;No. of Hours        ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE(Amount, "No. of Hours" * Rate);
                                                              END;

                                                   CaptionML=ENU=No. of Hours;
                                                   DecimalPlaces=2:3;
                                                   BlankZero=Yes }
    { 70  ;   ;Employee No.        ;Code20        ;TableRelation=Employee;
                                                   OnValidate=BEGIN
                                                                VALIDATE(Rate, GetRate);
                                                              END;

                                                   CaptionML=ENU=Employee No. }
    { 80  ;   ;Employee Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Employee No.)));
                                                   CaptionML=ENU=Employee Name;
                                                   Editable=No }
    { 90  ;   ;Norm Hours          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Norm Hours" WHERE (No.=FIELD(Employee No.)));
                                                   CaptionML=ENU=Norm Hours;
                                                   Editable=No }
    { 100 ;   ;Description         ;Text30        ;CaptionML=ENU=Description }
    { 110 ;   ;Leave Type          ;Code10        ;TableRelation="Leave Type";
                                                   CaptionML=ENU=Leave Type }
    { 130 ;   ;Document Date       ;Date          ;CaptionML=ENU=Document Date }
    { 140 ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   CaptionML=ENU=Source Code;
                                                   Editable=No }
    { 150 ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=ENU=Reason Code }
    { 160 ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=ENU=Posting No. Series }
    { 170 ;   ;Rate                ;Decimal       ;OnValidate=BEGIN
                                                                IF Rate <> 0 THEN
                                                                  TESTFIELD("Entry Type", "Entry Type"::"General Ledger");

                                                                VALIDATE(Amount, "No. of Hours" * Rate);
                                                              END;

                                                   CaptionML=ENU=Rate;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 180 ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                IF Amount <> 0 THEN
                                                                  TESTFIELD("Entry Type", "Entry Type"::"General Ledger");
                                                              END;

                                                   CaptionML=ENU=Amount;
                                                   BlankZero=Yes;
                                                   AutoFormatType=1 }
    { 190 ;   ;Entry Type          ;Option        ;OnValidate=BEGIN
                                                                IF "Entry Type" = "Entry Type"::"General Ledger" THEN
                                                                  "Leave Type" := '';
                                                                VALIDATE(Rate, GetRate);
                                                              END;

                                                   CaptionML=ENU=Entry Type;
                                                   OptionCaptionML=ENU=Hours,General Ledger;
                                                   OptionString=Hours,General Ledger }
  }
  KEYS
  {
    {    ;Journal Template Name,Journal Batch Name,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GenJnlTemplate@1000 : Record 80;
      GenJnlBatch@1001 : Record 232;
      LeaveJnlLine@1100485000 : Record 11020234;
      LeaveHourRateRec@1100485002 : Record 11020262;
      NoSeriesMgt@1009 : Codeunit 396;

    PROCEDURE EmptyLine@5() : Boolean;
    BEGIN
      EXIT(("Employee No." = '') AND ("No. of Hours" = 0));
    END;

    PROCEDURE SetUpNewLine@8(LastLeaveJnlLine@1100485002 : Record 11020234);
    BEGIN
      GenJnlTemplate.GET("Journal Template Name");
      GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
      LeaveJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
      LeaveJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
      IF LeaveJnlLine.FIND('-') THEN BEGIN
        "Posting Date" := LastLeaveJnlLine."Posting Date";
        "Document Date" := LastLeaveJnlLine."Document Date";
        "Document No." := LastLeaveJnlLine."Document No.";
        "Entry Type" := LastLeaveJnlLine."Entry Type";
      END ELSE BEGIN
        "Posting Date" := WORKDATE;
        "Document Date" := WORKDATE;
        IF GenJnlBatch."No. Series" <> '' THEN BEGIN
          CLEAR(NoSeriesMgt);
          "Document No." := NoSeriesMgt.TryGetNextNo(GenJnlBatch."No. Series","Posting Date");
        END;
      END;
      "Source Code" := GenJnlTemplate."Source Code";
      "Reason Code" := GenJnlBatch."Reason Code";
      "Posting No. Series" := GenJnlBatch."Posting No. Series";
      Description := '';
    END;

    PROCEDURE GetRate@1100485000() : Decimal;
    BEGIN
      IF "Entry Type" <> "Entry Type"::"General Ledger" THEN
        EXIT(0);

      LeaveHourRateRec.SETCURRENTKEY("Employee No.","Start Date");
      LeaveHourRateRec.SETRANGE("Employee No.", "Employee No.");
      LeaveHourRateRec."Employee No." := "Employee No.";
      LeaveHourRateRec."Start Date" := "Posting Date";
      IF LeaveHourRateRec.FIND('=<>') THEN
        EXIT(LeaveHourRateRec."Hour Rate");
    END;

    BEGIN
    END.
  }
}

