OBJECT Table 11020245 Means
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 GetProjSetup;
                 ProjSetupRec.TESTFIELD("Means Nos.");
                 NoSeriesMgt.InitSeries(ProjSetupRec."Means Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
               "Last Updated" := CURRENTDATETIME; //140416
             END;

    OnModify=VAR
               lvSmallProjOperLineRec@1100485000 : Record 11012092;
             BEGIN
               lvSmallProjOperLineRec.SETRANGE(Type, lvSmallProjOperLineRec.Type::Means);
               lvSmallProjOperLineRec.SETRANGE("No.", "No.");
               lvSmallProjOperLineRec.SETFILTER("Vendor No.", '<>%1', "Vendor No.");
               IF lvSmallProjOperLineRec.FIND('-') THEN BEGIN
                 lvSmallProjOperLineRec."Vendor No." := "Vendor No.";
                 lvSmallProjOperLineRec.MODIFY;
               END;
               "Last Updated" := CURRENTDATETIME; //140416
             END;

    CaptionML=ENU=Means;
    LookupPageID=Page11020289;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  GetProjSetup;
                                                                  NoSeriesMgt.TestManual(ProjSetupRec."Means Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 15  ;   ;Cost Type           ;Option        ;OnValidate=BEGIN
                                                                IF "Cost Type" <> "Cost Type"::Labor THEN BEGIN
                                                                  TESTFIELD("Hours Rate Code", '');
                                                                  TESTFIELD(Norm, 0);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Cost Type;
                                                   OptionCaptionML=ENU=Labor,Material,Subcontracting,Plant,Sundry;
                                                   OptionString=Labor,Material,Subcontracting,Plant,Sundry }
    { 20  ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Cost Type=FIELD(Cost Type),
                                                                                               Global Dimension No.=CONST(2));
                                                   CaptionML=ENU=Cost Object }
    { 30  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 40  ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=ENU=Unit of Measure }
    { 50  ;   ;Costprice           ;Decimal       ;CaptionML=ENU=Costprice }
    { 60  ;   ;Hours Rate Code     ;Code20        ;TableRelation="Hour Rate";
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Cost Type", "Cost Type"::Labor);
                                                              END;

                                                   CaptionML=ENU=Rate Code }
    { 70  ;   ;Norm                ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Cost Type", "Cost Type"::Labor);
                                                              END;

                                                   CaptionML=ENU=Norm;
                                                   BlankZero=Yes }
    { 80  ;   ;Sales Price         ;Decimal       ;CaptionML=ENU=Sales Price }
    { 90  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=ENU=Vendor No. }
    { 100 ;   ;Vendor Description  ;Text50        ;CaptionML=ENU=Vendor Description }
    { 110 ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=No }
    { 120 ;   ;Customer Filter     ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Customer Filter }
    { 130 ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter }
    { 11128000;;Last Updated       ;DateTime       }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Cost Object                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ProjSetupRec@1100485001 : Record 315;
      NoSeriesMgt@1100485002 : Codeunit 396;
      ProjSetup@1100485000 : Boolean;

    PROCEDURE AssistEdit@2() : Boolean;
    BEGIN
      GetProjSetup;
      ProjSetupRec.TESTFIELD("Means Nos.");
      IF NoSeriesMgt.SelectSeries(ProjSetupRec."Means Nos.",xRec."No. Series","No. Series") THEN BEGIN
        NoSeriesMgt.SetSeries("No.");
        EXIT(TRUE);
      END;
    END;

    PROCEDURE GetProjSetup@1100485000();
    BEGIN
      IF ProjSetup THEN
        EXIT;

      ProjSetup := TRUE;
      ProjSetupRec.GET;
    END;

    BEGIN
    {
      4PSHM ITERO.MH 140416 Added field "Last Updated" and code onInsert() and onModify()
    }
    END.
  }
}

