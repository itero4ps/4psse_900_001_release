OBJECT Table 11020314 Comment Line Extended
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Created by" := USERID;
             END;

    CaptionML=ENU=Comment Line Extended;
    Description=Comment line with extended key;
    LookupPageID=Page11071832;
    DrillDownPageID=Page11071832;
  }
  FIELDS
  {
    { 10  ;   ;Table1 Name         ;Option        ;CaptionML=ENU=Table1 Name;
                                                   OptionCaptionML=ENU=Job,Engineering Project,CableReel,Issue,Assembly Project;
                                                   OptionString=Job,Engineering Project,CableReel,Issue,Assembly Project }
    { 20  ;   ;Table1 No.          ;Code20        ;TableRelation=IF (Table1 Name=CONST(Job)) Job;
                                                   CaptionML=ENU=Table1 No. }
    { 30  ;   ;Table2 Name         ;Option        ;CaptionML=ENU=Table2 Name;
                                                   OptionCaptionML=ENU=Project Principal,Build Number,Location,System,Device,Mat.Type,Cable Type," ",Extension Contract,Engineering Doc.,Requirement,Identification Plate,Extra Need Type,Node;
                                                   OptionString=Project Principal,Build Number,Location,System,Device,Mat.Type,Cable Type, ,Extension Contract,Engineering Doc.,Requirement,Id Plate Type,Extra Need Type,Node }
    { 40  ;   ;Table2 No.          ;Code110       ;TableRelation=IF (Table2 Name=CONST(Project Principal)) "Project Principal".Principal WHERE (Project No.=FIELD(Table1 No.));
                                                   CaptionML=ENU=Table2 No.;
                                                   Description=Size because of field Own ID in tabel Engineering Document }
    { 50  ;   ;Table3 Name         ;Option        ;CaptionML=ENU=Table3 Name;
                                                   OptionCaptionML=ENU=,Cable,Device;
                                                   OptionString=[ ,Cable,Device] }
    { 60  ;   ;Table3 No.          ;Code30        ;CaptionML=ENU=Table3 No. }
    { 70  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 80  ;   ;Date                ;Date          ;CaptionML=ENU=Date }
    { 90  ;   ;Code                ;Code10        ;CaptionML=ENU=Code }
    { 100 ;   ;Comment             ;Text80        ;CaptionML=ENU=Comment }
    { 110 ;   ;Created by          ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Created by;
                                                   Editable=No }
    { 120 ;   ;Comment Code        ;Code20        ;TableRelation="Comment Code";
                                                   CaptionML=ENU=Comment Code }
    { 11012010;;Line Break         ;Boolean       ;CaptionML=ENU=Line Break }
  }
  KEYS
  {
    {    ;Table1 Name,Table1 No.,Table2 Name,Table2 No.,Table3 Name,Table3 No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE SetUpNewLine@1();
    VAR
      CommentLine@1000 : Record 11020314;
    BEGIN
      //**4PS.so C021826
      //CommentLine.SETRANGE("Table1 Name","Table1 Name");
      //CommentLine.SETRANGE("Table1 No.","Table1 No.");
      //CommentLine.SETRANGE("Table2 Name","Table2 Name");
      //CommentLine.SETRANGE("Table2 No.","Table2 No.");
      //CommentLine.SETRANGE("Table3 Name","Table3 Name");
      //CommentLine.SETRANGE("Table3 No.","Table3 No.");
      //CommentLine.SETRANGE(Date,WORKDATE);
      //IF NOT CommentLine.FINDFIRST THEN
      //**4PS.eo
        Date := WORKDATE;
    END;

    PROCEDURE CopyTypeComments@1100485007(iTable1@1100485003 : 'Job,Engineering Project';iTable2@1100485002 : 'Project Principal,Build Number,Location,System,Device,Mat.Type,Cable Type," "';iFromProject@1100485006 : Code[20];iFromTypeCode@1100485001 : Code[30];iToProj@1100485000 : Code[20];iToTypeCode@1100485009 : Code[30];VAR ioToCommentRec@1100485007 : Record 11020314);
    VAR
      lvFromCommentRec@1100485004 : Record 11020314;
    BEGIN
      //used in project engineering. Copies comments from one cable/device type to another
      lvFromCommentRec.SETRANGE("Table1 Name",iTable1);
      lvFromCommentRec.SETRANGE("Table1 No.",iFromProject);
      lvFromCommentRec.SETRANGE("Table2 Name",iTable2);
      lvFromCommentRec.SETRANGE("Table2 No.",iFromTypeCode);
      IF lvFromCommentRec.FINDSET THEN
        REPEAT
          ioToCommentRec := lvFromCommentRec;
          ioToCommentRec."Table1 No." := iToProj;
          ioToCommentRec."Table2 No." := iToTypeCode;
          ioToCommentRec.INSERT(TRUE);
        UNTIL lvFromCommentRec.NEXT = 0;
    END;

    PROCEDURE CommentsSync@1100485006(iTable1@1100485009 : 'Job,Engineering Project';iTable2@1100485011 : 'Project Principal,Build Number,Location,System,Device,Mat.Type,Cable Type," "';iFromProject@1100485008 : Code[20];iFromTypeCode@1100485007 : Code[30];iToProj@1100485006 : Code[20];iToType@1100485010 : Code[30]) : Boolean;
    VAR
      lvTempCommentRec@1100485000 : TEMPORARY Record 11020314;
      lvCommentRec@1100485001 : Record 11020314;
      lvFinished@1100485002 : Boolean;
      lvSync@1100485003 : Boolean;
      lv1Found@1100485004 : Boolean;
      lv2Found@1100485005 : Boolean;
    BEGIN
      //used in project engineering. Determines for a device type or a cable type whether the comments differ from the source
      CopyTypeComments(iTable1, iTable2, iFromProject, iFromTypeCode, iToProj, iToType, lvTempCommentRec);
      lvCommentRec.SETRANGE("Table1 Name",iTable1);
      lvCommentRec.SETRANGE("Table1 No.",iToProj);
      lvCommentRec.SETRANGE("Table2 Name",iTable2);
      lvCommentRec.SETRANGE("Table2 No.",iToType);
      lv1Found := lvCommentRec.FINDFIRST;
      lv2Found := lvTempCommentRec.FINDFIRST;
      IF lv1Found <> lv2Found THEN
        EXIT(FALSE);
      IF NOT lv1Found AND NOT lv2Found  THEN
        EXIT(TRUE);
      lvSync := TRUE;
      REPEAT
        IF FORMAT(lvCommentRec) <> FORMAT(lvTempCommentRec) THEN
          lvSync := FALSE;
        lv1Found := lvCommentRec.NEXT <> 0;
        lv2Found := lvTempCommentRec.NEXT <> 0;
        lvSync := lvSync AND (lv1Found = lv2Found);
        lvFinished := (NOT lv1Found OR NOT lv2Found);
      UNTIL lvFinished OR NOT lvSync;
      EXIT(lvSync);
    END;

    PROCEDURE SyncComments@1100485004(iTable1@1100485002 : 'Job,Engineering Project';iTable2@1100485000 : 'Project Principal,Build Number,Location,System,Device,Mat.Type,Cable Type," "';iFromProject@1100485008 : Code[20];iFromTypeCode@1100485007 : Code[30];iToProj@1100485006 : Code[20];iToType@1100485010 : Code[30]);
    VAR
      lvCommentRec@1100485001 : Record 11020314;
    BEGIN
      lvCommentRec.SETRANGE("Table1 Name",iTable1);
      lvCommentRec.SETRANGE("Table1 No.",iToProj);
      lvCommentRec.SETRANGE("Table2 Name",iTable2);
      lvCommentRec.SETRANGE("Table2 No.",iToType);
      IF lvCommentRec.FINDFIRST THEN BEGIN
        lvCommentRec.DELETEALL;
      END;
      CopyTypeComments(iTable1, iTable2, iFromProject, iFromTypeCode, iToProj, iToType, lvCommentRec);
    END;

    BEGIN
    END.
  }
}

