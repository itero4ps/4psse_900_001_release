OBJECT Table 11020316 Stock Surcharge
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Time Modified" := CURRENTDATETIME;
               "Time Posted" := 0DT;
               "Actual Amount" := 0;
               "Actual Percentage" := 0;
             END;

    OnDelete=BEGIN
               SKUSurchargeRec.RESET;
               SKUSurchargeRec.SETRANGE("Surcharge Code",Code);
               IF SKUSurchargeRec.COUNT > 0 THEN
                 ERROR(Text001, Code);
             END;

    CaptionML=ENU=Stock Surcharge;
    LookupPageID=Page11071834;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text30        ;CaptionML=ENU=Description }
    { 3   ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                IF Percentage <> 0 THEN
                                                                  TESTFIELD(Amount, 0);

                                                                IF "Actual Amount" <> Amount THEN
                                                                  "Time Modified" := CURRENTDATETIME;
                                                              END;

                                                   CaptionML=ENU=Amount }
    { 4   ;   ;Percentage          ;Decimal       ;OnValidate=BEGIN
                                                                IF Amount <> 0 THEN
                                                                  TESTFIELD(Percentage, 0);

                                                                IF "Actual Percentage" <> Percentage THEN
                                                                  "Time Modified" := CURRENTDATETIME;
                                                              END;

                                                   CaptionML=ENU=Percentage }
    { 5   ;   ;Cost Object Surcharge;Code20       ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2),
                                                                                               Cost Type=FILTER(<Revenue));
                                                   CaptionML=ENU=Cost Object Surcharge }
    { 6   ;   ;Coverage Account    ;Code20        ;TableRelation="G/L Account".No.;
                                                   CaptionML=ENU=Coverage Account }
    { 7   ;   ;Transfer Account    ;Code20        ;TableRelation="G/L Account".No.;
                                                   CaptionML=ENU=Transfer Account }
    { 10  ;   ;Time Modified       ;DateTime      ;CaptionML=ENU=Time Modified;
                                                   Editable=No }
    { 11  ;   ;Time Posted         ;DateTime      ;CaptionML=ENU=Time Posted }
    { 12  ;   ;Actual Amount       ;Decimal       ;CaptionML=ENU=Actual Amount }
    { 13  ;   ;Actual Percentage   ;Decimal       ;CaptionML=ENU=Actual Percentage }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SKUSurchargeRec@1100485000 : Record 11020317;
      Text001@1100485001 : TextConst 'ENU=There are still SKU surcharges present with Code: %1.';

    PROCEDURE ImplementSKUSurchargeChanges@1100485002(PostingDT@1100485000 : DateTime);
    VAR
      lvSurchargeRec@1100485002 : Record 11020316;
      lvSKUSurchargeRec@1100485001 : Record 11020317;
      lvBaseModified@1100485003 : Boolean;
    BEGIN
      IF lvSurchargeRec.FINDSET(TRUE, FALSE) THEN
        REPEAT
          lvBaseModified := FALSE;
          IF (lvSurchargeRec."Actual Amount" <> lvSurchargeRec.Amount) OR
             (lvSurchargeRec."Actual Percentage" <> lvSurchargeRec.Percentage) THEN BEGIN
            lvSurchargeRec."Actual Amount" := lvSurchargeRec.Amount;
            lvSurchargeRec."Actual Percentage" := lvSurchargeRec.Percentage;
            lvSurchargeRec."Time Posted" := PostingDT;
            lvBaseModified := TRUE;
          END;
          lvSKUSurchargeRec.SETRANGE("Surcharge Code", lvSurchargeRec.Code);
          IF lvSKUSurchargeRec.FINDSET(TRUE, FALSE) THEN
            REPEAT
              IF lvSKUSurchargeRec."To Delete" THEN BEGIN
                lvSKUSurchargeRec."Deletion Posted" := TRUE;
                lvSKUSurchargeRec.DELETE(TRUE);
              END ELSE BEGIN
                IF (lvSKUSurchargeRec."Time Posted" = 0DT) OR lvBaseModified THEN BEGIN
                  lvSKUSurchargeRec."Time Posted" := PostingDT;
                  lvSKUSurchargeRec.MODIFY(TRUE);
                END;
              END;
            UNTIL lvSKUSurchargeRec.NEXT = 0;

          lvSurchargeRec.MODIFY(TRUE);
        UNTIL lvSurchargeRec.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

