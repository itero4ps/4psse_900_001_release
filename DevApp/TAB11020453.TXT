OBJECT Table 11020453 Hour Portal User
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=VAR
               CustomLicense@1100525001 : Record 11012449;
               HourPortalUser@1100525002 : Record 11020453;
               Load4PSHPTLicense@1100525000 : Codeunit 11126047;
             BEGIN
               IF NOT Load4PSHPTLicense.IsLicenseValid() THEN
                 ERROR(Text002, CustomLicense.TABLECAPTION);

               CustomLicense.GET(CustomLicense."Licence Type"::"4PS HPT");

               HourPortalUser.SETFILTER(Login, '<>%1', Login);
               IF HourPortalUser.COUNT >= CustomLicense."Number of Licenses" THEN
                 ERROR(Text003, FIELDCAPTION(Login), CustomLicense."Number of Licenses");
             END;

    CaptionML=ENU=Hour Portal User;
  }
  FIELDS
  {
    { 10  ;   ;Login               ;Text100       ;OnValidate=VAR
                                                                HourPortalUser@1100525000 : Record 11020453;
                                                              BEGIN
                                                                //Login := LOWERCASE(Login);
                                                                "Login Lower Case" := LOWERCASE(Login);

                                                                HourPortalUser.SETFILTER(Login, '<>%1', Login);
                                                                HourPortalUser.SETRANGE("Login Lower Case", "Login Lower Case");
                                                                IF HourPortalUser.FINDFIRST THEN
                                                                  ERROR(Text004, FIELDCAPTION(Login), Login, HourPortalUser.Login);
                                                              END;

                                                   CaptionML=ENU=Login;
                                                   NotBlank=Yes }
    { 20  ;   ;Company             ;Text30        ;TableRelation="Site Mngmnt. Hours Companies";
                                                   CaptionML=ENU=Company;
                                                   NotBlank=Yes }
    { 30  ;   ;User ID             ;Code50        ;TableRelation="User Setup";
                                                   OnValidate=VAR
                                                                UserSetup@1100409000 : Record 91;
                                                              BEGIN
                                                                IF ("User ID" <> '') THEN BEGIN
                                                                  UserSetup.GET("User ID");
                                                                  UserSetup.TESTFIELD("Employee No.");
                                                                  "Employee No" := UserSetup."Employee No.";
                                                                  CALCFIELDS("Employee Name", "Employee CAO");
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=User ID }
    { 40  ;   ;Employee No         ;Code20        ;TableRelation="Site Mngmnt. Hours Employee".No. WHERE (Company=FIELD(Company));
                                                   OnValidate=BEGIN
                                                                IF ("User ID" <> '') THEN
                                                                  FIELDERROR("Employee No");
                                                                CALCFIELDS("Employee Name", "Employee CAO");
                                                              END;

                                                   CaptionML=ENU=Employee No }
    { 41  ;   ;Employee Name       ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Site Mngmnt. Hours Employee"."Full Name" WHERE (Company=FIELD(Company),
                                                                                                                       No.=FIELD(Employee No)));
                                                   CaptionML=ENU=Employee Name;
                                                   Editable=No }
    { 42  ;   ;Employee CAO        ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Site Mngmnt. Hours Employee".CAO WHERE (Company=FIELD(Company),
                                                                                                               No.=FIELD(Employee No)));
                                                   CaptionML=ENU=Employee CAO;
                                                   Editable=No }
    { 43  ;   ;Permissions of a    ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Permissions of a" WHERE (No.=FIELD(Employee No)));
                                                   CaptionML=ENU=Permissions of a;
                                                   OptionCaptionML=ENU=" ,Project Manager,Site Manager,Project Engineer,Estimator,Supervisor,Buyercontact,Purchaser";
                                                   OptionString=[ ,Project Manager,Site Manager,Project Engineer,Estimator,Supervisor,Buyercontact,Purchaser];
                                                   Editable=No }
    { 50  ;   ;Password            ;Text250       ;OnValidate=VAR
                                                                EncryptionMgt@1100528302 : Codeunit 824;
                                                              BEGIN
                                                                IF (Password = '') THEN
                                                                  EXIT;
                                                                Password := EncryptionMgt.CalculateStringMD5(Password);
                                                              END;

                                                   ExtendedDatatype=Masked;
                                                   CaptionML=ENU=Password }
    { 70  ;   ;Login Lower Case    ;Text100       ;CaptionML=ENU=Login Lower Case }
  }
  KEYS
  {
    {    ;Login                                   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Employee@1100528300 : Record 5200;
      UserSetup@1100528301 : Record 91;
      Text000@1100528302 : TextConst 'ENU=Your new password for the Hour Portal is: %2\You can login with your account (%1) and this new password.';
      Text001@1100528303 : TextConst 'ENU=Your new password for the Hour Portal';
      Text002@1100525001 : TextConst 'ENU=Table %1 is corrupted.';
      Text003@1100525000 : TextConst 'ENU=%1 not allowed, maximum number of licenses (%1) is already reached.';
      Text004@1100525002 : TextConst 'ENU=%1 %2 not allowed, %1 %3 is already in use.';

    LOCAL PROCEDURE ICchange@1100528301();
    BEGIN
      Employee.CHANGECOMPANY(Company);
      UserSetup.CHANGECOMPANY(Company);
    END;

    PROCEDURE GenerateAllUsers@1100528304();
    VAR
      SiteMngmntHoursCompanies@1100528300 : Record 11012356;
      SiteMngmntHoursEmployee@1100528301 : Record 11012357;
      StringFunctions@1100528302 : Codeunit 11012273;
      ReadablePassword@1100528303 : Text;
    BEGIN
      IF (NOT SiteMngmntHoursCompanies.FINDSET) THEN
        EXIT;
      REPEAT
        SiteMngmntHoursEmployee.SETRANGE(Company, SiteMngmntHoursCompanies.Company);
        IF (SiteMngmntHoursEmployee.FINDSET) THEN BEGIN
          REPEAT
            INIT;
            Company := SiteMngmntHoursEmployee.Company;

            ICchange;
            Employee.GET(SiteMngmntHoursEmployee."No.");
            IF (NOT SiteMngmntHoursEmployee.External) THEN BEGIN
              UserSetup.SETRANGE("Employee No.", SiteMngmntHoursEmployee."No.");
              IF (UserSetup.FINDFIRST) THEN BEGIN
                //Login := LOWERCASE(COPYSTR(UserSetup."User ID", StringFunctions.RSTRPOS(UserSetup."User ID", '\')+1));
                Login := COPYSTR(UserSetup."User ID", StringFunctions.RSTRPOS(UserSetup."User ID", '\')+1);
                "Login Lower Case" := LOWERCASE(Login);
                VALIDATE("User ID", UserSetup."User ID");
              END;
            END ELSE BEGIN
              //Login := LOWERCASE(Employee.Initials+Employee."Middle Name"+Employee."Last Name");
              Login := Employee.Initials+Employee."Middle Name"+Employee."Last Name";
              "Login Lower Case" := LOWERCASE(Login);
              VALIDATE("Employee No", Employee."No.");
            END;

            IF (Login <> '') AND (Employee."Company E-Mail" <> '') AND
               (Employee."Show in Hours Portal")
            THEN BEGIN
              ReadablePassword := GeneratePassword;
              VALIDATE(Password, ReadablePassword);
              IF (INSERT(TRUE)) THEN
                MailPassword(ReadablePassword, Employee."Company E-Mail");
            END;
          UNTIL (SiteMngmntHoursEmployee.NEXT = 0);
        END;
      UNTIL (SiteMngmntHoursCompanies.NEXT = 0);
    END;

    LOCAL PROCEDURE MailPassword@1100528309(ReadablePassword@1100528303 : Text;EmailURL@1100528304 : Text);
    VAR
      MailCu@1100528302 : Codeunit 397;
      MailBody@1100528301 : Text[260];
    BEGIN
      MailBody := STRSUBSTNO(Text000, Login, ReadablePassword);
      MailCu.NewMessage(EmailURL,'','',Text001, MailBody, '', FALSE);
    END;

    LOCAL PROCEDURE GeneratePassword@1100528302() : Text;
    VAR
      StringFunctions@1100525000 : Codeunit 11012273;
      PasswordPlain@1100525001 : Text;
    BEGIN
      StringFunctions.GeneratePasswordPlainText(PasswordPlain, 10); //#kz, 20150114
      EXIT(PasswordPlain);
    END;

    BEGIN
    END.
  }
}

