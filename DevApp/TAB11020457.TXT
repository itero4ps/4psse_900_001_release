OBJECT Table 11020457 Estimate Merge Field
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Estimate Merge Field;
    LookupPageID=Page11125314;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code20        ;CaptionML=ENU=Code }
    { 100 ;   ;Table No.           ;Option        ;CaptionML=ENU=Table;
                                                   OptionCaptionML=ENU=" ,Company Information,,,,Try-Out Quote,Estimate,Sub-Estimate,Part";
                                                   OptionString=[ ,CompInfo,,,,TryOutQuote,Estimate,SubEstimate,Part] }
    { 101 ;   ;Field No.           ;Integer       ;OnValidate=BEGIN
                                                                SetrangeTableNo;

                                                                gvRecField.SETRANGE("No.", "Field No.");
                                                                IF NOT gvRecField.FINDFIRST THEN BEGIN
                                                                  "Field Name" := '';
                                                                  IF "Field No." <> 0 THEN
                                                                    MESSAGE(Text001, "Field No.", "Table No.");
                                                                END ELSE BEGIN
                                                                  "Field Name" := gvRecField."Field Caption";
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              SetrangeTableNo;

                                                              gvFrmTableFields.SETTABLEVIEW(gvRecField);
                                                              gvFrmTableFields.EDITABLE   := FALSE;
                                                              gvFrmTableFields.LOOKUPMODE := TRUE;
                                                              IF gvFrmTableFields.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                gvFrmTableFields.GETRECORD(gvRecField);
                                                                "Field No." := gvRecField."No.";
                                                                "Field Name" := gvRecField."Field Caption";
                                                              END;
                                                              CLEAR(gvFrmTableFields);
                                                            END;

                                                   CaptionML=ENU=Field No. }
    { 102 ;   ;Field Name          ;Text30        ;CaptionML=ENU=Field Name;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      gvRecField@1210190002 : Record 2000000041;
      gvFrmTableFields@1210190001 : Page 11012403;
      RecRef@1210190000 : RecordRef;
      Text001@1210190004 : TextConst 'ENU=Field %1 not present in Table %2';
      Text002@1210190003 : TextConst 'ENU=******************************';
      EstTotal@1210190005 : Decimal;
      SubEstTotal@1210190006 : Decimal;
      PartTotal@1210190007 : Decimal;

    PROCEDURE SetrangeTableNo@4();
    BEGIN
      gvRecField.RESET;
      CASE "Table No." OF
        "Table No."::CompInfo : gvRecField.SETRANGE(TableNo, DATABASE::"Company Information");
        "Table No."::TryOutQuote: gvRecField.SETRANGE(TableNo, DATABASE::"Try-out Quote");
        "Table No."::Estimate: gvRecField.SETRANGE(TableNo, DATABASE::Estimate);
        "Table No."::SubEstimate: gvRecField.SETRANGE(TableNo, DATABASE::"Sub-Estimate NEW");
        "Table No."::Part: gvRecField.SETRANGE(TableNo, DATABASE::"Estimate Part NEW");
      END;
    END;

    PROCEDURE GetSubstitute@1210190004(DocNo@1100526201 : Code[20];SubDoc1@1210190003 : Code[20];SubDoc2@1210190004 : Code[20];SubDoc3@1210190005 : Code[20]) rTxtContent@11012000 : Text[250];
    VAR
      lvCompInfoRec@1210190002 : Record 79;
      lvTryOutRec@1210190000 : Record 11012758;
      lvEstRec@1210190006 : Record 11012151;
      lvSubEstRec@1210190001 : Record 11072074;
      lvEstPartRec@1210190007 : Record 11072073;
    BEGIN
      CASE "Table No." OF
        "Table No."::CompInfo:
          BEGIN
            lvCompInfoRec.GET;
            RecRef.GETTABLE(lvCompInfoRec);
            rTxtContent := AnyTableFieldValue(DATABASE::"Company Information", "Field No.");
          END;
        "Table No."::TryOutQuote:
          BEGIN
            lvEstRec.GET(DocNo);
            DocNo := lvEstRec.GetTryOut(2);
            IF NOT lvTryOutRec.GET(DocNo) THEN EXIT('');
            RecRef.GETTABLE(lvTryOutRec);
            rTxtContent := AnyTableFieldValue(DATABASE::"Try-out Quote", "Field No.");
          END;
        "Table No."::Estimate:
          BEGIN
            lvEstRec.GET(DocNo);
            RecRef.GETTABLE(lvEstRec);
            rTxtContent := AnyTableFieldValue(DATABASE::Estimate, "Field No.");
          END;
        "Table No."::SubEstimate:
          BEGIN
            lvSubEstRec.GET(DocNo, SubDoc1);
            RecRef.GETTABLE(lvSubEstRec);
            rTxtContent := AnyTableFieldValue(DATABASE::"Sub-Estimate NEW", "Field No.");
          END;
        "Table No."::Part:
          BEGIN
            lvEstPartRec.GET(DocNo, SubDoc1, SubDoc2, SubDoc3);
            RecRef.GETTABLE(lvEstPartRec);
            rTxtContent := AnyTableFieldValue(DATABASE::"Estimate Part NEW", "Field No.");
          END;
      END;
    END;

    PROCEDURE AnyTableFieldValue@1210190010(TableNo@1210190000 : Integer;FieldNo@1210190001 : Integer) : Text[250];
    VAR
      DecimalValue@1210190002 : Decimal;
      FldRef@1100526200 : FieldRef;
      FieldRec@1100526202 : Record 2000000041;
    BEGIN
      //copied from codeunit 110120401
      FldRef := RecRef.FIELD(FieldNo);
      IF NOT FieldRec.GET(TableNo,FieldNo) THEN
        EXIT(Text002);

      IF FieldRec.Class = FieldRec.Class::FlowField THEN
        FldRef.CALCFIELD;

      IF FieldRec.Type = FieldRec.Type::Decimal THEN BEGIN
        EVALUATE(DecimalValue,FORMAT(FldRef.VALUE));
        EXIT(FormatDecimal(DecimalValue));
      END;

      EXIT(FORMAT(FldRef));
    END;

    PROCEDURE FormatDecimal@1210190009(DecimalValue@1210190000 : Decimal) : Text[30];
    BEGIN
      //copied from codeunit 110120401
      EXIT(FORMAT(DecimalValue,0,'<Precision,2:><Standard format,0>'));
    END;

    PROCEDURE SetTotals@1210190000(iLevel@1210190003 : Integer;iEstTotal@1210190000 : Decimal;iSubEstTotal@1210190001 : Decimal;iPartTotal@1210190002 : Decimal);
    BEGIN
      IF iLevel >= 1 THEN
        EstTotal := iEstTotal;
      IF iLevel >= 2 THEN
        SubEstTotal := iSubEstTotal;
      IF iLevel >= 3 THEN
        PartTotal := iPartTotal;
    END;

    BEGIN
    END.
  }
}

