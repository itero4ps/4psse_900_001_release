OBJECT Table 11020459 BasePart Quantity Sheet Header
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               QuantitySheetLine.SETRANGE("Part Group","Part Group");
               QuantitySheetLine.SETRANGE(Part,Part);
               QuantitySheetLine.SETRANGE("Quantity Sheet",Code);
               QuantitySheetLine.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Base Part Quantity Sheet Header;
    LookupPageID=Page11229783;
    DrillDownPageID=Page11229783;
  }
  FIELDS
  {
    { 10  ;   ;Part Group          ;Code10        ;TableRelation="Estimate Group".No. WHERE (Type=CONST(Part));
                                                   CaptionML=ENU=Part Group }
    { 15  ;   ;Part                ;Code20        ;TableRelation="Estimate Base Part".Code WHERE (Part Group=FIELD(Part Group));
                                                   CaptionML=ENU=Part;
                                                   NotBlank=Yes }
    { 16  ;   ;Part Description    ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Estimate Base Part".Description WHERE (Part Group=FIELD(Part Group),
                                                                                                              Code=FIELD(Part)));
                                                   CaptionML=ENU=Part Description;
                                                   Editable=No }
    { 20  ;   ;Code                ;Code10        ;CaptionML=ENU=Estimate Quantity Sheet;
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 40  ;   ;Total Quantity      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("BasePart Quantity Sheet Line".Quantity WHERE (Part Group=FIELD(Part Group),
                                                                                                                  Part=FIELD(Part),
                                                                                                                  Quantity Sheet=FIELD(Code)));
                                                   CaptionML=ENU=Total Quantity;
                                                   Editable=No }
    { 50  ;   ;Total Length        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("BasePart Quantity Sheet Line".Length WHERE (Part Group=FIELD(Part Group),
                                                                                                                Part=FIELD(Part),
                                                                                                                Quantity Sheet=FIELD(Code)));
                                                   CaptionML=ENU=Total Length;
                                                   Editable=No }
    { 60  ;   ;Total Width         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("BasePart Quantity Sheet Line".Width WHERE (Part Group=FIELD(Part Group),
                                                                                                               Part=FIELD(Part),
                                                                                                               Quantity Sheet=FIELD(Code)));
                                                   CaptionML=ENU=Total Width;
                                                   Editable=No }
    { 70  ;   ;Total Height        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("BasePart Quantity Sheet Line".Height WHERE (Part Group=FIELD(Part Group),
                                                                                                                Part=FIELD(Part),
                                                                                                                Quantity Sheet=FIELD(Code)));
                                                   CaptionML=ENU=Total Height;
                                                   Editable=No }
    { 80  ;   ;Total Result        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("BasePart Quantity Sheet Line".Result WHERE (Part Group=FIELD(Part Group),
                                                                                                                Part=FIELD(Part),
                                                                                                                Quantity Sheet=FIELD(Code)));
                                                   CaptionML=ENU=Total Result;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Part Group,Part,Code                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      QuantityRelation@1100525000 : Record 11020463;
      QuantitySheetLine@1100525002 : Record 11020460;
      Result@1100525003 : Decimal;

    PROCEDURE WriteDataFromSheetToBasePart@1100525000(PartGroup@1100525000 : Code[20];PartCode@1100409000 : Code[20];Code@1100525001 : Code[10]);
    BEGIN
      QuantityRelation.SETFILTER("Estimate No.", '%1', '');
      QuantityRelation.SETFILTER("Sub-Estimate No.", '%1', '');
      QuantityRelation.SETRANGE("Part Group", PartGroup);
      QuantityRelation.SETRANGE(Part, PartCode);
      IF Code <> '' THEN
        QuantityRelation.SETRANGE(QuantityRelation."Quantity Sheet", Code);
      IF QuantityRelation.FINDSET THEN BEGIN
        REPEAT
          IF QuantitySheetLine.GET(QuantityRelation."Part Group", QuantityRelation.Part,
             QuantityRelation."Quantity Sheet",
             QuantityRelation."Quantity Sheet Line") THEN BEGIN
            CalcResultRelation();
            WriteDataToBasePart(Result);
          END;
        UNTIL QuantityRelation.NEXT = 0;
      END;
    END;

    PROCEDURE CalcResultRelation@1100525003();
    VAR
      FieldCde@1100525000 : Code[1];
    BEGIN
      FieldCde := QuantityRelation."Quantity Sheet Field";
      IF QuantitySheetLine.Total <> QuantitySheetLine.Total::Subtotal THEN BEGIN
        CASE FieldCde OF
          'A':Result := QuantitySheetLine.Quantity;
          'B':Result := QuantitySheetLine.Length;
          'C':Result := QuantitySheetLine."Total Length";
          'D':Result := QuantitySheetLine.Width;
          'E':Result := QuantitySheetLine."Total Width";
          'F':Result := QuantitySheetLine.Height;
          'G':Result := QuantitySheetLine."Total Height";
        ELSE
          Result := QuantitySheetLine.Result;
        END;
      END ELSE BEGIN
        QuantitySheetLine.CALCFIELDS("Sum Quantity","Sum Result",
          "Sum Length","Sum Width","Sum Height",
          "Sum Total Length","Sum Total Width","Sum Total Height");
        CASE FieldCde OF
          'A':Result := QuantitySheetLine."Sum Quantity";
          'B':Result := QuantitySheetLine."Sum Length";
          'C':Result := QuantitySheetLine."Sum Total Length";
          'D':Result := QuantitySheetLine."Sum Width";
          'E':Result := QuantitySheetLine."Sum Total Width";
          'F':Result := QuantitySheetLine."Sum Height";
          'G':Result := QuantitySheetLine."Sum Total Height";
        ELSE
          Result := QuantitySheetLine."Sum Result";
        END;
      END;
    END;

    PROCEDURE WriteDataToBasePart@1100525001(iResult@1100525000 : Decimal);
    VAR
      EstSetup@1100528900 : Record 11012150;
      OldPartLineRec@1100528901 : Record 11012187;
      NewPartLineRec@1100528902 : Record 11072076;
    BEGIN
      EstSetup.GET;
      IF EstSetup.Converted THEN BEGIN
        IF NewPartLineRec.GET(QuantityRelation."Part Group", QuantityRelation.Part, QuantityRelation."Estimate Line") THEN BEGIN
          NewPartLineRec.VALIDATE(Quantity,iResult);
          NewPartLineRec.MODIFY(TRUE);
        END;
      END ELSE BEGIN
        IF OldPartLineRec.GET(QuantityRelation."Part Group", QuantityRelation.Part, QuantityRelation."Estimate Line") THEN BEGIN
          OldPartLineRec.VALIDATE(Quantity,iResult);
          OldPartLineRec.MODIFY(TRUE);
        END;
      END;
    END;

    BEGIN
    END.
  }
}

