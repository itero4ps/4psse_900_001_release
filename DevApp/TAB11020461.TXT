OBJECT Table 11020461 BaseRcp Quantity Sheet Header
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               QuantitySheetLine.SETRANGE("Recipe Group","Recipe Group");
               QuantitySheetLine.SETRANGE(Recipe,Recipe);
               QuantitySheetLine.SETRANGE("Quantity Sheet",Code);
               QuantitySheetLine.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Base Recipe Quantity Sheet Header;
    LookupPageID=Page11229786;
    DrillDownPageID=Page11229786;
  }
  FIELDS
  {
    { 10  ;   ;Recipe Group        ;Code10        ;TableRelation="Recipe Group";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Recipe Group }
    { 15  ;   ;Recipe              ;Code20        ;TableRelation="Base Recipe".No. WHERE (Recipe Group=FIELD(Recipe Group));
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Recipe;
                                                   NotBlank=Yes }
    { 16  ;   ;Recipe Description  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Base Recipe".Description WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                       No.=FIELD(Recipe)));
                                                   CaptionML=ENU=Recipe Description;
                                                   Editable=No }
    { 20  ;   ;Code                ;Code10        ;CaptionML=ENU=Estimate Quantity Sheet;
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 40  ;   ;Total Quantity      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("BaseRcp Quantity Sheet Line".Quantity WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                                 Recipe=FIELD(Recipe),
                                                                                                                 Quantity Sheet=FIELD(Code)));
                                                   CaptionML=ENU=Total Quantity;
                                                   Editable=No }
    { 50  ;   ;Total Length        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("BaseRcp Quantity Sheet Line".Length WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                               Recipe=FIELD(Recipe),
                                                                                                               Quantity Sheet=FIELD(Code)));
                                                   CaptionML=ENU=Total Length;
                                                   Editable=No }
    { 60  ;   ;Total Width         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("BaseRcp Quantity Sheet Line".Width WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                              Recipe=FIELD(Recipe),
                                                                                                              Quantity Sheet=FIELD(Code)));
                                                   CaptionML=ENU=Total Width;
                                                   Editable=No }
    { 70  ;   ;Total Height        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("BaseRcp Quantity Sheet Line".Height WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                               Recipe=FIELD(Recipe),
                                                                                                               Quantity Sheet=FIELD(Code)));
                                                   CaptionML=ENU=Total Height;
                                                   Editable=No }
    { 80  ;   ;Total Result        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("BaseRcp Quantity Sheet Line".Result WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                               Recipe=FIELD(Recipe),
                                                                                                               Quantity Sheet=FIELD(Code)));
                                                   CaptionML=ENU=Total Result;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Recipe Group,Recipe,Code                ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      QuantityRelation@1100525000 : Record 11020463;
      QuantitySheetLine@1100525002 : Record 11020465;
      Result@1100525003 : Decimal;

    PROCEDURE WriteDataFromSheetToBaseRecipe@1100525000(RecipeGroup@1100525000 : Code[20];RecipeCode@1100409000 : Code[20];Code@1100525001 : Code[10]);
    BEGIN
      QuantityRelation.SETFILTER("Estimate No.", '%1', '');
      QuantityRelation.SETFILTER("Sub-Estimate No.", '%1', '');
      QuantityRelation.SETRANGE("Recipe Group", RecipeGroup);
      QuantityRelation.SETRANGE(Recipe, RecipeCode);
      IF Code <> '' THEN
        QuantityRelation.SETRANGE(QuantityRelation."Quantity Sheet", Code);
      IF QuantityRelation.FINDSET THEN BEGIN
        REPEAT
          IF QuantitySheetLine.GET(QuantityRelation."Recipe Group", QuantityRelation.Recipe,
             QuantityRelation."Quantity Sheet",
             QuantityRelation."Quantity Sheet Line") THEN BEGIN
            CalcResultRelation();
            WriteDataToBaseRecipe(Result);
          END;
        UNTIL QuantityRelation.NEXT = 0;
      END;
    END;

    PROCEDURE CalcResultRelation@1100525003();
    VAR
      FieldCde@1100525000 : Code[1];
    BEGIN
      FieldCde := QuantityRelation."Quantity Sheet Field";
      IF QuantitySheetLine.Total <> QuantitySheetLine.Total::Subtotal THEN BEGIN
        CASE FieldCde OF
          'A':Result := QuantitySheetLine.Quantity;
          'B':Result := QuantitySheetLine.Length;
          'C':Result := QuantitySheetLine."Total Length";
          'D':Result := QuantitySheetLine.Width;
          'E':Result := QuantitySheetLine."Total Width";
          'F':Result := QuantitySheetLine.Height;
          'G':Result := QuantitySheetLine."Total Height";
        ELSE
          Result := QuantitySheetLine.Result;
        END;
      END ELSE BEGIN
        QuantitySheetLine.CALCFIELDS("Sum Quantity","Sum Result",
          "Sum Length","Sum Width","Sum Height",
          "Sum Total Length","Sum Total Width","Sum Total Height");
        CASE FieldCde OF
          'A':Result := QuantitySheetLine."Sum Quantity";
          'B':Result := QuantitySheetLine."Sum Length";
          'C':Result := QuantitySheetLine."Sum Total Length";
          'D':Result := QuantitySheetLine."Sum Width";
          'E':Result := QuantitySheetLine."Sum Total Width";
          'F':Result := QuantitySheetLine."Sum Height";
          'G':Result := QuantitySheetLine."Sum Total Height";
        ELSE
          Result := QuantitySheetLine."Sum Result";
        END;
      END;
    END;

    PROCEDURE WriteDataToBaseRecipe@1100525001(iResult@1100525000 : Decimal);
    VAR
      EstSetup@1100528900 : Record 11012150;
      OldRecipeLineRec@1100528901 : Record 11012156;
      NewRecipeLineRec@1100528902 : Record 11020582;
    BEGIN
      EstSetup.GET;
      IF EstSetup.Converted THEN BEGIN
        IF NewRecipeLineRec.GET(QuantityRelation."Recipe Group", QuantityRelation.Recipe, QuantityRelation."Recipe Line") THEN BEGIN
          NewRecipeLineRec.VALIDATE(Quantity,iResult);
          NewRecipeLineRec.MODIFY(TRUE);
        END;
      END ELSE BEGIN
        IF OldRecipeLineRec.GET('','','','',0,QuantityRelation."Recipe Group", QuantityRelation.Recipe, QuantityRelation."Recipe Line") THEN BEGIN
          OldRecipeLineRec.VALIDATE(Quantity,iResult);
          OldRecipeLineRec.MODIFY(TRUE);
        END;
      END;
    END;

    BEGIN
    END.
  }
}

