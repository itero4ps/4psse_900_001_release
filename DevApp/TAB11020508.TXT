OBJECT Table 11020508 Posted Pl. Transport Cost Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Posted Transport Cost Line;
  }
  FIELDS
  {
    { 10  ;   ;Transport Order No. ;Code20        ;TableRelation="Posted Plant Transport Order";
                                                   CaptionML=ENU=Transport Order No.;
                                                   Editable=No }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No.;
                                                   Editable=No }
    { 30  ;   ;Plant Order No.     ;Code20        ;TableRelation="Transport Plant Order Line"."Plant Order No." WHERE (Transport Order No.=FIELD(Transport Order No.));
                                                   CaptionML=ENU=Plant Order No.;
                                                   Editable=No }
    { 31  ;   ;Transfer Date       ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Order"."Transfer Date" WHERE (No.=FIELD(Plant Order No.)));
                                                   CaptionML=ENU=Transfer Date;
                                                   Editable=No }
    { 32  ;   ;From Location       ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Order"."From Location" WHERE (No.=FIELD(Plant Order No.)));
                                                   CaptionML=ENU=From Location;
                                                   Editable=No }
    { 33  ;   ;To Location         ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Order"."To Location" WHERE (No.=FIELD(Plant Order No.)));
                                                   CaptionML=ENU=To Location;
                                                   Editable=No }
    { 40  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   CaptionML=ENU=Plant Type;
                                                   Editable=No }
    { 60  ;   ;Type                ;Option        ;InitValue=Text;
                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Transport,Load/Unload,Text;
                                                   OptionString=Transport,Load/Unload,Text;
                                                   Editable=No }
    { 70  ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Text)) "Standard Text"
                                                                 ELSE IF (Type=CONST(Load/Unload)) "Dimension Value".Code WHERE (Plant=CONST(Load/Unload),
                                                                                                                                 Global Dimension No.=CONST(2))
                                                                                                                                 ELSE "Dimension Value".Code WHERE (Plant=CONST(Transport),
                                                                                                                                                                    Global Dimension No.=CONST(2));
                                                   CaptionML=ENU=No.;
                                                   Editable=No }
    { 80  ;   ;Description         ;Text50        ;CaptionML=ENU=Description;
                                                   Editable=No }
    { 90  ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 100 ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=ENU=Unit of Measure;
                                                   Editable=No }
    { 110 ;   ;Price               ;Decimal       ;CaptionML=ENU=Price;
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 120 ;   ;Amount              ;Decimal       ;CaptionML=ENU=Amount;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 130 ;   ;Invoice-to          ;Option        ;CaptionML=ENU=Invoice-to;
                                                   OptionCaptionML=ENU=" ,Internal,Project,Location,From Location,To Location";
                                                   OptionString=[ ,Internal,Project,Location,From Location,To Location];
                                                   BlankZero=Yes;
                                                   Description=Deviation regarding to transport order header;
                                                   Editable=No }
    { 150 ;   ;Order Amount        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Posted Pl. Transport Cost Line".Amount WHERE (Transport Order No.=FIELD(Transport Order No.)));
                                                   CaptionML=ENU=Order Amount;
                                                   Editable=No }
    { 160 ;   ;Linked Transport Order (Cost);Code20;
                                                   TableRelation="Posted Plant Transport Order";
                                                   CaptionML=ENU=Linked Transport Order (Cost);
                                                   Editable=No }
    { 170 ;   ;Linked Plant Order No. (Cost);Code20;
                                                   TableRelation="Transport Plant Order Line"."Plant Order No." WHERE (Transport Order No.=FIELD("Linked Transport Order (Cost)"));
                                                   CaptionML=ENU=Linked Plant Order No. (Cost);
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Transport Order No.,Line No.            ;SumIndexFields=Amount;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PlantSetupRec@1100485000 : Record 11012550;
      HoldWeightPlantOrderTotWeight@1100485007 : Decimal;
      HoldWeightPlantOrderNo@1100485008 : Code[20];
      HoldWeightType@1100485001 : Option;

    PROCEDURE GetWeightCostLine@1100485008() : Decimal;
    VAR
      lvWeight@1100485000 : Decimal;
    BEGIN
      PlantSetupRec.GET;
      IF PlantSetupRec."Lnk TO Cost on Main TO allowed" AND
        (Type = Type::"Load/Unload") AND ("Linked Transport Order (Cost)" <> '')
      THEN BEGIN
        IF "Linked Plant Order No. (Cost)" <> '' THEN
          lvWeight := lvGetTotalWeightPlantOrder("Linked Plant Order No. (Cost)")
        ELSE
          lvWeight := lvGetTotalWeightTransOrder("Linked Transport Order (Cost)");
      END ELSE BEGIN
        IF "Plant Order No." <> '' THEN BEGIN
          IF "Plant Type" <> '' THEN
            lvWeight := lvGetTotalWeightPOPlantType("Plant Order No.", "Plant Type")
          ELSE
            lvWeight := lvGetTotalWeightPlantOrder("Plant Order No.")
        END ELSE
          lvWeight := lvGetTotalWeightTransOrder("Transport Order No.");
      END;

      EXIT(lvWeight);
    END;

    LOCAL PROCEDURE lvGetTotalWeightTransOrder@1100485004(ITransOrderNo@1100485002 : Code[20]) : Decimal;
    VAR
      lvTransPlantOrderRec@1100485000 : Record 11020509;
      lvWeight@1100485001 : Decimal;
    BEGIN
      IF ITransOrderNo = '' THEN
        EXIT(0);

      lvTransPlantOrderRec.SETRANGE("Transport Order No.", ITransOrderNo);
      IF NOT lvTransPlantOrderRec.FIND('-') THEN
        EXIT(0);

      REPEAT
        lvWeight := lvWeight + lvGetTotalWeightPlantOrder(lvTransPlantOrderRec."Plant Order No.");
      UNTIL lvTransPlantOrderRec.NEXT = 0;

      EXIT(lvWeight);
    END;

    LOCAL PROCEDURE lvGetTotalWeightPlantOrder@1100485001(IPlantOrderNo@1100485001 : Code[20]) : Decimal;
    VAR
      lvPlantOrderLine@1100485000 : Record 11012557;
    BEGIN
      IF IPlantOrderNo = '' THEN
        EXIT(0);

      IF (IPlantOrderNo <> HoldWeightPlantOrderNo) OR (Type <> HoldWeightType) THEN BEGIN
        lvPlantOrderLine."Plant Order No." := IPlantOrderNo;
        HoldWeightPlantOrderTotWeight := lvPlantOrderLine.PlantOrderTotalWeight((Type = Type::"Load/Unload"));
        HoldWeightPlantOrderNo := IPlantOrderNo;
        HoldWeightType := Type;
      END;

      EXIT(HoldWeightPlantOrderTotWeight);
    END;

    LOCAL PROCEDURE lvGetTotalWeightPOPlantType@1100525000(IPlantOrderNo@1100485001 : Code[20];IPlantType@1100525000 : Code[20]) : Decimal;
    VAR
      lvPlantOrderLine@1100485000 : Record 11012557;
    BEGIN
      IF (IPlantOrderNo = '') OR (IPlantType = '') THEN
        EXIT(0);

      lvPlantOrderLine."Plant Order No." := IPlantOrderNo;
      EXIT(lvPlantOrderLine.PlantOrderTotalWeightPlantType(IPlantType, (Type = Type::"Load/Unload")));
    END;

    BEGIN
    END.
  }
}

