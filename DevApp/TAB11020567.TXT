OBJECT Table 11020567 Extended Field Property
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ExtendedFieldProperty@1100525001 : Record 11020567;
               ChangeLogSetupTable@1100525000 : Record 403;
               ChangeLogSetupField@1100525003 : Record 404;
               RoleFieldAutorisation@1100525002 : Record 11020568;
             BEGIN
               ExtendedFieldProperty.SETRANGE("Table No.", "Table No.");
               IF (ExtendedFieldProperty.COUNT = 1) THEN
                 IF ChangeLogSetupTable.GET("Table No.") THEN
                   IF (ChangeLogSetupTable."Log Modification" = ChangeLogSetupTable."Log Modification"::"Some Fields") AND
                      (ChangeLogSetupTable."Log Insertion" = 0) AND
                      (ChangeLogSetupTable."Log Deletion" = 0) THEN BEGIN
                     ChangeLogSetupField.SETRANGE("Table No.", "Table No.");
                     IF ChangeLogSetupField.ISEMPTY THEN
                       ChangeLogSetupTable.DELETE;
                   END;

               RoleFieldAutorisation.SETRANGE("Table No.","Table No.");
               RoleFieldAutorisation.SETRANGE("Field No.","Field No.");
               RoleFieldAutorisation.DELETEALL;
             END;

    OnRename=VAR
               Text11012000@1100525000 : TextConst 'ENU="Rename not allowed; delete and insert instead."';
             BEGIN
               ERROR(Text11012000);
             END;

    CaptionML=ENU=Extended Field Property;
    LookupPageID=Page11124831;
    DrillDownPageID=Page11124831;
  }
  FIELDS
  {
    { 10  ;   ;Table No.           ;Integer       ;TableRelation=Object.ID WHERE (Type=FILTER(Table));
                                                   OnLookup=VAR
                                                              Object@1100525001 : Record 2000000001;
                                                              TableObjectsForm@1100525000 : Page 669;
                                                            BEGIN
                                                              Object.SETRANGE(Type, Object.Type::Table);
                                                              IF "Table No." <> 0 THEN BEGIN
                                                                Object.Type := Object.Type::Table;
                                                                Object.ID := "Table No.";
                                                                TableObjectsForm.SETRECORD(Object);
                                                              END;
                                                              TableObjectsForm.SETTABLEVIEW(Object);
                                                              TableObjectsForm.EDITABLE(FALSE);
                                                              TableObjectsForm.LOOKUPMODE(TRUE);
                                                              IF TableObjectsForm.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                TableObjectsForm.GETRECORD(Object);
                                                                VALIDATE("Table No.", Object.ID);
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Table No.;
                                                   NotBlank=Yes }
    { 20  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  Class=CONST(Normal),
                                                                                  Type=FILTER(<>BLOB));
                                                   CaptionML=ENU=Field No.;
                                                   NotBlank=Yes }
    { 30  ;   ;Check Field Authorization;Boolean  ;OnValidate=VAR
                                                                FieldAuthMgt@1100525000 : Codeunit 11012395;
                                                                lvExtendedTableRec@1100525001 : Record 11020573;
                                                                ChangeLogSetupTable@1100525002 : Record 403;
                                                              BEGIN
                                                                IF "Check Field Authorization" THEN BEGIN
                                                                  FieldAuthMgt.PermissionCheck;
                                                                  IF lvExtendedTableRec.IsFieldAuthorizationAllowed("Table No.") THEN
                                                                    ERROR(STRSUBSTNO(Text001, FIELDCAPTION("Check Field Authorization"), "Table No."));
                                                                  IF NOT ChangeLogSetupTable.GET("Table No.") THEN BEGIN
                                                                    ChangeLogSetupTable.INIT;
                                                                    ChangeLogSetupTable.VALIDATE("Table No.","Table No.");
                                                                    ChangeLogSetupTable.VALIDATE("Log Modification",ChangeLogSetupTable."Log Modification"::"Some Fields");
                                                                    ChangeLogSetupTable.INSERT;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Check Field Authorization }
    { 40  ;   ;Check Completion control;Boolean   ;OnValidate=VAR
                                                                lvExtendedTableRec@1100525000 : Record 11020573;
                                                              BEGIN
                                                                IF "Check Completion control" THEN BEGIN
                                                                  IF NOT lvExtendedTableRec.IsCompletionControlAllowed("Table No.") THEN
                                                                    ERROR(STRSUBSTNO(Text001, FIELDCAPTION("Check Completion control"), "Table No."));

                                                                  CALCFIELDS("Field Type");
                                                                  IF IsCompletionControlNotAllowed(Rec) THEN
                                                                    ERROR(STRSUBSTNO(Text002, FIELDCAPTION("Check Completion control"), "Field Type"));
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Check Completion control }
    { 70  ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table No.),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Name;
                                                   Editable=No }
    { 80  ;   ;Field Caption       ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Caption;
                                                   Editable=No }
    { 90  ;   ;Table Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(Table),
                                                                                         ID=FIELD(Table No.)));
                                                   CaptionML=ENU=Table Name;
                                                   Editable=No }
    { 100 ;   ;Table Caption       ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=ENU=Table Caption;
                                                   Editable=No }
    { 110 ;   ;Field Type          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Type Name" WHERE (TableNo=FIELD(Table No.),
                                                                                               No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Type;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Table No.,Field No.                     ;Clustered=Yes }
    {    ;Check Field Authorization                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100525002 : TextConst 'ENU=%1 not possible for table %2';
      Text002@1100525001 : TextConst 'ENU=%1 not possible for table %2';

    PROCEDURE IsCompletionControlNotAllowed@1100525000(lvExtFieldPropertyRec@1100525000 : Record 11020567) : Boolean;
    BEGIN
      WITH lvExtFieldPropertyRec DO
        EXIT("Field Type" IN ['Option', 'Boolean']);
    END;

    PROCEDURE FieldInCompletionControl@1100528400(TableNo@1100528400 : Integer;FieldNo@1100528401 : Integer) : Boolean;
    VAR
      ExtendedFieldProperty@1100528402 : Record 11020567;
    BEGIN
      ExtendedFieldProperty.SETRANGE("Table No.", TableNo);
      ExtendedFieldProperty.SETRANGE("Field No.", FieldNo);
      ExtendedFieldProperty.SETRANGE("Check Completion control", TRUE);
      EXIT(NOT ExtendedFieldProperty.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

