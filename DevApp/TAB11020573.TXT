OBJECT Table 11020573 Extended Table Property
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    CaptionML=ENU=Extended Table Property;
  }
  FIELDS
  {
    { 20  ;   ;Table ID            ;Integer       ;CaptionML=ENU=Table ID;
                                                   Editable=No }
    { 35  ;   ;Type of Table       ;Option        ;CaptionML=ENU=Type of Table;
                                                   OptionCaptionML=ENU=,Setup,Common Data,Basic Data,Financial Setup,Basic Cards,Input,Transaction,Not Applicable,Tools;
                                                   OptionString=,Setup,Common Data,Basic Data,Financial Setup,Basic Cards,Input,Transaction,Not Applicable,Tools;
                                                   Editable=No }
    { 50  ;   ;Table Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(Table),
                                                                                         ID=FIELD(Table ID)));
                                                   CaptionML=ENU=Table Name;
                                                   Editable=No }
    { 60  ;   ;Table Caption       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=ENU=Table Caption;
                                                   Editable=No }
    { 100 ;   ;Page ID             ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Page));
                                                   CaptionML=ENU=Page ID;
                                                   Editable=No }
    { 150 ;   ;License permission  ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("License Permission"."Read Permission" WHERE (Object Type=CONST(TableData),
                                                                                                                    Object Number=FIELD(Table ID)));
                                                   CaptionML=ENU=Current License Permission;
                                                   OptionCaptionML=ENU=" ,Yes,Indirect";
                                                   OptionString=[ ,Yes,Indirect];
                                                   Editable=No }
    { 160 ;   ;Granule             ;Text250       ;CaptionML=ENU=Granule;
                                                   Editable=No }
    { 170 ;   ;Granule Name        ;Text50        ;CaptionML=ENU=Granule Name;
                                                   Editable=No }
    { 180 ;   ;Update No.          ;Text30        ;CaptionML=ENU=Update No.;
                                                   Editable=No }
    { 500 ;   ;DPA Field No.       ;Integer       ;CaptionML=ENU=DPA Field No.;
                                                   Editable=No }
    { 510 ;   ;DPA Filter Field No.;Integer       ;CaptionML=ENU=DPA Filter Field No.;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Table ID                                ;Clustered=Yes }
    {    ;Type of Table                            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ExtTablePropRec@1100525005 : Record 11020573;
      Window@1100525003 : Dialog;
      Progress@1100525002 : Integer;
      NoOfRecords@1100525001 : Integer;

    PROCEDURE UpdateProgressWindow@1100525005(NewProgress@11012000 : Integer);
    BEGIN
      IF NoOfRecords > 0 THEN BEGIN
        NewProgress := ROUND(NewProgress / NoOfRecords * 100,1);
        IF (NewProgress <> Progress) THEN
          Window.UPDATE(1,100 * NewProgress);
        Progress := NewProgress;
      END;
    END;

    PROCEDURE IsCheckSetupLineAllowed@1100525003(TableID@1100525000 : Integer) : Boolean;
    BEGIN
      WITH ExtTablePropRec DO BEGIN
        GET(TableID);
        EXIT(
          ("Type of Table" IN [
             "Type of Table"::Setup,
             "Type of Table"::"Common Data",
             "Type of Table"::"Basic Data",
             "Type of Table"::"Financial Setup"]));
      END;
    END;

    PROCEDURE IsRimImportExportAllowed@1100525001(TableID@1100525000 : Integer) : Boolean;
    BEGIN
      WITH ExtTablePropRec DO BEGIN
        GET(TableID);
        EXIT(
          ("Type of Table" IN [
             "Type of Table"::"Common Data",
             "Type of Table"::"Basic Data",
             "Type of Table"::"Financial Setup"]) AND
           NOT IsMercashTable(TableID));
      END;
    END;

    PROCEDURE IsXMLImportExportAllowed@1100525000(TableID@1100525000 : Integer) : Boolean;
    BEGIN
      WITH ExtTablePropRec DO BEGIN
        GET(TableID);
        EXIT(
          "Type of Table" IN [
            "Type of Table"::Setup,
            "Type of Table"::"Common Data",
            "Type of Table"::"Basic Data",
            "Type of Table"::"Financial Setup"]);
      END;
    END;

    PROCEDURE IsCompletionControlAllowed@1100525002(TableID@1100525000 : Integer) : Boolean;
    BEGIN
      WITH ExtTablePropRec DO BEGIN
        GET(TableID);
        EXIT(
          ("Type of Table" IN [
             "Type of Table"::"Common Data",
             "Type of Table"::"Basic Data",
             "Type of Table"::"Basic Cards"]) AND
           NOT IsMercashTable(TableID));
      END;
    END;

    PROCEDURE IsFieldAuthorizationAllowed@1100525004(TableID@1100525001 : Integer) : Boolean;
    VAR
      lvExtendedTablePropertyRec@1100525000 : Record 11020573;
    BEGIN
      IF lvExtendedTablePropertyRec.GET(TableID) THEN
        EXIT(
          (lvExtendedTablePropertyRec."Type of Table" IN
            [lvExtendedTablePropertyRec."Type of Table"::Setup,
            lvExtendedTablePropertyRec."Type of Table"::"Financial Setup",
            lvExtendedTablePropertyRec."Type of Table"::"Not Applicable",
            lvExtendedTablePropertyRec."Type of Table"::Tools]) AND
          NOT IsMercashTable(TableID));
    END;

    PROCEDURE GetDPAFieldNo@1100525006(TabNo@1100525000 : Integer) : Integer;
    VAR
      lvExtendedTablePropertyRec@1100525001 : Record 11020573;
    BEGIN
      IF lvExtendedTablePropertyRec.GET(TabNo) THEN
        EXIT(lvExtendedTablePropertyRec."DPA Field No.");
    END;

    PROCEDURE GetDPAFilterFieldNo@1100525013(TabNo@1100525000 : Integer) : Integer;
    VAR
      lvExtendedTablePropertyRec@1100525001 : Record 11020573;
    BEGIN
      IF lvExtendedTablePropertyRec.GET(TabNo) THEN
        EXIT(lvExtendedTablePropertyRec."DPA Filter Field No.");
    END;

    PROCEDURE IsMercashTable@1100525008(TableID@1100525000 : Integer) : Boolean;
    BEGIN
      EXIT(
        (TableID >= 11020700) AND (TableID <= 11021999) OR
        (TableID >= 11111447) AND (TableID <= 11111946));
    END;

    BEGIN
    END.
  }
}

