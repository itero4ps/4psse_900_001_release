OBJECT Table 11020581 Base Recipe
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CentrManagementCU.EditBaseRecipe(Rec,1,xRec);  //DP00381
             END;

    OnModify=BEGIN
               CentrManagementCU.EditBaseRecipe(Rec,2,xRec);  //DP00381
             END;

    OnDelete=BEGIN
               RecipeLineRec.SETRANGE("Recipe Group","Recipe Group");
               RecipeLineRec.SETRANGE(Recipe,"No.");
               RecipeLineRec.DELETEALL(TRUE);

               EstCodeRec.SETRANGE("Recipe Group","Recipe Group");
               EstCodeRec.SETRANGE(Recipe,"No.");
               EstCodeRec.DELETEALL;

               EstPartLineRec.SETRANGE("Recipe Group","Recipe Group");
               EstPartLineRec.SETRANGE(Recipe,"No.");
               EstPartLineRec.DELETEALL(TRUE);

               TextRec.SETRANGE("Table Name",TextRec."Table Name"::Estimate);
               TextRec.SETRANGE(Group,"Recipe Group");
               TextRec.SETRANGE("No.","No.");
               TextRec.DELETEALL;

               CentrManagementCU.EditBaseRecipe(Rec,4,xRec);  //DP00381
             END;

    CaptionML=ENU=Recipe;
    LookupPageID=Page11125073;
  }
  FIELDS
  {
    { 1   ;   ;Recipe Group        ;Code10        ;TableRelation="Recipe Group";
                                                   CaptionML=ENU=Recipe Group;
                                                   NotBlank=Yes }
    { 10  ;   ;No.                 ;Code20        ;OnValidate=VAR
                                                                MainRec@1210190000 : Record 11020581;
                                                                RecipeLineRec@1100409007 : Record 11020582;
                                                                RecipeLineRec2@1100409006 : Record 11020582;
                                                                ChrgRec@1100409003 : Record 11229771;
                                                                ChrgRec2@1100409002 : Record 11229771;
                                                                EstCodeRec@1100409001 : Record 11229772;
                                                                EstCodeRec2@1100409000 : Record 11229772;
                                                              BEGIN
                                                                //db.sn, 18-08-11: M28184
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  MainRec := xRec;
                                                                  IF MainRec.FIND THEN BEGIN
                                                                    CentrManagementCU.EditBaseRecipe(Rec,3,xRec);  //DP00381

                                                                    RecipeLineRec.SETRANGE("Recipe Group",xRec."Recipe Group");
                                                                    RecipeLineRec.SETRANGE(Recipe,xRec."No.");
                                                                    IF RecipeLineRec.FINDSET(TRUE, FALSE) THEN BEGIN
                                                                      REPEAT
                                                                        RecipeLineRec2.COPY(RecipeLineRec);
                                                                        RecipeLineRec2."Recipe Group" := "Recipe Group";
                                                                        RecipeLineRec2.Recipe := "No.";
                                                                        RecipeLineRec2.INSERT;
                                                                      UNTIL RecipeLineRec.NEXT = 0;
                                                                      RecipeLineRec.DELETEALL;
                                                                    END;

                                                                    ChrgRec.SETFILTER("Estimate No.", '%1', '');
                                                                    ChrgRec.SETRANGE("Recipe Group",xRec."Recipe Group");
                                                                    ChrgRec.SETRANGE(Recipe,xRec."No.");
                                                                    IF ChrgRec.FINDSET(TRUE, FALSE) THEN BEGIN
                                                                      REPEAT
                                                                        ChrgRec2.COPY(ChrgRec);
                                                                        ChrgRec2."Recipe Group" := "Recipe Group";
                                                                        ChrgRec2.Recipe := "No.";
                                                                        ChrgRec2.INSERT;
                                                                      UNTIL ChrgRec.NEXT = 0;
                                                                      ChrgRec.DELETEALL;
                                                                    END;

                                                                    //db.sn, 29-10-12: T000942
                                                                    EstCodeRec.SETFILTER("Estimate No.", '%1', '');
                                                                    EstCodeRec.SETRANGE("Recipe Group",xRec."Recipe Group");
                                                                    EstCodeRec.SETRANGE(Recipe,xRec."No.");
                                                                    IF EstCodeRec.FINDSET(TRUE, FALSE) THEN BEGIN
                                                                      REPEAT
                                                                        EstCodeRec2.COPY(EstCodeRec);
                                                                        EstCodeRec2."Recipe Group" := "Recipe Group";
                                                                        EstCodeRec2.Recipe := "No.";
                                                                        EstCodeRec2.INSERT;
                                                                      UNTIL EstCodeRec.NEXT = 0;
                                                                      EstCodeRec.DELETEALL;
                                                                    END;
                                                                    //db.en, 29-10-12: T000942
                                                                  END;
                                                                END;
                                                                //db.en, 18-08-11: M28184
                                                              END;

                                                   CaptionML=ENU=No.;
                                                   NotBlank=Yes;
                                                   Description=Recipe Code }
    { 20  ;   ;Description         ;Text100       ;CaptionML=ENU=Description }
    { 25  ;   ;Recipe Group Description;Text50    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Estimate Group".Description WHERE (Type=CONST(Recipe),
                                                                                                          No.=FIELD(Recipe Group)));
                                                   CaptionML=ENU=Recipe Group Description;
                                                   Editable=No }
    { 30  ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure".Code;
                                                   CaptionML=ENU=Unit of Measure }
    { 40  ;   ;Manufacturer        ;Code20        ;TableRelation="ETIM Vendor (Central Mgt)".Code;
                                                   CaptionML=ENU=Manufacturer }
    { 45  ;   ;Rate Group          ;Code10        ;TableRelation="Rate Group".No.;
                                                   CaptionML=ENU=Rate Group }
    { 46  ;   ;Rate Code           ;Code10        ;TableRelation=IF (Rates Based On=CONST(Rate Group)) "Rate by Group"."Rate Code"
                                                                 ELSE IF (Rates Based On=CONST(Budget Rate)) "Budget Rate".Code;
                                                   CaptionML=ENU=Rate Code;
                                                   Description=[DP00381: lookup (TableRelation) changed; function CheckRate expired] }
    { 47  ;   ;Rates Based On      ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Estimate Setup"."Rates Based On");
                                                   CaptionML=ENU=Rates Based On;
                                                   OptionCaptionML=ENU=Rate Group,Budget Rate;
                                                   OptionString=Rate Group,Budget Rate;
                                                   Editable=No }
    { 50  ;   ;Norm Group          ;Code10        ;TableRelation="Norm Group".Code;
                                                   OnValidate=BEGIN
                                                                IF "Norm Group" <> xRec."Norm Group" THEN BEGIN
                                                                  TESTFIELD(Fixed, FALSE);
                                                                  IF CONFIRM(Text000,FALSE) THEN BEGIN
                                                                    RecipeLineRec.SETRANGE("Recipe Group","Recipe Group");
                                                                    RecipeLineRec.SETRANGE(Recipe,"No.");
                                                                    IF RecipeLineRec.FINDSET(TRUE, FALSE) THEN BEGIN
                                                                      REPEAT
                                                                        IF RecipeLineRec."Recipe (Child)" <> '' THEN
                                                                          RecipeLineRec."Norm Group" := ''  //db, 24-06-14: C010709
                                                                        ELSE
                                                                          RecipeLineRec."Norm Group" := "Norm Group";
                                                                        RecipeLineRec.DetermineNorm;
                                                                        RecipeLineRec.MODIFY;
                                                                      UNTIL RecipeLineRec.NEXT = 0;
                                                                    END;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Norm Group }
    { 60  ;   ;Hours               ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line".Hours WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                   Recipe=FIELD(No.),
                                                                                                   Cost Type (Hours)=CONST(Labor)));
                                                   CaptionML=ENU=Hours;
                                                   DecimalPlaces=2:;
                                                   Editable=No }
    { 61  ;   ;Labor Cost Amount   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Labor Amount" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                            Recipe=FIELD(No.)));
                                                   CaptionML=ENU=Labor Cost Amount;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 62  ;   ;Material Cost Amount;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Nett Cost Amount" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                                Recipe=FIELD(No.),
                                                                                                                Cost Type (Price)=CONST(Material)));
                                                   CaptionML=ENU=Material Cost Amount;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 63  ;   ;Subcontracting Cost Amount;Decimal ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Nett Cost Amount" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                                Recipe=FIELD(No.),
                                                                                                                Cost Type (Price)=CONST(Subcontracting)));
                                                   CaptionML=ENU=Subcontracting Cost Amount;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 64  ;   ;Plant Cost Amount   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Nett Cost Amount" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                                Recipe=FIELD(No.),
                                                                                                                Cost Type (Price)=CONST(Plant)));
                                                   CaptionML=ENU=Plant Cost Amount;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 65  ;   ;Cost Amount         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Cost Amount" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                           Recipe=FIELD(No.)));
                                                   CaptionML=ENU=Cost Amount;
                                                   NotBlank=No;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 66  ;   ;Gross Amount        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Gross Amount" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                            Recipe=FIELD(No.)));
                                                   CaptionML=ENU=Gross Amount;
                                                   Description=C026570: filter on CostType=Material disabled;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 67  ;   ;Buy Back Amount     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Buy Back Amount" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                               Recipe=FIELD(No.)));
                                                   CaptionML=ENU=Buy Back Amount;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 68  ;   ;Total Weight        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Total Weight" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                            Recipe=FIELD(No.)));
                                                   CaptionML=ENU=Total Weight;
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 70  ;   ;Blocked             ;Boolean       ;CaptionML=ENU=Blocked }
    { 80  ;   ;Length              ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE(Volume);
                                                              END;

                                                   CaptionML=ENU=Length;
                                                   DecimalPlaces=0:5 }
    { 81  ;   ;Width               ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE(Volume);
                                                              END;

                                                   CaptionML=ENU=Width;
                                                   DecimalPlaces=0:5 }
    { 82  ;   ;Height              ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE(Volume);
                                                              END;

                                                   CaptionML=ENU=Height;
                                                   DecimalPlaces=0:5 }
    { 85  ;   ;Packaging Unit      ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=ENU=Packaging Unit }
    { 86  ;   ;Volume              ;Decimal       ;OnValidate=VAR
                                                                lv1@1210190000 : Decimal;
                                                                lv2@1210190001 : Decimal;
                                                                lv3@1210190002 : Decimal;
                                                              BEGIN
                                                                lv1 := Length;
                                                                lv2 := Width;
                                                                lv3 := Height;
                                                                IF (lv1 = 0) AND (lv2 = 0) AND (lv3 = 0) THEN BEGIN
                                                                  Volume := 0;
                                                                  EXIT;
                                                                END;

                                                                IF Length = 0 THEN lv1 := 1;
                                                                IF Width = 0 THEN lv2 := 1;
                                                                IF Height = 0 THEN lv3 := 1;

                                                                Volume := lv1 * lv2 * lv3;
                                                              END;

                                                   CaptionML=ENU=Volume;
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 90  ;   ;Coding System       ;Code10        ;TableRelation="Coding System";
                                                   OnValidate=VAR
                                                                RcpLineRec@1210190001 : Record 11020582;
                                                                EstCodeRec@1210190000 : Record 11229772;
                                                              BEGIN
                                                                RcpLineRec.SETRANGE("Recipe Group", "Recipe Group");
                                                                RcpLineRec.SETRANGE(Recipe, "No.");
                                                                RcpLineRec.MODIFYALL(Code, '');

                                                                EstCodeRec.SETFILTER("Estimate No.", '%1', '');
                                                                EstCodeRec.SETRANGE("Recipe Group", "Recipe Group");
                                                                EstCodeRec.SETRANGE(Recipe, "No.");
                                                                EstCodeRec.SETRANGE("Coding System", "Coding System");
                                                                IF EstCodeRec.FIND('-') THEN BEGIN
                                                                  REPEAT
                                                                    IF EstCodeRec."Recipe Line" <> 0 THEN BEGIN
                                                                      IF RcpLineRec.GET(EstCodeRec."Recipe Group", EstCodeRec.Recipe, EstCodeRec."Recipe Line") THEN  //db, 01-08-14
                                                                      BEGIN
                                                                        RcpLineRec.Code := EstCodeRec.Code;
                                                                        RcpLineRec.MODIFY;
                                                                      END;
                                                                    END;
                                                                  UNTIL EstCodeRec.NEXT = 0;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Coding System }
    { 100 ;   ;Fixed               ;Boolean       ;OnValidate=BEGIN
                                                                IF Fixed <> xRec.Fixed THEN
                                                                  IF UserSetup.GET(USERID) THEN
                                                                    IF UserSetup."No Access Recipe Fixed" THEN
                                                                      ERROR(Text11012002,FIELDCAPTION(Fixed));  //db, 25-07-14: C010030
                                                              END;

                                                   CaptionML=ENU=Fixed }
    { 110 ;   ;Condition           ;Code10        ;TableRelation=Condition.Code;
                                                   CaptionML=ENU=Condition }
    { 115 ;   ;Condition Group     ;Code20        ;TableRelation="Base Recipe".No. WHERE (Recipe Group=FIELD(Recipe Group));
                                                   CaptionML=ENU=Condition Group }
    { 117 ;   ;Picture             ;BLOB          ;CaptionML=ENU=Picture;
                                                   SubType=Bitmap }
    { 120 ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Comment Line (Group)" WHERE (Table Name=CONST(Estimate),
                                                                                                   Group=FIELD(Recipe Group),
                                                                                                   No.=FIELD(No.)));
                                                   CaptionML=ENU=Comment;
                                                   Editable=No }
    { 2000;   ;Sundry Cost Amount  ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Nett Cost Amount" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                                Recipe=FIELD(No.),
                                                                                                                Cost Type (Price)=CONST(Sundry)));
                                                   CaptionML=ENU=Sundry Cost Amount;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 2010;   ;Labor Sales Amount  ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Labor Sales Amount" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                                  Recipe=FIELD(No.)));
                                                   CaptionML=ENU=Labor Sales Amount;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 2020;   ;Material Sales Amount;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Nett Sales Amount" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                                 Recipe=FIELD(No.),
                                                                                                                 Cost Type (Price)=CONST(Material)));
                                                   CaptionML=ENU=Material Sales Amount;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 2030;   ;Subcontracting Sales Amount;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Nett Sales Amount" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                                 Recipe=FIELD(No.),
                                                                                                                 Cost Type (Price)=CONST(Subcontracting)));
                                                   CaptionML=ENU=Subcontracting Sales Amount;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 2040;   ;Plant Sales Amount  ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Nett Sales Amount" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                                 Recipe=FIELD(No.),
                                                                                                                 Cost Type (Price)=CONST(Plant)));
                                                   CaptionML=ENU=Plant Sales Amount;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 2050;   ;Sundry Sales Amount ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Nett Sales Amount" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                                 Recipe=FIELD(No.),
                                                                                                                 Cost Type (Price)=CONST(Sundry)));
                                                   CaptionML=ENU=Sundry Sales Amount;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 2060;   ;Sales Amount        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Sales Amount" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                            Recipe=FIELD(No.)));
                                                   CaptionML=ENU=Sales Amount;
                                                   NotBlank=No;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 2070;   ;Automatic Ext. Texts;Boolean       ;CaptionML=ENU=Automatic Ext. Texts }
    { 2080;   ;Hours (Sales)       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Base Recipe Line"."Hours (Sales)" WHERE (Recipe Group=FIELD(Recipe Group),
                                                                                                             Recipe=FIELD(No.),
                                                                                                             Cost Type (Hours)=CONST(Labor)));
                                                   CaptionML=ENU=Hours (Sales);
                                                   DecimalPlaces=2:;
                                                   Editable=No }
    { 2090;   ;Quantity Sheet Feature;Code10      ;TableRelation="Est. Quantity Sheet Feature";
                                                   OnValidate=BEGIN
                                                                //db, 13-01-15: default for Estimate Line with Recipe Code; NO default voor Recipe Line.
                                                              END;

                                                   CaptionML=ENU=Quantity Sheet Feature }
    { 2100;   ;Surcharge % Labor   ;Decimal       ;OnValidate=BEGIN
                                                                UpdateSurcharge(0);
                                                              END;

                                                   CaptionML=ENU=Surcharge % Labor;
                                                   BlankZero=Yes }
    { 2101;   ;Surcharge % Material;Decimal       ;OnValidate=BEGIN
                                                                UpdateSurcharge(1);
                                                              END;

                                                   CaptionML=ENU=Surcharge % Material;
                                                   BlankZero=Yes }
    { 2102;   ;Surcharge % Subcontracting;Decimal ;OnValidate=BEGIN
                                                                UpdateSurcharge(2);
                                                              END;

                                                   CaptionML=ENU=Surcharge % Subcontracting;
                                                   BlankZero=Yes }
    { 2103;   ;Surcharge % Plant   ;Decimal       ;OnValidate=BEGIN
                                                                UpdateSurcharge(3);
                                                              END;

                                                   CaptionML=ENU=Surcharge % Plant;
                                                   BlankZero=Yes }
    { 2104;   ;Surcharge % Sundry  ;Decimal       ;OnValidate=BEGIN
                                                                UpdateSurcharge(4);
                                                              END;

                                                   CaptionML=ENU=Surcharge % Sundry;
                                                   BlankZero=Yes }
    { 11125678;;Plant Rates in Hours;Boolean      ;OnValidate=VAR
                                                                RecipeLine@1100525000 : Record 11020582;
                                                                EstRAWSetup@1100525001 : Record 11125673;
                                                              BEGIN
                                                                //IF NOT EstRAWSetup.READPERMISSION THEN
                                                                //  TESTFIELD("Plant Rates in Hours", FALSE);  //db, 02-06-16

                                                                RecipeLine.SETRANGE("Recipe Group", "Recipe Group");
                                                                RecipeLine.SETRANGE(Recipe, "No.");

                                                                IF RecipeLine.FINDSET(TRUE) THEN
                                                                  REPEAT
                                                                    IF NOT "Plant Rates in Hours" THEN BEGIN
                                                                      RecipeLine."Norm (Plant)" := 0;
                                                                      RecipeLine."Production Rate (Plant)" := 0;
                                                                      RecipeLine."Hours (Plant)" := 0;
                                                                    END;
                                                                    RecipeLine.DeterminePlantPrice;
                                                                    RecipeLine.MODIFY;
                                                                  UNTIL RecipeLine.NEXT = 0;
                                                              END;

                                                   CaptionML=ENU=Plant Rates in Hours;
                                                   Description=RAW }
  }
  KEYS
  {
    {    ;Recipe Group,No.                        ;Clustered=Yes }
    {    ;No.                                      }
    {    ;Description                              }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Recipe Group,No.,Description             }
  }
  CODE
  {
    VAR
      Text000@11012004 : TextConst 'ENU=Process default norm group in recipe lines ?';
      EstSetup@1100485000 : Record 11012150;
      RecipeLineRec@11012000 : Record 11020582;
      EstPartLineRec@1100528900 : Record 11072076;
      TextRec@1210190002 : Record 11020595;
      EstCodeRec@1100409000 : Record 11229772;
      CentrManagementCU@1210190001 : Codeunit 11012010;
      Text11012002@1100409001 : TextConst 'ENU=You have no permission to change %1';
      UserSetup@1100409002 : Record 91;

    PROCEDURE UpdateSurcharge@1100528902(CostType@1100485000 : Integer);
    VAR
      RcpLineRec@1100525000 : Record 11020582;
    BEGIN
      //DP00594
      RcpLineRec.SETRANGE("Recipe Group", "Recipe Group");
      RcpLineRec.SETRANGE(Recipe, "No.");
      RcpLineRec.SETRANGE("Surcharge Type", RcpLineRec."Surcharge Type"::" ");
      RcpLineRec.SETFILTER("Recipe (Child)", '%1', '');  //db, 05-11-15: C026570
      IF CostType = 0 THEN
        RcpLineRec.SETFILTER("Surcharge % Labor", '%1|%2', 0, xRec."Surcharge % Labor");
      IF CostType = 1 THEN
        RcpLineRec.SETFILTER("Surcharge % Price", '%1|%2', 0, xRec."Surcharge % Material");
      IF CostType = 2 THEN
        RcpLineRec.SETFILTER("Surcharge % Price", '%1|%2', 0, xRec."Surcharge % Subcontracting");
      IF CostType = 3 THEN
        RcpLineRec.SETFILTER("Surcharge % Price", '%1|%2', 0, xRec."Surcharge % Plant");
      IF CostType = 4 THEN
        RcpLineRec.SETFILTER("Surcharge % Price", '%1|%2', 0, xRec."Surcharge % Sundry");
      IF RcpLineRec.FINDSET(TRUE, FALSE) THEN BEGIN
        REPEAT
          IF CostType = 0 THEN
            RcpLineRec.VALIDATE("Surcharge % Labor", "Surcharge % Labor");
          IF CostType = 1 THEN
            IF RcpLineRec."Cost Type (Price)" = RcpLineRec."Cost Type (Price)"::Material THEN
              RcpLineRec.VALIDATE("Surcharge % Price", "Surcharge % Material");
          IF CostType = 2 THEN
            IF RcpLineRec."Cost Type (Price)" = RcpLineRec."Cost Type (Price)"::Subcontracting THEN
              RcpLineRec.VALIDATE("Surcharge % Price", "Surcharge % Subcontracting");
          IF CostType = 3 THEN
            IF RcpLineRec."Cost Type (Price)" = RcpLineRec."Cost Type (Price)"::Plant THEN
              RcpLineRec.VALIDATE("Surcharge % Price", "Surcharge % Plant");
          IF CostType = 4 THEN
            IF RcpLineRec."Cost Type (Price)" = RcpLineRec."Cost Type (Price)"::Sundry THEN
              RcpLineRec.VALIDATE("Surcharge % Price", "Surcharge % Sundry");
          RcpLineRec.MODIFY;
        UNTIL RcpLineRec.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

