OBJECT Table 11020649 Assembly Yard No. Obj. Element
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD(Element);
               CheckIfGenertdMatReqLinesExist;
               CheckQuantity;
               CheckEntityNo;
             END;

    OnModify=BEGIN
               TESTFIELD(Element);
               CheckIfGenertdMatReqLinesExist;
               CheckEntityNo;
             END;

    OnDelete=BEGIN
               CheckIfGenertdMatReqLinesExist;
             END;

    OnRename=BEGIN
               CheckIfGenertdMatReqLinesExist;
               CheckEntityNo;
             END;

    CaptionML=ENU=Assembly Yard No. Obj. Element;
    LookupPageID=Page11124975;
    DrillDownPageID=Page11124975;
  }
  FIELDS
  {
    { 10  ;   ;Seq. No.            ;Integer       ;CaptionML=ENU=Seq. No. }
    { 20  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   CaptionML=ENU=Project No.;
                                                   NotBlank=Yes }
    { 30  ;   ;Yard No.            ;Code20        ;CaptionML=ENU=Yard No. }
    { 40  ;   ;Entity Type         ;Option        ;CaptionML=ENU=Entity Type;
                                                   OptionCaptionML=ENU=None,Cable,Device,Extra Need,,,,,,Position;
                                                   OptionString=None,Cable,Device,ExtraNeed,,,,,,Position }
    { 50  ;   ;System No.          ;Code20        ;CaptionML=ENU=System No. }
    { 60  ;   ;Entity No.          ;Code20        ;CaptionML=ENU=Entity No. }
    { 70  ;   ;Element             ;Code20        ;TableRelation="Project Element".Element WHERE (Project No.=FIELD(Project No.));
                                                   OnValidate=VAR
                                                                ProjectElement@1100528400 : Record 11012010;
                                                              BEGIN
                                                                IF (Element <> '') THEN
                                                                  IF (ProjectElement.GET("Project No.", Element)) THEN
                                                                    "Element Type" := ProjectElement."Element Type"
                                                                ELSE
                                                                  CLEAR("Element Type");
                                                              END;

                                                   ValidateTableRelation=Yes;
                                                   CaptionML=ENU=Element;
                                                   NotBlank=Yes }
    { 72  ;   ;Element Type        ;Option        ;FieldClass=Normal;
                                                   CaptionML=ENU=Element Type;
                                                   OptionCaptionML=ENU=General,Production,Installation;
                                                   OptionString=General,Production,Installation }
    { 80  ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:0 }
    { 90  ;   ;Proj. Mat. Req. Lines Exist;Boolean;FieldClass=FlowField;
                                                   CalcFormula=Exist("Project Material Req. Line" WHERE (Project No.=FIELD(Project No.),
                                                                                                         Yard No.=FIELD(Yard No.),
                                                                                                         Entity Type=FIELD(Entity Type),
                                                                                                         System No.=FIELD(System No.),
                                                                                                         Entity No.=FIELD(Entity No.),
                                                                                                         Element=FIELD(Element)));
                                                   CaptionML=ENU=Proj. Mat. Req. Lines Exist;
                                                   Editable=No }
    { 100 ;   ;Generated Mt. Req. Lines Exist;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("Project Material Req. Line" WHERE (Project No.=FIELD(Project No.),
                                                                                                         Yard No.=FIELD(Yard No.),
                                                                                                         Entity Type=FIELD(Entity Type),
                                                                                                         System No.=FIELD(System No.),
                                                                                                         Entity No.=FIELD(Entity No.),
                                                                                                         Element=FIELD(Element),
                                                                                                         Origin=FILTER(Assembly Object Material Requirement)));
                                                   CaptionML=ENU=Generated Mt. Req. Lines Exist;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Seq. No.                                ;Clustered=Yes }
    {    ;Project No.,Yard No.,Entity Type,System No.,Entity No.,Element,Element Type;
                                                   SumIndexFields=Quantity }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100528401 : TextConst 'ENU=Generated %1s exist for %2 %3, %4 %5 and %6 %7.';
      Text005@1210190000 : TextConst 'ENU=%1 should not exceed %2.';

    LOCAL PROCEDURE CheckIfGenertdMatReqLinesExist@1100528405();
    VAR
      ProjectMaterialReqLine@1100528401 : Record 11012526;
    BEGIN
      CALCFIELDS("Generated Mt. Req. Lines Exist");
      IF "Generated Mt. Req. Lines Exist" THEN
        ERROR(
          Text001,
          ProjectMaterialReqLine.TABLECAPTION,
          FIELDCAPTION("Project No."),
          "Project No.",
          FIELDCAPTION("Entity No."),
          "Entity No.",
          FIELDCAPTION(Element),
          Element);
    END;

    LOCAL PROCEDURE CheckQuantity@1210190000();
    VAR
      AssemblyYardNumberObject@1210190000 : Record 11020596;
      ProjectElement@1210190001 : Record 11012010;
    BEGIN
      //kzwerver, 110525, c, #27630
      AssemblyYardNumberObject.GET("Project No.", "Yard No.", "Entity Type", "System No.", "Entity No.");

      IF (ProjectElement.GET("Project No.", Element)) THEN BEGIN
        CASE ProjectElement."Element Type" OF
          ProjectElement."Element Type"::Production:
            CheckIfTotalQtyExceedsMax(AssemblyYardNumberObject, ProjectElement."Element Type"::Production);
          ProjectElement."Element Type"::Installation:
            CheckIfTotalQtyExceedsMax(AssemblyYardNumberObject, ProjectElement."Element Type"::Installation);
        END;
      END;
    END;

    LOCAL PROCEDURE CheckIfTotalQtyExceedsMax@1210190006(AssemblyYardNumberObject@1210190000 : Record 11020596;ElementType@1210190001 : 'General,Production,Installation');
    VAR
      NewQuantity@1210190002 : Decimal;
    BEGIN
      //kzwerver, 110525, c, #27630
      AssemblyYardNumberObject.SETFILTER("Element Type Filter", '%1', ElementType);
      AssemblyYardNumberObject.CALCFIELDS("Total Assigned Qty.");
      NewQuantity := AssemblyYardNumberObject."Total Assigned Qty." + Quantity;
      IF NewQuantity > AssemblyYardNumberObject.Quantity THEN
        ERROR(
          Text005,
          AssemblyYardNumberObject.FIELDCAPTION("Total Assigned Qty."),
          AssemblyYardNumberObject.Quantity);
    END;

    LOCAL PROCEDURE CheckEntityNo@1100528401();
    VAR
      JobsSetup@1100528400 : Record 315;
    BEGIN
      IF "Entity Type" = "Entity Type"::None THEN
        EXIT;

      IF JobsSetup.GET AND
         JobsSetup."Entity No. Mandatory"
      THEN
        TESTFIELD("Entity No.");
    END;

    BEGIN
    END.
  }
}

