OBJECT Table 11020665 Warranty Data per Purch. Order
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CopyStandardWarrantyTxtToOrder;
             END;

    OnDelete=BEGIN
               RemoveWarrantyTxtOrder;
             END;

    CaptionML=ENU=Warranty Data per Purchase Order;
    LookupPageID=Page11124856;
  }
  FIELDS
  {
    { 5   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order }
    { 10  ;   ;Document No.        ;Code20        ;TableRelation="Purchase Header".No. WHERE (Document Type=FIELD(Document Type));
                                                   OnValidate=VAR
                                                                PurchaseHeader@1210190000 : Record 38;
                                                              BEGIN
                                                                IF PurchaseHeader.GET("Document Type","Document No.") THEN
                                                                  "Job No." := PurchaseHeader."Job No.";
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=No. }
    { 15  ;   ;Warranty Code       ;Code10        ;TableRelation="Warranty Object";
                                                   OnValidate=BEGIN
                                                                GetStandardWarrantyValues;
                                                              END;

                                                   CaptionML=ENU=Warranty Code }
    { 16  ;   ;Warranty Description;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Warranty Object".Description WHERE (Code=FIELD(Warranty Code)));
                                                   CaptionML=ENU=Warranty Description }
    { 20  ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   CaptionML=ENU=Project No. }
    { 25  ;   ;Warranty Period     ;DateFormula   ;CaptionML=ENU=Warranty Period }
    { 30  ;   ;Retention Percentage;Decimal       ;OnValidate=BEGIN
                                                                IF "Retention Percentage" <> 0 THEN
                                                                  TESTFIELD("Retention Amount",0);
                                                              END;

                                                   CaptionML=ENU=Retention Percentage }
    { 35  ;   ;Retention Amount    ;Decimal       ;OnValidate=BEGIN
                                                                IF "Retention Amount" <> 0 THEN
                                                                  TESTFIELD("Retention Percentage",0);
                                                              END;

                                                   CaptionML=ENU=Retention Amount }
    { 40  ;   ;Final Date Back     ;Date          ;CaptionML=ENU=Final Date Back }
    { 45  ;   ;Warranty Start Date ;Date          ;CaptionML=ENU=Warranty Start Date }
    { 50  ;   ;Warranty Certification;Text20      ;CaptionML=ENU=Warranty Certification }
    { 55  ;   ;Warranty Obligation satisfied;Boolean;
                                                   CaptionML=ENU=Warranty Obligation satisfied }
    { 60  ;   ;Text                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Warranty Data Comment Line" WHERE (Table Name=CONST(Warranty Data),
                                                                                                         No.=FIELD(Document No.)));
                                                   CaptionML=ENU=Text }
  }
  KEYS
  {
    {    ;Document Type,Document No.,Warranty Code;Clustered=Yes }
    {    ;Job No.                                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetStandardWarrantyValues@1210190002();
    VAR
      WarrantyObject@1210190000 : Record 11020664;
    BEGIN
      IF WarrantyObject.GET("Warranty Code") THEN BEGIN
        "Warranty Period" := WarrantyObject."Warranty Period";
        "Retention Percentage" := WarrantyObject."Retention Percentage";
        "Retention Amount" := WarrantyObject."Retention Amount";
      END;
    END;

    PROCEDURE CopyStandardWarrantyTxtToOrder@1210190000();
    VAR
      CommentLine@1210190002 : Record 97;
      WarrantyDataCommentLine@1210190001 : Record 11020667;
    BEGIN
      CommentLine.SETRANGE("Table Name",CommentLine."Table Name"::"Warranty Object");
      CommentLine.SETRANGE("No.","Warranty Code");
      IF CommentLine.FINDSET THEN
        REPEAT
          WarrantyDataCommentLine."Table Name" := WarrantyDataCommentLine."Table Name"::"Warranty Data";
          WarrantyDataCommentLine."No." := "Document No.";
          WarrantyDataCommentLine."Warranty Code" := "Warranty Code";
          WarrantyDataCommentLine."Line No." := CommentLine."Line No.";
          WarrantyDataCommentLine.Date := WORKDATE;
          WarrantyDataCommentLine.Code := CommentLine.Code;
          WarrantyDataCommentLine.Comment := CommentLine.Comment;
          WarrantyDataCommentLine."Comment Code" := CommentLine."Comment Code";
          WarrantyDataCommentLine."Created By" := USERID;
          WarrantyDataCommentLine."Time Created" := TIME;
          WarrantyDataCommentLine.INSERT;
        UNTIL CommentLine.NEXT = 0;;
    END;

    PROCEDURE RemoveWarrantyTxtOrder@1210190001();
    VAR
      WarrantyDataCommentLine@1210190001 : Record 11020667;
    BEGIN
      WarrantyDataCommentLine.SETRANGE("Table Name", WarrantyDataCommentLine."Table Name"::"Warranty Data");
      WarrantyDataCommentLine.SETRANGE("No.","Document No.");
      WarrantyDataCommentLine.SETRANGE("Warranty Code","Warranty Code");
      WarrantyDataCommentLine.DELETEALL;
    END;

    BEGIN
    END.
  }
}

