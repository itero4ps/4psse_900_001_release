OBJECT Table 11020691 Conversion File Format
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00Toolsv1;
  }
  PROPERTIES
  {
    CaptionML=ENU=Conversion File Format;
    LookupPageID=Page11125035;
    DrillDownPageID=Page11125035;
  }
  FIELDS
  {
    { 10  ;   ;Object ID           ;Integer       ;CaptionML=ENU=Object ID }
    { 15  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 20  ;   ;Column No.          ;Code10        ;CaptionML=ENU=Column No. }
    { 30  ;   ;Table ID            ;Integer       ;CaptionML=ENU=Table ID }
    { 40  ;   ;Field No.           ;Integer       ;CaptionML=ENU=Field No. }
    { 45  ;   ;Description         ;Text250       ;CaptionML=ENU=Description }
    { 50  ;   ;Import Data Type    ;Text30        ;CaptionML=ENU=Import Data Type }
    { 60  ;   ;Comment             ;Text250       ;CaptionML=ENU=Comment }
    { 110 ;   ;Table Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(Table ID)));
                                                   CaptionML=ENU=Table Name;
                                                   Editable=No }
    { 115 ;   ;Table Caption       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=ENU=Table Caption;
                                                   Editable=No }
    { 120 ;   ;Field Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table ID),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Name;
                                                   Editable=No }
    { 130 ;   ;Field Caption       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table ID),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Caption;
                                                   Editable=No }
    { 140 ;   ;Field Data Type     ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Type Name" WHERE (TableNo=FIELD(Table ID),
                                                                                               No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Data Type;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Object ID,Line No.                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100529900 : TextConst 'ENU=Old No. (Use Old No. OR create New No. by NoSeries and store Old No.)';
      ConvMgt@1100529901 : Codeunit 11020212;
      Text002@1100529902 : TextConst 'ENU=New No. (Manual New No. and store Old No.)';
      Text003@1100529903 : TextConst 'ENU=Old No. (New No. will be searched in 4PS) OR New No.';
      Text004@1100529904 : TextConst 'ENU="<empty>=False, [1]=True"';

    PROCEDURE InsertColumnLine@1210190000(ObjectID@1210190002 : Integer;ColumnCde@1210190008 : Code[20];TableID@1210190003 : Integer;DescriptionTxt@1210190004 : Text[250];ImportTypeTxt@1210190005 : Text[30];CommentTxt@1210190006 : Text[250]);
    BEGIN
      INIT;
      "Object ID" := ObjectID;
      "Line No." := NewLineNo(ObjectID);
      INSERT(TRUE);

      "Column No." := ColumnCde;
      "Table ID" := TableID;
      "Field No." := ConvMgt.GetFieldNoFromFieldName(TableID,DescriptionTxt);
      "Import Data Type" := ImportTypeTxt;

      CASE CommentTxt OF
        'OldOrNewTxt': Comment := Text001;
        'NewTxt': Comment := Text002;
        'OldTxt': Comment := Text003;
        'OneTrueTxt' : Comment := Text004;
      ELSE
        Comment := CommentTxt;
      END;

      CALCFIELDS("Field Caption");
      Description := "Field Caption";
      IF Description = '' THEN
        Description := CommentTxt;

      MODIFY(TRUE);
    END;

    PROCEDURE DeleteColumnLines@1100529900(ObjectID@1100529900 : Integer);
    VAR
      ConversionFileFormat@1100529901 : Record 11020691;
    BEGIN
      ConversionFileFormat.SETRANGE("Object ID",ObjectID);
      IF ConversionFileFormat.FINDFIRST THEN
        ConversionFileFormat.DELETEALL;
    END;

    PROCEDURE NewLineNo@1100529903(ObjectID@1100529900 : Integer) : Integer;
    VAR
      ConversionFileFormat@1100529901 : Record 11020691;
    BEGIN
      ConversionFileFormat.SETRANGE("Object ID",ObjectID);
      IF ConversionFileFormat.FINDLAST THEN
        EXIT(ConversionFileFormat."Line No."+10)
      ELSE
        EXIT(10);
    END;

    BEGIN
    END.
  }
}

