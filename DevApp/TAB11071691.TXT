OBJECT Table 11071691 Service-Object
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceSetup@1100528600 : Record 11012800;
             BEGIN
               ServiceSetup.GET;
               IF "No." = '' THEN BEGIN
                 ServiceSetup.TESTFIELD("Object Nos.");
                 NoSeriesMgt.InitSeries(ServiceSetup."Object Nos.", xRec."No. Series", 0D, "No.", "No. Series");
               END;
               "Input by" := USERID;
               "Input Date" := CREATEDATETIME(TODAY,TIME);

               UpdateTenderCode;
             END;

    OnModify=VAR
               BuildingObjectRelation@1100528300 : Record 11071820;
             BEGIN
               BuildingObjectRelation.SETRANGE("Object No.", "No.");
               IF (NOT BuildingObjectRelation.ISEMPTY) THEN
                 TESTFIELD("Service Location No.", '');

               "Modified by" := USERID;
               "Last date modified" := CREATEDATETIME(TODAY,TIME);
             END;

    OnDelete=VAR
               ServiceInspectionLine@1100528601 : Record 11071751;
               ObjectSpecificationLine@1100528602 : Record 11071781;
               RelatedObject@1100528603 : Record 11071704;
               BuildingObjectRelation@1100528300 : Record 11071820;
               MaintenanceSchemeVersion@1100528600 : Record 11071743;
               ObjectReplacementValue@1100528604 : Record 11128779;
               NEN2767ConditionMeasHeader@1100528605 : Record 11128776;
               ObjectConsumptionComponent@1100528800 : Record 11071969;
               PurchaseLineServiceObject@1100528801 : Record 11072194;
               ServiceObjectProperty@1100529000 : Record 11072287;
             BEGIN
               TestNotInUse;

               ServiceInspectionLine.SETRANGE("Inspection Type", ServiceInspectionLine."Inspection Type"::"Object (Calls)");
               ServiceInspectionLine.SETRANGE("Service Contract No.", '');
               ServiceInspectionLine.SETRANGE(Code, "No.");
               ServiceInspectionLine.DELETEALL(TRUE);

               ObjectSpecificationLine.SETRANGE("Specification Type", ObjectSpecificationLine."Specification Type"::Object);
               ObjectSpecificationLine.SETRANGE(Code, "No.");
               ObjectSpecificationLine.DELETEALL(TRUE);

               RelatedObject.SETRANGE("Object No.", "No.");
               RelatedObject.DELETEALL(TRUE);
               RelatedObject.SETRANGE("Object No.");
               RelatedObject.SETRANGE("Related Object");
               RelatedObject.SETRANGE("Related Object", "No.");
               RelatedObject.DELETEALL(TRUE);

               BuildingObjectRelation.SETCURRENTKEY("Object No.", "Building Code");
               BuildingObjectRelation.SETRANGE("Object No.", "No.");
               BuildingObjectRelation.DELETEALL(TRUE);

               MaintenanceSchemeVersion.SETRANGE("Line Type", MaintenanceSchemeVersion."Line Type"::"Object Installation");
               MaintenanceSchemeVersion.SETRANGE(Code, "No.");
               MaintenanceSchemeVersion.DELETEALL(TRUE);

               ObjectReplacementValue.SETRANGE("Service Object No.", "No.");
               IF NOT ObjectReplacementValue.ISEMPTY THEN
                 ObjectReplacementValue.DELETEALL(TRUE);

               NEN2767ConditionMeasHeader.SETRANGE("Service Object No.", "No.");
               IF NOT NEN2767ConditionMeasHeader.ISEMPTY THEN
                 NEN2767ConditionMeasHeader.DELETEALL(TRUE);

               ObjectConsumptionComponent.SETRANGE("Object No.", "No.");
               ObjectConsumptionComponent.DELETEALL(TRUE);

               PurchaseLineServiceObject.SETRANGE("Service Object No.", "No.");
               IF PurchaseLineServiceObject.FINDSET THEN
                 REPEAT
                   PurchaseLineServiceObject.ServiceObjectDeleted;
                 UNTIL PurchaseLineServiceObject.NEXT = 0;

               ServiceObjectProperty.SETRANGE("Service Object No.", "No.");
               ServiceObjectProperty.DELETEALL;
             END;

    CaptionML=ENU=Object;
    LookupPageID=Page11125145;
    DrillDownPageID=Page11125145;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=VAR
                                                                ServiceSetup@1100528600 : Record 11012800;
                                                              BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  ServiceSetup.GET;
                                                                  NoSeriesMgt.TestManual(ServiceSetup."Object Nos."); //mg.c, 15-12-10: M24121
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 20  ;   ;Standard Object No. ;Code20        ;TableRelation="Standard Object".No. WHERE (Expired=CONST(No));
                                                   OnValidate=VAR
                                                                StandardObject@1100525000 : Record 11020334;
                                                              BEGIN
                                                                IF StandardObject.GET("Standard Object No.") THEN BEGIN
                                                                  "Warranty Code Customer" := StandardObject."Warranty Code";
                                                                  IF ((Description <> '') AND (Description <> StandardObject.Description)) OR
                                                                     (("Description 2" <> '') AND ("Description 2" <> StandardObject."Description 2"))
                                                                  THEN BEGIN
                                                                    IF CONFIRM(Text000, TRUE, StandardObject.Description, StandardObject."Description 2") THEN BEGIN
                                                                      VALIDATE(Description, StandardObject.Description);
                                                                      VALIDATE("Description 2", StandardObject."Description 2");
                                                                    END;
                                                                  END ELSE BEGIN
                                                                    VALIDATE(Description, StandardObject.Description);
                                                                    VALIDATE("Description 2", StandardObject."Description 2");
                                                                  END;
                                                                  "Condition Measurement Method" := StandardObject."Default Condition Meas. Method";
                                                                  "NEN2767 Element Code" := StandardObject."NEN2767 Element Code";
                                                                  Subcontracting := StandardObject.Subcontracting;
                                                                  VALIDATE("Brand Code", StandardObject."Brand Code");
                                                                  VALIDATE("Model Code", StandardObject."Model Code");
                                                                  VALIDATE("Type Code", StandardObject."Type Code");
                                                                  VALIDATE("Hide in Service Portal", StandardObject."Hide in Service Portal"); //klaasz.n, SPT
                                                                  VALIDATE("Item No.", StandardObject."Item No.");
                                                                END;
                                                                UpdateTenderCode;
                                                                CopyObjectLevelSpecs;
                                                                IF xRec."Standard Object No." <> "Standard Object No." THEN BEGIN
                                                                  UpdateContractObjects;
                                                                  ApplySMAgreements;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceObjectManagement@1100528600 : Codeunit 11012808;
                                                            BEGIN
                                                              IF ServiceObjectManagement.HandleLookupStandardObject('', FALSE, "Standard Object No.") THEN
                                                                VALIDATE("Standard Object No.");
                                                            END;

                                                   CaptionML=ENU=Standard Object No. }
    { 30  ;   ;Object Customer     ;Code20        ;CaptionML=ENU=Object Customer }
    { 35  ;   ;Brand Code          ;Code20        ;TableRelation="Service Object Brand";
                                                   OnValidate=BEGIN
                                                                IF xRec."Brand Code" <> "Brand Code" THEN
                                                                  VALIDATE("Model Code", '');
                                                                CALCFIELDS("Brand Description");
                                                              END;

                                                   CaptionML=ENU=Brand }
    { 40  ;   ;Brand Description   ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Object Brand".Description WHERE (Code=FIELD(Brand Code)));
                                                   CaptionML=ENU=Brand Description;
                                                   Editable=No }
    { 45  ;   ;Type Code           ;Code20        ;TableRelation="Service Object Type".Code WHERE (Brand Code=FIELD(Brand Code),
                                                                                                   Model Code=FIELD(Model Code));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Type Description");
                                                              END;

                                                   CaptionML=ENU=Type }
    { 50  ;   ;Type Description    ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Object Type".Description WHERE (Brand Code=FIELD(Brand Code),
                                                                                                               Model Code=FIELD(Model Code),
                                                                                                               Code=FIELD(Type Code)));
                                                   CaptionML=ENU=Type Description;
                                                   Editable=No }
    { 55  ;   ;Model Code          ;Code20        ;TableRelation="Service Object Model".Code WHERE (Brand Code=FIELD(Brand Code));
                                                   OnValidate=BEGIN
                                                                IF xRec."Model Code" <> "Model Code" THEN
                                                                  VALIDATE("Type Code", '');
                                                                CALCFIELDS("Model Description");
                                                              END;

                                                   CaptionML=ENU=Model }
    { 60  ;   ;Model Description   ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Object Model".Description WHERE (Brand Code=FIELD(Brand Code),
                                                                                                                Code=FIELD(Model Code)));
                                                   CaptionML=ENU=Model Description;
                                                   Editable=No }
    { 70  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 75  ;   ;Description 2       ;Text50        ;CaptionML=ENU=Description 2 }
    { 100 ;   ;Standard Object Description;Text50 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Object".Description WHERE (No.=FIELD(Standard Object No.)));
                                                   CaptionML=ENU=Standard Object Description;
                                                   Editable=No }
    { 110 ;   ;Standard Object Description 2;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Object"."Description 2" WHERE (No.=FIELD(Standard Object No.)));
                                                   CaptionML=ENU=Standard Object Description 2;
                                                   Editable=No }
    { 120 ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=No }
    { 130 ;   ;Service Location No.;Code20        ;TableRelation="Service Location";
                                                   OnValidate=VAR
                                                                RelatedObject@1100526200 : Record 11071704;
                                                                ContractObject@1100528601 : Record 11071702;
                                                              BEGIN
                                                                //mg, 21-01-11
                                                                IF xRec."Service Location No." <> "Service Location No." THEN BEGIN
                                                                  RelatedObject.SETRANGE("Object No.", "No.");
                                                                  RelatedObject.DELETEALL;

                                                                  RelatedObject.SETCURRENTKEY("Related Object");
                                                                  RelatedObject.SETRANGE("Object No.");
                                                                  RelatedObject.SETRANGE("Related Object", "No.");
                                                                  RelatedObject.DELETEALL;

                                                                  CALCFIELDS("Location Description", "Location Address", "Location Address 2", "Location City", "Location Contact Name",
                                                                    "Location Post Code", "Location Country/Region Code");


                                                                  ContractObject.SETCURRENTKEY("Service Object No.");
                                                                  ContractObject.SETRANGE("Service Object No.", "No.");
                                                                  IF ContractObject.FINDSET THEN
                                                                    REPEAT
                                                                      ContractObject.VALIDATE("Service Location No.", "Service Location No.");
                                                                      ContractObject.MODIFY;
                                                                    UNTIL ContractObject.NEXT = 0;
                                                                END;
                                                                IF "Service Location No." <> '' THEN
                                                                  TestNotInBuilding;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceLocation@1210190000 : Record 11012801;
                                                            BEGIN
                                                              IF ServiceLocation.GET("Service Location No.") THEN;
                                                              IF PAGE.RUNMODAL(0, ServiceLocation) = ACTION::LookupOK THEN
                                                                VALIDATE("Service Location No.", ServiceLocation."No.");
                                                            END;

                                                   CaptionML=ENU=Service Location No. }
    { 140 ;   ;In use since        ;Date          ;CaptionML=ENU=In use since }
    { 150 ;   ;Warranty Start Date Customer;Date  ;CaptionML=ENU=Warranty Start Date Customer }
    { 180 ;   ;Warranty Code Customer;Code10      ;TableRelation="Service Warranty";
                                                   CaptionML=ENU=Warranty Code Customer }
    { 190 ;   ;Warranty Description (Cust.);Text30;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Warranty".Description WHERE (Code=FIELD(Warranty Code Customer)));
                                                   CaptionML=ENU=Warranty Description (Customer);
                                                   Editable=No }
    { 200 ;   ;Modified by         ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Modified by;
                                                   Editable=No }
    { 210 ;   ;Last date modified  ;DateTime      ;CaptionML=ENU=Last date modified;
                                                   Editable=No }
    { 220 ;   ;Input by            ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Input by;
                                                   Editable=No }
    { 230 ;   ;Input Date          ;DateTime      ;CaptionML=ENU=Input Date;
                                                   Editable=No }
    { 240 ;   ;Preferred Employee OBS;Code20      ;TableRelation="Resource (WOP)" WHERE (Type=CONST(Employee));
                                                   OnValidate=VAR
                                                                lvPrefEmplRec@1100525000 : Record 11012843;
                                                              BEGIN
                                                              END;

                                                   CaptionML=ENU=Preferred Employee OBSOLETE;
                                                   Description=OBSOLETE;
                                                   Editable=No }
    { 260 ;   ;Building            ;Text30        ;CaptionML=ENU=Building }
    { 270 ;   ;Floor               ;Text30        ;CaptionML=ENU=Floor }
    { 280 ;   ;Room                ;Text30        ;CaptionML=ENU=Room }
    { 290 ;   ;Customer Feature    ;Text30        ;CaptionML=ENU=Customer Feature }
    { 300 ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Comment Line" WHERE (Table Name=CONST(ServObject),
                                                                                           No.=FIELD(No.)));
                                                   CaptionML=ENU=Comment;
                                                   Editable=No }
    { 310 ;   ;Text                ;Text250       ;CaptionML=ENU=Text }
    { 320 ;   ;Replace from        ;Date          ;CaptionML=ENU=Replace from }
    { 330 ;   ;Replace with Standard Object;Code20;TableRelation="Standard Object".No.;
                                                   CaptionML=ENU=Replace with Standard Object }
    { 340 ;   ;System-Created Replacement;Boolean ;CaptionML=ENU=System-Created Replacement }
    { 350 ;   ;Replaced by         ;Code20        ;TableRelation=Service-Object.No.;
                                                   OnValidate=BEGIN
                                                                TESTFIELD("System-Created Replacement", FALSE);
                                                              END;

                                                   CaptionML=ENU=Replaced by }
    { 360 ;   ;Replaces            ;Code20        ;TableRelation=Service-Object.No.;
                                                   CaptionML=ENU=Replaces }
    { 370 ;   ;Location Description;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".Description WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=Locatie Description;
                                                   Editable=No }
    { 380 ;   ;Location Address    ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".Address WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=Address;
                                                   Editable=No }
    { 390 ;   ;Location Address 2  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location"."Address 2" WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=Address 2;
                                                   Editable=No }
    { 400 ;   ;Location City       ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".City WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=City;
                                                   Editable=No }
    { 410 ;   ;Location Contact Name;Text50       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location"."Contact Name" WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=Contact Name;
                                                   Editable=No }
    { 420 ;   ;Location Post Code  ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location"."Post Code" WHERE (No.=FIELD(Service Location No.)));
                                                   TableRelation="Post Code";
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Post Code;
                                                   Editable=No }
    { 430 ;   ;Location Country/Region Code;Code10;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location"."Country/Region Code" WHERE (No.=FIELD(Service Location No.)));
                                                   TableRelation=Country/Region;
                                                   CaptionML=ENU=Country/Region Code;
                                                   Editable=No }
    { 440 ;   ;Tender Code System 1;Code10        ;TableRelation="Coding System"."Coding System";
                                                   OnValidate=BEGIN
                                                                IF "Tender Code System 1" <> xRec."Tender Code System 1" THEN
                                                                  "Tender Code 1" := '';
                                                              END;

                                                   CaptionML=ENU=Tender Code System 1;
                                                   Editable=No }
    { 450 ;   ;Tender Code 1       ;Code20        ;TableRelation="Code per Coding System".Code WHERE (Coding System=FIELD(Tender Code System 1));
                                                   CaptionML=ENU=Tender Code 1 }
    { 460 ;   ;Discipline          ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Object".Discipline WHERE (No.=FIELD(Standard Object No.)));
                                                   TableRelation=Discipline;
                                                   CaptionML=ENU=Discipline;
                                                   Editable=No }
    { 470 ;   ;Trade Speciality    ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Object"."Trade Speciality" WHERE (No.=FIELD(Standard Object No.)));
                                                   TableRelation="Trade Speciality".Code WHERE (Discipline=FIELD(Discipline));
                                                   CaptionML=ENU=Trade Speciality;
                                                   Editable=No }
    { 480 ;   ;Year of Manufacture ;Integer       ;CaptionML=ENU=Year of Manufacture }
    { 490 ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   OnValidate=VAR
                                                                Company@1100528600 : Record 2000000006;
                                                              BEGIN
                                                                VALIDATE("Project No.");
                                                                IF "Company Name" = COMPANYNAME THEN
                                                                  "Company Name" := '';
                                                                IF "Company Name" <> '' THEN
                                                                  Company.GET("Company Name");
                                                              END;

                                                   CaptionML=ENU=Company Name }
    { 500 ;   ;Project No.         ;Code20        ;TableRelation=IF (Company Name=CONST()) Job;
                                                   OnValidate=VAR
                                                                Job@1100528600 : Record 11072003;
                                                              BEGIN
                                                                IF "Project No." = '' THEN BEGIN
                                                                  "Plot No." := '';
                                                                  EXIT;
                                                                END;

                                                                IF "Company Name" = '' THEN
                                                                  Job.CHANGECOMPANY(COMPANYNAME)
                                                                ELSE
                                                                  Job.CHANGECOMPANY("Company Name");
                                                                Job.GET("Project No.");
                                                              END;

                                                   CaptionML=ENU=Project No. }
    { 510 ;   ;Plot No.            ;Code10        ;TableRelation=IF (Company Name=CONST()) Plot."Plot No." WHERE (Project No.=FIELD(Project No.));
                                                   OnValidate=VAR
                                                                Plot@1100528600 : Record 11012500;
                                                              BEGIN
                                                                IF "Plot No." = '' THEN
                                                                  EXIT;
                                                                TESTFIELD("Project No.");
                                                                IF "Company Name" = '' THEN
                                                                  Plot.CHANGECOMPANY(COMPANYNAME)
                                                                ELSE
                                                                  Plot.CHANGECOMPANY("Company Name");
                                                                Plot.GET("Project No.", "Plot No.");
                                                              END;

                                                   CaptionML=ENU=Plot No. }
    { 520 ;   ;Quantity            ;Integer       ;InitValue=1;
                                                   CaptionML=ENU=Quantity;
                                                   MinValue=1 }
    { 530 ;   ;Blocked             ;Boolean       ;OnValidate=BEGIN
                                                                IF Blocked THEN
                                                                  UpdateContractObjectBlocked();
                                                              END;

                                                   CaptionML=ENU=Blocked }
    { 540 ;No ;Reserved msquared   ;Code20        ;CaptionML=ENU=Reserved }
    { 550 ;   ;Tender Code System 2;Code10        ;TableRelation="Coding System"."Coding System";
                                                   OnValidate=BEGIN
                                                                IF "Tender Code System 2" <> xRec."Tender Code System 2" THEN
                                                                  "Tender Code 2" := '';
                                                              END;

                                                   CaptionML=ENU=Tender Code System 2;
                                                   Editable=No }
    { 560 ;   ;Tender Code 2       ;Code20        ;TableRelation="Code per Coding System".Code WHERE (Coding System=FIELD(Tender Code System 2));
                                                   CaptionML=ENU=Tender Code 2 }
    { 620 ;   ;Yard No. (AYNO)     ;Code20        ;CaptionML=ENU=Yard No. (AYNO);
                                                   Editable=No }
    { 630 ;   ;Entity Type (AYNO)  ;Option        ;CaptionML=ENU=Entity Type (AYNO);
                                                   OptionCaptionML=ENU=None,Cable,Device,Extra Need,,,,,,Position;
                                                   OptionString=None,Cable,Device,ExtraNeed,,,,,,Position;
                                                   Editable=No }
    { 635 ;   ;System No. (AYNO)   ;Code20        ;CaptionML=ENU=System No. (AYNO);
                                                   Editable=No }
    { 640 ;   ;Entity No. (AYNO)   ;Code20        ;CaptionML=ENU=Entity No. (AYNO);
                                                   Editable=No }
    { 650 ;   ;Warranty Start Date Vendor;Date    ;CaptionML=ENU=Warranty Start Date Vendor }
    { 660 ;   ;Warranty Code Vendor;Code10        ;TableRelation="Service Warranty";
                                                   CaptionML=ENU=Warranty Code Vendor }
    { 670 ;   ;Warranty Date Vendor;Date          ;CaptionML=ENU=Warranty Date Vendor }
    { 680 ;   ;Warranty Date Customer;Date        ;CaptionML=ENU=Warranty Date Customer }
    { 690 ;   ;NEN2767 Element Code;Code20        ;TableRelation="NEN2767 Element";
                                                   OnValidate=BEGIN
                                                                NEN2767Management.TestNEN2767Licensed;
                                                              END;

                                                   CaptionML=ENU=NEN2767 Element }
    { 700 ;   ;Condition Measurement Method;Option;OnValidate=BEGIN
                                                                NEN2767Management.TestNEN2767Licensed;
                                                              END;

                                                   CaptionML=ENU=Condition Measurement Method;
                                                   OptionCaptionML=ENU=Scale,Repair Cost;
                                                   OptionString=Scale,Repair Cost }
    { 710 ;   ;Desired Condition Score;Option     ;OnValidate=BEGIN
                                                                NEN2767Management.TestNEN2767Licensed;
                                                              END;

                                                   CaptionML=ENU=Desired Condition Score;
                                                   OptionCaptionML=ENU=" ,1,2,3,4,5,6";
                                                   OptionString=[ ,1,2,3,4,5,6] }
    { 720 ;   ;Measure Quantity    ;Decimal       ;CaptionML=ENU=Measure Quantity }
    { 730 ;   ;Unit Of Measure     ;Code10        ;TableRelation="Unit of Measure".Code;
                                                   CaptionML=ENU=Unit Of Measure }
    { 740 ;   ;Bill-to Vendor No.  ;Code20        ;TableRelation=Customer;
                                                   CaptionML=ENU=Bill-to Vendor No. }
    { 750 ;   ;Picture             ;BLOB          ;CaptionML=ENU=Picture;
                                                   SubType=Bitmap }
    { 760 ;   ;Subcontracting      ;Boolean       ;CaptionML=ENU=Subcontracting }
    { 770 ;   ;Object Group        ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Object"."Object Group" WHERE (No.=FIELD(Standard Object No.)));
                                                   CaptionML=ENU=Object Group;
                                                   Editable=No }
    { 780 ;   ;Status              ;Code10        ;TableRelation="Service Object Status";
                                                   OnValidate=VAR
                                                                ServiceObjectStatus@1100525000 : Record 11071980;
                                                              BEGIN
                                                                IF ServiceObjectStatus.GET(Status) THEN
                                                                  IF Blocked <> ServiceObjectStatus.Block THEN
                                                                    VALIDATE(Blocked, ServiceObjectStatus.Block);
                                                              END;

                                                   CaptionML=ENU=Status }
    { 790 ;   ;Ending Date         ;Date          ;OnValidate=VAR
                                                                ContractObject@1100528600 : Record 11071702;
                                                              BEGIN
                                                                IF "Ending Date" <> 0D THEN BEGIN
                                                                  ContractObject.SETCURRENTKEY("Service Object No.");
                                                                  ContractObject.SETRANGE("Service Object No.", "No.");
                                                                  ContractObject.SETFILTER("Ending Date", '%1|>%2', 0D, "Ending Date");
                                                                  IF ContractObject.FINDSET THEN
                                                                    REPEAT
                                                                      ContractObject.VALIDATE("Ending Date", "Ending Date");
                                                                      ContractObject.MODIFY(TRUE);
                                                                    UNTIL ContractObject.NEXT = 0;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Ending Date }
    { 820 ;   ;Fixed Asset No.     ;Code20        ;TableRelation="Fixed Asset";
                                                   OnValidate=VAR
                                                                FA@1100529000 : Record 5600;
                                                                ServiceSetup@1100529001 : Record 11012800;
                                                              BEGIN
                                                                IF "Fixed Asset No." <> '' THEN BEGIN
                                                                  ServiceSetup.GET;
                                                                  ServiceSetup.TESTFIELD("Activation Cost");
                                                                  FA.CheckLinkServObjectAllowed("Fixed Asset No.", "No.");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Fixed Asset No. }
    { 840 ;   ;Hide in Service Portal;Boolean     ;CaptionML=ENU=Hide in Service Portal }
    { 850 ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=ENU=Vendor No. }
    { 860 ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=ENU=Item No.;
                                                   Editable=No }
    { 880 ;   ;BIM Model No.       ;Code20        ;TableRelation="BIM Model";
                                                   CaptionML=ENU=BIM Model No.;
                                                   Editable=No }
    { 890 ;   ;IFC Object ID       ;Text30        ;CaptionML=ENU=IFC Object ID;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Service Location No.                     }
    {    ;Project No.,Yard No. (AYNO),Entity Type (AYNO),System No. (AYNO),Entity No. (AYNO) }
    {    ;Standard Object No.                      }
    {    ;Fixed Asset No.                          }
    {    ;Item No.                                 }
    {    ;BIM Model No.,IFC Object ID              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NoSeriesMgt@1100525001 : Codeunit 396;
      Text000@1100528600 : TextConst 'ENU=Replace existing description by %1/%2?';
      Text001@1100528601 : TextConst 'ENU=Object %1 is linked to Service Contract %2, removal not allowed';
      Text002@1100528602 : TextConst 'ENU=The Object Will Be Disabled Within Contracts, Continue?';
      Text003@1100528300 : TextConst 'ENU=Object %1 related to one or more buildings.';
      Text004@1100528301 : TextConst 'ENU=No Preferred Building defined in %1 with object %2';
      Text005@1100528603 : TextConst 'ENU=No Service Location(s) selected with Building 51';
      Text006@1100528604 : TextConst 'ENU=Object %1 is used in table %2. Removal not allowed.';
      NEN2767Management@1100528605 : Codeunit 11012863;

    PROCEDURE AssistEdit@2(OldObject@1000 : Record 11071691) : Boolean;
    VAR
      ObjectRec@1001 : Record 11071691;
      ServiceSetup@1100528600 : Record 11012800;
    BEGIN
      WITH ObjectRec DO BEGIN
        ObjectRec := Rec;
        ServiceSetup.GET;
        ServiceSetup.TESTFIELD("Object Nos."); //mg.c, 15-12-10: M24121
        IF NoSeriesMgt.SelectSeries(
          ServiceSetup."Object Nos.",OldObject."No. Series", "No. Series")
        THEN BEGIN //mg.c, 15-12-10: M24121
          NoSeriesMgt.SetSeries("No.");
          Rec := ObjectRec;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE UpdateTenderCode@1100528600();
    VAR
      StandardObject@1100528601 : Record 11020334;
    BEGIN
      IF StandardObject.GET("Standard Object No.") THEN BEGIN
        "Tender Code System 1" := StandardObject."Tender Code System 1";
        "Tender Code 1" := StandardObject."Tender Code 1";
        "Tender Code System 2" := StandardObject."Tender Code System 2";
        "Tender Code 2" := StandardObject."Tender Code 2";
      END;
    END;

    PROCEDURE CopyObjectLevelSpecs@1100528612();
    VAR
      ObjectSpecificationLine@1100528601 : Record 11071781;
      DummyObjectSpecificationLine@1100528602 : Record 11071781;
      ObjectSpecificationMgt@1100528600 : Codeunit 11012848;
    BEGIN
      IF "Standard Object No." = '' THEN
        EXIT;

      ObjectSpecificationLine.SETRANGE("Specification Type", ObjectSpecificationLine."Specification Type"::"Standard Object");
      ObjectSpecificationLine.SETRANGE(Code, "Standard Object No.");
      ObjectSpecificationLine.SETRANGE("Object Level", TRUE);
      ObjectSpecificationLine.SETRANGE(Type, ObjectSpecificationLine.Type::Line);
      IF ObjectSpecificationLine.FINDSET THEN
        REPEAT
          IF ObjectSpecificationLine.Type = ObjectSpecificationLine.Type::Line THEN BEGIN
            ObjectSpecificationMgt.FindOrCreateObjectSpecGroup("No.",
              ObjectSpecificationLine.GetObjSpecificationGroupCode, DummyObjectSpecificationLine);
          END;
          ObjectSpecificationMgt.FindOrCreateObjectSpec("No.", ObjectSpecificationLine."Specification Code",
            DummyObjectSpecificationLine);
        UNTIL ObjectSpecificationLine.NEXT = 0;
    END;

    PROCEDURE UpdateContractObjectBlocked@1100528602();
    VAR
      ContractObject@1100528600 : Record 11071702;
    BEGIN
      ContractObject.SETCURRENTKEY("Service Object No.");
      ContractObject.SETRANGE("Service Object No.", "No.");
      ContractObject.SETRANGE("Ending Date", 0D);
      IF NOT ContractObject.ISEMPTY THEN BEGIN
        IF NOT CONFIRM(Text002, TRUE) THEN
          ERROR('');
        ContractObject.MODIFYALL("Ending Date", TODAY, TRUE);
      END;
    END;

    PROCEDURE TestNotInBuilding@1100528300();
    VAR
      BuildingObjectRelation@1100528300 : Record 11071820;
    BEGIN
      BuildingObjectRelation.SETCURRENTKEY("Object No.");
      BuildingObjectRelation.SETRANGE("Object No.", "No.");
      IF NOT BuildingObjectRelation.ISEMPTY THEN
        ERROR(Text003, "No.");
    END;

    PROCEDURE ShowBuildingRelations@1100528302();
    VAR
      BuildingObjectRelation@1100528300 : Record 11071820;
    BEGIN
      TESTFIELD("Service Location No.", '');

      BuildingObjectRelation.FILTERGROUP(9);
      BuildingObjectRelation.SETRANGE("Object No.", "No.");
      BuildingObjectRelation.FILTERGROUP(0);
      BuildingObjectRelation.SETCURRENTKEY("Object No.");
      PAGE.RUNMODAL(PAGE::"Building-Object Relations", BuildingObjectRelation);
    END;

    PROCEDURE GetPrefServiceLocationNo@1100528301(IWithErrorCheck@1100528300 : Boolean) : Code[20];
    VAR
      BuildingObjectRelation@1100528301 : Record 11071820;
      Building@1100528302 : Record 11071819;
      ServiceLocation@1100528600 : Record 11012801;
      Complex@1100525000 : Record 11071816;
    BEGIN
      IF IWithErrorCheck THEN
        TESTFIELD("No.");

      IF "Service Location No." <> '' THEN
        EXIT("Service Location No.");

      BuildingObjectRelation.SETCURRENTKEY("Object No.");
      BuildingObjectRelation.SETRANGE("Object No.", "No.");
      IF BuildingObjectRelation.ISEMPTY THEN BEGIN
        IF IWithErrorCheck THEN
          TESTFIELD("Service Location No.");
      END ELSE BEGIN
        IF BuildingObjectRelation.COUNT > 1 THEN BEGIN
          BuildingObjectRelation.SETRANGE("Preferred Building", TRUE);
          IF BuildingObjectRelation.ISEMPTY THEN
            IF IWithErrorCheck THEN
              ERROR(Text004, BuildingObjectRelation.TABLECAPTION, "No.")
            ELSE
              EXIT;
        END;
        BuildingObjectRelation.FINDFIRST;
        IF IWithErrorCheck THEN
          BuildingObjectRelation.TESTFIELD("Building Code");
        IF IWithErrorCheck THEN
          Building.GET(BuildingObjectRelation."Building Code")
        ELSE
          IF NOT Building.GET(BuildingObjectRelation."Building Code") THEN
            EXIT;
        ServiceLocation.SETCURRENTKEY("Building Code");
        ServiceLocation.SETRANGE("Building Code", Building.Code);
        IF ServiceLocation.ISEMPTY THEN
          IF IWithErrorCheck THEN
            ERROR(Text005, Building.Code);
        IF ServiceLocation.COUNT > 1 THEN BEGIN
          IF Building."Preferred Service Location No." <> '' THEN
            EXIT(Building."Preferred Service Location No.");
          IF Complex.GET(Building."Complex Code") THEN BEGIN
            IF IWithErrorCheck THEN
              Complex.TESTFIELD("Preferred Service Location No.");
            EXIT(Complex."Preferred Service Location No.");
          END ELSE BEGIN
            IF IWithErrorCheck THEN
              Building.TESTFIELD("Preferred Service Location No.");
            EXIT;
          END;
        END ELSE
          IF Complex.GET(Building."Complex Code") THEN
            IF Complex."Preferred Service Location No." <> '' THEN
              EXIT(Complex."Preferred Service Location No.");
        ServiceLocation.FINDFIRST;
        EXIT(ServiceLocation."No.");
      END;
    END;

    PROCEDURE BelongsToServiceLocation@1100528303(IServiceLocationNo@1100528302 : Code[20]) : Boolean;
    VAR
      BuildingObjectRelation@1100528301 : Record 11071820;
      ServiceLocation@1100528300 : Record 11012801;
    BEGIN
      IF ("Service Location No." <> '') AND ("Service Location No." = IServiceLocationNo) THEN
        EXIT(TRUE);
      IF NOT ServiceLocation.GET(IServiceLocationNo) THEN
        EXIT(FALSE);
      IF ServiceLocation."Building Code" = '' THEN
        EXIT(FALSE);

      EXIT(BuildingObjectRelation.GET(ServiceLocation."Building Code", "No."));
    END;

    PROCEDURE TestNotInUse@1100528603();
    VAR
      ContractObject@1100528600 : Record 11071702;
      MaintenanceProposalLine@1100528601 : Record 11071696;
      ServiceContractArrangement@1100528602 : Record 11071705;
      ServiceCall@1100528603 : Record 11012822;
      ServiceOrderObject@1100528604 : Record 11071724;
      ObjectModificationLine@1100528605 : Record 11071701;
      ReplacementLine@1100528606 : Record 11071710;
    BEGIN
      ContractObject.SETCURRENTKEY("Service Object No.");
      ContractObject.SETRANGE("Service Object No.", "No.");
      IF ContractObject.FINDFIRST THEN
        ERROR(Text001, ContractObject."Service Object No.", ContractObject."Service Contract No.");

      ServiceContractArrangement.SETRANGE("Object No.", "No.");
      IF NOT ServiceContractArrangement.ISEMPTY THEN
        ERROR(Text006, "No.", ServiceContractArrangement.TABLECAPTION);

      MaintenanceProposalLine.SETRANGE("Object No.", "No.");
      IF NOT MaintenanceProposalLine.ISEMPTY THEN
        ERROR(Text006, "No.", MaintenanceProposalLine.TABLECAPTION);

      ServiceCall.SETRANGE("Object No.", "No.");
      IF NOT ServiceCall.ISEMPTY THEN
        ERROR(Text006, "No.", ServiceCall.TABLECAPTION);

      ServiceOrderObject.SETRANGE("Object No.", "No.");
      IF NOT ServiceOrderObject.ISEMPTY THEN
        ERROR(Text006, "No.", ServiceOrderObject.TABLECAPTION);

      ObjectModificationLine.SETRANGE("Object No.", "No.");
      IF NOT ObjectModificationLine.ISEMPTY THEN
        ERROR(Text006, "No.", ObjectModificationLine.TABLECAPTION);

      ReplacementLine.SETRANGE("Object No.", "No.");
      IF NOT ReplacementLine.ISEMPTY THEN
        ERROR(Text006, "No.", ReplacementLine.TABLECAPTION);
    END;

    PROCEDURE VendorWillBeCharged@1100528604(IServiceOrderNo@1100528603 : Code[20];IPostingDate@1100528600 : Date;VAR OBilltoVendorNo@1100528602 : Code[20]) : Boolean;
    VAR
      ServWarrDiscountTerm@1100528601 : Record 11071811;
      ServiceOrder@1100528604 : Record 11012823;
    BEGIN
      IF IServiceOrderNo <> '' THEN BEGIN
        IF NOT ServiceOrder.GET(IServiceOrderNo) THEN
          EXIT;
        IF NOT (ServiceOrder."Source Type" IN [ServiceOrder."Source Type"::Call, ServiceOrder."Source Type"::Direct]) THEN
          EXIT;
      END; // else called from service call
      IF ("Warranty Code Customer" <> '') AND ("Warranty Start Date Customer" <> 0D) AND ("Bill-to Vendor No." <> '') AND
         (IPostingDate >= "Warranty Start Date Customer")
      THEN BEGIN
        ServWarrDiscountTerm.SETRANGE("Warranty Code", "Warranty Code Customer");
        ServWarrDiscountTerm.SETRANGE("Cost Type", ServWarrDiscountTerm."Cost Type"::" ");
        ServWarrDiscountTerm.SETRANGE(Percentage, 100);
        IF NOT ServWarrDiscountTerm.FINDSET THEN
          EXIT;
        REPEAT
          IF (FORMAT(ServWarrDiscountTerm.Period) = '') OR
             (IPostingDate <= CALCDATE(ServWarrDiscountTerm.Period, "Warranty Start Date Customer"))
          THEN BEGIN
            OBilltoVendorNo := "Bill-to Vendor No.";
            EXIT(TRUE);
          END;
        UNTIL ServWarrDiscountTerm.NEXT = 0;
      END;
    END;

    PROCEDURE GetDepartmentCode@1100528605(IServiceContractNo@1100528600 : Code[20]) : Code[20];
    VAR
      ServiceLocation@1100528601 : Record 11012801;
      ServiceContract@1100528602 : Record 11012812;
      MaintenanceManagement@1100528604 : Codeunit 11012829;
      ServiceLocationNo@1100528603 : Code[20];
    BEGIN
      ServiceLocationNo := GetPrefServiceLocationNo(FALSE);
      IF ServiceLocationNo <> '' THEN
        IF ServiceLocation.GET(ServiceLocationNo) THEN
          IF ServiceLocation."Global Dimension 1 Code" <> '' THEN
            EXIT(ServiceLocation."Global Dimension 1 Code");
      IF IServiceContractNo = '' THEN
        MaintenanceManagement.GetNoOfActualContractsForObj("No.", TODAY, IServiceContractNo);
      IF ServiceContract.GET(IServiceContractNo) THEN
        EXIT(ServiceContract."Global Dimension 1 Code");
    END;

    PROCEDURE GetPreferredBuildingCode@1100528601() : Code[20];
    VAR
      BuildingObjectRelation@1100528600 : Record 11071820;
    BEGIN
      BuildingObjectRelation.SETCURRENTKEY("Object No.");
      BuildingObjectRelation.SETRANGE("Object No.", "No.");
      IF BuildingObjectRelation.COUNT = 1 THEN BEGIN
        BuildingObjectRelation.FINDFIRST;
        EXIT(BuildingObjectRelation."Building Code");
      END ELSE BEGIN
        BuildingObjectRelation.SETRANGE("Preferred Building", TRUE);
        IF BuildingObjectRelation.FINDFIRST THEN
          EXIT(BuildingObjectRelation."Building Code");
        BuildingObjectRelation.SETRANGE("Preferred Building");
        IF BuildingObjectRelation.FINDFIRST THEN
          EXIT(BuildingObjectRelation."Building Code");
      END;
    END;

    PROCEDURE ApplySMAgreements@1100528606();
    VAR
      ServiceContract@1100528602 : Record 11012812;
      ContractObject@1100528601 : Record 11071702;
      MaintenanceManagement@1100528600 : Codeunit 11012829;
    BEGIN
      IF MODIFY(TRUE) THEN; // Needed for HandleSMAgreement();
      MaintenanceManagement.GetActualContractDSForObject("No.", TODAY, ServiceContract);
      IF ServiceContract.FINDSET THEN
        REPEAT
          IF ContractObject.GET(ServiceContract."No.", "No.") THEN BEGIN
            ContractObject.HandleSMAgreement(TRUE, TRUE);
            ContractObject.MODIFY(TRUE);
          END;
        UNTIL ServiceContract.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateContractObjects@1100528607();
    VAR
      ContractObject@1100528600 : Record 11071702;
    BEGIN
      MODIFY;
      ContractObject.SETCURRENTKEY("Service Object No.");
      ContractObject.SETRANGE("Service Object No.", "No.");
      ContractObject.SETFILTER("Ending Date", '%1|>=%2', 0D, TODAY);
      ContractObject.SetDoNotConfirmMaintenanceSchemeCreation(TRUE);
      IF ContractObject.FINDSET THEN
        REPEAT
          ContractObject.FillDefaultMaintScheme;
        UNTIL ContractObject.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

