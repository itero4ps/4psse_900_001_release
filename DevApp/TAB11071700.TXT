OBJECT Table 11071700 Object Modification
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 ServSetup.GET;
                 ServSetup.TESTFIELD("Object Modification Nos.");
                 NoSeriesMgt.InitSeries(ServSetup."Object Modification Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               "Input by" := USERID;
               "Input Date" := CREATEDATETIME(TODAY,TIME);
             END;

    OnDelete=VAR
               ObjectModificationLine@1100528600 : Record 11071701;
               MaintSchemeVersionLine@1100528601 : Record 11071744;
             BEGIN
               ObjectModificationLine.SETRANGE("Modification No.", "No.");
               ObjectModificationLine.DELETEALL(TRUE);

               MaintSchemeVersionLine.SETRANGE("Line Type", MaintSchemeVersionLine."Line Type"::Modification);
               MaintSchemeVersionLine.SETRANGE("Type Code", "No.");
               MaintSchemeVersionLine.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Modification;
    LookupPageID=Page11125166;
    DrillDownPageID=Page11125166;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code10        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  ServSetup.GET;
                                                                  NoSeriesMgt.TestManual(ServSetup."Object Modification Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 20  ;   ;Standard Object No. ;Code20        ;TableRelation="Standard Object".No.;
                                                   OnLookup=VAR
                                                              ServiceObjectManagement@1100528600 : Codeunit 11012808;
                                                            BEGIN
                                                              IF ServiceObjectManagement.HandleLookupStandardObject('', FALSE, "Standard Object No.") THEN
                                                                VALIDATE("Standard Object No.");
                                                            END;

                                                   CaptionML=ENU=Standard Object No. }
    { 30  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 40  ;   ;Resolve before      ;Date          ;CaptionML=ENU=Resolve before }
    { 60  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=No }
    { 70  ;   ;Discipline          ;Code10        ;TableRelation=Discipline;
                                                   CaptionML=ENU=Discipline;
                                                   Description=SERV_CONS }
    { 80  ;   ;Trade Speciality    ;Code10        ;TableRelation="Trade Speciality".Code WHERE (Discipline=FIELD(Discipline));
                                                   CaptionML=ENU=Trade Speciality;
                                                   Description=SERV_CONS }
    { 90  ;   ;Trade Expertise     ;Code10        ;TableRelation="Trade Expertise";
                                                   OnValidate=VAR
                                                                TradeExpRec@1100525000 : Record 11020286;
                                                              BEGIN
                                                                TradeExpRec.GetRelatedValue(Discipline, "Trade Speciality", "Trade Expertise");
                                                              END;

                                                   CaptionML=ENU=Trade Expertise;
                                                   Description=SERV_CONS }
    { 100 ;   ;Input by            ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Input by }
    { 110 ;   ;Input Date          ;DateTime      ;CaptionML=ENU=Input Date }
    { 160 ;   ;Customer No.        ;Code20        ;TableRelation=Customer.No.;
                                                   OnValidate=VAR
                                                                Customer@1100528600 : Record 18;
                                                              BEGIN
                                                                IF "Customer No." <> '' THEN BEGIN
                                                                  Customer.GET("Customer No.");
                                                                  "Price Book Type" := Customer."Price Book Type";
                                                                  "Price Book Code" := Customer."Price Book Code";
                                                                  "Price Book Index Date" := Customer."Price Book Index Date";
                                                                  "Unit Price Set" := Customer."Unit Price Set";
                                                                  "Unit Price Status" := Customer."Unit Price Status";
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Customer No. }
    { 170 ;   ;Customer Name       ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.Name WHERE (No.=FIELD(Customer No.)));
                                                   CaptionML=ENU=Customer Name;
                                                   Editable=No }
    { 180 ;   ;Price Book Type     ;Option        ;OnValidate=VAR
                                                                ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                              BEGIN
                                                                CASE "Price Book Type" OF
                                                                  "Price Book Type"::None:
                                                                    VALIDATE("Price Book Code", '');
                                                                  "Price Book Type"::Customer:
                                                                    BEGIN
                                                                      "Price Book Code" := "Customer No.";
                                                                      "Price Book Index Date" :=
                                                                        ServiceUnitPriceManagement.GetLatestPriceBookIndexDate("Price Book Type", "Customer No.");
                                                                      IF "Price Book Index Date" = 0D THEN
                                                                        "Price Book Code" := '';
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Price Book Type;
                                                   OptionCaptionML=ENU=None,Internal,Customer;
                                                   OptionString=None,Internal,Customer }
    { 190 ;   ;Price Book Code     ;Code20        ;TableRelation="Service Price Book".Code WHERE (Type=FIELD(Price Book Type));
                                                   OnValidate=VAR
                                                                ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                              BEGIN
                                                                IF ("Price Book Code" = '') OR ("Price Book Code" <> xRec."Price Book Code") THEN BEGIN
                                                                  VALIDATE("Price Book Index Date", 0D);
                                                                  VALIDATE("Unit Price Set", '');
                                                                  VALIDATE("Unit Price Status", "Unit Price Status"::" ");
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ServicePriceBook@1100528600 : Record 11071768;
                                                              ServicePriceBookList@1100528601 : Page 11125269;
                                                            BEGIN
                                                              ServicePriceBook.SETRANGE(Type, "Price Book Type");
                                                              ServicePriceBookList.LOOKUPMODE(TRUE);
                                                              ServicePriceBookList.SETTABLEVIEW(ServicePriceBook);
                                                              IF ServicePriceBookList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                ServicePriceBookList.GETRECORD(ServicePriceBook);
                                                                "Price Book Code" := ServicePriceBook.Code;
                                                                "Price Book Index Date" := ServicePriceBook."Index Date";
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Price Book Code }
    { 200 ;   ;Price Book Index Date;Date         ;OnValidate=VAR
                                                                ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                              BEGIN
                                                                IF "Price Book Index Date" <> 0D THEN
                                                                  IF "Price Book Type" = "Price Book Type"::None THEN
                                                                    FIELDERROR("Price Book Index Date");
                                                              END;

                                                   CaptionML=ENU=Price Book Index Date }
    { 210 ;   ;Unit Price Status   ;Option        ;OnValidate=BEGIN
                                                                IF "Unit Price Status" <> "Unit Price Status"::" " THEN
                                                                  IF "Price Book Type" = "Price Book Type"::None THEN
                                                                    FIELDERROR("Unit Price Status");
                                                              END;

                                                   CaptionML=ENU=Unit Price Status;
                                                   OptionCaptionML=ENU=" ,Pre-Recording,Recording,Realization";
                                                   OptionString=[ ,Pre-Recording,Recording,Realization] }
    { 220 ;   ;Settlement Method   ;Option        ;CaptionML=ENU=Settlement Method;
                                                   OptionCaptionML=ENU=Cost Plus,Fixed Price;
                                                   OptionString=Cost Plus,Fixed Price }
    { 230 ;   ;Master Project      ;Code20        ;TableRelation=Job.No. WHERE (Internal Project=CONST(No));
                                                   OnValidate=VAR
                                                                Job@1100528601 : Record 11072003;
                                                                ProjectElement@1100528602 : Record 11012010;
                                                              BEGIN
                                                                IF "Master Project" <> '' THEN BEGIN
                                                                  Job.GET("Master Project");
                                                                  Job.TESTFIELD("Project Status", Job."Project Status"::Production);
                                                                END;
                                                                IF NOT ProjectElement.GET("Master Project", "Master Element") THEN
                                                                  "Master Element" := '';
                                                              END;

                                                   CaptionML=ENU=Master Project }
    { 235 ;   ;Master Project Description;Text50  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Master Project)));
                                                   CaptionML=ENU=Master Project Description;
                                                   Editable=No }
    { 240 ;   ;Master Element      ;Code20        ;TableRelation="Project Element".Element WHERE (Project No.=FIELD(Master Project));
                                                   OnValidate=VAR
                                                                ProjectElement@1100528600 : Record 11012010;
                                                                FormatManagement@1100528601 : Codeunit 11012022;
                                                              BEGIN
                                                                IF "Master Element" <> '' THEN BEGIN
                                                                  FormatManagement.FormatElem("Master Element", 2, '');
                                                                  ProjectElement.GET("Master Project", "Master Element");
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Master Element }
    { 250 ;   ;Order Kind          ;Code10        ;TableRelation="Service Order Indication".Code WHERE (Indication=CONST(Kind));
                                                   CaptionML=ENU=Order Kind }
    { 260 ;   ;Unit Price Set      ;Code20        ;TableRelation="Service Unit Price Set".Code WHERE (Price Book Type=FIELD(Price Book Type),
                                                                                                      Price Book Code=FIELD(Price Book Code),
                                                                                                      Price Book Index Date=FIELD(Price Book Index Date));
                                                   OnValidate=BEGIN
                                                                IF "Unit Price Set" <> '' THEN
                                                                  VALIDATE("Unit Price Status", "Unit Price Status"::"Pre-Recording");
                                                              END;

                                                   CaptionML=ENU=Unit Price Set }
    { 270 ;   ;On Hold             ;Code3         ;TableRelation="On Hold Code".Code;
                                                   CaptionML=ENU=On Hold }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ServSetup@1100526200 : Record 11012800;
      NoSeriesMgt@1100526201 : Codeunit 396;

    PROCEDURE AssistEdit@2(OldApplianceModification@1000 : Record 11071700) : Boolean;
    VAR
      ApplianceModification@1100526200 : Record 11071700;
    BEGIN
      //mg.sn, 15-12-10: M24139
      ServSetup.GET;
      ServSetup.TESTFIELD("Object Modification Nos.");
      ApplianceModification := Rec;
      IF NoSeriesMgt.SelectSeries(ServSetup."Object Modification Nos.", OldApplianceModification."No. Series","No. Series") THEN
      BEGIN
        ServSetup.GET;
        NoSeriesMgt.SetSeries("No.");
        Rec := ApplianceModification;
        EXIT(TRUE);
      END;
      //mg.en, 15-12-10: M24139
    END;

    BEGIN
    END.
  }
}

