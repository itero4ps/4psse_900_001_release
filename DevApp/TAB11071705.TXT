OBJECT Table 11071705 Service Contract Arrangement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               VisitingScheme@1100526201 : Record 11071705;
             BEGIN
               IF ("Line No." = 0) OR VisitingScheme.GET("Service Contract No.", "Object No.", "Maintenance Scheme", "Line No.") THEN
                 "Line No." := GetNextLineNo();

               IF "Maintenance Year" = 0 THEN
                 FIELDERROR("Maintenance Year");

               CheckUniqueDWY;
               UpdateControlPeriods("Theoretical Maintenance Date");
             END;

    OnModify=BEGIN
               CheckModifyAllowed();

               CheckUniqueDWY;
             END;

    OnDelete=BEGIN
               CheckModifyAllowed();
             END;

    CaptionML=ENU=Service Contract Arrangement;
    LookupPageID=Page11125309;
    DrillDownPageID=Page11125309;
  }
  FIELDS
  {
    { 10  ;   ;Service Contract No.;Code20        ;TableRelation=Service-Contract.No.;
                                                   CaptionML=ENU=Service Contract No.;
                                                   Editable=No }
    { 20  ;   ;Object No.          ;Code20        ;TableRelation=Service-Object.No.;
                                                   OnValidate=VAR
                                                                ServiceObject@1100528600 : Record 11071691;
                                                              BEGIN
                                                                IF "Object No." <> '' THEN
                                                                  IF ServiceObject.GET("Object No.") THEN
                                                                    "Service Location No." := ServiceObject."Service Location No.";
                                                              END;

                                                   CaptionML=ENU=Object No.;
                                                   Editable=No }
    { 25  ;   ;Object Description  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Description WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=ENU=Object Description;
                                                   Editable=No }
    { 27  ;   ;Object Description 2;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Description 2" WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=ENU=Object Description 2;
                                                   Editable=No }
    { 40  ;   ;Maintenance Scheme  ;Code20        ;TableRelation="Maintenance Scheme".Code;
                                                   CaptionML=ENU=Maintenance Scheme;
                                                   Editable=No }
    { 45  ;   ;Maintenance Scheme Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Scheme".Description WHERE (Code=FIELD(Maintenance Scheme)));
                                                   CaptionML=ENU=Maintenance Scheme Description;
                                                   Editable=No }
    { 50  ;   ;Service Package     ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Contract Object"."Service Package" WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                                 Service Object No.=FIELD(Object No.)));
                                                   CaptionML=ENU=Service Package;
                                                   Editable=No }
    { 55  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 60  ;   ;Maintenance Year    ;Integer       ;OnValidate=BEGIN
                                                                IF "Maintenance Week" <> 0 THEN BEGIN
                                                                  CheckWeekNo("Maintenance Day", "Maintenance Week", "Maintenance Year");
                                                                  IF xRec."Maintenance Year" <> "Maintenance Year" THEN
                                                                    RecalcTheoreticalMaintDate();
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Maintenance Year }
    { 70  ;   ;Maintenance Week    ;Integer       ;OnValidate=BEGIN
                                                                CheckWeekNo("Maintenance Day", "Maintenance Week", "Maintenance Year");

                                                                IF xRec."Maintenance Week" <> "Maintenance Week" THEN
                                                                  RecalcTheoreticalMaintDate();
                                                              END;

                                                   CaptionML=ENU=Maintenance Week }
    { 80  ;   ;Maintenance Day     ;Option        ;OnValidate=BEGIN
                                                                IF (xRec."Maintenance Day" <> "Maintenance Day") AND ("Maintenance Day" IN [1..7]) THEN
                                                                  RecalcTheoreticalMaintDate();
                                                              END;

                                                   CaptionML=ENU=Maintenance Day;
                                                   OptionCaptionML=ENU=" ,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday,Weekday,Weekend";
                                                   OptionString=[ ,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday,Weekday,Weekend] }
    { 90  ;   ;Planned Maintenance Date;Date      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Proposal Line"."Starting Date Maintenance" WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                                                     Object No.=FIELD(Object No.),
                                                                                                                                     Maintenance Scheme=FIELD(Maintenance Scheme),
                                                                                                                                     Service Contract Arr. Line No.=FIELD(Line No.)));
                                                   CaptionML=ENU=Planned Maintenance Date;
                                                   Editable=No }
    { 100 ;   ;Service Order Starting Date;Date   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order"."Starting Date" WHERE (No.=FIELD(Service Order No.)));
                                                   CaptionML=ENU=Execute Maintenance Date;
                                                   Editable=No }
    { 110 ;   ;Maintenance Interval;DateFormula   ;CaptionML=ENU=Maintenance Interval;
                                                   Editable=No }
    { 120 ;   ;Maintenance Proposal No.;Code20    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Proposal Line"."Maintenance Proposal" WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                                                Object No.=FIELD(Object No.),
                                                                                                                                Maintenance Scheme=FIELD(Maintenance Scheme),
                                                                                                                                Service Contract Arr. Line No.=FIELD(Line No.),
                                                                                                                                Expired=CONST(No)));
                                                   OnLookup=VAR
                                                              MaintenanceProposalConsObj@1100526200 : Record 11071695;
                                                            BEGIN
                                                              IF MaintenanceProposalConsObj.GET("Maintenance Proposal No.") THEN
                                                                PAGE.RUN(PAGE::"Maintenance Proposal List", MaintenanceProposalConsObj);
                                                            END;

                                                   CaptionML=ENU=Maintenance Proposal;
                                                   Editable=No }
    { 130 ;   ;Service Order No.   ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order Object"."Service Order No." WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                                        Object No.=FIELD(Object No.),
                                                                                                                        Source=CONST(Maintenance),
                                                                                                                        Maintenance Scheme=FIELD(Maintenance Scheme),
                                                                                                                        Source Line No.=FIELD(Line No.)));
                                                   OnLookup=VAR
                                                              ServiceOrder@1100526200 : Record 11012823;
                                                            BEGIN
                                                              IF ServiceOrder.GET("Service Order No.") THEN
                                                                PAGE.RUN(PAGE::"Service-Order List", ServiceOrder);
                                                            END;

                                                   CaptionML=ENU=Service Order No.;
                                                   Editable=No }
    { 140 ;   ;Firm Planned        ;Boolean       ;CaptionML=ENU=Firm Planned }
    { 150 ;   ;Theoretical Maintenance Date;Date  ;CaptionML=ENU=Theoretical Maintenance Date;
                                                   Editable=No }
    { 160 ;   ;Service Order Execution Date;Date  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order Object"."Execution Date" WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                                     Object No.=FIELD(Object No.),
                                                                                                                     Maintenance Scheme=FIELD(Maintenance Scheme),
                                                                                                                     Source Line No.=FIELD(Line No.),
                                                                                                                     Source=CONST(Maintenance)));
                                                   CaptionML=ENU=Service Order Execution Date;
                                                   Editable=No }
    { 170 ;   ;Finished            ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order Object".Finished WHERE (Service Order No.=FIELD(Service Order No.),
                                                                                                             Service Contract No.=FIELD(Service Contract No.),
                                                                                                             Object No.=FIELD(Object No.),
                                                                                                             Source=CONST(Maintenance),
                                                                                                             Code=FIELD(Maintenance Scheme),
                                                                                                             Source Line No.=FIELD(Line No.)));
                                                   CaptionML=ENU=Finished;
                                                   Editable=No }
    { 180 ;   ;Service Order Status;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order".Status WHERE (No.=FIELD(Service Order No.)));
                                                   CaptionML=ENU=Service Order Status;
                                                   OptionCaptionML=ENU=Open,,In Process,Technical Finished,Administrative Finished,Finished,Cancelled;
                                                   OptionString=Open,,In Process,Technical Finished,Administrative Finished,Finished,Cancelled;
                                                   Editable=No }
    { 190 ;   ;Cluster             ;Code10        ;TableRelation="Service Cluster".Code;
                                                   OnValidate=BEGIN
                                                                IF xRec.Cluster <> Cluster THEN
                                                                  "Firm Planned" := TRUE;
                                                                ValidateServiceCluster;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupServiceCluster;
                                                            END;

                                                   CaptionML=ENU=Cluster }
    { 200 ;   ;Maintenance Bandwidth;DateFormula  ;CaptionML=ENU=Maintenance Bandwidth;
                                                   Editable=No }
    { 210 ;   ;Starting Time Maint. Window;Time   ;OnValidate=BEGIN
                                                                TestMaintenanceTimes;
                                                              END;

                                                   CaptionML=ENU=Starting Time Maintenance Window }
    { 220 ;   ;Ending Time Maint. Window;Time     ;OnValidate=BEGIN
                                                                TestMaintenanceTimes;
                                                              END;

                                                   CaptionML=ENU=Ending Time Maintenance Window }
    { 230 ;   ;Service Location No.;Code20        ;TableRelation="Service Location";
                                                   CaptionML=ENU=Service Location No.;
                                                   Editable=No }
    { 240 ;   ;Service Location Description;Text50;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".Description WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=Service Location Description;
                                                   Editable=No }
    { 250 ;   ;Address             ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".Address WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=Address;
                                                   Editable=No }
    { 260 ;   ;Address 2           ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location"."Address 2" WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=Address 2;
                                                   Editable=No }
    { 270 ;   ;City                ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".City WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=City;
                                                   Editable=No }
    { 280 ;   ;County              ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".County WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=County;
                                                   Editable=No }
    { 290 ;   ;Post Code           ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location"."Post Code" WHERE (No.=FIELD(Service Location No.)));
                                                   TableRelation="Post Code";
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Post Code;
                                                   Editable=No }
    { 300 ;   ;Comment             ;Text50        ;OnValidate=BEGIN
                                                                IF Comment <> '' THEN BEGIN
                                                                  CALCFIELDS("Maintenance Proposal No.", "Service Order No.");
                                                                  IF ("Maintenance Proposal No." = '') AND ("Service Order No." = '') AND NOT "Firm Planned" THEN
                                                                    MESSAGE(Text005);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Comment }
    { 310 ;   ;Cancelled           ;Boolean       ;OnValidate=BEGIN
                                                                IF Cancelled THEN BEGIN
                                                                  CALCFIELDS("Maintenance Proposal No.", "Service Order No.");
                                                                  IF "Maintenance Proposal No." <> '' THEN
                                                                    ERROR(Text001);
                                                                  IF "Service Order No." <> '' THEN
                                                                    ERROR(Text002);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Cancelled }
    { 320 ;   ;Discipline          ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Scheme".Discipline WHERE (Code=FIELD(Maintenance Scheme)));
                                                   TableRelation=Discipline;
                                                   CaptionML=ENU=Discipline;
                                                   Editable=No }
    { 330 ;   ;Trade Speciality    ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Scheme"."Trade Speciality" WHERE (Code=FIELD(Maintenance Scheme)));
                                                   TableRelation="Trade Speciality".Code WHERE (Discipline=FIELD(Discipline));
                                                   CaptionML=ENU=Trade Speciality;
                                                   Editable=No }
    { 340 ;   ;Service Category    ;Code10        ;TableRelation="Service Category".Code WHERE (Service Contract No.=FIELD(Service Contract No.));
                                                   CaptionML=ENU=Service Category }
    { 350 ;   ;Object Brand Code   ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Brand Code" WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=ENU=Object Brand Code;
                                                   Editable=No }
    { 360 ;   ;Object Model Code   ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Model Code" WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=ENU=Object Model Code;
                                                   Editable=No }
    { 370 ;   ;Object Type Code    ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Type Code" WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=ENU=Object Type Code;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Service Contract No.,Object No.,Maintenance Scheme,Line No.;
                                                   Clustered=Yes }
    {    ;Service Contract No.,Object No.,Maintenance Scheme,Maintenance Year,Maintenance Week,Maintenance Day }
    {    ;Service Contract No.,Maintenance Year,Maintenance Week,Maintenance Day }
    {    ;Service Contract No.,Object No.,Maintenance Scheme,Theoretical Maintenance Date }
    {    ;Object No.                               }
    {    ;Service Location No.,Maintenance Year,Maintenance Week,Maintenance Day }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100526200 : TextConst 'ENU=Invalid Week No.';
      Text001@1100526201 : TextConst 'ENU=Maintenance Proposal already created, modification prohibited.';
      Text002@1100526202 : TextConst 'ENU=Service Order No. already created, modification prohibited.';
      Text003@1100526203 : TextConst 'ENU=Contract Arrangement Line already exists';
      Text004@1100528600 : TextConst 'ENU=Must occur after  %1';
      Text005@1100528601 : TextConst 'ENU=Agreement not used or Firm Planned, comment may be erased.';

    PROCEDURE GetMaintenanceDate@1100526200() : Date;
    VAR
      Day@1100526200 : Integer;
      MaintenanceDate@1100526202 : Date;
      MaintenanceDay@1100526201 : Option;
    BEGIN
      IF "Theoretical Maintenance Date" = 0D THEN
        EXIT(0D);
      Day := DATE2DWY("Theoretical Maintenance Date", 1);
      MaintenanceDate := "Theoretical Maintenance Date";
      CASE "Maintenance Day" OF
        "Maintenance Day"::Monday.."Maintenance Day"::Sunday:
          MaintenanceDay := "Maintenance Day";
        "Maintenance Day"::Weekday:
          BEGIN
            IF Day = 6 THEN
              MaintenanceDay := 5;
            IF Day = 7 THEN BEGIN
              MaintenanceDay := 1;
              MaintenanceDate := CALCDATE('+1W', MaintenanceDate);
            END;
          END;
        "Maintenance Day"::Weekend:
          BEGIN
            IF Day IN [1..3] THEN BEGIN
              MaintenanceDate := CALCDATE('-1W', MaintenanceDate);
              MaintenanceDay := 7;
            END;
            IF Day IN [4..5] THEN BEGIN
              MaintenanceDay := 6;
            END;
          END;
      END;
      IF MaintenanceDay <> "Maintenance Day"::" " THEN BEGIN
      //  Week := DATE2DWY(MaintenanceDate, 2);
      //  Year := DATE2DWY(MaintenanceDate, 3);
        MaintenanceDate := DWY2DATE(MaintenanceDay, "Maintenance Week", "Maintenance Year");
      END;
      EXIT(MaintenanceDate);
    END;

    PROCEDURE GetNextLineNo@1100526201() : Integer;
    VAR
      VisitingScheme@1100526200 : Record 11071705;
    BEGIN
      VisitingScheme.SETRANGE("Service Contract No.", "Service Contract No.");
      VisitingScheme.SETRANGE("Object No.", "Object No.");
      VisitingScheme.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
      IF VisitingScheme.FINDLAST THEN
        EXIT(VisitingScheme."Line No." + 10000)
      ELSE
        EXIT(10000);
    END;

    PROCEDURE CheckWeekNo@1100526203(IDay@1100526202 : Integer;IWeek@1100526208 : Integer;IYear@1100526207 : Integer);
    VAR
      lvDate@1100526203 : Date;
      lvYear@1100526204 : Integer;
      lvWeek@1100526205 : Integer;
    BEGIN
      IF IWeek = 0 THEN
        ERROR(Text000);
      IF NOT (IDay IN [1..7]) THEN
        IDay := 1;
      lvDate := DWY2DATE(IDay, IWeek, IYear);
      lvWeek := DATE2DWY(lvDate, 2);
      lvYear := DATE2DWY(lvDate, 3);
      IF (lvWeek <> IWeek) OR (lvYear <> IYear) THEN
        ERROR(Text000);
    END;

    PROCEDURE CheckModifyAllowed@1100526205();
    BEGIN
      CALCFIELDS("Maintenance Proposal No.", "Service Order No.");
      IF "Maintenance Proposal No." <> '' THEN
        ERROR(Text001);
      IF "Service Order No." <> '' THEN
        ERROR(Text002);
    END;

    PROCEDURE CheckUniqueDWY@1100526202();
    VAR
      ServiceContractArrangement@1100526203 : Record 11071705;
    BEGIN
      ServiceContractArrangement.SETRANGE("Service Contract No.", "Service Contract No.");
      ServiceContractArrangement.SETRANGE("Object No.", "Object No.");
      ServiceContractArrangement.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
      ServiceContractArrangement.SETRANGE("Maintenance Year", "Maintenance Year");
      ServiceContractArrangement.SETRANGE("Maintenance Week", "Maintenance Week");
      ServiceContractArrangement.SETRANGE("Maintenance Day", "Maintenance Day");
      ServiceContractArrangement.SETFILTER("Line No.", '<>%1', "Line No.");
      IF ServiceContractArrangement.FINDFIRST THEN
        FIELDERROR("Maintenance Week", Text003);
    END;

    PROCEDURE IsUniqueDWY@1100528606() : Boolean;
    VAR
      ServiceContractArrangement@1100526203 : Record 11071705;
    BEGIN
      ServiceContractArrangement.SETRANGE("Service Contract No.", "Service Contract No.");
      ServiceContractArrangement.SETRANGE("Object No.", "Object No.");
      ServiceContractArrangement.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
      ServiceContractArrangement.SETRANGE("Maintenance Year", "Maintenance Year");
      ServiceContractArrangement.SETRANGE("Maintenance Week", "Maintenance Week");
      ServiceContractArrangement.SETRANGE("Maintenance Day", "Maintenance Day");
      ServiceContractArrangement.SETFILTER("Line No.", '<>%1', "Line No.");
      EXIT(ServiceContractArrangement.ISEMPTY);
    END;

    PROCEDURE RecalcTheoreticalMaintDate@1100526204();
    BEGIN
      "Firm Planned" := TRUE;
      IF "Maintenance Day" IN ["Maintenance Day"::Monday.."Maintenance Day"::Sunday] THEN
        "Theoretical Maintenance Date" := DWY2DATE("Maintenance Day", "Maintenance Week", "Maintenance Year")
      ELSE
        "Theoretical Maintenance Date" := DWY2DATE(1, "Maintenance Week", "Maintenance Year");
    END;

    PROCEDURE UpdateControlPeriods@1100528600(IUntil@1100528600 : Date);
    VAR
      ContractControlPeriod@1100528601 : Record 11071746;
    BEGIN
      ContractControlPeriod.UpdateControlPeriodsUntil("Service Contract No.", IUntil);
    END;

    LOCAL PROCEDURE TestMaintenanceTimes@1100528602();
    BEGIN
      IF ("Starting Time Maint. Window" = 0T) OR ("Ending Time Maint. Window" = 0T) THEN
        EXIT;
      IF "Ending Time Maint. Window" <= "Starting Time Maint. Window" THEN
        FIELDERROR("Ending Time Maint. Window", STRSUBSTNO(Text004, FIELDCAPTION("Starting Time Maint. Window")));
    END;

    PROCEDURE ValidateServiceCluster@1100528603();
    VAR
      ServiceContract@1100528601 : Record 11012812;
      ServiceCluster@1100528600 : Record 11071742;
    BEGIN
      IF Cluster <> '' THEN BEGIN
        ServiceContract.GET("Service Contract No.");
        IF ServiceContract."Use Contract Clusters" THEN
          ServiceCluster.GET("Service Contract No.", Cluster)
        ELSE
          ServiceCluster.GET('', Cluster);
      END;
    END;

    PROCEDURE LookupServiceCluster@1100528601();
    VAR
      ServiceContract@1100528600 : Record 11012812;
      ServiceCluster@1100528601 : Record 11071742;
    BEGIN
      ServiceContract.GET("Service Contract No.");
      IF ServiceContract."Use Contract Clusters" THEN BEGIN
        ServiceCluster.SETRANGE("Service Contract No.", "Service Contract No.");
        IF Cluster <> '' THEN
          ServiceCluster.GET("Service Contract No.", Cluster);
      END ELSE BEGIN
        ServiceCluster.SETRANGE("Service Contract No.", '');
        IF Cluster <> '' THEN
          ServiceCluster.GET('', Cluster);
      END;
      IF PAGE.RUNMODAL(0, ServiceCluster) = ACTION::LookupOK THEN
        VALIDATE(Cluster, ServiceCluster.Code);
    END;

    PROCEDURE IsMaintPropLineExpired@1100528604() : Boolean;
    VAR
      MaintenanceProposalLine@1100528600 : Record 11071696;
    BEGIN
      CALCFIELDS("Maintenance Proposal No.");
      MaintenanceProposalLine.SETRANGE("Maintenance Proposal", "Maintenance Proposal No.");
      MaintenanceProposalLine.SETRANGE("Service Contract No.", "Service Contract No.");
      MaintenanceProposalLine.SETRANGE("Object No.", "Object No.");
      MaintenanceProposalLine.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
      MaintenanceProposalLine.SETRANGE("Service Contract Arr. Line No.", "Line No.");
      IF MaintenanceProposalLine.FINDFIRST THEN
        EXIT(MaintenanceProposalLine.Expired);
    END;

    PROCEDURE GetPrefServiceLocationNo@1100528301(IWithErrorCheck@1100528300 : Boolean) : Code[20];
    VAR
      ServiceObject@1100528301 : Record 11071691;
    BEGIN
      IF ServiceObject.GET("Object No.") THEN
        EXIT(ServiceObject.GetPrefServiceLocationNo(IWithErrorCheck));
    END;

    PROCEDURE GetPreferredBuildingCode@1100528605() : Code[20];
    VAR
      ServiceObject@1100528600 : Record 11071691;
    BEGIN
      IF ServiceObject.GET("Object No.") THEN
        EXIT(ServiceObject.GetPreferredBuildingCode);
    END;

    PROCEDURE GetDisciplineTradeSpeciality@1100528636(VAR ODiscipline@1100528604 : Code[10];VAR OTradeSpeciality@1100528605 : Code[10]);
    VAR
      ServiceOrder@1100528601 : Record 11012823;
      MaintenanceProposalLine@1100528602 : Record 11071696;
      MaintenanceManagement@1100528600 : Codeunit 11012829;
    BEGIN
      CALCFIELDS("Service Order No.", "Maintenance Proposal No.");
      CASE TRUE OF
        "Service Order No." <> '':
          BEGIN
            IF ServiceOrder.GET("Service Order No.") THEN BEGIN
              ODiscipline := ServiceOrder.Discipline;
              OTradeSpeciality := ServiceOrder."Trade Speciality";
            END;
          END;
        "Maintenance Proposal No." <> '':
          BEGIN
            MaintenanceProposalLine.SETCURRENTKEY("Service Contract No.", "Object No.");
            MaintenanceProposalLine.SETRANGE("Service Contract No.", "Service Contract No.");
            MaintenanceProposalLine.SETRANGE("Object No.", "Object No.");
            MaintenanceProposalLine.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
            MaintenanceProposalLine.SETRANGE("Service Contract Arr. Line No.", "Line No.");
            IF MaintenanceProposalLine.FINDFIRST THEN BEGIN
              ODiscipline := MaintenanceProposalLine.Discipline;
              OTradeSpeciality := MaintenanceProposalLine."Trade Speciality";
            END;
          END;
      ELSE
        MaintenanceManagement.DetermineDisciplineAndTradeSpeciality("Object No.", "Maintenance Scheme", ODiscipline, OTradeSpeciality);
      END;
    END;

    BEGIN
    END.
  }
}

