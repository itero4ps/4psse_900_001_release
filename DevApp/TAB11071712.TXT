OBJECT Table 11071712 Object Material Inventory
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ObjectMaterialInventory@1100528600 : Record 11071712;
             BEGIN
               CentralDataManagement.EditObjectMaterialInventory(Rec, 1, xRec);
               IF BORG THEN
                 BORGManagement.TestBORGUser;

               IF "Line No." = 0 THEN BEGIN
                 ObjectMaterialInventory.SETRANGE(Type, Type);
                 ObjectMaterialInventory.SETRANGE(Code, Code);
                 IF ObjectMaterialInventory.FINDLAST THEN
                   "Line No." := ObjectMaterialInventory."Line No.";
                 "Line No." += 10000;
               END;
             END;

    OnModify=BEGIN
               CentralDataManagement.EditObjectMaterialInventory(Rec, 2, xRec);
               IF BORG THEN
                 BORGManagement.TestBORGUser;
             END;

    OnDelete=BEGIN
               CentralDataManagement.EditObjectMaterialInventory(Rec, 4, xRec);
               IF BORG THEN
                 BORGManagement.TestBORGUser;
             END;

    CaptionML=ENU=Object Material Inventory;
  }
  FIELDS
  {
    { 5   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Type <> xRec.Type THEN BEGIN
                                                                  MainRec := xRec;
                                                                  IF MainRec.FIND THEN
                                                                    CentralDataManagement.EditObjectMaterialInventory(Rec, 3, xRec);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Object,Standard Object;
                                                   OptionString=Object,Standard Object }
    { 10  ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(Object)) Service-Object
                                                                 ELSE IF (Type=CONST(Standard Object)) "Standard Object";
                                                   OnValidate=BEGIN
                                                                IF Code <> xRec.Code THEN BEGIN
                                                                  MainRec := xRec;
                                                                  IF MainRec.FIND THEN
                                                                    CentralDataManagement.EditObjectMaterialInventory(Rec, 3, xRec);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Code }
    { 15  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 20  ;   ;Item No.            ;Code20        ;TableRelation=Item.No.;
                                                   OnValidate=BEGIN
                                                                IF xRec."Item No." <> "Item No." THEN BEGIN
                                                                  IF "Item No." <> '' THEN
                                                                    ValidateItem(ItemTypeOpt::Item);
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              HandleLookupItem(FIELDNO("Item No."));
                                                            END;

                                                   CaptionML=ENU=Item No. }
    { 30  ;   ;Manufacturer        ;Code15        ;TableRelation="ETIM Vendor (Central Mgt)";
                                                   CaptionML=ENU=Manufacturer }
    { 40  ;   ;Basic Item          ;Code20        ;TableRelation="Basic Item"."Product Code" WHERE (Manufacturer=FIELD(Manufacturer));
                                                   OnValidate=BEGIN
                                                                IF xRec."Basic Item" <> "Basic Item" THEN BEGIN
                                                                  IF "Basic Item" <> '' THEN
                                                                    ValidateItem(ItemTypeOpt::"Basic Item");
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              HandleLookupItem(FIELDNO("Basic Item"));
                                                            END;

                                                   CaptionML=ENU=Basic Item }
    { 50  ;   ;Vendor (Trade Item) ;Code15        ;TableRelation="ETIM Vendor (Central Mgt)";
                                                   CaptionML=ENU=Vendor (Trade Item) }
    { 60  ;   ;Trade Item          ;Code20        ;TableRelation=IF (Vendor (Trade Item)=FILTER(<>'')) "Trade Item"."Item Code" WHERE (Vendor=FIELD("Vendor (Trade Item)"))
                                                                 ELSE IF (Vendor (Trade Item)=FILTER('')) "Trade Item"."Item Code";
                                                   OnValidate=BEGIN
                                                                IF xRec."Trade Item" <> "Trade Item" THEN BEGIN
                                                                  IF "Trade Item" <> '' THEN
                                                                    ValidateItem(ItemTypeOpt::"Trade Item");
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              HandleLookupItem(FIELDNO("Trade Item"));
                                                            END;

                                                   CaptionML=ENU=Trade Item }
    { 70  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 80  ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity }
    { 90  ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure".Code;
                                                   OnLookup=BEGIN
                                                              HandleLookupUnit;
                                                            END;

                                                   CaptionML=ENU=Unit of Measure }
    { 100 ;   ;Zone (BORG)         ;Text30        ;CaptionML=ENU=Zone (BORG) }
    { 110 ;   ;Location (BORG)     ;Text30        ;CaptionML=ENU=Location (BORG) }
    { 120 ;   ;BORG                ;Boolean       ;OnValidate=VAR
                                                                BORGManagement@1100528600 : Codeunit 11012859;
                                                              BEGIN
                                                                IF BORG THEN
                                                                  BORGManagement.TestBORGUser;
                                                              END;

                                                   CaptionML=ENU=BORG }
    { 1350;   ;Warranty Start Date Vendor;Date    ;CaptionML=ENU=Warranty Start Date Vendor }
    { 1360;   ;Warranty Date Vendor;Date          ;CaptionML=ENU=Warranty Date Vendor }
    { 1370;   ;Warranty Start Date Customer;Date  ;CaptionML=ENU=Warranty Start Date Customer }
    { 1380;   ;Warranty Date Customer;Date        ;CaptionML=ENU=Warranty Date Customer }
    { 1390;   ;Warranty Code Vendor;Code10        ;TableRelation="Service Warranty";
                                                   CaptionML=ENU=Warranty Code Vendor }
    { 1400;   ;Warranty Code Customer;Code10      ;TableRelation="Service Warranty";
                                                   CaptionML=ENU=Warranty Code Customer }
  }
  KEYS
  {
    {    ;Type,Code,Line No.                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CentralDataManagement@1100528600 : Codeunit 11012010;
      BORGManagement@1100528603 : Codeunit 11012859;
      MainRec@1100528601 : Record 11071712;
      ItemTypeOpt@1100526200 : 'Item,Basic Item,Trade Item,Trade Vendor';

    PROCEDURE ValidateItem@3(ITypeArtOpt@11012000 : 'Item,Basic Item,Trade Item,Trade Vendor');
    VAR
      ValidateItemsCU@1100526200 : Codeunit 11012033;
      Desc1Txt@1210190001 : Text[50];
      Desc2Txt@1210190003 : Text[50];
      lvDummyCode20@1100526201 : Code[20];
      lvDummyDec@1100526202 : Decimal;
      lvDummyDate@1100526203 : Date;
    BEGIN
      ValidateItemsCU.ValidateItem(
        ITypeArtOpt, "Trade Item", "Item No.", Manufacturer, "Vendor (Trade Item)", "Basic Item",
        lvDummyCode20, Desc1Txt, "Unit of Measure", lvDummyDec,
        lvDummyDec, lvDummyDate, lvDummyDec, Desc2Txt, lvDummyCode20,
        lvDummyCode20, lvDummyCode20, lvDummyDate, lvDummyCode20);

      IF Desc2Txt = '' THEN
        Description := Desc1Txt
      ELSE
        Description := COPYSTR((Desc1Txt + '\' + Desc2Txt), 1, MAXSTRLEN(Description));
      VALIDATE(Description);
    END;

    PROCEDURE HandleLookupItem@1100525003(lvFieldNo@1100525004 : Integer) lvRefresh : Boolean;
    VAR
      ItemRec@1100525012 : Record 27;
      BasicItemRec@1100525011 : Record 11012316;
      TradeItemRec@1100525010 : Record 11012317;
      ItemFrm@1100525009 : Page 31;
      BasicItemFrm@1100525008 : Page 11012333;
      TradeItemFrm@1100525007 : Page 11012334;
      g_ItemCode@1100525014 : Code[20];
      g_Vendor@1100525013 : Code[20];
    BEGIN
      IF lvFieldNo = FIELDNO("Item No.") THEN BEGIN
        CLEAR(ItemFrm);
        ItemFrm.LOOKUPMODE(TRUE);
        ItemFrm.DetermineInterface(DATABASE::"Object Material Inventory",
          Code,'','','',0,'','',"Line No.",'','');

        IF ItemRec.GET("Item No.") THEN
          ItemFrm.SETRECORD(ItemRec);

        IF ItemFrm.RUNMODAL = ACTION::LookupOK THEN BEGIN
          ItemFrm.GETRECORD(ItemRec);
          VALIDATE("Item No.", ItemRec."No.");
        END ELSE BEGIN
          lvRefresh := ItemFrm.CheckMultiCopy;
        END;
      END;

      IF lvFieldNo = FIELDNO("Basic Item") THEN BEGIN
        CLEAR(BasicItemFrm);
        BasicItemFrm.LOOKUPMODE(TRUE);
        BasicItemFrm.SetSelection(DATABASE::"Object Material Inventory",
          Code,'','','',0,'','',"Line No.",'','');

        BasicItemRec.InitLookup(BasicItemRec, "Basic Item", Manufacturer);
        BasicItemFrm.SETRECORD(BasicItemRec);

        IF BasicItemFrm.RUNMODAL = ACTION::LookupOK THEN BEGIN
          BasicItemFrm.GETRECORD(BasicItemRec);
          VALIDATE(Manufacturer, BasicItemRec.Manufacturer);
          VALIDATE("Basic Item", BasicItemRec."Product Code");
        END ELSE BEGIN
          lvRefresh := BasicItemFrm.CheckMultiCopy;
        END;
      END;

      IF lvFieldNo = FIELDNO("Trade Item") THEN BEGIN
        g_ItemCode := '';
        g_Vendor := '';

        CLEAR(TradeItemFrm);

        TradeItemFrm.LOOKUPMODE(TRUE);
        TradeItemFrm.SetSelection(DATABASE::"Object Material Inventory",
          Code, '', '', '', 0, '', '', "Line No.", '', '',
          "Vendor (Trade Item)", "Trade Item", Manufacturer, "Basic Item", g_ItemCode, g_Vendor);

        TradeItemRec.InitLookup(TradeItemRec, "Basic Item", Manufacturer, "Trade Item", "Vendor (Trade Item)");
        TradeItemFrm.SETRECORD(TradeItemRec);

        IF TradeItemFrm.RUNMODAL = ACTION::LookupOK THEN BEGIN
          TradeItemFrm.GETRECORD(TradeItemRec);
          Manufacturer := TradeItemRec.Manufacturer;
          "Basic Item" := TradeItemRec."Product Code";
          "Vendor (Trade Item)" := TradeItemRec.Vendor;
          VALIDATE("Trade Item", TradeItemRec."Item Code");
        END ELSE BEGIN
          lvRefresh := TradeItemFrm.CheckMultiCopy;
        END;
      END;

      EXIT(lvRefresh);
    END;

    PROCEDURE HandleLookupUnit@1210190011();
    VAR
      ItemUnitRec@1210190000 : Record 5404;
      lvUnit@1210190001 : Code[10];
    BEGIN
      lvUnit := ItemUnitRec.HandleLookupUnit("Item No.", "Basic Item", "Trade Item", Manufacturer, "Vendor (Trade Item)");
      IF (lvUnit <> '') AND (lvUnit <> "Unit of Measure") THEN BEGIN
        xRec."Unit of Measure" := "Unit of Measure";
        VALIDATE("Unit of Measure", lvUnit);
      END;
    END;

    BEGIN
    END.
  }
}

