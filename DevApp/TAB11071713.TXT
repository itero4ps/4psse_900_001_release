OBJECT Table 11071713 Object Imp. Worksheet
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 ServSetup.GET;
                 ServSetup.TESTFIELD("Object Imp. Worksheet Nos.");
                 NoSeriesMgt.InitSeries(ServSetup."Object Imp. Worksheet Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    OnDelete=VAR
               ObjectImpWorksheetLine@1100525000 : Record 11071714;
             BEGIN
               ObjectImpWorksheetLine.SETRANGE("Worksheet No.", "No.");
               ObjectImpWorksheetLine.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Object Import Worksheet;
    LookupPageID=Page11125250;
    DrillDownPageID=Page11125250;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code10        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  ServSetup.GET;
                                                                  NoSeriesMgt.TestManual(ServSetup."Object Imp. Worksheet Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 20  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=No }
    { 30  ;   ;File Name           ;Text250       ;CaptionML=ENU=File Name }
    { 40  ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   CaptionML=ENU=Customer No. }
    { 50  ;   ;Customer Name       ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.Name WHERE (No.=FIELD(Customer No.)));
                                                   CaptionML=ENU=Customer Name;
                                                   Editable=No }
    { 55  ;   ;Import Type         ;Option        ;OnValidate=BEGIN
                                                                UpdateTypeAndCodeInLines;
                                                              END;

                                                   CaptionML=ENU=Import Type;
                                                   OptionCaptionML=ENU=Service Contract,Service Survey;
                                                   OptionString=Service Contract,Service Survey }
    { 60  ;   ;Code                ;Code20        ;TableRelation=IF (Import Type=CONST(Service Contract)) Service-Contract
                                                                 ELSE IF (Import Type=CONST(Service Survey)) "Service Survey" WHERE (Input Method=CONST(Object));
                                                   OnValidate=VAR
                                                                ServiceContract@1100526200 : Record 11012812;
                                                                ServiceSurvey@1100528600 : Record 11071803;
                                                                ObjectImpWorksheetLine@1100525000 : Record 11071714;
                                                              BEGIN
                                                                IF (xRec.Code <> '') AND (Code = '') THEN BEGIN
                                                                  ObjectImpWorksheetLine.SETRANGE("Worksheet No.", "No.");
                                                                  IF NOT ObjectImpWorksheetLine.ISEMPTY THEN
                                                                    ERROR(Text001);
                                                                END;

                                                                IF "Customer No." = '' THEN BEGIN
                                                                  CASE "Import Type" OF
                                                                    "Import Type"::"Service Contract":
                                                                      IF ServiceContract.GET(Code) THEN BEGIN
                                                                        "Customer No." := ServiceContract."Customer No.";
                                                                        CALCFIELDS("Customer Name");
                                                                      END;
                                                                    "Import Type"::"Service Survey":
                                                                      IF ServiceSurvey.GET(Code) THEN BEGIN
                                                                        "Customer No." := ServiceSurvey."Customer No.";
                                                                        CALCFIELDS("Customer Name");
                                                                      END;
                                                                  END;
                                                                END;
                                                                UpdateTypeAndCodeInLines;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceContract@1210190000 : Record 11012812;
                                                              ServiceSurvey@1100528600 : Record 11071803;
                                                            BEGIN
                                                              CASE "Import Type" OF
                                                                "Import Type"::"Service Contract":
                                                                  BEGIN
                                                                    IF Code <> '' THEN
                                                                      IF ServiceContract.GET(Code) THEN;
                                                                    IF PAGE.RUNMODAL(0, ServiceContract) = ACTION::LookupOK THEN
                                                                      VALIDATE(Code, ServiceContract."No.");
                                                                  END;
                                                                "Import Type"::"Service Survey":
                                                                  BEGIN
                                                                    ServiceSurvey.SETRANGE("Input Method", ServiceSurvey."Input Method"::Object);
                                                                    IF ServiceSurvey.GET(Code) THEN;
                                                                    IF PAGE.RUNMODAL(0, ServiceSurvey) = ACTION::LookupOK THEN
                                                                      VALIDATE(Code, ServiceSurvey."No.");
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Code }
    { 70  ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   OnValidate=VAR
                                                                Company@1100528600 : Record 2000000006;
                                                              BEGIN
                                                                IF "Company Name" = COMPANYNAME THEN
                                                                  "Company Name" := '';
                                                                IF "Company Name" <> '' THEN
                                                                  Company.GET("Company Name");
                                                              END;

                                                   CaptionML=ENU=Company Name }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ServSetup@1100526200 : Record 11012800;
      NoSeriesMgt@1100526201 : Codeunit 396;
      Text000@1100526202 : TextConst 'ENU=File %1 not found';
      Text001@1100525000 : TextConst 'ENU=There are lines present, modifying nog allowed';

    PROCEDURE AssistEdit@2(OldObjectImpWorksheet@1000 : Record 11071713) : Boolean;
    VAR
      ObjectImpWorksheet@1100526200 : Record 11071713;
    BEGIN
      ServSetup.GET;
      ServSetup.TESTFIELD("Object Imp. Worksheet Nos.");
      ObjectImpWorksheet := Rec;
      IF NoSeriesMgt.SelectSeries(ServSetup."Object Imp. Worksheet Nos.", OldObjectImpWorksheet."No. Series","No. Series") THEN
      BEGIN
        ServSetup.GET;
        NoSeriesMgt.SetSeries("No.");
        Rec := ObjectImpWorksheet;
        EXIT(TRUE);
      END;
    END;

    PROCEDURE Import@1100526200();
    VAR
      ObjectImpWorksheetLine@1100526201 : Record 11071714;
      TempBlob@1100525002 : Record 99008535;
      FileMgt@1100525000 : Codeunit 419;
      ImportServiceObjects@1100525001 : XMLport 11012800;
      IStream@1100525003 : InStream;
    BEGIN
      TESTFIELD("File Name");
      TESTFIELD(Code);
      IF NOT FileMgt.ClientFileExists("File Name") THEN
        ERROR(Text000, "File Name");
      ImportServiceObjects.SetDefaults("No.");
      FileMgt.BLOBImport4PS(TempBlob, "File Name", FALSE);
      TempBlob.Blob.CREATEINSTREAM(IStream);
      ImportServiceObjects.SETSOURCE(IStream);
      ImportServiceObjects.IMPORT;

      ObjectImpWorksheetLine.SETRANGE("Worksheet No.", "No.");
      ObjectImpWorksheetLine.SETRANGE("Create Location", TRUE);
      IF ObjectImpWorksheetLine.FINDSET(TRUE) THEN
        REPEAT
          ObjectImpWorksheetLine.Normalize(FALSE);
          ObjectImpWorksheetLine.MODIFY;
        UNTIL ObjectImpWorksheetLine.NEXT = 0;

      ObjectImpWorksheetLine.SETRANGE("Create Location", FALSE);
      IF ObjectImpWorksheetLine.FINDSET(TRUE) THEN
        REPEAT
          ObjectImpWorksheetLine.Normalize(FALSE);
          ObjectImpWorksheetLine.MODIFY;
        UNTIL ObjectImpWorksheetLine.NEXT = 0;
    END;

    PROCEDURE UpdateTypeAndCodeInLines@1100528600();
    VAR
      ObjectImpWorksheetLine@1100528600 : Record 11071714;
    BEGIN
      ObjectImpWorksheetLine.SETRANGE("Worksheet No.", "No.");
      ObjectImpWorksheetLine.MODIFYALL(Code, '');
      ObjectImpWorksheetLine.MODIFYALL("Import Type", "Import Type");
      ObjectImpWorksheetLine.MODIFYALL(Code, Code);
    END;

    BEGIN
    END.
  }
}

