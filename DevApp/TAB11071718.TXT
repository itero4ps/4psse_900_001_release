OBJECT Table 11071718 Object Filter Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Object Filter Line;
  }
  FIELDS
  {
    { 10  ;   ;Filter Code         ;Code10        ;TableRelation="Object Filter";
                                                   CaptionML=ENU=Filter Code }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No.;
                                                   Editable=No }
    { 30  ;   ;Table Type          ;Option        ;OnValidate=BEGIN
                                                                CASE "Table Type" OF
                                                                  "Table Type"::"Service Object":
                                                                    "Table No." := DATABASE::"Service-Object";
                                                                  "Table Type"::"Standard Object":
                                                                    "Table No." := DATABASE::"Standard Object";
                                                                  "Table Type"::"Service Location":
                                                                    "Table No." := DATABASE::"Service Location";
                                                                  "Table Type"::"Service Contract":
                                                                    "Table No." := DATABASE::"Service-Contract";
                                                                END;
                                                                VALIDATE("Field No.", 0);
                                                                ClearValueFilter;
                                                              END;

                                                   CaptionML=ENU=Table Type;
                                                   OptionCaptionML=ENU=Service Object,Standard Object,Object Specification,,,,,Service Location,Service Contract;
                                                   OptionString=Service Object,Standard Object,Object Specification,,,,,Service Location,Service Contract }
    { 35  ;   ;Table No.           ;Integer       ;OnValidate=BEGIN
                                                                ClearValueFilter;
                                                              END;

                                                   CaptionML=ENU=Table No. }
    { 40  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  Class=CONST(Normal),
                                                                                  Type=FILTER(<>BLOB));
                                                   OnValidate=BEGIN
                                                                ClearValueFilter;
                                                                CALCFIELDS("Field Name", "Field Caption");
                                                              END;

                                                   OnLookup=VAR
                                                              Field@1100525000 : Record 2000000041;
                                                            BEGIN
                                                              Field.SETRANGE(TableNo, "Table No.");
                                                              Field.SETRANGE(Class, Field.Class::Normal);
                                                              Field.SETFILTER(Type, '<>%1', Field.Type::BLOB);
                                                              IF Field.GET("Table No.", "Field No.") THEN;
                                                              IF PAGE.RUNMODAL(PAGE::"Table Fields", Field) = ACTION::LookupOK THEN
                                                                VALIDATE("Field No.", Field."No.");
                                                            END;

                                                   CaptionML=ENU=Field No.;
                                                   BlankZero=Yes }
    { 50  ;   ;Specification Code  ;Code10        ;TableRelation=IF (Table Type=CONST(Object Specification)) "Base Object Specification Line".Code;
                                                   OnValidate=BEGIN
                                                                ClearValueFilter;
                                                              END;

                                                   CaptionML=ENU=Specification Code }
    { 60  ;   ;Specification Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Base Object Specification Line".Description WHERE (Code=FIELD(Specification Code)));
                                                   CaptionML=ENU=Specification Description;
                                                   Editable=No }
    { 70  ;   ;Specification Field Type;Option    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Base Object Specification Line"."Field Type" WHERE (Code=FIELD(Specification Code)));
                                                   CaptionML=ENU=Specification Field Type;
                                                   OptionCaptionML=ENU="Date,Value,Text,Option, ";
                                                   OptionString=[Date,Value,Text,Option, ];
                                                   Editable=No }
    { 80  ;   ;Specification Unit Of Measure;Code10;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Base Object Specification Line"."Unit Of Measure" WHERE (Code=FIELD(Specification Code)));
                                                   TableRelation="Unit of Measure";
                                                   CaptionML=ENU=Specification Unit Of Measure;
                                                   Editable=No }
    { 90  ;   ;Value Filter        ;Text250       ;OnValidate=BEGIN
                                                                EvaluateFilter();
                                                              END;

                                                   OnLookup=VAR
                                                              BaseObjectSpecificationOpt@1210190000 : Record 11071780;
                                                            BEGIN
                                                              CALCFIELDS("Specification Field Type");
                                                              IF "Specification Field Type" = "Specification Field Type"::Option THEN BEGIN
                                                                BaseObjectSpecificationOpt.SETRANGE("Base Object Spec. Line Code", "Specification Code");
                                                                IF PAGE.RUNMODAL(0, BaseObjectSpecificationOpt) = ACTION::LookupOK THEN
                                                                  VALIDATE("Value Filter", BaseObjectSpecificationOpt.Description);
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Value Filter }
    { 100 ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table No.),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Name;
                                                   Editable=No }
    { 110 ;   ;Field Caption       ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Caption;
                                                   Editable=No }
    { 120 ;   ;Field Type          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Type Name" WHERE (TableNo=FIELD(Table No.),
                                                                                               No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Type;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Filter Code,Line No.                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1210190001 : TextConst 'ENU=t';
      Text001@1210190000 : TextConst 'ENU=Option Value %1 not found';

    PROCEDURE EvaluateFilter@1210190000();
    VAR
      ObjectSpecificationLine@1210190000 : Record 11071781;
      ServiceObject@1210190003 : Record 11071691;
      StandardObject@1210190004 : Record 11020334;
      ServiceLocation@1100528600 : Record 11012801;
      ServiceContract@1100528601 : Record 11012812;
      TableRef@1210190001 : RecordRef;
      FieldRef@1210190002 : FieldRef;
    BEGIN
      CASE "Table Type" OF
        "Table Type"::"Object Specification":
          BEGIN
            CALCFIELDS("Specification Field Type");
            ObjectSpecificationLine.SETRANGE(Code, "Specification Code");
            CASE "Specification Field Type" OF
              "Specification Field Type"::Date:
                BEGIN
                  ConvertDateFilter("Value Filter");
                  ObjectSpecificationLine.SETFILTER("Value (Date)", "Value Filter");
                  "Value Filter" := ObjectSpecificationLine.GETFILTER("Value (Date)");
                END;
              "Specification Field Type"::Value:
                BEGIN
                  ObjectSpecificationLine.SETFILTER("Value (Value)", "Value Filter");
                  "Value Filter" := ObjectSpecificationLine.GETFILTER("Value (Value)");
                END;
              "Specification Field Type"::Option:;
            ELSE
              ObjectSpecificationLine.SETFILTER(Value, "Value Filter");
            END;
          END;
        "Table Type"::"Service Object",
        "Table Type"::"Standard Object",
        "Table Type"::"Service Location",
        "Table Type"::"Service Contract":
          BEGIN
            TableRef.OPEN("Table No.");
      //      case "Table Type" of
      //        "Table Type"::"Service Object":
      //          TableRef.GETTABLE(ServiceObject);
      //        "Table Type"::"Standard Object":
      //          TableRef.GETTABLE(StandardObject);
      //        "Table Type"::"Service Location":
      //          TableRef.gettable(ServiceLocation);
      //        "Table Type"::"Service Contract":
      //          TableRef.gettable(ServiceContract);
      //      end;
            FieldRef := TableRef.FIELD("Field No.");
            IF FORMAT(FieldRef.TYPE) = 'Date' THEN
              ConvertDateFilter("Value Filter");
            FieldRef.SETFILTER("Value Filter");
            "Value Filter" := FieldRef.GETFILTER;
          END;
      END;
    END;

    PROCEDURE ClearValueFilter@1210190001();
    BEGIN
      IF ("Table Type" <> xRec."Table Type") OR
        ("Table No." <> xRec."Table No.") OR
        ("Field No." <> xRec."Field No.") OR
        ("Specification Code" <> xRec."Specification Code") THEN
        "Value Filter" := '';
    END;

    PROCEDURE CheckOptionValue@1100528604(ICheckValue@1100528601 : Text[80]) : Text[80];
    VAR
      BaseObjectSpecificationOpt@1100528600 : Record 11071780;
    BEGIN
      BaseObjectSpecificationOpt.SETRANGE("Base Object Spec. Line Code", "Specification Code");
      BaseObjectSpecificationOpt.SETRANGE(Description, ICheckValue);
      IF BaseObjectSpecificationOpt.FINDFIRST THEN
        EXIT(BaseObjectSpecificationOpt.Description);
      BaseObjectSpecificationOpt.SETFILTER(Description, '@' + ICheckValue + '*');
      IF BaseObjectSpecificationOpt.COUNT = 1 THEN BEGIN
        BaseObjectSpecificationOpt.FINDFIRST;
        EXIT(BaseObjectSpecificationOpt.Description);
      END;
      ERROR(Text001, ICheckValue);
    END;

    PROCEDURE ConvertDateFilter@1210190003(VAR OValueFilter@1210190000 : Text[250]);
    VAR
      Pos@1210190001 : Integer;
    BEGIN
      Pos := STRPOS(LOWERCASE(OValueFilter), Text000);
      IF Pos <> 0 THEN BEGIN
        OValueFilter := DELSTR(OValueFilter, Pos, STRLEN(Text000));
        OValueFilter := INSSTR(OValueFilter, FORMAT(TODAY), Pos);
      END;
    END;

    BEGIN
    END.
  }
}

