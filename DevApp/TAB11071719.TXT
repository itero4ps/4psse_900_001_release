OBJECT Table 11071719 Service Location Structure
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               lvStuctureLineRec@1100485000 : Record 11071720;
             BEGIN
               lvStuctureLineRec.SETRANGE("Service Location No.", "Service Location No.");
               lvStuctureLineRec.SETRANGE("Structure No.", "No.");
               lvStuctureLineRec.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Service Location Structure;
    LookupPageID=Page11125197;
    DrillDownPageID=Page11125197;
  }
  FIELDS
  {
    { 10  ;   ;Service Location No.;Code20        ;TableRelation="Service Location".No.;
                                                   CaptionML=ENU=Service Location }
    { 30  ;   ;No.                 ;Integer       ;CaptionML=ENU=No.;
                                                   MinValue=1;
                                                   MaxValue=5;
                                                   NotBlank=Yes }
    { 40  ;   ;Description         ;Text80        ;CaptionML=ENU=Description;
                                                   NotBlank=Yes }
    { 50  ;   ;Available for 4PS Mobile;Boolean   ;OnValidate=VAR
                                                                ServiceLocationStructure@1210190000 : Record 11071719;
                                                              BEGIN
                                                                IF (xRec."Available for 4PS Mobile" <> "Available for 4PS Mobile") AND "Available for 4PS Mobile" THEN BEGIN
                                                                  ServiceLocationStructure.SETRANGE("Service Location No.", "Service Location No.");
                                                                  ServiceLocationStructure.SETFILTER("No.", '<>%1', "No.");
                                                                  ServiceLocationStructure.SETRANGE("Available for 4PS Mobile", TRUE);
                                                                  IF ServiceLocationStructure.FINDFIRST THEN
                                                                    IF CONFIRM(Text000, TRUE, ServiceLocationStructure."No.") THEN
                                                                      ServiceLocationStructure.MODIFYALL("Available for 4PS Mobile", FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Available for 4PS Mobile }
    { 60  ;   ;Default for LTMP    ;Boolean       ;OnValidate=VAR
                                                                ServiceLocationStructure@1100528600 : Record 11071719;
                                                                ServiceLocation@1100528601 : Record 11012801;
                                                              BEGIN
                                                                IF "Default for LTMP" THEN BEGIN
                                                                  ServiceLocationStructure.SETRANGE("Service Location No.", "Service Location No.");
                                                                  ServiceLocationStructure.SETFILTER("No.", '<>%1', "No.");
                                                                  ServiceLocationStructure.SETRANGE("Default for LTMP", TRUE);
                                                                  IF ServiceLocationStructure.FINDFIRST THEN BEGIN
                                                                    IF NOT CONFIRM(Text001, TRUE, ServiceLocationStructure."No.") THEN
                                                                      ERROR('');
                                                                    ServiceLocationStructure."Default for LTMP" := FALSE;
                                                                    ServiceLocationStructure.MODIFY;
                                                                  END;
                                                                END ELSE BEGIN
                                                                  IF ServiceLocation.GET("Service Location No.") THEN
                                                                    IF ServiceLocation."LT. Maint. Plan Location" THEN
                                                                      MESSAGE(Text002, "Service Location No.");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Default for LTMP }
  }
  KEYS
  {
    {    ;Service Location No.,No.                ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1210190000 : TextConst 'ENU=Location Structure No. %1 already selected for 4PS Mobile, continue?';
      Text001@1100528600 : TextConst 'ENU=Location Structure No. %1 already selected for LTMP, continue?';
      Text002@1100528601 : TextConst 'ENU=Service Location %1 is used for LTMP. A default Location Structure is necessary.';
      Text003@1100528602 : TextConst 'ENU=Select a Location Structure';

    PROCEDURE GetStructureByMenu@1100528600(IServiceLocationNo@1100528600 : Code[20];VAR OServiceLocationStructure@1100528601 : Record 11071719) : Boolean;
    VAR
      NoOfStructures@1100528602 : Integer;
      MenuString@1100528603 : Text;
    BEGIN
      OServiceLocationStructure.RESET;
      OServiceLocationStructure.SETRANGE("Service Location No.", IServiceLocationNo);
      NoOfStructures := OServiceLocationStructure.COUNT;
      CASE TRUE OF
        NoOfStructures = 0:
          EXIT(FALSE);
        NoOfStructures = 1:
          OServiceLocationStructure.FINDFIRST;
        NoOfStructures > 1:
          BEGIN
            IF OServiceLocationStructure.FINDSET THEN
              REPEAT
                IF MenuString <> '' THEN
                  MenuString := MenuString + ',';
                MenuString := MenuString + STRSUBSTNO('%1 - %2', OServiceLocationStructure."No.", OServiceLocationStructure.Description);
              UNTIL OServiceLocationStructure.NEXT = 0;
            OServiceLocationStructure.FINDFIRST;
            OServiceLocationStructure.NEXT(STRMENU(MenuString, 1, Text003) -1);
          END;
      END;
      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

