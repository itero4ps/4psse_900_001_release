OBJECT Table 11071782 Service Order Obj. Spec. Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               TESTFIELD("Processed Date", 0D);
             END;

    CaptionML=ENU=Service Order Object Specification Line;
    LookupPageID=Page11126088;
    DrillDownPageID=Page11126088;
  }
  FIELDS
  {
    { 5   ;   ;Service Order No.   ;Code20        ;TableRelation="Service Order";
                                                   CaptionML=ENU=Service Order No.;
                                                   Editable=No }
    { 6   ;   ;Service Object No.  ;Code20        ;TableRelation=Service-Object.No.;
                                                   CaptionML=ENU=Service Object No.;
                                                   Editable=No }
    { 10  ;   ;Specification Type  ;Option        ;CaptionML=ENU=Specification Type;
                                                   OptionCaptionML=ENU=Standard Object,Object,Object Line;
                                                   OptionString=Standard Object,Object,Object Line;
                                                   Editable=No }
    { 20  ;   ;Code                ;Code20        ;TableRelation=IF (Specification Type=CONST(Standard Object)) "Standard Object"
                                                                 ELSE IF (Specification Type=CONST(Object)) Service-Object;
                                                   CaptionML=ENU=Code;
                                                   Editable=No }
    { 25  ;   ;Object Line No.     ;Integer       ;CaptionML=ENU=Object Line No.;
                                                   Editable=No }
    { 30  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No.;
                                                   Editable=No }
    { 40  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF (xRec.Type <> Type) AND (Type = Type::Group) THEN BEGIN
                                                                  "Field Type" := "Field Type"::" ";
                                                                  "Unit Of Measure" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Group,Line;
                                                   OptionString=Group,Line }
    { 50  ;   ;Specification Code  ;Code10        ;TableRelation=IF (Type=CONST(Group)) "Object Specification Group"
                                                                 ELSE IF (Type=CONST(Line),
                                                                          Specification Type=CONST(Standard Object)) "Base Object Specification Line" WHERE (Level=CONST(Standard Object))
                                                                          ELSE IF (Type=CONST(Line),
                                                                                   Specification Type=CONST(Object)) "Base Object Specification Line" WHERE (Level=CONST(Object));
                                                   OnValidate=VAR
                                                                ObjectSpecificationGroup@1100528601 : Record 11071778;
                                                                BaseObjectSpecificationLine@1100528600 : Record 11071779;
                                                              BEGIN
                                                                IF xRec."Specification Code" <> "Specification Code" THEN BEGIN
                                                                  CASE Type OF
                                                                    Type::Group:
                                                                      BEGIN
                                                                        ObjectSpecificationGroup.GET("Specification Code");
                                                                        VALIDATE(Description, ObjectSpecificationGroup.Description);
                                                                      END;
                                                                    Type::Line:
                                                                      BEGIN
                                                                        BaseObjectSpecificationLine.GET("Specification Code");
                                                                        VALIDATE(Description, BaseObjectSpecificationLine.Description);
                                                                        VALIDATE("Field Type", BaseObjectSpecificationLine."Field Type");
                                                                        VALIDATE("Unit Of Measure", BaseObjectSpecificationLine."Unit Of Measure");
                                                                        "Old Value" := '';
                                                                        "New Value" := '';
                                                                      END;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Specification Code }
    { 60  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 70  ;   ;Field Type          ;Option        ;OnValidate=BEGIN
                                                                IF "Field Type" = "Field Type"::" " THEN
                                                                  TESTFIELD(Type, Type::Group);
                                                              END;

                                                   CaptionML=ENU=Field Type;
                                                   OptionCaptionML=ENU="Date,Value,Text,Option, ";
                                                   OptionString=[Date,Value,Text,Option, ];
                                                   Editable=No }
    { 80  ;   ;Unit Of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=ENU=Unit Of Measure;
                                                   Editable=No }
    { 90  ;   ;Old Value           ;Text80        ;OnValidate=BEGIN
                                                                IF BORG THEN
                                                                  BORGManagement.TestBORGUser;
                                                                EvaluateValue(FALSE);
                                                                IF BORG THEN
                                                                  "Old Value" := BORGManagement.EncodeValue("Old Value");
                                                              END;

                                                   OnLookup=VAR
                                                              BaseObjectSpecificationOpt@1100528600 : Record 11071780;
                                                            BEGIN
                                                              IF "Field Type" = "Field Type"::Option THEN BEGIN
                                                                BaseObjectSpecificationOpt.SETRANGE("Base Object Spec. Line Code", "Specification Code");
                                                                IF PAGE.RUNMODAL(0, BaseObjectSpecificationOpt) = ACTION::LookupOK THEN
                                                                  VALIDATE("Old Value", BaseObjectSpecificationOpt.Description);
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Old Value }
    { 95  ;   ;New Value           ;Text80        ;OnValidate=BEGIN
                                                                IF BORG THEN
                                                                  BORGManagement.TestBORGUser;
                                                                EvaluateValue(TRUE);
                                                                IF BORG THEN
                                                                  "New Value" := BORGManagement.EncodeValue("New Value");
                                                              END;

                                                   OnLookup=VAR
                                                              BaseObjectSpecificationOpt@1100528600 : Record 11071780;
                                                            BEGIN
                                                              IF "Field Type" = "Field Type"::Option THEN BEGIN
                                                                BaseObjectSpecificationOpt.SETRANGE("Base Object Spec. Line Code", "Specification Code");
                                                                IF PAGE.RUNMODAL(0, BaseObjectSpecificationOpt) = ACTION::LookupOK THEN
                                                                  VALIDATE("New Value", BaseObjectSpecificationOpt.Description);
                                                              END;
                                                            END;

                                                   CaptionML=ENU=New Value }
    { 100 ;   ;Specification Group Code;Code10    ;TableRelation="Service Inspection Group";
                                                   CaptionML=ENU=Specification Group Code;
                                                   Editable=No }
    { 110 ;   ;Specification Line No.;Integer     ;CaptionML=ENU=Specification Line No.;
                                                   Editable=No }
    { 120 ;   ;Editable by Field Service;Boolean  ;CaptionML=ENU=Editable by Field Service;
                                                   Editable=No }
    { 130 ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=" ,Received,Approved,Disapproved";
                                                   OptionString=[ ,Received,Approved,Disapproved] }
    { 140 ;   ;Processed Date      ;Date          ;CaptionML=ENU=Processed Date;
                                                   Editable=No }
    { 150 ;   ;Service Object Description;Text50  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Description WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=ENU=Service Object Description;
                                                   Editable=No }
    { 155 ;   ;Service Object Description 2;Text50;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Description 2" WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=ENU=Service Object Description 2;
                                                   Editable=No }
    { 160 ;   ;BORG                ;Boolean       ;CaptionML=ENU=BORG;
                                                   Editable=No }
    { 170 ;   ;Publish on Service Portal;Boolean  ;CaptionML=ENU=Publish on Service Portal;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Service Order No.,Service Object No.,Specification Type,Code,Object Line No.,Line No.;
                                                   Clustered=Yes }
    {    ;Service Order No.,Service Object No.,Specification Group Code,Line No. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100528601 : TextConst 'ENU=Option Value %1 not found';
      Text002@1100528600 : TextConst 'ENU=t';
      BORGManagement@1100528602 : Codeunit 11012859;
      Text003@1100528603 : TextConst 'ENU=Processed Specification Lines cannot change Status';

    PROCEDURE GetObjSpecificationGroupCode@1100528601() : Code[10];
    VAR
      ObjectSpecificationLine@1100528600 : Record 11071781;
    BEGIN
      ObjectSpecificationLine.SETRANGE("Specification Type", "Specification Type");
      ObjectSpecificationLine.SETRANGE(Code, Code);
      ObjectSpecificationLine.SETFILTER("Line No.", '<%1', "Line No.");
      ObjectSpecificationLine.SETRANGE(Type, Type::Group);
      IF ObjectSpecificationLine.FINDLAST THEN
        EXIT(ObjectSpecificationLine."Specification Code");
    END;

    PROCEDURE EvaluateValue@1100528603(NewValue@1100528602 : Boolean);
    VAR
      DummyDate@1100528600 : Date;
      DummyDecimal@1100528601 : Decimal;
      CheckValue@1100528603 : Text[80];
    BEGIN
      IF NewValue THEN
        CheckValue := "New Value"
      ELSE
        CheckValue := "Old Value";

      IF CheckValue = '' THEN EXIT;
      CASE "Field Type" OF
        "Field Type"::Date:
          BEGIN
            IF LOWERCASE("New Value") = Text002 THEN
              CheckValue := FORMAT(TODAY)
            ELSE BEGIN
              EVALUATE(DummyDate, CheckValue);
              CheckValue := FORMAT(DummyDate);
            END;
          END;
        "Field Type"::Value:
          BEGIN
            EVALUATE(DummyDecimal, CheckValue);
            CheckValue := FORMAT(DummyDecimal, 0, 1);
          END;
        "Field Type"::Text:;
        "Field Type"::Option:
          CheckValue := CheckOptionValue(CheckValue);
      END;

      IF NewValue THEN
        "New Value" := CheckValue
      ELSE
        "Old Value" := CheckValue;
    END;

    PROCEDURE CheckOptionValue@1100528604(ICheckValue@1100528601 : Text[80]) : Text[80];
    VAR
      BaseObjectSpecificationOpt@1100528600 : Record 11071780;
    BEGIN
      BaseObjectSpecificationOpt.SETRANGE("Base Object Spec. Line Code", "Specification Code");
      BaseObjectSpecificationOpt.SETRANGE(Description, ICheckValue);
      IF BaseObjectSpecificationOpt.FINDFIRST THEN
        EXIT(BaseObjectSpecificationOpt.Description);
      BaseObjectSpecificationOpt.SETFILTER(Description, '@' + ICheckValue + '*');
      IF BaseObjectSpecificationOpt.COUNT = 1 THEN BEGIN
        BaseObjectSpecificationOpt.FINDFIRST;
        EXIT(BaseObjectSpecificationOpt.Description);
      END;
      ERROR(Text001, ICheckValue);
    END;

    PROCEDURE GetOptionDescriptionFromCode@1210190000(OptionCode@1100528601 : Code[10]) : Text[80];
    VAR
      BaseObjectSpecificationOpt@1100528600 : Record 11071780;
    BEGIN
      BaseObjectSpecificationOpt.SETRANGE("Base Object Spec. Line Code", "Specification Code");
      BaseObjectSpecificationOpt.SETRANGE(Code, OptionCode);
      IF BaseObjectSpecificationOpt.FINDFIRST THEN
        EXIT(BaseObjectSpecificationOpt.Description)
      ELSE
        EXIT(OptionCode);
    END;

    PROCEDURE HandleSpecificationCodeLookup@1100528600(EditMode@1100528600 : Boolean);
    VAR
      ObjectSpecificationGroup@1100528602 : Record 11071778;
      BaseObjectSpecificationLine@1100528601 : Record 11071779;
    BEGIN
      CASE Type OF
        Type::Group:
          BEGIN
            IF ObjectSpecificationGroup.GET("Specification Code") THEN;
            IF PAGE.RUNMODAL(0, ObjectSpecificationGroup) = ACTION::LookupOK THEN
              IF EditMode THEN
                VALIDATE("Specification Code", ObjectSpecificationGroup.Code);
          END;
        Type::Line:
          BEGIN
            IF "Specification Group Code" <> '' THEN
              BaseObjectSpecificationLine.SETFILTER("Specification Group Code", '%1|%2', '', "Specification Group Code");
            IF "Specification Type" = "Specification Type"::"Standard Object" THEN
              BaseObjectSpecificationLine.SETRANGE(Level, BaseObjectSpecificationLine.Level::"Standard Object")
            ELSE
              BaseObjectSpecificationLine.SETRANGE(Level, BaseObjectSpecificationLine.Level::Object);
            IF BaseObjectSpecificationLine.GET("Specification Code") THEN;
            IF PAGE.RUNMODAL(0, BaseObjectSpecificationLine) = ACTION::LookupOK THEN
              IF EditMode THEN
                VALIDATE("Specification Code", BaseObjectSpecificationLine.Code);
          END;
      END;
    END;

    PROCEDURE GetValue@1100528602(INewValue@1100528600 : Boolean;VAR ODecoded@1100528601 : Boolean) : Text[80];
    VAR
      RetValue@1100528602 : Text[80];
    BEGIN
      ODecoded := TRUE;
      IF INewValue THEN
        RetValue := "New Value"
      ELSE
        RetValue := "Old Value";
      IF NOT BORG THEN
        EXIT(RetValue);
      EXIT(BORGManagement.DecodeValue(RetValue, FALSE, ODecoded));
    END;

    PROCEDURE SetStatusCollectively@1100528605(VAR IOServiceOrderObjSpecLine@1100528601 : Record 11071782;INewStatus@1100528600 : ' ,Received,Approved,Disapproved');
    VAR
      TotalNoOfSpecLines@1100528602 : Integer;
    BEGIN
      TotalNoOfSpecLines := IOServiceOrderObjSpecLine.COUNT;
      IOServiceOrderObjSpecLine.SETRANGE("Processed Date", 0D);
      IF TotalNoOfSpecLines <> IOServiceOrderObjSpecLine.COUNT THEN
        ERROR(Text003);
      IOServiceOrderObjSpecLine.SETRANGE(Type, IOServiceOrderObjSpecLine.Type::Line);
      IF IOServiceOrderObjSpecLine.FINDSET THEN
        REPEAT
          IOServiceOrderObjSpecLine.VALIDATE(Status, INewStatus);
          IOServiceOrderObjSpecLine.MODIFY(TRUE);
        UNTIL IOServiceOrderObjSpecLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

