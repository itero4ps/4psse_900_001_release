OBJECT Table 11071804 Service Survey Location
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceSetup@1100528600 : Record 11012800;
             BEGIN
               IF "Location Structure Format" = '' THEN BEGIN
                 ServiceSetup.GET;
                 "Location Structure Format" := ServiceSetup."Def. Location Structure Format";
               END;
             END;

    OnDelete=VAR
               ServiceEstLocStructure@1100528600 : Record 11071805;
               ServSurvLocationObject@1100528601 : Record 11071813;
             BEGIN
               ServiceEstLocStructure.SETRANGE("Survey No.", "Survey No.");
               ServiceEstLocStructure.SETRANGE("Location Code", "Location Code");
               ServiceEstLocStructure.DELETEALL(TRUE);

               ServSurvLocationObject.SETRANGE("Survey No.", "Survey No.");
               ServSurvLocationObject.SETRANGE("Location Code", "Location Code");
               ServSurvLocationObject.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Service Site Survey Location;
    LookupPageID=Page11126254;
    DrillDownPageID=Page11126254;
  }
  FIELDS
  {
    { 10  ;   ;Survey No.          ;Code20        ;TableRelation="Service Survey".No.;
                                                   CaptionML=ENU=Survey No.;
                                                   NotBlank=Yes }
    { 20  ;   ;Location Code       ;Code10        ;CaptionML=ENU=Location Code;
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 40  ;   ;Description 2       ;Text50        ;CaptionML=ENU=Description 2 }
    { 50  ;   ;Address             ;Text50        ;OnValidate=BEGIN
                                                                VALIDATE("Post Code");
                                                              END;

                                                   CaptionML=ENU=Address;
                                                   Editable=Yes }
    { 60  ;   ;Address 2           ;Text50        ;CaptionML=ENU=Address 2;
                                                   Editable=Yes }
    { 70  ;   ;Post Code           ;Code20        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code"
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code" WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(City, "Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                                NormalizePostCodeHouseNo();
                                                                DetermineServiceLocation();
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Post Code;
                                                   Editable=Yes }
    { 75  ;   ;County              ;Text30        ;CaptionML=ENU=County;
                                                   Editable=Yes }
    { 80  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=ENU=Country/Region Code }
    { 90  ;   ;City                ;Text30        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code".City
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=City;
                                                   Editable=Yes }
    { 110 ;   ;Phone No.           ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Phone No. }
    { 120 ;   ;Fax No.             ;Text30        ;CaptionML=ENU=Fax No. }
    { 130 ;   ;E-Mail              ;Text80        ;CaptionML=ENU=E-Mail }
    { 140 ;   ;Mobile Phone No.    ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Mobile Phone No. }
    { 150 ;   ;Customer Feature    ;Text30        ;CaptionML=ENU=Customer Feature }
    { 160 ;   ;Complex Code        ;Code10        ;TableRelation=Complex;
                                                   CaptionML=ENU=Complex Code }
    { 170 ;   ;Location Structure Format;Code20   ;OnValidate=VAR
                                                                ServiceSurveyStructureLine@1100528602 : Record 11071806;
                                                                pos@1100528600 : Integer;
                                                                Level@1100528601 : Integer;
                                                              BEGIN
                                                                IF "Location Structure Format" = '' THEN
                                                                  EXIT;

                                                                Level := 1;
                                                                REPEAT
                                                                  pos += 1;
                                                                  IF COPYSTR("Location Structure Format", pos, 1) = '.' THEN
                                                                    Level += 1;
                                                                UNTIL pos = STRLEN("Location Structure Format");
                                                                IF Level > 3 THEN
                                                                  ERROR(Text000);

                                                                ServiceSurveyStructureLine.SETRANGE("Survey No.", "Survey No.");
                                                                ServiceSurveyStructureLine.SETRANGE("Location Code", "Location Code");
                                                                IF NOT ServiceSurveyStructureLine.ISEMPTY THEN
                                                                  ERROR(Text001, ServiceSurveyStructureLine.TABLECAPTION);
                                                              END;

                                                   CaptionML=ENU=Location Structure Format }
    { 190 ;   ;Include in Contract ;Boolean       ;InitValue=Yes;
                                                   OnValidate=VAR
                                                                ServSurvLocationObject@1100528600 : Record 11071813;
                                                              BEGIN
                                                                ServSurvLocationObject.SETRANGE("Survey No.", "Survey No.");
                                                                ServSurvLocationObject.SETRANGE("Location Code", "Location Code");
                                                                ServSurvLocationObject.MODIFYALL("Include in Contract", "Include in Contract", TRUE);
                                                              END;

                                                   CaptionML=ENU=Include in Contract }
    { 200 ;   ;Service Location No.;Code20        ;TableRelation="Service Location";
                                                   OnValidate=VAR
                                                                ServiceLocation@1100528600 : Record 11012801;
                                                              BEGIN
                                                                TESTFIELD("Service Location Created", FALSE);
                                                                IF CurrFieldNo = FIELDNO("Service Location No.") THEN BEGIN
                                                                  IF ServiceLocation.GET("Service Location No.") THEN
                                                                    IF CONFIRM(Text002) THEN BEGIN
                                                                      Description := ServiceLocation.Description;
                                                                      "Description 2" := ServiceLocation."Description 2";
                                                                      Address := ServiceLocation.Address;
                                                                      "Address 2" := ServiceLocation."Address 2";
                                                                      "Post Code" := ServiceLocation."Post Code";
                                                                      "Country/Region Code" := ServiceLocation."Country/Region Code";
                                                                      City := ServiceLocation.City;
                                                                      "Phone No." := ServiceLocation."Phone No.";
                                                                      "Fax No." := ServiceLocation."Fax No.";
                                                                      "E-Mail" := ServiceLocation."E-Mail";
                                                                      "Mobile Phone No." := ServiceLocation."Mobile Phone No.";
                                                                      "Customer Feature" := ServiceLocation."Customer Feature";
                                                                      "Complex Code" := ServiceLocation."Complex Code";
                                                                      "House No." := ServiceLocation."House No.";
                                                                      "House No. Ext." := ServiceLocation."House No. Ext.";
                                                                    END;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceLocation@1100528600 : Record 11012801;
                                                            BEGIN
                                                              ServiceLocation.SETCURRENTKEY("Post Code", "House No.");
                                                              ServiceLocation.SETRANGE("Post Code", "Post Code");
                                                              ServiceLocation.SETRANGE("House No.", "House No.");
                                                              ServiceLocation.SETRANGE("House No. Ext.", "House No. Ext.");
                                                              IF "Service Location No." <> '' THEN
                                                                ServiceLocation.GET("Service Location No.");
                                                              IF PAGE.RUNMODAL(0, ServiceLocation) = ACTION::LookupOK THEN
                                                                VALIDATE("Service Location No.", ServiceLocation."No.");
                                                            END;

                                                   CaptionML=ENU=Service Location No. }
    { 210 ;   ;Service Location Created;Boolean   ;CaptionML=ENU=Service Location Created;
                                                   Editable=No }
    { 220 ;   ;House No.           ;Code20        ;CaptionML=ENU=House No.;
                                                   Editable=No }
    { 230 ;   ;House No. Ext.      ;Code10        ;CaptionML=ENU=House No. Extension;
                                                   Editable=No }
    { 240 ;   ;Total Cost Amount   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Serv. Surv. Location Object"."Cost Amount" WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                      Location Code=FIELD(Location Code)));
                                                   CaptionML=ENU=Total Cost Amount;
                                                   Editable=No }
    { 250 ;   ;Total Sales Amount  ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Serv. Surv. Location Object"."Sales Amount" WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                       Location Code=FIELD(Location Code)));
                                                   CaptionML=ENU=Total Sales Amount;
                                                   Editable=No }
    { 260 ;   ;Total Hours         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Serv. Surv. Location Object"."Total Hours" WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                      Location Code=FIELD(Location Code)));
                                                   CaptionML=ENU=Total Hours;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Survey No.,Location Code                ;Clustered=Yes }
    {    ;Survey No.,Post Code                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PostCode@1210190000 : Record 225;
      Text000@1100528600 : TextConst 'ENU=Only 3 levels allowed';
      Text001@1100528601 : TextConst 'ENU=%1 already exists';
      Text002@1100528602 : TextConst 'ENU=Copy Address of Service Location?';

    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    VAR
      DimMgt@1100525000 : Codeunit 408;
      DimValRec@1100525001 : Record 349;
    BEGIN
      IF ShortcutDimCode = '' THEN EXIT;
      DimMgt.ValidateDimValueCode(FieldNumber, ShortcutDimCode);
      DimMgt.GetDimValueRec(FieldNumber, ShortcutDimCode, DimValRec, TRUE,'');
      IF NOT DimMgt.CheckDimValue(DimValRec."Dimension Code", ShortcutDimCode) THEN
        ERROR(DimMgt.GetDimErr);
    END;

    PROCEDURE NormalizePostCodeHouseNo@1100526200();
    VAR
      ObjectImpWorkSheetMgt@1100526200 : Codeunit 11012839;
      PostCode@1100526201 : Code[20];
    BEGIN
      IF ObjectImpWorkSheetMgt.NormalizePostCode("Post Code", "Country/Region Code", PostCode) THEN
        "Post Code" := PostCode;
      "House No." := ObjectImpWorkSheetMgt.DetermineHouseNo(Address, "House No. Ext.");
    END;

    PROCEDURE GetMarginPerc@1100528603() MarginPerc : Decimal;
    BEGIN
      CALCFIELDS("Total Cost Amount", "Total Sales Amount");
      IF "Total Cost Amount" <> 0 THEN
        MarginPerc := ("Total Sales Amount" - "Total Cost Amount") / "Total Cost Amount" * 100
      ELSE
        MarginPerc := 0;
    END;

    PROCEDURE GetNextLocationCode@1100528600() : Code[10];
    VAR
      ServiceSurveyLocation@1100528600 : Record 11071804;
    BEGIN
      ServiceSurveyLocation.SETRANGE("Survey No.", "Survey No.");
      IF NOT ServiceSurveyLocation.FINDLAST THEN
        EXIT('001')
      ELSE
        EXIT(INCSTR(ServiceSurveyLocation."Location Code"));
    END;

    PROCEDURE DetermineServiceLocation@1100528601();
    VAR
      ServiceLocation@1100528600 : Record 11012801;
    BEGIN
      "Service Location No." := '';
      ServiceLocation.SETCURRENTKEY("Post Code");
      ServiceLocation.SETRANGE("Post Code", "Post Code");
      ServiceLocation.SETRANGE("House No.", "House No.");
      ServiceLocation.SETRANGE("House No. Ext.", "House No. Ext.");
      IF ServiceLocation.FINDFIRST THEN
        "Service Location No." := ServiceLocation."No.";
    END;

    PROCEDURE ShowDetails@1100528616();
    VAR
      ServSurvLocObjDetail@1100528600 : Record 11071807;
    BEGIN
      ServSurvLocObjDetail.SETRANGE("Survey No.", "Survey No.");
      ServSurvLocObjDetail.SETRANGE("Location Code", "Location Code");
      PAGE.RUNMODAL(PAGE::"Service Survey Line Details", ServSurvLocObjDetail);
    END;

    BEGIN
    END.
  }
}

