OBJECT Table 11071868 Sales Header Extension
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Sales Header Extension;
  }
  FIELDS
  {
    { 10  ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,Invoice Proposal;
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,Invoice Proposal }
    { 20  ;   ;Document No.        ;Code20        ;TableRelation="Sales Header".No. WHERE (Document Type=FIELD(Document Type));
                                                   CaptionML=ENU=Document No. }
    { 30  ;   ;E-Mail (Invoices)   ;Text80        ;OnValidate=VAR
                                                                RecRef@1100528701 : RecordRef;
                                                                FldRef@1100528700 : FieldRef;
                                                              BEGIN
                                                                RecRef.GETTABLE(Rec);
                                                                FldRef := RecRef.FIELD(FIELDNO("E-Mail (Invoices)"));
                                                                ContactDataValidation.ValidateEmail(FldRef, "E-Mail (Invoices)");

                                                                IF "E-Mail (Invoices)" = '' THEN BEGIN
                                                                  "Invoice per E-Mail" := FALSE;
                                                                  IF "Electronic Invoicing" = "Electronic Invoicing"::"E-Mail" THEN
                                                                    "Electronic Invoicing" := "Electronic Invoicing"::Print;
                                                                END;
                                                              END;

                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=ENU=E-Mail (Invoices) }
    { 40  ;   ;Invoice per E-Mail  ;Boolean       ;OnValidate=BEGIN
                                                                IF "Invoice per E-Mail" THEN
                                                                  TESTFIELD("E-Mail (Invoices)");
                                                              END;

                                                   CaptionML=ENU=Invoice per E-Mail;
                                                   Description=Obsolete }
    { 50  ;   ;E-Mail (Shipments)  ;Text80        ;OnValidate=VAR
                                                                RecRef@1100528701 : RecordRef;
                                                                FldRef@1100528700 : FieldRef;
                                                              BEGIN
                                                                RecRef.GETTABLE(Rec);
                                                                FldRef := RecRef.FIELD(FIELDNO("E-Mail (Shipments)"));
                                                                ContactDataValidation.ValidateEmail(FldRef, "E-Mail (Shipments)");

                                                                IF "E-Mail (Shipments)" = '' THEN
                                                                  "Shipment per E-Mail" := FALSE;
                                                              END;

                                                   CaptionML=ENU=E-Mail (Shipments) }
    { 60  ;   ;Shipment per E-Mail ;Boolean       ;OnValidate=BEGIN
                                                                IF "Shipment per E-Mail" THEN
                                                                  TESTFIELD("E-Mail (Shipments)");
                                                              END;

                                                   CaptionML=ENU=Shipment per E-Mail }
    { 70  ;   ;Electronic Invoicing;Option        ;OnValidate=BEGIN
                                                                IF "Electronic Invoicing" = "Electronic Invoicing"::"E-Mail" THEN
                                                                  TESTFIELD("E-Mail (Invoices)");
                                                              END;

                                                   CaptionML=ENU=Electronic Invoicing;
                                                   OptionCaptionML=ENU=Print,,E-Mail,,GLN (XML),Digipoort (XML),Svefaktura (XML),OIOUBL (DK),UBL (NO);
                                                   OptionString=Print,,E-Mail,,GLN (XML),Digipoort (XML),Svefaktura (XML),OIOUBL (DK),UBL (NO) }
    { 80  ;   ;E-Mail (Order Confirmations);Text80;OnValidate=VAR
                                                                RecRef@1100528701 : RecordRef;
                                                                FldRef@1100528700 : FieldRef;
                                                              BEGIN
                                                                RecRef.GETTABLE(Rec);
                                                                FldRef := RecRef.FIELD(FIELDNO("E-Mail (Order Confirmations)"));
                                                                ContactDataValidation.ValidateEmail(FldRef, "E-Mail (Order Confirmations)");

                                                                IF "E-Mail (Order Confirmations)" = '' THEN
                                                                  "Order Confirmation per E-Mail" := FALSE;
                                                              END;

                                                   CaptionML=ENU=E-Mail (Order Confirmations) }
    { 90  ;   ;Order Confirmation per E-Mail;Boolean;
                                                   OnValidate=BEGIN
                                                                IF "Order Confirmation per E-Mail" THEN
                                                                  TESTFIELD("E-Mail (Order Confirmations)");
                                                              END;

                                                   CaptionML=ENU=Order Confirmation per E-Mail }
    { 100 ;   ;E-Mail Order Confirm. Sent by;Code50;
                                                   TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              LoginManagement@1100528700 : Codeunit 418;
                                                            BEGIN
                                                              LoginManagement.LookupUserID("E-Mail Order Confirm. Sent by");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=ENU=E-Mail Order Confirm. Sent by;
                                                   Editable=No }
    { 110 ;   ;E-Mail Order Confirm. Sent on;Date ;CaptionML=ENU=E-Mail Order Confirm. Sent on;
                                                   Editable=No }
    { 120 ;   ;Combine E-Mail Attachments;Boolean ;CaptionML=ENU=Combine E-Mail Attachments }
    { 130 ;   ;Compress Dyn. Inv. Prop. Lines;Boolean;
                                                   CaptionML=ENU=Compress Dyn. Inv. Prop. Lines }
  }
  KEYS
  {
    {    ;Document Type,Document No.              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ContactDataValidation@1100528700 : Codeunit 11012275;

    PROCEDURE GetSalesHeadExtension@1100525001(DocumentType@1100528701 : Option;DocumentNo@1100528700 : Code[20]);
    BEGIN
      IF NOT GET(DocumentType, DocumentNo) THEN BEGIN
        INIT;
        "Document Type" := DocumentType;
        "Document No."  := DocumentNo;
      END;
    END;

    PROCEDURE UpdateSalesHeadExtension@1100525000(DocumentType@1100525002 : Option;DocumentNo@1100525000 : Code[20]);
    VAR
      SalesHeaderExtension@1100525001 : Record 11071868;
      SalesHeaderExtensionInit@1100525003 : Record 11071868;
      DeleteExt@1100525004 : Boolean;
    BEGIN
      SalesHeaderExtensionInit.INIT;
      SalesHeaderExtensionInit."Document Type" := DocumentType;
      SalesHeaderExtensionInit."Document No."  := DocumentNo;
      IF FORMAT(Rec) = FORMAT(SalesHeaderExtensionInit) THEN
        DeleteExt := TRUE
      ELSE
        DeleteExt := FALSE;

      IF NOT SalesHeaderExtension.GET(DocumentType, DocumentNo) THEN BEGIN
        IF DeleteExt THEN
          EXIT;
        SalesHeaderExtension.INIT;
        SalesHeaderExtension."Document Type" := DocumentType;
        SalesHeaderExtension."Document No."  := DocumentNo;
        SalesHeaderExtension.INSERT;
      END ELSE BEGIN
        IF DeleteExt THEN BEGIN
          SalesHeaderExtension.DELETE;
          EXIT;
        END;
      END;
      SalesHeaderExtension.TRANSFERFIELDS(Rec, FALSE);
      SalesHeaderExtension.MODIFY;
    END;

    PROCEDURE CopySalesHeadExtension@1100525002(FromDocumentType@1100525001 : Option;FromDocumentNo@1100525000 : Code[20];ToDocumentType@1100525003 : Option;ToDocumentNo@1100525002 : Code[20]);
    VAR
      FromSalesHeaderExtension@1100525004 : Record 11071868;
      ToSalesHeaderExtension@1100525005 : Record 11071868;
    BEGIN
      IF ToSalesHeaderExtension.GET(ToDocumentType, ToDocumentNo) THEN
        ToSalesHeaderExtension.DELETE;
      IF FromSalesHeaderExtension.GET(FromDocumentType, FromDocumentNo) THEN BEGIN
        ToSalesHeaderExtension."Document Type" := ToDocumentType;
        ToSalesHeaderExtension."Document No."  := ToDocumentNo;
        ToSalesHeaderExtension.TRANSFERFIELDS(FromSalesHeaderExtension, FALSE);
        ToSalesHeaderExtension.INSERT;
      END;
    END;

    BEGIN
    END.
  }
}

