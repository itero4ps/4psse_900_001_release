OBJECT Table 11071911 Sales Report Text Value
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Document Type,Text Line Type,Language Code,Text Line No.;
    OnDelete=VAR
               SalesReportTextTableLink@1100525000 : Record 11071913;
             BEGIN
               SalesReportTextTableLink.SETRANGE("Document Type", "Document Type");
               SalesReportTextTableLink.SETRANGE("Text Line Type", "Text Line Type");
               SalesReportTextTableLink.SETRANGE("Language Code", "Language Code");
               SalesReportTextTableLink.SETRANGE("Text Line No.", "Text Line No.");
               SalesReportTextTableLink.SETRANGE("Text Value Line No.", "Line No.");
               SalesReportTextTableLink.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Sales Report Text Value;
  }
  FIELDS
  {
    { 10  ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=,,Pro Forma Invoice,Pro Forma Credit Memo,,,,Posted Invoice,Posted Credit Memo;
                                                   OptionString=,,Pro Forma Invoice,Pro Forma Credit Memo,,,,Posted Invoice,Posted Credit Memo }
    { 40  ;   ;Text Line Type      ;Option        ;CaptionML=ENU=Text Line Type;
                                                   OptionCaptionML=ENU=Header,Before Detail,Detail,After Detail,Footer;
                                                   OptionString=Header,Before Detail,Detail,After Detail,Footer }
    { 50  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=ENU=Language Code }
    { 60  ;   ;Text Line No.       ;Integer       ;TableRelation="Std. Sales Report Text Line"."Line No." WHERE (Document Type=FIELD(Document Type),
                                                                                                                 Text Line Type=FIELD(Text Line Type),
                                                                                                                 Language Code=FIELD(Language Code));
                                                   CaptionML=ENU=Text Line No. }
    { 70  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 150 ;   ;Related Table No.   ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                CheckIfValueHasNoTableLinks;
                                                              END;

                                                   CaptionML=ENU=Related Table No. }
    { 155 ;   ;Related Table Caption;Text250      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Related Table No.)));
                                                   CaptionML=ENU=Related Table Caption;
                                                   Editable=No }
    { 160 ;   ;Related Table Field No.;Integer    ;TableRelation=Field.No. WHERE (TableNo=FIELD(Related Table No.));
                                                   OnLookup=VAR
                                                              SalesReportTextManagement@1100525000 : Codeunit 11012369;
                                                            BEGIN
                                                              VALIDATE(
                                                                "Related Table Field No.",
                                                                SalesReportTextManagement.LookupFieldNo("Related Table No.", "Related Table Field No."));
                                                            END;

                                                   CaptionML=ENU=Related Table Field No. }
    { 170 ;   ;Related Table Field Caption;Text80 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Related Table No.),
                                                                                                   No.=FIELD(Related Table Field No.)));
                                                   CaptionML=ENU=Related Table Field Caption;
                                                   Editable=No }
    { 180 ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 190 ;   ;Codeunit No.        ;Integer       ;TableRelation=Object.ID WHERE (Company Name=CONST(),
                                                                                  Type=CONST(Codeunit));
                                                   CaptionML=ENU=Codeunit No. }
    { 200 ;   ;Function Name       ;Text128       ;CaptionML=ENU=Function Name }
    { 205 ;   ;Function Parameters ;Text250       ;CaptionML=ENU=Function Parameters }
    { 207 ;   ;Parameter Separator ;Text10        ;CaptionML=ENU=Parameter Separator }
    { 210 ;   ;Format              ;Option        ;CaptionML=ENU=Format;
                                                   OptionCaptionML=ENU=" ,Short Date,Long Date,Amount,Price";
                                                   OptionString=[ ,Short Date,Long Date,Amount,Price] }
    { 220 ;   ;Multiple Lines      ;Boolean       ;OnValidate=BEGIN
                                                                IF "Multiple Lines" THEN
                                                                  CheckIfCurrentValueIsOnlyMultipleLinesValue;
                                                              END;

                                                   CaptionML=ENU=Multiple Lines }
    { 500 ;   ;Record ID           ;RecordID      ;CaptionML=ENU=Record ID;
                                                   Description=Only used in codeunit. }
    { 510 ;   ;Related Record ID   ;RecordID      ;CaptionML=ENU=Related Record ID;
                                                   Description=Only used in codeunit. }
    { 520 ;   ;Result              ;Text250       ;CaptionML=ENU=Result;
                                                   Description=Only used in codeunit. }
  }
  KEYS
  {
    {    ;Document Type,Text Line Type,Language Code,Text Line No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'ENU=WHERE (%1)';
      Text001@1100525001 : TextConst 'ENU="%1=FIELD(%2)"';
      Text002@1100525002 : TextConst 'ENU="%1=CONST(%2)"';
      Text003@1100525003 : TextConst 'ENU=cannot be changed because there are still %1s present';

    PROCEDURE GetTableNo@1100525001() TableNo : Integer;
    VAR
      SalesReportTextManagement@1100525000 : Codeunit 11012369;
    BEGIN
      EXIT(SalesReportTextManagement.GetTableNo("Document Type", "Text Line Type"));
    END;

    PROCEDURE GetTableCaption@1100525000() : Text;
    VAR
      SalesReportTextManagement@1100525000 : Codeunit 11012369;
    BEGIN
      EXIT(SalesReportTextManagement.GetTableCaption("Document Type", "Text Line Type"));
    END;

    PROCEDURE GetFieldCaption@1100525003(TableNo@1100525000 : Integer;FieldNo@1100525001 : Integer) : Text;
    VAR
      SalesReportTextManagement@1100525003 : Codeunit 11012369;
    BEGIN
      EXIT(SalesReportTextManagement.GetFieldCaption(TableNo,FieldNo));
    END;

    PROCEDURE GetTableLinkText@1100525002() TableLinkText : Text;
    VAR
      SalesReportTextTableLink@1100525000 : Record 11071913;
      RelatedTableFieldCaption@1100525001 : Text;
      I@1100525002 : Integer;
    BEGIN
      IF "Related Table No." = 0 THEN
        EXIT;

      SalesReportTextTableLink.SETRANGE("Document Type", "Document Type");
      SalesReportTextTableLink.SETRANGE("Text Line Type", "Text Line Type");
      SalesReportTextTableLink.SETRANGE("Language Code", "Language Code");
      SalesReportTextTableLink.SETRANGE("Text Line No.", "Text Line No.");
      SalesReportTextTableLink.SETRANGE("Text Value Line No.", "Line No.");

      IF SalesReportTextTableLink.FINDSET THEN BEGIN
        REPEAT
          I := I + 1;
          IF I > 1 THEN
            TableLinkText := TableLinkText + ',';
          RelatedTableFieldCaption := GetFieldCaption(
            "Related Table No.", GetRelatedTableIndexFieldNo(I, "Related Table No."));
          IF RelatedTableFieldCaption <> '' THEN
            IF SalesReportTextTableLink.Type = SalesReportTextTableLink.Type::CONST THEN
              TableLinkText := TableLinkText + STRSUBSTNO(
                Text002, RelatedTableFieldCaption, SalesReportTextTableLink.Value)
            ELSE
              TableLinkText := TableLinkText + STRSUBSTNO(
                Text001, RelatedTableFieldCaption,
                GetFieldCaption(GetTableNo, SalesReportTextTableLink."Field No."));
        UNTIL SalesReportTextTableLink.NEXT = 0;
        TableLinkText:= STRSUBSTNO(Text000, TableLinkText);
      END;
    END;

    PROCEDURE GetRelatedTableIndexFieldNo@1100525004(I@1100525000 : Integer;TableNo@1100525001 : Integer) FieldNo : Integer;
    VAR
      RecRef@1100525002 : RecordRef;
      KeyRef@1100525003 : KeyRef;
      FieldRef@1100525004 : FieldRef;
    BEGIN
      IF TableNo = 0 THEN
        EXIT;

      RecRef.OPEN(TableNo);
      KeyRef := RecRef.KEYINDEX(1);
      IF I <= KeyRef.FIELDCOUNT THEN BEGIN
        FieldRef := KeyRef.FIELDINDEX(I);
        FieldNo := FieldRef.NUMBER;
      END;
      RecRef.CLOSE;
    END;

    PROCEDURE RunPageTableLinks@1100525005();
    VAR
      SalesReportTextTableLink@1100525001 : Record 11071913;
      SalesReportTextTableLinks@1100525000 : Page 11124826;
    BEGIN
      SalesReportTextTableLink.SETRANGE("Document Type", "Document Type");
      SalesReportTextTableLink.SETRANGE("Text Line Type", "Text Line Type");
      SalesReportTextTableLink.SETRANGE("Language Code", "Language Code");
      SalesReportTextTableLink.SETRANGE("Text Line No.", "Text Line No.");
      SalesReportTextTableLink.SETRANGE("Text Value Line No.", "Line No.");
      SalesReportTextTableLinks.SETTABLEVIEW(SalesReportTextTableLink);
      SalesReportTextTableLinks.RUNMODAL;
    END;

    PROCEDURE GetDataCaptionTxt@1100525006() : Text;
    BEGIN
      EXIT(STRSUBSTNO('%1 %2 %3 %4', "Document Type", "Text Line Type", "Language Code", "Text Line No."));
    END;

    PROCEDURE HasTableLinks@1100525007() : Boolean;
    VAR
      SalesReportTextTableLink@1100525000 : Record 11071913;
    BEGIN
      SalesReportTextTableLink.SETRANGE("Document Type", "Document Type");
      SalesReportTextTableLink.SETRANGE("Text Line Type", "Text Line Type");
      SalesReportTextTableLink.SETRANGE("Language Code", "Language Code");
      SalesReportTextTableLink.SETRANGE("Text Line No.", "Text Line No.");
      SalesReportTextTableLink.SETRANGE("Text Value Line No.", "Line No.");
      EXIT(NOT SalesReportTextTableLink.ISEMPTY);
    END;

    LOCAL PROCEDURE CheckIfValueHasNoTableLinks@1100525010();
    VAR
      SalesReportTextTableLink@1100525000 : Record 11071913;
    BEGIN
      IF HasTableLinks THEN
        FIELDERROR("Related Table No.", STRSUBSTNO(Text003, SalesReportTextTableLink.TABLECAPTION));
    END;

    PROCEDURE GetParameterSeparator@1100528400() : Text[10];
    BEGIN
      IF "Parameter Separator" <> '' THEN
        EXIT("Parameter Separator");

      EXIT(GetDefaultParameterSeparator);
    END;

    PROCEDURE GetDefaultParameterSeparator@1100528401() : Text[10];
    BEGIN
      EXIT(',');
    END;

    LOCAL PROCEDURE CheckIfCurrentValueIsOnlyMultipleLinesValue@1100528402();
    VAR
      SalesReportTextValue@1100528400 : Record 11071911;
    BEGIN
      SalesReportTextValue.SETRANGE("Document Type", "Document Type");
      SalesReportTextValue.SETRANGE("Text Line Type", "Text Line Type");
      SalesReportTextValue.SETRANGE("Language Code", "Language Code");
      SalesReportTextValue.SETRANGE("Text Line No.", "Text Line No.");
      SalesReportTextValue.SETRANGE("Multiple Lines", TRUE);
      SalesReportTextValue.SETFILTER("Line No.", '<>%1', "Line No.");
      IF SalesReportTextValue.FINDFIRST THEN
        SalesReportTextValue.TESTFIELD("Multiple Lines", FALSE);
    END;

    BEGIN
    END.
  }
}

