OBJECT Table 11071924 Service & Maintenance Cue
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Service Cue;
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=ENU=Primary Key }
    { 2   ;   ;Open Service Contracts;Integer     ;FieldClass=FlowField;
                                                   CalcFormula=Count(Service-Contract WHERE (Status=FILTER(..Invoicing Allowed)));
                                                   CaptionML=ENU=Open Service Contracts;
                                                   Editable=No }
    { 3   ;   ;Open Service Contracts by User;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count(Service-Contract WHERE (Status=FILTER(..Invoicing Allowed),
                                                                                             Contract Manager=FIELD(Salesperson Filter)));
                                                   CaptionML=ENU=Open Service Contracts by User;
                                                   Editable=No }
    { 4   ;   ;Open Maintenance Proposals;Integer ;FieldClass=FlowField;
                                                   CalcFormula=Count("Maintenance Proposal Header" WHERE (Completed=FILTER(No)));
                                                   CaptionML=ENU=Open Maintenance Proposals;
                                                   Editable=No }
    { 5   ;   ;Open Service Calls  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Service Call" WHERE (Status=FILTER(..In Process),
                                                                                           Assigned to=FIELD(Employee Filter)));
                                                   CaptionML=ENU=Open Service Calls;
                                                   Editable=No }
    { 6   ;   ;Open Service Orders ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Service Order" WHERE (Status=FILTER(Open|In Process|Technical Finished|Administrative Finished),
                                                                                            Assigned to=FIELD(Employee Filter)));
                                                   CaptionML=ENU=Open Service Orders;
                                                   Editable=No }
    { 7   ;   ;Open Service Orders (Plan);Integer ;FieldClass=FlowField;
                                                   CalcFormula=Count("Service Order" WHERE (Actual Reference Point (Gantt)=FIELD(FILTER(Actual Ref Point Filter 1)),
                                                                                            Assigned to=FIELD(FILTER(Employee Filter))));
                                                   CaptionML=ENU=Open Service Orders (Plan);
                                                   Editable=No }
    { 8   ;   ;Open Service Orders (Review);Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Service Order" WHERE (Actual Reference Point (Gantt)=FIELD(FILTER(Actual Ref Point Filter 2)),
                                                                                            Assigned to=FIELD(FILTER(Employee Filter))));
                                                   CaptionML=ENU=Open Service Orders (Review);
                                                   Editable=No }
    { 100 ;   ;Employee Filter     ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=Employee.No.;
                                                   CaptionML=ENU=Employee Filter }
    { 110 ;   ;Actual Ref Point Filter 1;Integer  ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Acutal Reference Point (Gantt Chart) Filter 1;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 120 ;   ;Actual Ref Point Filter 2;Integer  ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Acutal Reference Point (Gantt Chart) Filter 2;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 130 ;   ;Salesperson Filter  ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Salesperson/Purchaser;
                                                   CaptionML=ENU=Salesperson Filter }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CountCues@1100525000(FieldNumber@1100525000 : Integer) : Integer;
    VAR
      ServiceContract@1100525001 : Record 11012812;
      MaintenanceProposalHeader@1100525002 : Record 11071695;
      ServiceCall@1100525003 : Record 11012822;
      ServiceOrder@1100525004 : Record 11012823;
    BEGIN
      //**4PS C015772
      CASE FieldNumber OF
        FIELDNO("Open Service Contracts"):
          BEGIN
            ServiceContract.DPA_SetFilters('Page 11012825');
            ServiceContract.SETFILTER(Status,'..%1',ServiceContract.Status::"Invoicing Allowed");
            EXIT(ServiceContract.COUNT);
          END;
        FIELDNO("Open Service Contracts by User"):
          BEGIN
            ServiceContract.DPA_SetFilters('Page 11012825');
            ServiceContract.SETFILTER(Status,'..%1',ServiceContract.Status::"Invoicing Allowed");
            COPYFILTER("Salesperson Filter",ServiceContract."Contract Manager");
            EXIT(ServiceContract.COUNT);
          END;
        FIELDNO("Open Maintenance Proposals"):
          EXIT("Open Maintenance Proposals");
        FIELDNO("Open Service Calls"):
          BEGIN
            ServiceCall.DPA_SetFilters('Page 11012828');
            ServiceCall.SETFILTER(Status,'..%1',ServiceCall.Status::"In Process");
            COPYFILTER("Employee Filter",ServiceCall."Assigned to");
            EXIT(ServiceCall.COUNT);
          END;
        FIELDNO("Open Service Orders"):
          BEGIN
            ServiceOrder.DPA_SetFilters('Page 11012830');
            ServiceOrder.SETFILTER(Status,'..%1',ServiceOrder.Status::"Administrative Finished");
            COPYFILTER("Employee Filter",ServiceOrder."Assigned to");
            EXIT(ServiceOrder.COUNT);
          END;
        FIELDNO("Open Service Orders (Plan)"):
          BEGIN
            ServiceOrder.DPA_SetFilters('Page 11012830');
            COPYFILTER("Actual Ref Point Filter 1",ServiceOrder."Actual Reference Point (Gantt)");
            COPYFILTER("Employee Filter",ServiceOrder."Assigned to");
            EXIT(ServiceOrder.COUNT);
          END;
        FIELDNO("Open Service Orders (Review)"):
          BEGIN
            ServiceOrder.DPA_SetFilters('Page 11012830');
            COPYFILTER("Actual Ref Point Filter 2",ServiceOrder."Actual Reference Point (Gantt)");
            COPYFILTER("Employee Filter",ServiceOrder."Assigned to");
            EXIT(ServiceOrder.COUNT);
          END;
      END;
    END;

    LOCAL PROCEDURE GetOpenGlazingCallsDS@1100525003(VAR OGlazingServiceCall@1100525000 : Record 11125777);
    VAR
      UserSetup@1100525001 : Record 91;
    BEGIN
      OGlazingServiceCall.SETFILTER(Status,'..%1', OGlazingServiceCall.Status::"In Progress");
      IF UserSetup.GET(USERID) THEN
        OGlazingServiceCall.SETRANGE("Order Manager", UserSetup."Employee No.");
    END;

    PROCEDURE GetOpenGlazingCalls@1100525001() : Integer;
    VAR
      GlazingServiceCall@1100525002 : Record 11125777;
      InstallationMgt@1100525000 : Codeunit 11012268;
    BEGIN
      IF InstallationMgt.LicensePermissionForTableData(DATABASE::"Glazing Service Call") THEN BEGIN
        GetOpenGlazingCallsDS(GlazingServiceCall);
        EXIT(GlazingServiceCall.COUNT);
      END ELSE
        EXIT(0);
    END;

    PROCEDURE ShowOpenGlazingCalls@1100525002();
    VAR
      GlazingServiceCall@1100525000 : Record 11125777;
    BEGIN
      GetOpenGlazingCallsDS(GlazingServiceCall);
      PAGE.RUN(0, GlazingServiceCall);
    END;

    BEGIN
    END.
  }
}

