OBJECT Table 11071936 Serv. Disc. Filter Group Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD(Code);
             END;

    OnDelete=VAR
               ServDiscFilterGroupLine@1100528600 : Record 11071936;
             BEGIN
               ServDiscFilterGroupLine.SETRANGE(Code, Code);
               ServDiscFilterGroupLine.SETRANGE("Linked To Line No.", "Line No.");
               ServDiscFilterGroupLine.MODIFYALL("Linked To Line No.", 0, TRUE);
             END;

    CaptionML=ENU=Service Discount Filter Group Line;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code20        ;TableRelation="Serv. Discount Filter Group";
                                                   CaptionML=ENU=Code;
                                                   Editable=No }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No.;
                                                   Editable=No }
    { 30  ;   ;Table Type          ;Option        ;OnValidate=BEGIN
                                                                CASE "Table Type" OF
                                                                  "Table Type"::Item:
                                                                    "Table No." := DATABASE::Item;
                                                                  "Table Type"::"Trade Item":
                                                                    "Table No." := DATABASE::"Trade Item";
                                                                  "Table Type"::"Basic Item":
                                                                    "Table No." := DATABASE::"Basic Item";
                                                                  "Table Type"::"Service Order",
                                                                  "Table Type"::"8":
                                                                    "Table No." := DATABASE::"Service Order";
                                                                END;
                                                                VALIDATE("Field No.", 0);
                                                                ClearValueFilter;
                                                              END;

                                                   CaptionML=ENU=Table Type;
                                                   OptionCaptionML=ENU=Navision Item,Trade Item,Basic Item,,,,,Service Order;
                                                   OptionString=Item,Trade Item,Basic Item,,,,,Service Order }
    { 40  ;   ;Table No.           ;Integer       ;InitValue=27;
                                                   OnValidate=BEGIN
                                                                ClearValueFilter;
                                                              END;

                                                   CaptionML=ENU=Table No. }
    { 50  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  Class=CONST(Normal),
                                                                                  Type=FILTER(<>BLOB));
                                                   OnValidate=BEGIN
                                                                ClearValueFilter;
                                                              END;

                                                   CaptionML=ENU=Field No.;
                                                   BlankZero=Yes }
    { 60  ;   ;Value Filter        ;Text250       ;OnValidate=BEGIN
                                                                EvaluateFilter();
                                                              END;

                                                   CaptionML=ENU=Value Filter }
    { 70  ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table No.),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Name;
                                                   Editable=No }
    { 80  ;   ;Field Caption       ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Caption;
                                                   Editable=No }
    { 90  ;   ;Field Type          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Type Name" WHERE (TableNo=FIELD(Table No.),
                                                                                               No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Type;
                                                   Editable=No }
    { 100 ;   ;Linked To Line No.  ;Integer       ;TableRelation="Serv. Disc. Filter Group Line"."Line No." WHERE (Code=FIELD(Code),
                                                                                                                   Linked To Line No.=CONST(0),
                                                                                                                   Table Type=FIELD(Table Type));
                                                   OnValidate=BEGIN
                                                                TestLinkedLineNo;
                                                                TestInfiniteLoop;
                                                              END;

                                                   CaptionML=ENU=Linked To Line No. }
  }
  KEYS
  {
    {    ;Code,Line No.                           ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=t';
      Text001@1100528601 : TextConst 'ENU=This creates a loop';
      Text002@1100528602 : TextConst 'ENU=Only Links between same tables allowed';
      Text003@1100528603 : TextConst 'ENU=There are lines linked to this line, linking not allowed';

    PROCEDURE EvaluateFilter@1210190000();
    VAR
      Item@1100528600 : Record 27;
      TradeItem@1210190003 : Record 11012317;
      BasicItem@1210190004 : Record 11012316;
      ServiceAnalysis@1100528601 : Record 11012842;
      TableRef@1210190001 : RecordRef;
      FieldRef@1210190002 : FieldRef;
    BEGIN
      TableRef.OPEN("Table No.");
      FieldRef := TableRef.FIELD("Field No.");
      IF FORMAT(FieldRef.TYPE) = 'Date' THEN
        ConvertDateFilter("Value Filter");
      FieldRef.SETFILTER("Value Filter");
      "Value Filter" := FieldRef.GETFILTER;
    END;

    PROCEDURE ClearValueFilter@1210190001();
    BEGIN
      IF ("Table Type" <> xRec."Table Type") OR
        ("Table No." <> xRec."Table No.") OR
        ("Field No." <> xRec."Field No.") THEN
        "Value Filter" := '';
    END;

    PROCEDURE ConvertDateFilter@1210190003(VAR OValueFilter@1210190000 : Text[250]);
    VAR
      Pos@1210190001 : Integer;
    BEGIN
      Pos := STRPOS(LOWERCASE(OValueFilter), Text000);
      IF Pos <> 0 THEN BEGIN
        OValueFilter := DELSTR(OValueFilter, Pos, STRLEN(Text000));
        OValueFilter := INSSTR(OValueFilter, FORMAT(TODAY), Pos);
      END;
    END;

    PROCEDURE TestInfiniteLoop@1100528600();
    VAR
      TmpServDiscFilterGroupLine@1100528600 : TEMPORARY Record 11071936;
      ServDiscFilterGroupLine@1100528601 : Record 11071936;
    BEGIN
      IF "Linked To Line No." = 0 THEN
        EXIT;
      MODIFY;
      ServDiscFilterGroupLine := Rec;
      REPEAT
        TmpServDiscFilterGroupLine := ServDiscFilterGroupLine;
        IF NOT TmpServDiscFilterGroupLine.INSERT THEN
          ERROR(Text001);
        IF ServDiscFilterGroupLine."Linked To Line No." <> 0 THEN
          ServDiscFilterGroupLine.GET(Code, ServDiscFilterGroupLine."Linked To Line No.");
      UNTIL ServDiscFilterGroupLine."Linked To Line No." = 0;
    END;

    PROCEDURE TestLinkedLineNo@1100528601();
    VAR
      ServDiscFilterGroupLine@1100528600 : Record 11071936;
    BEGIN
      IF "Linked To Line No." = 0 THEN
        EXIT;
      ServDiscFilterGroupLine.GET(Code, "Linked To Line No.");
      IF ServDiscFilterGroupLine."Table Type" <> "Table Type" THEN
        ERROR(Text002);
      ServDiscFilterGroupLine.SETRANGE(Code, Code);
      ServDiscFilterGroupLine.SETRANGE("Linked To Line No.", "Line No.");
      IF NOT ServDiscFilterGroupLine.ISEMPTY THEN
        ERROR(Text003);
    END;

    BEGIN
    END.
  }
}

