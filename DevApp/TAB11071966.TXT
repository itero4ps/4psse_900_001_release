OBJECT Table 11071966 Service Cluster Seq.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceContractClusterSeq@1100528600 : Record 11071966;
             BEGIN
               IF "Seq. No." = 0 THEN BEGIN
                 ServiceContractClusterSeq.SETCURRENTKEY("Seq. No.");
                 ServiceContractClusterSeq.SETRANGE("Service Contract No.", "Service Contract No.");
                 IF ServiceContractClusterSeq.FINDLAST THEN
                   "Seq. No." := ServiceContractClusterSeq."Seq. No." +1
                 ELSE
                   "Seq. No." := 1;
               END;
             END;

    OnDelete=VAR
               ClusterEmployee@1100528600 : Record 11071967;
             BEGIN
               ClusterEmployee.SETRANGE("Service Contract No.", "Service Contract No.");
               ClusterEmployee.SETRANGE("Cluster Code", "Cluster Code");
               ClusterEmployee.SETRANGE("Seq. No.", "Seq. No.");
               ClusterEmployee.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Service Cluster Seq.;
    LookupPageID=Page11126445;
    DrillDownPageID=Page11126445;
  }
  FIELDS
  {
    { 5   ;   ;Service Contract No.;Code20        ;TableRelation="Service Cluster"."Service Contract No.";
                                                   CaptionML=ENU=Service Contract No.;
                                                   Editable=No }
    { 10  ;   ;Cluster Code        ;Code10        ;TableRelation="Service Cluster".Code WHERE (Service Contract No.=FIELD(Service Contract No.));
                                                   CaptionML=ENU=Cluster Code;
                                                   Editable=No }
    { 20  ;   ;Cluster Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Cluster".Description WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                           Code=FIELD(Cluster Code)));
                                                   CaptionML=ENU=Cluster Description;
                                                   Editable=No }
    { 30  ;   ;Seq. No.            ;Integer       ;CaptionML=ENU=Seq. No.;
                                                   Editable=No }
    { 35  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 40  ;   ;Execution Period Start Date;Date   ;OnValidate=BEGIN
                                                                IF "Execution Period Ending Date" <> 0D THEN
                                                                  IF "Execution Period Start Date" > "Execution Period Ending Date" THEN
                                                                    FIELDERROR("Execution Period Start Date", STRSUBSTNO(Text000, FIELDCAPTION("Execution Period Ending Date")));
                                                                UpdateCauseOfAbsReservations;
                                                              END;

                                                   CaptionML=ENU=Execution Period Start Date }
    { 50  ;   ;Execution Period Ending Date;Date  ;OnValidate=BEGIN
                                                                VALIDATE("Execution Period Start Date")
                                                              END;

                                                   CaptionML=ENU=Execution Period Ending Date }
    { 60  ;   ;Plan Margin Perc.   ;Decimal       ;OnValidate=BEGIN
                                                                IF "Plan Margin Perc." <> 0 THEN
                                                                  TESTFIELD("Plan Margin Hours", 0);
                                                              END;

                                                   CaptionML=ENU=Plan Margin Percentage }
    { 65  ;   ;Plan Margin Hours   ;Decimal       ;OnValidate=BEGIN
                                                                IF "Plan Margin Hours" <> 0 THEN
                                                                  TESTFIELD("Plan Margin Perc.", 0);
                                                              END;

                                                   CaptionML=ENU=Plan Margin Hours }
    { 70  ;   ;Subcontractor       ;Code20        ;TableRelation="Resource (WOP)" WHERE (Type=CONST(Subcontractor));
                                                   OnValidate=VAR
                                                                ResourceMgtWorkOrderPlan@1100528600 : Codeunit 11229277;
                                                              BEGIN
                                                                IF Subcontractor <> '' THEN BEGIN
                                                                  ResourceMgtWorkOrderPlan.TestResourceWithinCompanyPlanGroup(Subcontractor);
                                                                  CALCFIELDS("No. of Employees");
                                                                  TESTFIELD("No. of Employees", 0);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Subcontractor }
    { 80  ;No ;Subcontractor Commitment;Decimal   ;CaptionML=ENU=Subcontractor Commitment }
    { 90  ;   ;Last Possible Change Date;Date     ;OnValidate=BEGIN
                                                                IF "Last Possible Change Date" > "Execution Period Start Date" THEN
                                                                  FIELDERROR("Last Possible Change Date", STRSUBSTNO(Text000, FIELDCAPTION("Execution Period Start Date")));
                                                              END;

                                                   CaptionML=ENU=Last Possible Change Date }
    { 100 ;   ;No. of Employees    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Service Cluster Employee" WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                       Cluster Code=FIELD(Cluster Code),
                                                                                                       Seq. No.=FIELD(Seq. No.)));
                                                   CaptionML=ENU=No. of Employees;
                                                   Editable=No }
    { 110 ;   ;Last Rebuild        ;DateTime      ;CaptionML=ENU=Last Rebuild }
  }
  KEYS
  {
    {    ;Service Contract No.,Cluster Code,Seq. No.;
                                                   Clustered=Yes }
    {    ;Seq. No.                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=must occur before %1';

    PROCEDURE UpdateCauseOfAbsReservations@1100528600();
    VAR
      SCClusterEmployee@1100528600 : Record 11071967;
    BEGIN
      MODIFY;
      SCClusterEmployee.SETRANGE("Service Contract No.", "Service Contract No.");
      SCClusterEmployee.SETRANGE("Cluster Code", "Cluster Code");
      SCClusterEmployee.SETRANGE("Seq. No.", "Seq. No.");
      IF SCClusterEmployee.FINDSET THEN
        REPEAT
          SCClusterEmployee.VALIDATE("Resource No.");
        UNTIL SCClusterEmployee.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

