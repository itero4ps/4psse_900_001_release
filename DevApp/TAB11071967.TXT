OBJECT Table 11071967 Service Cluster Employee
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceContractClusterSeq@1100528600 : Record 11071966;
             BEGIN
               IF ServiceContractClusterSeq.GET("Service Contract No.", "Cluster Code", "Seq. No.") THEN
                 ServiceContractClusterSeq.TESTFIELD(Subcontractor, '');
               TESTFIELD("Resource No.");
             END;

    OnDelete=VAR
               EmployeeAbsence@1100528600 : Record 5207;
               ServiceSetup@1100528601 : Record 11012800;
               AvailableClusterSeqnBlock@1100525000 : Record 11071968;
               EmplNo@1100529000 : Code[20];
             BEGIN
               ServiceSetup.GET;
               ServiceSetup.TESTFIELD("Cause of Abs. for Reservations");
               EmplNo := ResourceNo2EmplNo("Resource No.");  //DP00465.n
               IF EmplNo <> '' THEN BEGIN  //DP00465.n
                 EmployeeAbsence.SETRANGE("Employee No.", EmplNo);  //DP00465.c
                 EmployeeAbsence.SETRANGE("Cause of Absence Code", ServiceSetup."Cause of Abs. for Reservations");
                 EmployeeAbsence.SETRANGE("Service Contract No.", "Service Contract No.");
                 EmployeeAbsence.SETRANGE("Contract Cluster", "Cluster Code");
                 EmployeeAbsence.SETRANGE("Contract Cluster Seqn.", "Seq. No.");
                 EmployeeAbsence.DELETEALL(TRUE);
               END;
               AvailableClusterSeqnBlock.SETRANGE("Service Contract No.", "Service Contract No.");
               AvailableClusterSeqnBlock.SETRANGE("Cluster Code", "Cluster Code");
               AvailableClusterSeqnBlock.SETRANGE("Cluster Seq. No.", "Seq. No.");
               AvailableClusterSeqnBlock.SETRANGE("Resource No.", "Resource No.");
               AvailableClusterSeqnBlock.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Service Cluster Employee;
    LookupPageID=Page11126446;
    DrillDownPageID=Page11126446;
  }
  FIELDS
  {
    { 5   ;   ;Service Contract No.;Code20        ;TableRelation="Service Cluster"."Service Contract No.";
                                                   CaptionML=ENU=Service Contract No.;
                                                   Editable=No }
    { 10  ;   ;Cluster Code        ;Code10        ;TableRelation="Service Cluster".Code WHERE (Service Contract No.=FIELD(Service Contract No.));
                                                   CaptionML=ENU=Cluster Code;
                                                   Editable=No }
    { 20  ;   ;Cluster Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Cluster".Description WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                           Code=FIELD(Cluster Code)));
                                                   CaptionML=ENU=Cluster Description;
                                                   Editable=No }
    { 30  ;   ;Seq. No.            ;Integer       ;TableRelation="Service Cluster Seq."."Seq. No." WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                          Cluster Code=FIELD(Cluster Code),
                                                                                                          Seq. No.=FIELD(Seq. No.));
                                                   CaptionML=ENU=Seq. No.;
                                                   Editable=No }
    { 40  ;   ;Resource No.        ;Code20        ;TableRelation="Resource (WOP)" WHERE (Type=CONST(Employee));
                                                   OnValidate=VAR
                                                                EmployeeAbsence@1100528600 : Record 5207;
                                                                ServiceContractClusterSeq@1100528601 : Record 11071966;
                                                                ServiceSetup@1100528602 : Record 11012800;
                                                                ResourceWOP@1100528603 : Record 11229278;
                                                                EmplNo@1100529000 : Code[20];
                                                              BEGIN
                                                                ServiceSetup.GET;
                                                                ServiceSetup.TESTFIELD("Cause of Abs. for Reservations");
                                                                ServiceContractClusterSeq.GET("Service Contract No.", "Cluster Code", "Seq. No.");

                                                                CALCFIELDS("Employee Name");
                                                                IF xRec."Resource No." <> '' THEN BEGIN
                                                                  EmplNo := ResourceNo2EmplNo(xRec."Resource No.");  //DP00465.n
                                                                  IF EmplNo <> '' THEN BEGIN  //DP00465.n
                                                                    EmployeeAbsence.SETRANGE("Employee No.", EmplNo);  //DP00465.c
                                                                    EmployeeAbsence.SETRANGE("Cause of Absence Code", ServiceSetup."Cause of Abs. for Reservations");
                                                                    EmployeeAbsence.SETRANGE("Service Contract No.", "Service Contract No.");
                                                                    EmployeeAbsence.SETRANGE("Contract Cluster", "Cluster Code");
                                                                    EmployeeAbsence.SETRANGE("Contract Cluster Seqn.", "Seq. No.");
                                                                    EmployeeAbsence.DELETEALL(TRUE);
                                                                  END;
                                                                END;

                                                                IF "Resource No." <> '' THEN BEGIN
                                                                  EmplNo := ResourceNo2EmplNo("Resource No.");  //DP00465.n
                                                                  IF EmplNo <> '' THEN BEGIN  //DP00465.n
                                                                    EmployeeAbsence."Entry No." := 0;
                                                                    EmployeeAbsence."Employee No." := EmplNo;  //DP00465.c
                                                                    EmployeeAbsence."From Date" := ServiceContractClusterSeq."Execution Period Start Date";
                                                                    EmployeeAbsence."To Date" := ServiceContractClusterSeq."Execution Period Ending Date";
                                                                    EmployeeAbsence.VALIDATE("Cause of Absence Code", ServiceSetup."Cause of Abs. for Reservations");
                                                                    EmployeeAbsence."Service Contract No." := "Service Contract No.";
                                                                    EmployeeAbsence."Contract Cluster" := "Cluster Code";
                                                                    EmployeeAbsence."Contract Cluster Seqn." := "Seq. No.";
                                                                    EmployeeAbsence.INSERT(TRUE);
                                                                  END;

                                                                  IF ResourceWOP.GET("Resource No.") THEN
                                                                    ResourceWOP.TestResourceWithinCompanyPlanGroup;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Resource No.;
                                                   Description=DP00465, based on Resource }
    { 50  ;   ;Employee Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Resource (WOP)".Name WHERE (No.=FIELD(Resource No.)));
                                                   CaptionML=ENU=Employee Name;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Service Contract No.,Cluster Code,Seq. No.,Resource No.;
                                                   Clustered=Yes }
    {    ;Resource No.                             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE ResourceNo2EmplNo@1100529000(ResourceNo@1100529000 : Code[20]) : Code[20];
    VAR
      Resource@1100529001 : Record 11229278;
    BEGIN
      IF Resource.GET(ResourceNo) THEN BEGIN
        IF Resource.Company = COMPANYNAME THEN
          EXIT(Resource."Source No.");
      END;
      EXIT('');
    END;

    BEGIN
    END.
  }
}

