OBJECT Table 11071985 Service Category
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceContract@1100528600 : Record 11012812;
             BEGIN
               IF "Service Contract No." <> '' THEN
                 IF ServiceContract.GET("Service Contract No.") THEN
                   ServiceContract.TESTFIELD("Budget Based on", ServiceContract."Budget Based on"::Contract);
             END;

    OnDelete=VAR
               ServiceContract@1100528603 : Record 11012812;
               ContractInstallment@1100528601 : Record 11071707;
               SCRevenueDivisionLine@1100528602 : Record 11071987;
               ContractObjectMaintenance@1100528600 : Record 11071692;
               SCCategoryControl@1100528604 : Record 11071988;
             BEGIN
               IF "Service Contract No." <> '' THEN BEGIN
                 IF ServiceContract.GET("Service Contract No.") THEN
                   IF (ServiceContract."Service Category (Calls)" = Code) OR (ServiceContract."Service Category (Extension)" = Code) THEN
                     ERROR(Text001, TABLECAPTION, Code, ServiceContract."No.");

                 SCRevenueDivisionLine.SETRANGE("Service Contract No.", "Service Contract No.");
                 SCRevenueDivisionLine.SETRANGE("Service Category", Code);
                 IF NOT SCRevenueDivisionLine.ISEMPTY THEN
                   ERROR(Text000, TABLECAPTION, Code, SCRevenueDivisionLine.TABLECAPTION);

                 ContractObjectMaintenance.SETRANGE("Service Contract No.", "Service Contract No.");
                 ContractObjectMaintenance.SETRANGE("Service Category", Code);
                 IF NOT ContractObjectMaintenance.ISEMPTY THEN
                   ERROR(Text000, TABLECAPTION, Code, ContractObjectMaintenance.TABLECAPTION);

                 SCCategoryControl.SETRANGE("Service Contract No.", "Service Contract No.");
                 SCCategoryControl.SETRANGE("Service Category", Code);
                 IF NOT SCCategoryControl.ISEMPTY THEN
                   ERROR(Text000, TABLECAPTION, Code, SCCategoryControl.TABLECAPTION);
               END;
             END;

    CaptionML=ENU=Service Category;
    LookupPageID=Page11229331;
    DrillDownPageID=Page11229331;
  }
  FIELDS
  {
    { 10  ;   ;Service Contract No.;Code20        ;TableRelation=Service-Contract;
                                                   CaptionML=ENU=Service Contract No.;
                                                   Editable=No }
    { 20  ;   ;Code                ;Code10        ;TableRelation=IF (Service Contract No.=FILTER(<>'')) "Service Category".Code WHERE (Service Contract No.=CONST());
                                                   OnValidate=VAR
                                                                ServiceCategory@1100528600 : Record 11071985;
                                                              BEGIN
                                                                IF "Service Contract No." <> '' THEN
                                                                  IF ServiceCategory.GET('', Code) THEN
                                                                    Description := ServiceCategory.Description;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text30        ;CaptionML=ENU=Description }
  }
  KEYS
  {
    {    ;Service Contract No.,Code               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=%1 %2 cannot be deleted, still being used in table %3';
      Text001@1100528601 : TextConst 'ENU=%1 %2 cannot be deleted, still being used in Service Contract %3';

    PROCEDURE ShowSCCategoryControl@1100528600();
    VAR
      SCCategoryControl@1100528600 : Record 11071988;
    BEGIN
      SCCategoryControl.SETRANGE("Service Contract No.", "Service Contract No.");
      SCCategoryControl.SETRANGE("Service Category", Code);
      IF SCCategoryControl.FINDLAST THEN
        SCCategoryControl.SETRANGE("Reporting Date", SCCategoryControl."Reporting Date");
      IF SCCategoryControl.FINDFIRST THEN;
      PAGE.RUN(PAGE::"SC Category Control List", SCCategoryControl);
    END;

    BEGIN
    END.
  }
}

