OBJECT Table 11072091 Insurance Journal Batch
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW17.00,4PS9.00 (5634);
  }
  PROPERTIES
  {
    DataCaptionFields=Name,Description;
    OnInsert=BEGIN
               LOCKTABLE;
               InsuranceJnlTempl.GET("Journal Template Name");
             END;

    OnDelete=BEGIN
               InsuranceJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               InsuranceJnlLine.SETRANGE("Journal Batch Name",Name);
               InsuranceJnlLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               InsuranceJnlLine.SETRANGE("Journal Template Name",xRec."Journal Template Name");
               InsuranceJnlLine.SETRANGE("Journal Batch Name",xRec.Name);
               WHILE InsuranceJnlLine.FINDFIRST DO
                 InsuranceJnlLine.RENAME("Journal Template Name",Name,InsuranceJnlLine."Line No.");
             END;

    CaptionML=ENU=Insurance Journal Batch;
    LookupPageID=Page11072147;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Insurance Journal Template";
                                                   CaptionML=ENU=Journal Template Name;
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Code10        ;CaptionML=ENU=Name;
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 4   ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   OnValidate=BEGIN
                                                                IF "Reason Code" <> xRec."Reason Code" THEN BEGIN
                                                                  InsuranceJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                  InsuranceJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                  InsuranceJnlLine.MODIFYALL("Reason Code","Reason Code");
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Reason Code }
    { 5   ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("No. Series" <> '') AND ("No. Series" = "Posting No. Series") THEN
                                                                  VALIDATE("Posting No. Series",'');
                                                              END;

                                                   CaptionML=ENU=No. Series }
    { 6   ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text000,"Posting No. Series"));
                                                                InsuranceJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                InsuranceJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                InsuranceJnlLine.MODIFYALL("Posting No. Series","Posting No. Series");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=ENU=Posting No. Series }
  }
  KEYS
  {
    {    ;Journal Template Name,Name              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=must not be %1';
      InsuranceJnlTempl@1001 : Record 11072090;
      InsuranceJnlLine@1002 : Record 11072092;

    PROCEDURE SetupNewBatch@3();
    BEGIN
      InsuranceJnlTempl.GET("Journal Template Name");
      "No. Series" := InsuranceJnlTempl."No. Series";
      "Posting No. Series" := InsuranceJnlTempl."Posting No. Series";
      "Reason Code" := InsuranceJnlTempl."Reason Code";
    END;

    BEGIN
    END.
  }
}

