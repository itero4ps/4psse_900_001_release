OBJECT Table 11072210 SO Plant Maintenance Scheme
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceOrderInfoMgt@1100528800 : Codeunit 11012841;
               ServiceOrder@1100528801 : Record 11012823;
             BEGIN
               ServiceOrderInfoMgt.TestPlantServiceOrder("Service Order No.",TRUE);
             END;

    OnDelete=VAR
               ServiceOrderBudgetLine@1100528800 : Record 11012875;
               ServiceOrderInspectionLine@1100528801 : Record 11071753;
             BEGIN
               ServiceOrderBudgetLine.SETRANGE("Budget Type", ServiceOrderBudgetLine."Budget Type"::Order);
               ServiceOrderBudgetLine.SETRANGE("Service Order No.", "Service Order No.");
               ServiceOrderBudgetLine.SETRANGE("Service Contract No.", '');
               ServiceOrderBudgetLine.SETRANGE("Object No.", '');
               ServiceOrderBudgetLine.SETRANGE("Maintenance Scheme Type", ServiceOrderBudgetLine."Maintenance Scheme Type"::Plant);
               ServiceOrderBudgetLine.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
               ServiceOrderBudgetLine.DELETEALL;

               ServiceOrderInspectionLine.SETRANGE("Service Order No.", "Service Order No.");
               ServiceOrderInspectionLine.SETRANGE("Service Object No.", '');
               ServiceOrderInspectionLine.SETRANGE("Maintenance Scheme Code", "Maintenance Scheme");
               ServiceOrderInspectionLine.SETRANGE("Inspection Type", ServiceOrderInspectionLine."Inspection Type"::Plant);
               ServiceOrderInspectionLine."Service Contract No." := '';
               ServiceOrderInspectionLine.DELETEALL;
             END;

    CaptionML=ENU=Service Order Plant Maintenance Scheme;
  }
  FIELDS
  {
    { 10  ;   ;Service Order No.   ;Code20        ;CaptionML=ENU=Service Order No.;
                                                   NotBlank=Yes }
    { 20  ;   ;Maintenance Scheme  ;Code20        ;TableRelation="Plant Maintenance Scheme".Code;
                                                   OnValidate=VAR
                                                                ServiceOrder@1100528803 : Record 11012823;
                                                                ServiceOrderBudgetLine@1100528800 : Record 11012875;
                                                                OPlantMaintenanceSchemeVersionLine@1100528801 : TEMPORARY Record 11072675;
                                                                OPlantInspectionLine@1100528807 : TEMPORARY Record 11072684;
                                                                PlantTypeMaintenanceScheme@1100528805 : Record 11072677;
                                                                IBaseInspectionList@1100528806 : Code[20];
                                                                ILevel@1100528802 : 'Maintenance Scheme,Plant Type,Plant Number';
                                                                LineNo@1100528804 : Integer;
                                                              BEGIN
                                                                //Fill service order budget lines from maintenance scheme (level plant number)
                                                                IF (xRec."Maintenance Scheme" <> '') THEN BEGIN
                                                                  DeleteMaintenanceSchemeServiceOrderBudgetLines(xRec."Maintenance Scheme");
                                                                END;

                                                                IF (xRec."Maintenance Scheme" <> "Maintenance Scheme") AND ("Maintenance Scheme" <> '') THEN BEGIN
                                                                  ServiceOrder.GET("Service Order No.");
                                                                  PlantMaintenanceManagement.GetApplicableActivities(
                                                                          OPlantMaintenanceSchemeVersionLine,
                                                                          ILevel::"Plant Number",
                                                                          ServiceOrder."Plant Type",
                                                                          ServiceOrder."Plant Number",
                                                                          "Maintenance Scheme",
                                                                          ServiceOrder."Starting Date",
                                                                          FALSE);

                                                                  IF OPlantMaintenanceSchemeVersionLine.FINDSET THEN BEGIN
                                                                    LineNo := GetNextServiceOrderBudgetLineNo;
                                                                    REPEAT
                                                                      AddServiceOrderBudgetLine(OPlantMaintenanceSchemeVersionLine, LineNo, ServiceOrder."Starting Date");
                                                                      LineNo += 10000;
                                                                    UNTIL OPlantMaintenanceSchemeVersionLine.NEXT = 0;
                                                                  END;

                                                                //Fill Service Order Inspection lines
                                                                  IBaseInspectionList :=  PlantMaintenanceManagement.GetBaseInspectionListByPlantNumber(ServiceOrder."Plant Type",ServiceOrder."Plant Number","Maintenance Scheme");

                                                                  PlantMaintenanceManagement.GetApplicableInspectionLines(
                                                                          OPlantInspectionLine,
                                                                          ILevel::"Plant Number",
                                                                          ServiceOrder."Plant Type",
                                                                          ServiceOrder."Plant Number",
                                                                          "Maintenance Scheme",
                                                                          ServiceOrder."Starting Date",
                                                                          IBaseInspectionList,
                                                                          FALSE);


                                                                  IF OPlantInspectionLine.FINDSET THEN BEGIN
                                                                    LineNo := GetNextServiceOrderInspectionLineNo;
                                                                    REPEAT
                                                                      AddServiceOrderInspectionLine(OPlantInspectionLine, LineNo);
                                                                      LineNo += 10000;
                                                                    UNTIL OPlantInspectionLine.NEXT = 0;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Maintenance Scheme;
                                                   NotBlank=Yes }
    { 30  ;   ;Maintenance Scheme Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Maintenance Scheme".Description WHERE (Code=FIELD(Maintenance Scheme)));
                                                   CaptionML=ENU=Maintenance Scheme Description }
  }
  KEYS
  {
    {    ;Service Order No.,Maintenance Scheme    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PlantMaintenanceManagement@1100528800 : Codeunit 11012872;

    LOCAL PROCEDURE GetNextServiceOrderBudgetLineNo@1100528805() : Integer;
    VAR
      ServiceOrderBudgetLine@1100528801 : Record 11012875;
    BEGIN
      ServiceOrderBudgetLine.SETRANGE("Budget Type",ServiceOrderBudgetLine."Budget Type"::Order);
      ServiceOrderBudgetLine.SETRANGE("Service Order No.", "Service Order No.");
      IF ServiceOrderBudgetLine.FINDLAST THEN
        EXIT(ServiceOrderBudgetLine."Line No." + 10000)
      ELSE
        EXIT(10000);
    END;

    LOCAL PROCEDURE GetNextServiceOrderInspectionLineNo@1100528801() : Integer;
    VAR
      ServiceOrderInspectionLine@1100528801 : Record 11071753;
    BEGIN
      ServiceOrderInspectionLine.SETRANGE("Service Order No.", "Service Order No.");
      ServiceOrderInspectionLine.SETRANGE("Service Object No.", '');
      ServiceOrderInspectionLine.SETRANGE("Maintenance Scheme Code", "Maintenance Scheme");
      IF ServiceOrderInspectionLine.FINDLAST THEN
        EXIT(ServiceOrderInspectionLine."Line No." + 10000)
      ELSE
        EXIT(10000);
    END;

    LOCAL PROCEDURE AddServiceOrderBudgetLine@1100528800(VAR OplantMaintenanceSchemeVersionLine@1100528800 : TEMPORARY Record 11072675;LineNo@1100528802 : Integer;ServiceOrderStartingDate@1100528803 : Date);
    VAR
      ServiceOrderBudgetLine@1100528801 : Record 11012875;
    BEGIN
      ServiceOrderBudgetLine.INIT;
      ServiceOrderBudgetLine."Budget Type" := ServiceOrderBudgetLine."Budget Type"::Order;
      ServiceOrderBudgetLine."Service Order No." := "Service Order No.";
      ServiceOrderBudgetLine.VALIDATE("Cost Type",OplantMaintenanceSchemeVersionLine."Cost Type");
      ServiceOrderBudgetLine."Service Contract No." := '';
      ServiceOrderBudgetLine."Object No." := '';
      ServiceOrderBudgetLine."Maintenance Scheme" := "Maintenance Scheme";
      ServiceOrderBudgetLine."Maintenance Scheme Type" := ServiceOrderBudgetLine."Maintenance Scheme Type"::Plant;
      ServiceOrderBudgetLine."Line No." := LineNo;
      ServiceOrderBudgetLine.Description := OplantMaintenanceSchemeVersionLine.Description;
      ServiceOrderBudgetLine."Index Date" := ServiceOrderStartingDate;
      ServiceOrderBudgetLine."Cost Object" := OplantMaintenanceSchemeVersionLine."Cost Object";
      ServiceOrderBudgetLine.Norm := OplantMaintenanceSchemeVersionLine.Norm;
      ServiceOrderBudgetLine."Rate Code" := OplantMaintenanceSchemeVersionLine."Rate Code";
      ServiceOrderBudgetLine.Rate := OplantMaintenanceSchemeVersionLine.Rate;
      ServiceOrderBudgetLine.Hours := OplantMaintenanceSchemeVersionLine.Hours;
      ServiceOrderBudgetLine."Gross Price" := OplantMaintenanceSchemeVersionLine."Gross Price";
      ServiceOrderBudgetLine."Item No." := OplantMaintenanceSchemeVersionLine."Item No.";
      ServiceOrderBudgetLine."Basic Item" := OplantMaintenanceSchemeVersionLine."Basic Item";
      ServiceOrderBudgetLine."Trade Item" := OplantMaintenanceSchemeVersionLine."Trade Item";
      ServiceOrderBudgetLine."Vendor (Trade Item)" := OplantMaintenanceSchemeVersionLine."Vendor (Trade Item)";
      ServiceOrderBudgetLine.Manufacturer := OplantMaintenanceSchemeVersionLine.Manufacturer;
      ServiceOrderBudgetLine."Cost Component" := OplantMaintenanceSchemeVersionLine."Cost Component";
      ServiceOrderBudgetLine.Vendor := OplantMaintenanceSchemeVersionLine.Vendor;
      ServiceOrderBudgetLine.VALIDATE("Unit of Measure", OplantMaintenanceSchemeVersionLine."Unit of Measure");
      ServiceOrderBudgetLine.VALIDATE(Quantity, OplantMaintenanceSchemeVersionLine.Quantity);


      ServiceOrderBudgetLine.INSERT;
    END;

    LOCAL PROCEDURE AddServiceOrderInspectionLine@1100528807(VAR OPlantInspectionLine@1100528800 : TEMPORARY Record 11072684;LineNo@1100528802 : Integer);
    VAR
      ServiceOrderInspectionLine@1100528801 : Record 11071753;
    BEGIN
      ServiceOrderInspectionLine.INIT;
      ServiceOrderInspectionLine."Service Order No." := "Service Order No.";
      ServiceOrderInspectionLine."Service Object No." := '';
      ServiceOrderInspectionLine."Maintenance Scheme Code" := "Maintenance Scheme";
      ServiceOrderInspectionLine."Line No." := LineNo;
      ServiceOrderInspectionLine."Inspection Type" := ServiceOrderInspectionLine."Inspection Type"::Plant;
      ServiceOrderInspectionLine."Service Contract No." := '';
      ServiceOrderInspectionLine.Code :=  OPlantInspectionLine."Level Code";
      ServiceOrderInspectionLine."Belongs to Scheme" := OPlantInspectionLine."Maintenance Scheme";
      ServiceOrderInspectionLine."Index Date" := OPlantInspectionLine."Index Date";
      ServiceOrderInspectionLine."Inspection Line No." := OPlantInspectionLine."Line No.";
      ServiceOrderInspectionLine.Type := OPlantInspectionLine.Type;
      ServiceOrderInspectionLine."Inspection Code" := OPlantInspectionLine."Inspection Code";
      ServiceOrderInspectionLine.Description := OPlantInspectionLine.Description;
      ServiceOrderInspectionLine."Field Type" := OPlantInspectionLine."Field Type";
      ServiceOrderInspectionLine."Unit Of Measure" := OPlantInspectionLine."Unit Of Measure";
      ServiceOrderInspectionLine."Inspection Group Code" := OPlantInspectionLine."Inspection Group Code";
      ServiceOrderInspectionLine.Mandatory := OPlantInspectionLine.Mandatory;
      ServiceOrderInspectionLine."Belongs to Base Insp. List" := OPlantInspectionLine."Base Inspection List";
      ServiceOrderInspectionLine."Plant Type" := OPlantInspectionLine."Plant Type";
      ServiceOrderInspectionLine.INSERT;
    END;

    LOCAL PROCEDURE DeleteMaintenanceSchemeServiceOrderBudgetLines@1100528803(iMaintenanceScheme@1100528801 : Code[20]);
    VAR
      ServiceOrderBudgetLine@1100528800 : Record 11012875;
    BEGIN
      ServiceOrderBudgetLine.SETRANGE("Budget Type", ServiceOrderBudgetLine."Budget Type"::Order);
      ServiceOrderBudgetLine.SETRANGE("Service Order No.", "Service Order No.");
      ServiceOrderBudgetLine.SETRANGE("Service Contract No.", '');
      ServiceOrderBudgetLine.SETRANGE("Object No.", '');
      ServiceOrderBudgetLine.SETRANGE("Maintenance Scheme Type", ServiceOrderBudgetLine."Maintenance Scheme Type"::Plant);
      ServiceOrderBudgetLine.SETRANGE("Maintenance Scheme", iMaintenanceScheme);
      ServiceOrderBudgetLine.DELETEALL;
    END;

    BEGIN
    END.
  }
}

