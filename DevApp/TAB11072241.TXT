OBJECT Table 11072241 Serv. Surv. Summary Sheet
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServSurvSummarySheet@1100528600 : Record 11072241;
             BEGIN
               IF (Code = '') AND ("Summary Type" = "Summary Type"::Survey) THEN BEGIN
                 ServSurvSummarySheet.SETRANGE("Summary Type", "Summary Type");
                 ServSurvSummarySheet.SETRANGE("Survey No.", "Survey No.");
                 IF ServSurvSummarySheet.FINDLAST THEN
                   Code := INCSTR(ServSurvSummarySheet.Code)
                 ELSE
                   Code := '001';
               END;
             END;

    OnDelete=VAR
               ServSurvSummarySheetLine@1100528600 : Record 11072242;
             BEGIN
               ServSurvSummarySheetLine.SETRANGE("Summary Type", "Summary Type");
               ServSurvSummarySheetLine.SETRANGE("Survey No.", "Survey No.");
               ServSurvSummarySheetLine.SETRANGE(Code, Code);
               ServSurvSummarySheetLine.SETRANGE("Summary Sheet Seq. No.", "Seq. No.");
               ServSurvSummarySheetLine.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Service Survey Summary Sheet;
    LookupPageID=Page11229408;
    DrillDownPageID=Page11229408;
  }
  FIELDS
  {
    { 10  ;   ;Summary Type        ;Option        ;CaptionML=ENU=Summary Type;
                                                   OptionCaptionML=ENU=Standard,Survey;
                                                   OptionString=Standard,Survey;
                                                   Editable=No }
    { 30  ;   ;Code                ;Code10        ;OnValidate=VAR
                                                                ServSurvSummarySheet@1100528600 : Record 11072241;
                                                              BEGIN
                                                                IF "Seq. No." = 0 THEN BEGIN
                                                                  ServSurvSummarySheet.SETRANGE("Summary Type", "Summary Type");
                                                                  ServSurvSummarySheet.SETRANGE(Code, Code);
                                                                  ServSurvSummarySheet.SETRANGE("Survey No.", "Survey No.");
                                                                  IF ServSurvSummarySheet.FINDLAST THEN
                                                                    "Seq. No." := ServSurvSummarySheet."Seq. No." +1
                                                                  ELSE
                                                                    "Seq. No." := 1;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 40  ;   ;Survey No.          ;Code20        ;TableRelation=IF (Summary Type=CONST(Survey)) "Service Survey".No.;
                                                   OnValidate=BEGIN
                                                                IF "Survey No." <> '' THEN
                                                                  TESTFIELD("Summary Type", "Summary Type"::Survey);
                                                              END;

                                                   CaptionML=ENU=Survey No.;
                                                   Editable=No }
    { 45  ;   ;Seq. No.            ;Integer       ;OnValidate=VAR
                                                                ServSurvSummarySheetLine@1100528600 : Record 11072242;
                                                              BEGIN
                                                                IF "Seq. No." <> xRec."Seq. No." THEN BEGIN
                                                                  ServSurvSummarySheetLine.SETRANGE("Summary Type", "Summary Type");
                                                                  ServSurvSummarySheetLine.SETRANGE("Survey No.", "Survey No.");
                                                                  ServSurvSummarySheetLine.SETRANGE(Code, Code);
                                                                  ServSurvSummarySheetLine.SETRANGE("Summary Sheet Seq. No.", xRec."Seq. No.");
                                                                  WHILE ServSurvSummarySheetLine.FINDSET(TRUE, TRUE) DO
                                                                    ServSurvSummarySheetLine.RENAME("Summary Type", "Survey No.", Code, "Seq. No.", ServSurvSummarySheetLine."Row No.");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Seq. No.;
                                                   MinValue=1;
                                                   NotBlank=Yes }
    { 50  ;   ;Index Date          ;Date          ;CaptionML=ENU=Index Date }
    { 60  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
  }
  KEYS
  {
    {    ;Summary Type,Survey No.,Code,Seq. No.   ;Clustered=Yes }
    {    ;Summary Type,Survey No.,Code,Index Date  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CopyFrom@1100528600(IFromServSurvSummarySheet@1100528607 : Record 11072241);
    VAR
      FromServSurvSummarySheetLine@1100528604 : Record 11072242;
      ToServSurvSummarySheetLine@1100528605 : Record 11072242;
      SummaryType@1100528606 : Option;
    BEGIN
      IF Description = '' THEN BEGIN
        VALIDATE(Description, IFromServSurvSummarySheet.Description);
        MODIFY(TRUE);
      END;

      FromServSurvSummarySheetLine.SETRANGE("Summary Type", IFromServSurvSummarySheet."Summary Type");
      FromServSurvSummarySheetLine.SETRANGE("Survey No.", IFromServSurvSummarySheet."Survey No.");
      FromServSurvSummarySheetLine.SETRANGE(Code, IFromServSurvSummarySheet.Code);
      FromServSurvSummarySheetLine.SETRANGE("Summary Sheet Seq. No.", IFromServSurvSummarySheet."Seq. No.");
      IF FromServSurvSummarySheetLine.FINDSET THEN
        REPEAT
          ToServSurvSummarySheetLine := FromServSurvSummarySheetLine;
          ToServSurvSummarySheetLine."Summary Type" := "Summary Type";
          ToServSurvSummarySheetLine.Code := Code;
          ToServSurvSummarySheetLine."Survey No." := "Survey No.";
          ToServSurvSummarySheetLine."Summary Sheet Seq. No." := "Seq. No.";
          IF ToServSurvSummarySheetLine."Cost Type" = ToServSurvSummarySheetLine."Cost Type"::Labor THEN
            ToServSurvSummarySheetLine.VALIDATE("Rate Code");
          ToServSurvSummarySheetLine.INSERT(TRUE);
        UNTIL FromServSurvSummarySheetLine.NEXT = 0;

      ReCalcSummaryLines;
    END;

    PROCEDURE ReCalcSummaryLines@1210190002();
    VAR
      ServSurvSummarySheetLine@1210190000 : Record 11072242;
    BEGIN
      ServSurvSummarySheetLine.SETRANGE("Summary Type", "Summary Type");
      ServSurvSummarySheetLine.SETRANGE(Code, Code);
      ServSurvSummarySheetLine.SETRANGE("Survey No.", "Survey No.");
      ServSurvSummarySheetLine.SETRANGE("Summary Sheet Seq. No.", "Seq. No.");
      IF ServSurvSummarySheetLine.FINDSET THEN BEGIN
        REPEAT
          ServSurvSummarySheetLine.CalcTotals;
          ServSurvSummarySheetLine.MODIFY;
        UNTIL ServSurvSummarySheetLine.NEXT = 0;
      END;
    END;

    PROCEDURE NewerVersionExists@1100528601() : Boolean;
    VAR
      ServSurvSummarySheet@1100528600 : Record 11072241;
    BEGIN
      ServSurvSummarySheet := Rec;
      ServSurvSummarySheet.SETCURRENTKEY("Summary Type", "Survey No.", Code, "Index Date");
      ServSurvSummarySheet.SETRECFILTER;
      ServSurvSummarySheet.SETRANGE("Seq. No.");
      ServSurvSummarySheet.SETFILTER("Index Date", '>=%1', "Index Date");
      IF ServSurvSummarySheet.FINDLAST THEN BEGIN
        IF ServSurvSummarySheet."Index Date" > "Index Date" THEN
          EXIT(TRUE);
        IF ServSurvSummarySheet."Seq. No." > "Seq. No." THEN
          EXIT(TRUE);
      END;
    END;

    PROCEDURE GetSummaryTotal@1100528602() : Decimal;
    VAR
      ServSurvSummarySheetLine@1100528600 : Record 11072242;
    BEGIN
      ServSurvSummarySheetLine.SETRANGE("Summary Type", ServSurvSummarySheetLine."Summary Type"::Survey);
      ServSurvSummarySheetLine.SETRANGE("Survey No.", "Survey No.");
      ServSurvSummarySheetLine.SETRANGE(Code, Code);
      ServSurvSummarySheetLine.SETRANGE("Summary Sheet Seq. No.", "Seq. No.");
      IF ServSurvSummarySheetLine.FINDLAST THEN
        ServSurvSummarySheetLine.CALCFIELDS("Summary Total Amount");
      EXIT(ServSurvSummarySheetLine."Summary Total Amount");
    END;

    BEGIN
    END.
  }
}

