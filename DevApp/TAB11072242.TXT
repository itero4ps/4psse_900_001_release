OBJECT Table 11072242 Serv. Surv. Summary Sheet Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Service Survey Summary Sheet Line;
  }
  FIELDS
  {
    { 10  ;   ;Summary Type        ;Option        ;CaptionML=ENU=Summary Type;
                                                   OptionCaptionML=ENU=Standard,Survey;
                                                   OptionString=Standard,Survey;
                                                   Editable=No }
    { 30  ;   ;Code                ;Code10        ;TableRelation="Serv. Surv. Summary Sheet".Code WHERE (Summary Type=FIELD(Summary Type));
                                                   CaptionML=ENU=Code;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 40  ;   ;Survey No.          ;Code20        ;TableRelation=IF (Summary Type=CONST(Survey)) "Service Survey".No.;
                                                   CaptionML=ENU=Survey No.;
                                                   Editable=No }
    { 45  ;   ;Summary Sheet Seq. No.;Integer     ;CaptionML=ENU=Summary Seq. No.;
                                                   MinValue=1 }
    { 50  ;   ;Index Date OBSOLETE ;Date          ;CaptionML=ENU=Index Date OBSOLETE;
                                                   Description=OBSOLETE;
                                                   Editable=No }
    { 55  ;   ;Index Date          ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Serv. Surv. Summary Sheet"."Index Date" WHERE (Summary Type=FIELD(Summary Type),
                                                                                                                      Survey No.=FIELD(Survey No.),
                                                                                                                      Code=FIELD(Code),
                                                                                                                      Seq. No.=FIELD(Summary Sheet Seq. No.)));
                                                   CaptionML=ENU=Index Date;
                                                   Editable=No }
    { 60  ;   ;Row No.             ;Code10        ;CaptionML=ENU=Row No.;
                                                   NotBlank=Yes;
                                                   Numeric=Yes }
    { 70  ;   ;Cost Type           ;Option        ;InitValue=Sundry;
                                                   OnValidate=BEGIN
                                                                IF "Cost Object" <> '' THEN BEGIN
                                                                  IF "Cost Type" <> xRec."Cost Type" THEN
                                                                    IF CONFIRM(Text001,TRUE) THEN BEGIN
                                                                      "Cost Object" := '';
                                                                    END ELSE BEGIN
                                                                      "Cost Type" := xRec."Cost Type";
                                                                      EXIT;
                                                                    END;
                                                                END;

                                                                IF (Type = Type::Surcharge) AND ("Cost Type" = "Cost Type"::Labor) THEN
                                                                  VALIDATE("Cost Type Filter", "Cost Type Filter"::Labor);

                                                                IF "Cost Type" <> xRec."Cost Type" THEN BEGIN
                                                                  IF "Cost Type" <> "Cost Type"::Labor THEN BEGIN
                                                                    Norm := 0;
                                                                    "Rate Code" := '';
                                                                    Rate := 0;
                                                                  END;
                                                                  IF "Cost Type" = "Cost Type"::Labor THEN
                                                                    Price := 0;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Cost Type;
                                                   OptionCaptionML=ENU=Labor,Material,Subcontracting,Plant,Sundry;
                                                   OptionString=Labor,Material,Subcontracting,Plant,Sundry }
    { 80  ;   ;Cost Object         ;Code20        ;TableRelation=IF (Cost Type=CONST(Labor)) "Dimension Value".Code WHERE (Cost Type=FILTER(Labor),
                                                                                                                           Global Dimension No.=CONST(2))
                                                                                                                           ELSE IF (Cost Type=CONST(Material)) "Dimension Value".Code WHERE (Cost Type=FILTER(Material),
                                                                                                                                                                                             Global Dimension No.=CONST(2))
                                                                                                                                                                                             ELSE IF (Cost Type=CONST(Plant)) "Dimension Value".Code WHERE (Cost Type=FILTER(Plant),
                                                                                                                                                                                                                                                            Global Dimension No.=CONST(2))
                                                                                                                                                                                                                                                            ELSE IF (Cost Type=CONST(Sundry)) "Dimension Value".Code WHERE (Cost Type=FILTER(Sundry),
                                                                                                                                                                                                                                                                                                                            Global Dimension No.=CONST(2))
                                                                                                                                                                                                                                                                                                                            ELSE IF (Cost Type=CONST(Subcontracting)) "Dimension Value".Code WHERE (Cost Type=FILTER(Subcontracting),
                                                                                                                                                                                                                                                                                                                                                                                                    Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                IF "Cost Object" <> '' THEN
                                                                  IF xRec."Cost Object" = '' THEN
                                                                    GetDefaultsCostObject()
                                                                  ELSE
                                                                    IF "Cost Object" <> xRec."Cost Object" THEN
                                                                      IF CONFIRM(
                                                                        Text002 +
                                                                        Text003)
                                                                      THEN
                                                                        GetDefaultsCostObject();
                                                              END;

                                                   CaptionML=ENU=Cost Object }
    { 90  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 100 ;   ;Type                ;Option        ;InitValue=Additional Costs;
                                                   OnValidate=BEGIN
                                                                IF (Type = Type::Surcharge) AND ("Cost Type" = "Cost Type"::Labor) THEN
                                                                  VALIDATE("Cost Type Filter","Cost Type Filter"::Labor)
                                                                ELSE
                                                                  "Cost Type Filter" := "Cost Type Filter"::All;

                                                                ResetValueFields();
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Text,Surcharge,Additional Costs,Subtotal;
                                                   OptionString=Text,Surcharge,Additional Costs,Subtotal }
    { 130 ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF (Quantity <> 0) AND (Type = Type::Subtotal) THEN
                                                                  TESTFIELD(Totaling);
                                                                IF CurrFieldNo = FIELDNO(Quantity) THEN
                                                                  CalcTotals;
                                                              END;

                                                   CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 140 ;   ;Norm                ;Decimal       ;OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO(Norm) THEN
                                                                  CalcTotals;
                                                              END;

                                                   CaptionML=ENU=Norm;
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 150 ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure".Code;
                                                   CaptionML=ENU=Unit of Measure }
    { 160 ;   ;Rate Code           ;Code10        ;TableRelation="Budget Rate";
                                                   OnValidate=VAR
                                                                ServiceSurvey@1100528602 : Record 11071803;
                                                                BudgetRateManagement@1100528600 : Codeunit 11012358;
                                                                BudgetRateFoundAt@1100528601 : Text[250];
                                                              BEGIN
                                                                IF "Rate Code" <> '' THEN
                                                                  TESTFIELD("Cost Type", "Cost Type"::Labor);
                                                                IF ("Cost Type" = "Cost Type"::Labor) THEN BEGIN
                                                                  IF NOT ServiceSurvey.GET("Survey No.") THEN
                                                                    ServiceSurvey.INIT;
                                                                  CALCFIELDS("Index Date");
                                                                  Rate := BudgetRateManagement.GetBudgetRate(
                                                                    1, '', '', '', ServiceSurvey."Global Dimension 1 Code", "Rate Code", "Index Date", BudgetRateFoundAt);
                                                                  VALIDATE(Rate);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Rate Code }
    { 170 ;   ;Rate                ;Decimal       ;OnValidate=BEGIN
                                                                CalcTotals;
                                                              END;

                                                   CaptionML=ENU=Rate;
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 180 ;   ;Price               ;Decimal       ;OnValidate=BEGIN
                                                                CASE "Cost Type" OF
                                                                  "Cost Type"::Labor:
                                                                    BEGIN
                                                                      MESSAGE(Text004);
                                                                      Price := 0;
                                                                    END;
                                                                END;
                                                                IF CurrFieldNo = FIELDNO(Price) THEN
                                                                  CalcTotals;
                                                              END;

                                                   CaptionML=ENU=Price;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 190 ;   ;Hours               ;Decimal       ;CaptionML=ENU=Hours;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 200 ;   ;Amount              ;Decimal       ;CaptionML=ENU=Amount;
                                                   BlankZero=Yes;
                                                   AutoFormatType=1 }
    { 210 ;   ;Subtotal            ;Decimal       ;CaptionML=ENU=Subtotal;
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 220 ;   ;Cost Type Filter    ;Option        ;InitValue=All;
                                                   OnValidate=BEGIN
                                                                CalcTotals;
                                                              END;

                                                   CaptionML=ENU=Cost Type Filter;
                                                   OptionCaptionML=ENU="Labor,Material,Subcontracting,Plant,Sundry,All, ";
                                                   OptionString=[Labor,Material,Subcontracting,Plant,Sundry,All, ] }
    { 230 ;   ;Cost Object Filter  ;Text80        ;TableRelation=IF (Cost Type Filter=FILTER(All)) "Dimension Value".Code WHERE (Global Dimension No.=CONST(2))
                                                                 ELSE IF (Cost Type Filter=FILTER(<>All)) "Dimension Value".Code WHERE (Global Dimension No.=CONST(2),
                                                                                                                                        Cost Type=FIELD(Cost Type Filter));
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Cost Object Filter }
    { 240 ;   ;Rate Code Filter    ;Text80        ;TableRelation="Budget Rate";
                                                   CaptionML=ENU=Rate Code Filter }
    { 250 ;   ;Totaling            ;Text250       ;InitValue=NVT;
                                                   OnValidate=BEGIN
                                                                CalcTotals;
                                                              END;

                                                   CaptionML=ENU=Totaling }
    { 300 ;   ;Summary Total Labor ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Serv. Surv. Summary Sheet Line".Amount WHERE (Summary Type=FIELD(Summary Type),
                                                                                                                  Code=FIELD(Code),
                                                                                                                  Survey No.=FIELD(Survey No.),
                                                                                                                  Summary Sheet Seq. No.=FIELD(Summary Sheet Seq. No.),
                                                                                                                  Cost Type=CONST(Labor)));
                                                   CaptionML=ENU=Summary Total Labor;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 310 ;   ;Summary Total Material;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Serv. Surv. Summary Sheet Line".Amount WHERE (Summary Type=FIELD(Summary Type),
                                                                                                                  Code=FIELD(Code),
                                                                                                                  Survey No.=FIELD(Survey No.),
                                                                                                                  Summary Sheet Seq. No.=FIELD(Summary Sheet Seq. No.),
                                                                                                                  Cost Type=CONST(Material)));
                                                   CaptionML=ENU=Summary Total Material;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 320 ;   ;Summary Total Subc. ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Serv. Surv. Summary Sheet Line".Amount WHERE (Summary Type=FIELD(Summary Type),
                                                                                                                  Code=FIELD(Code),
                                                                                                                  Survey No.=FIELD(Survey No.),
                                                                                                                  Summary Sheet Seq. No.=FIELD(Summary Sheet Seq. No.),
                                                                                                                  Cost Type=CONST(Subcontracting)));
                                                   CaptionML=ENU=Summary Total Subc.;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 330 ;   ;Summary Total Plant ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Serv. Surv. Summary Sheet Line".Amount WHERE (Summary Type=FIELD(Summary Type),
                                                                                                                  Code=FIELD(Code),
                                                                                                                  Survey No.=FIELD(Survey No.),
                                                                                                                  Summary Sheet Seq. No.=FIELD(Summary Sheet Seq. No.),
                                                                                                                  Cost Type=CONST(Plant)));
                                                   CaptionML=ENU=Summary Total Plant;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 340 ;   ;Summary Total Sundry;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Serv. Surv. Summary Sheet Line".Amount WHERE (Summary Type=FIELD(Summary Type),
                                                                                                                  Code=FIELD(Code),
                                                                                                                  Survey No.=FIELD(Survey No.),
                                                                                                                  Summary Sheet Seq. No.=FIELD(Summary Sheet Seq. No.),
                                                                                                                  Cost Type=CONST(Sundry)));
                                                   CaptionML=ENU=Summary Total Sundry;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 350 ;   ;Summary Total Amount;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Serv. Surv. Summary Sheet Line".Amount WHERE (Summary Type=FIELD(Summary Type),
                                                                                                                  Code=FIELD(Code),
                                                                                                                  Survey No.=FIELD(Survey No.),
                                                                                                                  Summary Sheet Seq. No.=FIELD(Summary Sheet Seq. No.)));
                                                   CaptionML=ENU=Summary Total Amount;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 360 ;   ;Summary Total Hours ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Serv. Surv. Summary Sheet Line".Hours WHERE (Summary Type=FIELD(Summary Type),
                                                                                                                 Code=FIELD(Code),
                                                                                                                 Survey No.=FIELD(Survey No.),
                                                                                                                 Summary Sheet Seq. No.=FIELD(Summary Sheet Seq. No.)));
                                                   CaptionML=ENU=Summary Total Hours;
                                                   Editable=No }
    { 370 ;   ;Time Quantity       ;Decimal       ;OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO("Time Quantity") THEN
                                                                  CalcTotals;
                                                              END;

                                                   CaptionML=ENU=Time Quantity;
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 380 ;   ;Unit of Time        ;Code10        ;TableRelation="Unit of Measure".Code;
                                                   CaptionML=ENU=Unit of Time }
  }
  KEYS
  {
    {    ;Summary Type,Survey No.,Code,Summary Sheet Seq. No.,Row No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100528600 : TextConst 'ENU=Change Cost Type ?';
      Text002@1100528601 : TextConst 'ENU=Copy Data from new Cost Object?\\';
      Text003@1100528602 : TextConst 'ENU=PS. Alleen lege velden worden gevuld. Bestaande gegevens worden niet overschreven.';
      Text004@1100528603 : TextConst 'ENU=Unit Price not allowed because Cost Type of this record is Labor !';

    LOCAL PROCEDURE GetDefaultsCostObject@3();
    VAR
      ServiceSurvey@1100528604 : Record 11071803;
      DimensionValue@1100528603 : Record 349;
      BudgetRateManagement@1100528600 : Codeunit 11012358;
      DimensionManagement@1100528602 : Codeunit 408;
      BudgetRateFoundAt@1100528601 : Text[250];
    BEGIN
      DimensionManagement.GetDimValueRec(2, "Cost Object", DimensionValue, TRUE, '');

      IF Description = '' THEN
        Description := DimensionValue.Name;
      IF "Unit of Measure" = '' THEN
        "Unit of Measure" := DimensionValue."Unit of Measure";

      CASE "Cost Type" OF
        "Cost Type"::Labor:
          BEGIN
            IF Norm = 0 THEN
              Norm := DimensionValue.Norm;
            IF NOT ServiceSurvey.GET("Survey No.") THEN
              ServiceSurvey.INIT;
            IF ("Rate Code" = '') AND (Rate = 0) THEN BEGIN
              "Rate Code" := DimensionValue."Rate Code";
              CALCFIELDS("Index Date");
              Rate := BudgetRateManagement.GetBudgetRate(
                1, '', '', '', ServiceSurvey."Global Dimension 1 Code", "Rate Code", "Index Date", BudgetRateFoundAt);
            END;
          END;
        ELSE
          IF Price = 0 THEN
            Price := DimensionValue.Price;
      END;
    END;

    LOCAL PROCEDURE ResetValueFields@1210190000();
    BEGIN
      Quantity := 0;
      Norm := 0;
      "Unit of Measure" := '';
      "Rate Code" := '';
      Rate := 0;
      Price := 0;
      Hours := 0;
      Amount := 0;
      IF Type = Type::"Additional Costs" THEN
        "Time Quantity" := 1
      ELSE
        "Time Quantity" := 0;
      "Cost Object Filter" := '';
      "Cost Type Filter" := "Cost Type Filter"::All;
      Totaling := '';
      Subtotal := 0;
    END;

    PROCEDURE CalcTotals@1();
    VAR
      ServSurvLocationObject@1210190000 : Record 11071813;
      ServSurvSummarySheetLine@1210190001 : Record 11072242;
      TotalLabor@1100528600 : Decimal;
      TotalHours@1100528601 : Decimal;
      TotalMaterial@1100528602 : Decimal;
      TotalSubcontracting@1100528603 : Decimal;
      TotalPlant@1100528604 : Decimal;
      TotalSundry@1100528605 : Decimal;
      TotalCost@1100528606 : Decimal;
      TotalSurcharge@1100528607 : Decimal;
    BEGIN
      IF Type = Type::"Additional Costs" THEN BEGIN
        IF "Cost Type" <> "Cost Type"::Labor THEN BEGIN
          Hours := 0;
          Amount := ROUND("Time Quantity" * Quantity * Price);
        END ELSE BEGIN
          Price := 0;
          Hours := ROUND("Time Quantity" * Quantity * Norm);
          Amount := ROUND("Time Quantity" * Quantity * Norm * Rate);
        END;
      END;

      IF ((Type = Type::Surcharge) OR (Type = Type::Subtotal)) AND ("Survey No." <> '') THEN BEGIN
        Amount := 0;
        Hours := 0;
        ServSurvLocationObject.SETRANGE("Survey No.", "Survey No.");
        IF ServSurvLocationObject.FINDSET THEN BEGIN
          REPEAT
            ServSurvLocationObject.GetTotalAmountByCostObject(
              "Cost Object Filter", "Rate Code Filter", TotalLabor, TotalHours, TotalMaterial, TotalSubcontracting,
              TotalPlant, TotalSundry, TotalCost, TotalSurcharge);

            CASE "Cost Type Filter" OF
              "Cost Type Filter"::Labor:
                BEGIN
                  Amount += TotalLabor;
                  Hours += TotalHours;
                END;
              "Cost Type Filter"::Material:
                Amount += TotalMaterial;
              "Cost Type Filter"::Subcontracting:
                Amount += TotalSubcontracting;
              "Cost Type Filter"::Plant:
                Amount += TotalPlant;
              "Cost Type Filter"::Sundry:
                Amount += TotalSundry;
              "Cost Type Filter"::All:
                BEGIN
                  Amount += TotalCost;
                  Hours += TotalHours;
                END;
            END;
          UNTIL ServSurvLocationObject.NEXT = 0;
        END;

        IF (Type = Type::Subtotal) AND (Totaling <> '') THEN BEGIN
          ServSurvSummarySheetLine.SETRANGE("Summary Type", "Summary Type");
          ServSurvSummarySheetLine.SETRANGE(Code, Code);
          ServSurvSummarySheetLine.SETRANGE("Survey No.", "Survey No.");
          ServSurvSummarySheetLine.SETRANGE("Summary Sheet Seq. No.", "Summary Sheet Seq. No.");
          ServSurvSummarySheetLine.SETFILTER("Row No.", Totaling);
          IF ServSurvSummarySheetLine.FINDSET THEN BEGIN
            REPEAT
              Amount += ServSurvSummarySheetLine.Amount;
              IF ServSurvSummarySheetLine."Cost Type" = ServSurvSummarySheetLine."Cost Type"::Labor THEN
                Hours += ServSurvSummarySheetLine.Hours;
            UNTIL ServSurvSummarySheetLine.NEXT = 0;
          END;
        END;

        IF Type = Type::Subtotal THEN BEGIN
          VALIDATE(Subtotal, Amount);
          IF Quantity = 0 THEN BEGIN
            Amount := 0;
            Hours := 0;
          END;
        END ELSE
          Subtotal := 0;

        ComputeSurcharge();
      END;
    END;

    PROCEDURE ComputeSurcharge@1210190008();
    BEGIN
      IF (Type <> Type::Surcharge) AND (Type <> Type::Subtotal) THEN
        EXIT;
      IF "Cost Type" <> "Cost Type"::Labor  THEN BEGIN
        VALIDATE(Price, ROUND(Amount / 100.0));
        VALIDATE(Amount, ROUND(Amount * Quantity / 100.0));
      END ELSE BEGIN
        VALIDATE(Norm, ROUND(Hours / 100.0));
        VALIDATE(Amount, ROUND(Hours * Quantity / 100.0 * Rate));
        VALIDATE(Hours, ROUND(Hours * Quantity / 100.0));
      END;
    END;

    PROCEDURE IsCostComponentEditable@1100528600() : Boolean;
    BEGIN
      EXIT(TRUE);
    END;

    PROCEDURE IsKindOfSurchargeEditable@1100528601() : Boolean;
    BEGIN
      EXIT((Type = Type::Surcharge) OR (Type = Type::Subtotal));
    END;

    PROCEDURE IsQuantityEditable@1100528602() : Boolean;
    BEGIN
      EXIT(Type <> Type::Text);
    END;

    PROCEDURE IsUnitOfMeasureEditable@1100528603() : Boolean;
    BEGIN
      EXIT((Type <> Type::Text) AND (Type <> Type::Subtotal));
    END;

    PROCEDURE IsRateEditable@1100528611() : Boolean;
    BEGIN
      EXIT(("Cost Type" = "Cost Type"::Labor) AND
           (Type <> Type::Text) AND (Type <> Type::Subtotal));// AND (Type <> Type::"Additional Costs"));
    END;

    PROCEDURE IsRateCodeEditable@1100528604() : Boolean;
    BEGIN
      EXIT(("Cost Type" = "Cost Type"::Labor) AND
           (Type <> Type::Text) AND (Type <> Type::Subtotal));// AND (Type <> Type::"Additional Costs"));
    END;

    PROCEDURE IsNormEditable@1100528605() : Boolean;
    BEGIN
      EXIT(("Cost Type" = "Cost Type"::Labor) AND
           (Type <> Type::Text) AND (Type <> Type::Surcharge) AND (Type <> Type::Subtotal));// AND (Type <> Type::"Additional Costs"));
    END;

    PROCEDURE IsPriceEditable@1100528606() : Boolean;
    BEGIN
      EXIT(("Cost Type" <> "Cost Type"::Labor) AND
           (Type <> Type::Text) AND (Type <> Type::Surcharge) AND (Type <> Type::Subtotal));
    END;

    PROCEDURE IsAmountEditable@1100528607() : Boolean;
    BEGIN
      EXIT(("Cost Type" = "Cost Type"::Labor) AND (Norm = 0) AND (Quantity = 0) AND
           (Type <> Type::Text) AND (Type <> Type::Surcharge) AND (Type <> Type::Subtotal));
    END;

    PROCEDURE IsHoursEditable@1100528614() : Boolean;
    BEGIN
      EXIT(("Cost Type" = "Cost Type"::Labor) AND (Norm = 0) AND (Quantity = 0) AND
           (Type <> Type::Text) AND (Type <> Type::Surcharge) AND (Type <> Type::Subtotal));
    END;

    PROCEDURE IsAmountHoursEditable@1100528615() : Boolean;
    BEGIN
    END;

    PROCEDURE IsCostTypeFilterEditable@1100528608() : Boolean;
    BEGIN
      EXIT((Type = Type::Surcharge) OR (Type = Type::Subtotal));
    END;

    PROCEDURE IsCostObjectFilterEditable@1100528609() : Boolean;
    BEGIN
      EXIT((Type = Type::Surcharge) OR (Type = Type::Subtotal));
    END;

    PROCEDURE IsRateCodeFilterEditable@1100528616() : Boolean;
    BEGIN
      EXIT((Type = Type::Surcharge) OR (Type = Type::Subtotal));
    END;

    PROCEDURE IsTotalingEditable@1100528610() : Boolean;
    BEGIN
      EXIT(Type = Type::Subtotal);
    END;

    PROCEDURE IsTimeQuantityEditable@1100528612() : Boolean;
    BEGIN
      EXIT(Type = Type::"Additional Costs");
    END;

    PROCEDURE IsUnitOfTimeEditable@1100528613() : Boolean;
    BEGIN
      EXIT(Type = Type::"Additional Costs");
    END;

    BEGIN
    END.
  }
}

