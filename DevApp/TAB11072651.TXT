OBJECT Table 11072651 Dispatch Note SiB
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               lvDispatchNoteLineRec@1100525000 : Record 11072652;
             BEGIN
               lvDispatchNoteLineRec.SETRANGE("Dispatch Note No.", "No.");
               lvDispatchNoteLineRec.DELETEALL;
             END;

    CaptionML=ENU=Dispatch Note SiB;
    LookupPageID=Page11072653;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;CaptionML=ENU=No.;
                                                   Editable=No }
    { 20  ;   ;Date                ;Date          ;CaptionML=ENU=Date;
                                                   Editable=No }
    { 30  ;   ;Reference Buyer     ;Text30        ;CaptionML=ENU=Reference Buyer;
                                                   Editable=No }
    { 40  ;   ;Order No. Buyer     ;Text30        ;CaptionML=ENU=Order No. Buyer;
                                                   Editable=No }
    { 50  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   OnValidate=VAR
                                                                ProjRec@1210190000 : Record 11072003;
                                                              BEGIN
                                                                //DP00065.sn
                                                                IF "Project No." <> '' THEN BEGIN
                                                                  TESTFIELD("Service Order No.", '');  //db, 16-09-14
                                                                  ProjRec.GET("Project No.");
                                                                  ProjRec.CheckProjectStatus(2);
                                                                  UpdateDispatchLines(FIELDNO("Project No."));  //db, 16-09-14
                                                                END;
                                                                //DP00065.en
                                                                CALCFIELDS("Project Description");
                                                                UpdateNotApprovedInvoiceLines(FIELDNO("Project No."));  //db, 16-09-14
                                                              END;

                                                   CaptionML=ENU=Project No. }
    { 51  ;   ;Project Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=ENU=Project Description;
                                                   Editable=No }
    { 60  ;   ;Order Confirmation No.;Text20      ;CaptionML=ENU=Order Confirmation No.;
                                                   Editable=No }
    { 70  ;   ;Planned Delivery Date/Time;DateTime;CaptionML=ENU=Planned Delivery Date/Time;
                                                   Editable=No }
    { 200 ;   ;GLN Buyer           ;Code20        ;CaptionML=ENU=GLN Buyer;
                                                   Editable=No }
    { 210 ;   ;Name Buyer          ;Text50        ;CaptionML=ENU=Name Buyer;
                                                   Editable=No }
    { 215 ;   ;Address Buyer       ;Text50        ;CaptionML=ENU=Address Buyer;
                                                   Editable=No }
    { 220 ;   ;Post Code Buyer     ;Code10        ;CaptionML=ENU=Post Code Buyer;
                                                   Editable=No }
    { 225 ;   ;City Buyer          ;Text50        ;CaptionML=ENU=City Buyer;
                                                   Editable=No }
    { 230 ;   ;Country Buyer       ;Code10        ;CaptionML=ENU=Country Buyer;
                                                   Editable=No }
    { 300 ;   ;GLN Vendor          ;Code20        ;TableRelation="ETIM Vendor (Central Mgt)";
                                                   CaptionML=ENU=GLN Vendor;
                                                   Editable=No }
    { 301 ;   ;GLN Alternative Vendor;Code20      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ETIM Vendor (Central Mgt)"."Alternative Vendor Code" WHERE (Code=FIELD(GLN Vendor)));
                                                   CaptionML=ENU=GLN Alternative Vendor;
                                                   Editable=No }
    { 310 ;   ;Name Vendor         ;Text50        ;CaptionML=ENU=Name Vendor;
                                                   Editable=No }
    { 315 ;   ;Address Vendor      ;Text50        ;CaptionML=ENU=Address Vendor;
                                                   Editable=No }
    { 320 ;   ;Post Code Vendor    ;Code20        ;CaptionML=ENU=Post Code Vendor;
                                                   Editable=No }
    { 325 ;   ;City Vendor         ;Text50        ;CaptionML=ENU=City Vendor;
                                                   Editable=No }
    { 330 ;   ;Country Vendor      ;Code10        ;CaptionML=ENU=Country Vendor;
                                                   Editable=No }
    { 340 ;   ;Contact Person Vendor;Text50       ;CaptionML=ENU=Contact Person Vendor;
                                                   Editable=No }
    { 345 ;   ;Phone No. Contact Vendor;Text30    ;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Phone No. Contact Vendor;
                                                   Editable=No }
    { 400 ;   ;GLN Shipping        ;Code20        ;CaptionML=ENU=GLN Shipping;
                                                   Editable=No }
    { 410 ;   ;Name Shipping       ;Text50        ;CaptionML=ENU=Name Shipping;
                                                   Editable=No }
    { 415 ;   ;Address Shipping    ;Text50        ;CaptionML=ENU=Address Shipping;
                                                   Editable=No }
    { 420 ;   ;Post Code Shipping  ;Code20        ;CaptionML=ENU=Post Code Shipping;
                                                   Editable=No }
    { 425 ;   ;City Shipping       ;Text50        ;CaptionML=ENU=City Shipping;
                                                   Editable=No }
    { 430 ;   ;Country Shipping    ;Code10        ;CaptionML=ENU=Country Shipping;
                                                   Editable=No }
    { 500 ;   ;GLN Delivery        ;Code20        ;CaptionML=ENU=GLN Delivery;
                                                   Editable=No }
    { 510 ;   ;Name Delivery       ;Text50        ;CaptionML=ENU=Name Delivery;
                                                   Editable=No }
    { 515 ;   ;Address Delivery    ;Text50        ;CaptionML=ENU=Address Delivery;
                                                   Editable=No }
    { 520 ;   ;Post Code Delivery  ;Code20        ;CaptionML=ENU=Post Code Delivery;
                                                   Editable=No }
    { 525 ;   ;City Delivery       ;Text50        ;CaptionML=ENU=City Delivery;
                                                   Editable=No }
    { 530 ;   ;Country Delivery    ;Code30        ;CaptionML=ENU=Country Delivery;
                                                   Editable=No }
    { 1000;   ;Approved            ;Boolean       ;OnValidate=BEGIN
                                                                IF Approved THEN BEGIN
                                                                  "Approved by" := USERID;
                                                                  "Approval Date/Time" := CURRENTDATETIME;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Approved }
    { 1010;   ;Approved by         ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Approved by;
                                                   Editable=No }
    { 1020;   ;Approval Date/Time  ;DateTime      ;CaptionML=ENU=Approval Date/Time;
                                                   Editable=No }
    { 1030;   ;Related Purch. Order No.;Code20    ;TableRelation="Purchase Header".No. WHERE (Document Type=CONST(Order));
                                                   OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO("Related Purch. Order No.") THEN
                                                                  TESTFIELD(Approved, FALSE);

                                                                AssistEditPurchOrder(0);
                                                              END;

                                                   OnLookup=BEGIN
                                                              AssistEditPurchOrder(1);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Related Purch. Order No.;
                                                   Description=DP00065 }
    { 1040;   ;Service Order No.   ;Code20        ;TableRelation="Service Order";
                                                   OnValidate=VAR
                                                                ServOrderRec@1210190000 : Record 11012823;
                                                              BEGIN
                                                                IF "Service Order No." <> '' THEN BEGIN  //db, 16-09-14
                                                                  TESTFIELD("Project No.", '');
                                                                  ServOrderRec.GET("Service Order No.");
                                                                  ServOrderRec.CheckStatus(2);
                                                                  UpdateDispatchLines(FIELDNO("Service Order No."));  //db, 16-09-14
                                                                END;
                                                                UpdateNotApprovedInvoiceLines(FIELDNO("Service Order No."));  //db, 16-09-14
                                                              END;

                                                   CaptionML=ENU=Service Order No.;
                                                   Description=DP00065 }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Approved,No.                             }
    {    ;Approved,Project No.                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE UpdateNotApprovedInvoiceLines@1100525000(iFieldNo@1100525002 : Integer);
    VAR
      lvInvLineRec@1100525000 : Record 11072654;
      lvInvoiceRec@1100525001 : Record 11072653;
    BEGIN
      lvInvLineRec.SETCURRENTKEY("Dispatch Note No.");
      lvInvLineRec.SETRANGE("Dispatch Note No.", "No.");
      lvInvLineRec.SETFILTER("G/L Account", '%1', '');  //db, 16-09-14
      //lvInvLineRec.SETFILTER("Project No.", '<>%1', "Project No.");  //db, 16-09-14
      IF lvInvLineRec.FINDSET(TRUE,FALSE) THEN BEGIN
        REPEAT
          IF lvInvLineRec."Invoice No." <> lvInvoiceRec."No." THEN BEGIN
            IF NOT lvInvoiceRec.GET(lvInvLineRec."Invoice No.") THEN BEGIN
              lvInvoiceRec."No." := lvInvLineRec."Invoice No.";
              lvInvoiceRec.Approved := TRUE;  //* Do not update project in the invoice line.
            END;
          END;
          IF NOT lvInvoiceRec.Approved THEN BEGIN
            IF iFieldNo = FIELDNO("Project No.") THEN BEGIN  //db, 16-09-14
              IF (lvInvLineRec."Service Order No." = '') THEN BEGIN
                lvInvLineRec."Project No." := "Project No.";
                lvInvLineRec.MODIFY;
              END;
            END;
            IF iFieldNo = FIELDNO("Service Order No.") THEN BEGIN  //db, 16-09-14
              IF (lvInvLineRec."Project No." = '') THEN BEGIN
                lvInvLineRec."Service Order No." := "Service Order No.";
                lvInvLineRec.MODIFY;
              END;
            END;
          END;
        UNTIL lvInvLineRec.NEXT = 0;
      END;
    END;

    PROCEDURE UpdateDispatchLines@1210190001(iFieldNo@1100525000 : Integer);
    VAR
      lvDispatchLineRec@1210190000 : Record 11072652;
    BEGIN
      //DP00065
      lvDispatchLineRec.SETRANGE("Dispatch Note No.", "No.");
      lvDispatchLineRec.SETFILTER("G/L Account", '%1', '');  //db, 16-09-14
      IF lvDispatchLineRec.FINDSET(TRUE,FALSE) THEN BEGIN
        REPEAT
          IF iFieldNo = FIELDNO("Project No.") THEN BEGIN  //db, 16-09-14
            IF (lvDispatchLineRec."Service Order No." = '') THEN BEGIN
              lvDispatchLineRec."Project No." := "Project No.";
              lvDispatchLineRec.MODIFY;
            END;
          END;
          IF iFieldNo = FIELDNO("Service Order No.") THEN BEGIN  //db, 16-09-14
            IF (lvDispatchLineRec."Project No." = '') THEN BEGIN
              lvDispatchLineRec."Service Order No." := "Service Order No.";
              lvDispatchLineRec.MODIFY;
            END;
          END;
        UNTIL lvDispatchLineRec.NEXT=  0;
      END;
    END;

    PROCEDURE AssistEditPurchOrder@1210190000(iAction@1210190000 : Integer);
    VAR
      PurchHeaderRec@1210190001 : Record 38;
      EtimVendorRec@1210190002 : Record 11012313;
      lvVendor@1100528900 : Code[20];
    BEGIN
      //DP00065
      CALCFIELDS("GLN Alternative Vendor");
      IF "GLN Alternative Vendor" <> '' THEN  //db, 02-02-12
        lvVendor := "GLN Alternative Vendor"
      ELSE
        lvVendor := "GLN Vendor";
      EtimVendorRec.GET(lvVendor);
      EtimVendorRec.TESTFIELD("Vendor (Purchase)");

      IF iAction = 1 THEN BEGIN
        PurchHeaderRec.SETCURRENTKEY("Document Type","Buy-from Vendor No.");
        PurchHeaderRec.SETRANGE("Document Type", PurchHeaderRec."Document Type"::Order);
        PurchHeaderRec.SETRANGE("Buy-from Vendor No.", EtimVendorRec."Vendor (Purchase)");
        IF PAGE.RUNMODAL(0, PurchHeaderRec) = ACTION::LookupOK THEN BEGIN
          PurchHeaderRec.TESTFIELD("Buy-from Vendor No.", EtimVendorRec."Vendor (Purchase)");
          IF NOT Approved THEN
            "Related Purch. Order No." := PurchHeaderRec."No.";
        END;
      END;

      IF "Related Purch. Order No." <> '' THEN BEGIN
        PurchHeaderRec.GET(PurchHeaderRec."Document Type"::Order, "Related Purch. Order No.");
        PurchHeaderRec.TESTFIELD("Buy-from Vendor No.", EtimVendorRec."Vendor (Purchase)");
      END;
    END;

    PROCEDURE DetermineVendor@1100525005() : Code[20];
    VAR
      lvVendRelRec@1100525002 : Record 11012313;
    BEGIN
      //DP00065
      CALCFIELDS("GLN Alternative Vendor");
      IF "GLN Alternative Vendor" <> '' THEN BEGIN
        IF NOT lvVendRelRec.GET("GLN Alternative Vendor") THEN lvVendRelRec.INIT;
        IF lvVendRelRec."Vendor (Purchase)" <> '' THEN
          EXIT(lvVendRelRec."Vendor (Purchase)");
      END;

      IF NOT lvVendRelRec.GET("GLN Vendor") THEN lvVendRelRec.INIT;
      EXIT(lvVendRelRec."Vendor (Purchase)");
    END;

    BEGIN
    END.
  }
}

