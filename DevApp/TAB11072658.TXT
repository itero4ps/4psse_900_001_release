OBJECT Table 11072658 INSBOU Purchase Order Response
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               INSBOUPurchOrderRespLine@1100528300 : Record 11072659;
             BEGIN
               IF ("Process Status" = "Process Status"::Processing) THEN
                 FIELDERROR("Process Status");

               INSBOUPurchOrderRespLine.SETRANGE("Vendor No.", "Vendor No.");
               INSBOUPurchOrderRespLine.SETRANGE(OrderResponsNumber, OrderResponsNumber);
               INSBOUPurchOrderRespLine.DELETEALL(TRUE);
             END;

    CaptionML=ENU=INSBOU Purchase Order Response;
  }
  FIELDS
  {
    { 5   ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=ENU=Vendor No.;
                                                   NotBlank=Yes }
    { 6   ;   ;Vendor Name         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=ENU=Vendor Name;
                                                   Editable=No }
    { 10  ;   ;OrderResponsNumber  ;Code17        ;CaptionML=ENU=Order Response Number;
                                                   NotBlank=Yes }
    { 20  ;   ;OrderResponseDate   ;Date          ;CaptionML=ENU=Order Response Date }
    { 30  ;   ;OrderResponseTime   ;Time          ;CaptionML=ENU=Order Response Time }
    { 40  ;   ;StatusCode          ;Code3         ;CaptionML=ENU=Status Code }
    { 50  ;   ;TotalAmount         ;Decimal       ;CaptionML=ENU=Total Amount }
    { 60  ;   ;BuyersOrderNumber   ;Code17        ;CaptionML=ENU=Buyers Order Number }
    { 70  ;   ;ProjectNumber       ;Code17        ;CaptionML=ENU=Project Number }
    { 80  ;   ;FreeText            ;Text250       ;CaptionML=ENU=Free Text }
    { 90  ;   ;DeliveryDateTimeInformation;Text30 ;CaptionML=ENU=Delivery Date Time Information }
    { 100 ;   ;ScheduledDeliveryDate;Date         ;CaptionML=ENU=Scheduled Delivery Date }
    { 110 ;   ;ScheduledDeliveryTime;Time         ;CaptionML=ENU=Scheduled Delivery Time }
    { 120 ;   ;DeliveryTimeFrame   ;Text30        ;CaptionML=ENU=Delivery Time Frame }
    { 130 ;   ;DeliveryDateEarliest;Date          ;CaptionML=ENU=Delivery Date Earliest }
    { 140 ;   ;DeliveryTimeEarliest;Time          ;CaptionML=ENU=Delivery Time Earliest }
    { 150 ;   ;DeliveryDateLatest  ;Date          ;CaptionML=ENU=Delivery Date Latest }
    { 160 ;   ;DeliveryTimeLatest  ;Time          ;CaptionML=ENU=Delivery Time Latest }
    { 170 ;   ;Buyer               ;Text30        ;CaptionML=ENU=Buyer }
    { 180 ;   ;BuyerGLN            ;Code13        ;CaptionML=ENU=GLN }
    { 190 ;   ;BuyerName           ;Text50        ;CaptionML=ENU=Name }
    { 200 ;   ;BuyerName2          ;Text50        ;CaptionML=ENU=Name2 }
    { 210 ;   ;BuyerStreetAndNumber;Text35        ;CaptionML=ENU=Street And Number }
    { 220 ;   ;BuyerCity           ;Text35        ;CaptionML=ENU=City }
    { 230 ;   ;BuyerPostalCode     ;Code9         ;CaptionML=ENU=Postal Code }
    { 240 ;   ;BuyerCountry        ;Code2         ;CaptionML=ENU=Buyer Country }
    { 250 ;   ;Supplier            ;Text30        ;CaptionML=ENU=Supplier }
    { 260 ;   ;SupplierGLN         ;Code13        ;CaptionML=ENU=GLN }
    { 270 ;   ;SupplierName        ;Text50        ;CaptionML=ENU=Name }
    { 280 ;   ;SupplierName2       ;Text50        ;CaptionML=ENU=Name2 }
    { 290 ;   ;SupplierStreetAndNumber;Text35     ;CaptionML=ENU=Street And Number }
    { 300 ;   ;SupplierCity        ;Text35        ;CaptionML=ENU=City }
    { 310 ;   ;SupplierPostalCode  ;Code9         ;CaptionML=ENU=PostalCode }
    { 320 ;   ;SupplierCountry     ;Code2         ;CaptionML=ENU=Country }
    { 330 ;   ;SupplierContactInformation;Text30  ;CaptionML=ENU=Contact Information }
    { 340 ;   ;SupplierContactPersonName;Text35   ;CaptionML=ENU=Contact Person Name }
    { 350 ;   ;SupplierPhoneNumber ;Code20        ;CaptionML=ENU=Phone Number }
    { 360 ;   ;EmailAddress        ;Text250       ;CaptionML=ENU=Email Address }
    { 370 ;   ;DeliveryParty       ;Text30        ;CaptionML=ENU=DeliveryParty }
    { 380 ;   ;DeliveryPartyGLN    ;Code13        ;CaptionML=ENU=GLN }
    { 390 ;   ;DeliveryPartyName   ;Text50        ;CaptionML=ENU=Name }
    { 400 ;   ;DeliveryPartyName2  ;Text50        ;CaptionML=ENU=Name2 }
    { 410 ;   ;DeliveryPartyStreetAndNumber;Text35;CaptionML=ENU=StreetAndNumber }
    { 420 ;   ;DeliveryPartyCity   ;Text35        ;CaptionML=ENU=City }
    { 430 ;   ;DeliveryPartyPostalCode;Code9      ;CaptionML=ENU=PostalCode }
    { 440 ;   ;DeliveryPartyCountry;Code2         ;CaptionML=ENU=Country }
    { 450 ;   ;DeliveryPartyLocationDescripti;Text70;
                                                   CaptionML=ENU=Location Description }
    { 460 ;   ;DeliveryPartyContactgegevens;Text30;CaptionML=ENU=Contact gegevens }
    { 470 ;   ;DeliveryPartyContactPersonName;Text35;
                                                   CaptionML=ENU=Contact Person Name }
    { 490 ;   ;DeliveryPartyPhoneNumber;Code20    ;CaptionML=ENU=Phone Number }
    { 500 ;   ;ShipFrom            ;Text30        ;CaptionML=ENU=ShipFrom }
    { 510 ;   ;ShipFromGLN         ;Code13        ;CaptionML=ENU=GLN }
    { 520 ;   ;ShipFromName        ;Text50        ;CaptionML=ENU=Name }
    { 530 ;   ;ShipFromName2       ;Text50        ;CaptionML=ENU=Name2 }
    { 540 ;   ;ShipFromStreetAndNumber;Text35     ;CaptionML=ENU=Street And Number }
    { 550 ;   ;ShipFromCity        ;Text35        ;CaptionML=ENU=City }
    { 560 ;   ;ShipFromPostalCode  ;Code9         ;CaptionML=ENU=Postal Code }
    { 570 ;   ;ShipFromCountry     ;Code2         ;CaptionML=ENU=Country }
    { 580 ;   ;UltimateConsignee   ;Text30        ;CaptionML=ENU=UltimateConsignee }
    { 590 ;   ;UltimateConsigneeGLN;Code13        ;CaptionML=ENU=GLN }
    { 600 ;   ;UltimateConsigneeName;Text50       ;CaptionML=ENU=Name }
    { 610 ;   ;UltimateConsigneeName2;Text50      ;CaptionML=ENU=Name2 }
    { 620 ;   ;UltimateConsigneeStreetAndNumb;Text35;
                                                   CaptionML=ENU=Street And Number }
    { 630 ;   ;UltimateConsigneeCity;Text35       ;CaptionML=ENU=City }
    { 640 ;   ;UltimateConsigneePostalCode;Code9  ;CaptionML=ENU=Postal Code }
    { 650 ;   ;UltimateConsigneeCountry;Code2     ;CaptionML=ENU=Country }
    { 660 ;   ;Charge              ;Text30        ;CaptionML=ENU=Charge }
    { 670 ;   ;ChargeTypeCode      ;Code3         ;CaptionML=ENU=TypeCode }
    { 680 ;   ;ChargeAmount        ;Decimal       ;CaptionML=ENU=Amount }
    { 690 ;   ;ChargeVatInformation;Text30        ;CaptionML=ENU=VatInformation }
    { 700 ;   ;ChargeVATRate       ;Code3         ;CaptionML=ENU=VATRate }
    { 710 ;   ;ChargeVATPercentage ;Decimal       ;CaptionML=ENU=VATPercentage }
    { 720 ;   ;Allowance           ;Text30        ;CaptionML=ENU=Allowance }
    { 730 ;   ;AllowanceTypeCode   ;Code3         ;CaptionML=ENU=TypeCode }
    { 740 ;   ;AllowanceAmount     ;Decimal       ;CaptionML=ENU=Amount }
    { 750 ;   ;AlowanceVATRate     ;Code3         ;CaptionML=ENU=VATRate }
    { 760 ;   ;AllowanceVATPercentage;Decimal     ;CaptionML=ENU=VATPercentage }
    { 1000;   ;Process Status      ;Option        ;CaptionML=ENU=Process Status;
                                                   OptionCaptionML=ENU=Received,,,Processing,,,Processed;
                                                   OptionString=Received,,,Processing,,,Processed;
                                                   Editable=No }
    { 1010;   ;Message Id          ;Text50        ;CaptionML=ENU=Message Id;
                                                   Editable=No }
    { 1020;   ;Error Message       ;Text250       ;CaptionML=ENU=Error Message;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Vendor No.,OrderResponsNumber           ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528300 : TextConst 'ENU=%1 %2 already exsists.';
      Text001@1100528301 : TextConst 'ENU=There are no %1s or more than 1 %1s found for %2 %3.';

    PROCEDURE ProcessOrderResponses@1100528300(VAR INSBOUPurchaseOrderResponse@1100528300 : Record 11072658);
    VAR
      INSBOUOrderResponse@1100528301 : Codeunit 11072078;
    BEGIN
      INSBOUOrderResponse.RUN(INSBOUPurchaseOrderResponse);
    END;

    PROCEDURE CreatePurchaseOrder@1100528301(VAR INSBOUPurchaseOrderResponse@1100528301 : Record 11072658);
    VAR
      PurchaseHeader@1100528300 : Record 38;
      PurchasesPayablesSetup@1100525001 : Record 312;
      NoSeriesManagement@1100525000 : Codeunit 396;
    BEGIN
      IF (NOT INSBOUPurchaseOrderResponse.FINDSET) THEN
        EXIT;

      REPEAT
        IF (INSBOUPurchaseOrderResponse.BuyersOrderNumber = '') THEN BEGIN
          PurchasesPayablesSetup.GET;
          INSBOUPurchaseOrderResponse.BuyersOrderNumber := NoSeriesManagement.GetNextNo(PurchasesPayablesSetup."Order Nos.", TODAY, TRUE);
          INSBOUPurchaseOrderResponse.MODIFY;
        END;
        INSBOUPurchaseOrderResponse.TESTFIELD(SupplierGLN);
        IF (INSBOUPurchaseOrderResponse.StatusCode = '7') THEN
          INSBOUPurchaseOrderResponse.FIELDERROR(StatusCode);
        IF (PurchaseHeader.GET(INSBOUPurchaseOrderResponse.DetermineDocumentType,
          INSBOUPurchaseOrderResponse.BuyersOrderNumber))
        THEN
          ERROR(Text000, PurchaseHeader.TABLECAPTION, INSBOUPurchaseOrderResponse.BuyersOrderNumber);

        PurchaseHeader.INIT;
        PurchaseHeader."Document Type" := INSBOUPurchaseOrderResponse.DetermineDocumentType;
        PurchaseHeader."No." := INSBOUPurchaseOrderResponse.BuyersOrderNumber;
        PurchaseHeader.VALIDATE("Buy-from Vendor No.", GetVendorNo(INSBOUPurchaseOrderResponse.SupplierGLN));
        PurchaseHeader.InitRecord;
        PurchaseHeader.INSERT(TRUE);

        PurchaseHeader."Job No." := INSBOUPurchaseOrderResponse.ProjectNumber;
        PurchaseHeader."Vendor Order No." := INSBOUPurchaseOrderResponse.OrderResponsNumber;
        PurchaseHeader."Vendor Shipment No." := INSBOUPurchaseOrderResponse.OrderResponsNumber;

        PurchaseHeader."Ship-to Name" := INSBOUPurchaseOrderResponse.DeliveryPartyName;
        PurchaseHeader."Ship-to Name 2" := INSBOUPurchaseOrderResponse.DeliveryPartyName2;
        PurchaseHeader."Ship-to Address" := INSBOUPurchaseOrderResponse.DeliveryPartyStreetAndNumber;
        PurchaseHeader."Ship-to City" := INSBOUPurchaseOrderResponse.DeliveryPartyCity;

        PurchaseHeader."Order Date" := INSBOUPurchaseOrderResponse.OrderResponseDate;
        PurchaseHeader."Expected Receipt Date" := INSBOUPurchaseOrderResponse.OrderResponseDate;
        PurchaseHeader."Due Date" := INSBOUPurchaseOrderResponse.OrderResponseDate;
        PurchaseHeader."Document Date" := INSBOUPurchaseOrderResponse.OrderResponseDate;
        PurchaseHeader."Posting Date" := INSBOUPurchaseOrderResponse.OrderResponseDate;

        PurchaseHeader.MODIFY(TRUE);

        IF (INSBOUPurchaseOrderResponse.FreeText <> '') THEN
          CreateTextLines(INSBOUPurchaseOrderResponse);

        CreatePurchaseLines(INSBOUPurchaseOrderResponse, PurchaseHeader);
      UNTIL (INSBOUPurchaseOrderResponse.NEXT = 0);
    END;

    LOCAL PROCEDURE CreatePurchaseLines@1100528306(VAR INSBOUPurchaseOrderResponse@1100528306 : Record 11072658;PurchaseHeader@1100528300 : Record 38);
    VAR
      INSBOUPurchOrderRespLine@1100528302 : Record 11072659;
      INSBOUPartialDelivery@1100528303 : Record 11012340;
      PurchaseLine@1100528301 : Record 39;
      Vendor@1100528304 : Record 23;
      GeneralLedgerSetup@1100528305 : Record 98;
      ETIMVendorCentralMgt@1100525000 : Record 11012304;
    BEGIN
      INSBOUPurchOrderRespLine.SETRANGE("Vendor No.", INSBOUPurchaseOrderResponse."Vendor No.");
      INSBOUPurchOrderRespLine.SETRANGE(OrderResponsNumber, INSBOUPurchaseOrderResponse.OrderResponsNumber);
      IF (NOT INSBOUPurchOrderRespLine.FINDSET) THEN
        EXIT;

      Vendor.GET(PurchaseHeader."Buy-from Vendor No.");
      GeneralLedgerSetup.GET;
      REPEAT
        INSBOUPurchOrderRespLine.TESTFIELD(OrderLineIdentification);
        INSBOUPurchOrderRespLine.TESTFIELD(GTIN);
        INSBOUPurchOrderRespLine.CALCFIELDS("Partial Delivery");

        PurchaseLine.INIT;
        PurchaseLine."Document Type" := PurchaseHeader."Document Type";
        PurchaseLine."Document No." := PurchaseHeader."No.";
        PurchaseLine."Line No." := INSBOUPurchOrderRespLine.OrderLineIdentification;
        PurchaseLine.INSERT(TRUE);

        PurchaseLine.VALIDATE("Job No.", PurchaseHeader."Job No.");
        PurchaseLine.VALIDATE("Vendor (Trade Item)", ETIMVendorCentralMgt.GetVendorFromGLN(INSBOUPurchaseOrderResponse.SupplierGLN));  //Vendor."Vendor (Trade Item)");
        PurchaseLine.VALIDATE("Trade Item", GetTradeItem(PurchaseLine."Vendor (Trade Item)", INSBOUPurchOrderRespLine));
        PurchaseLine.VALIDATE(Quantity, INSBOUPurchOrderRespLine.PlannedDeliveryQuantity);
        PurchaseLine.VALIDATE("Unit of Measure", INSBOUPurchOrderRespLine.QuantityMeasureUnitCode);
        IF (INSBOUPurchOrderRespLine.NetLineAmount <> 0) THEN BEGIN
          PurchaseLine.VALIDATE(Amount, INSBOUPurchOrderRespLine.NetLineAmount);
          PurchaseLine.VALIDATE("Direct Unit Cost",
            ROUND(PurchaseLine.Amount / PurchaseLine.Quantity, GeneralLedgerSetup."Unit-Amount Rounding Precision"));
        END;
        IF (INSBOUPurchOrderRespLine."Partial Delivery") THEN
          PurchaseLine.VALIDATE(Quantity, 0);
        PurchaseLine.MODIFY(TRUE);

        IF (INSBOUPurchOrderRespLine."Partial Delivery") THEN
          CreatePartialDeliveryLines(INSBOUPurchOrderRespLine, PurchaseLine);
      UNTIL (INSBOUPurchOrderRespLine.NEXT = 0);
    END;

    LOCAL PROCEDURE CreatePartialDeliveryLines@1100528304(INSBOUPurchOrderRespLine@1100528300 : Record 11072659;iPurchaseLine@1100528301 : Record 39);
    VAR
      INSBOUPartialDelivery@1100528303 : Record 11012340;
      PurchaseLine@1100528302 : Record 39;
      PartialDeliveryLineNo@1100528304 : Integer;
    BEGIN
      INSBOUPartialDelivery.SETRANGE("Vendor No.", INSBOUPurchOrderRespLine."Vendor No.");
      INSBOUPartialDelivery.SETRANGE(OrderResponsNumber, INSBOUPurchOrderRespLine.OrderResponsNumber);
      INSBOUPartialDelivery.SETRANGE(LineNumber, INSBOUPurchOrderRespLine.LineNumber);
      IF (NOT INSBOUPartialDelivery.FINDSET) THEN
        EXIT;

      PartialDeliveryLineNo := 0;
      REPEAT
        PartialDeliveryLineNo += 1;
        PurchaseLine.COPY(iPurchaseLine);
        PurchaseLine."Line No." := iPurchaseLine."Line No." + PartialDeliveryLineNo;
        PurchaseLine.INSERT(TRUE);

        PurchaseLine.VALIDATE(Quantity, INSBOUPartialDelivery.PlannedPartialDeliveryQuantity);
        PurchaseLine.VALIDATE("Unit of Measure", INSBOUPartialDelivery.QuantityMeasureUnitCode);
        PurchaseLine.VALIDATE("Planned Receipt Date", INSBOUPartialDelivery.ScheduledDeliveryDate);
        PurchaseLine.MODIFY(TRUE);
      UNTIL (INSBOUPartialDelivery.NEXT = 0);
    END;

    LOCAL PROCEDURE CreateTextLines@1100528308(INSBOUPurchaseOrderResponse@1100528301 : Record 11072658);
    VAR
      PurchaseText@1100528300 : Record 11012067;
      LastLineNo@1100528302 : Integer;
    BEGIN
      LastLineNo := 0;
      PurchaseText.SETRANGE("Line Type", PurchaseText."Line Type"::"Purchase Order");
      PurchaseText.SETRANGE("No.", INSBOUPurchaseOrderResponse.BuyersOrderNumber);
      PurchaseText.SETRANGE("Purchase Action", '');
      IF (PurchaseText.FINDLAST) THEN
        LastLineNo := PurchaseText."Line No.";

      PurchaseText.RESET;
      PurchaseText.INIT;
      PurchaseText."Line Type" := PurchaseText."Line Type"::"Purchase Order";
      PurchaseText."No." := INSBOUPurchaseOrderResponse.BuyersOrderNumber;
      PurchaseText."Line No." := LastLineNo + 10000;
      PurchaseText.Text := COPYSTR(INSBOUPurchaseOrderResponse.FreeText, 1, MAXSTRLEN(PurchaseText.Text));
      PurchaseText."Line Break" := TRUE;
      PurchaseText.INSERT(TRUE);
    END;

    LOCAL PROCEDURE GetVendorNo@1100528302(GLN@1100528300 : Code[13]) : Code[20];
    VAR
      ETIMVendorCentralMgt@1100528301 : Record 11012304;
    BEGIN
      EXIT(ETIMVendorCentralMgt.GetVendorPurchaseFromGLN(GLN));
    END;

    LOCAL PROCEDURE GetTradeItem@1100528309(VendorNo@1100528302 : Code[20];INSBOUPurchOrderRespLine@1100528300 : Record 11072659) : Code[20];
    VAR
      TradeItem@1100528301 : Record 11012317;
    BEGIN
      TradeItem.SETRANGE(Vendor, VendorNo);
      TradeItem.SETRANGE("GTIN Code (Item)", INSBOUPurchOrderRespLine.GTIN);
      IF (TradeItem.COUNT = 0) THEN
        CreateTradeItem(VendorNo, INSBOUPurchOrderRespLine);
      IF (TradeItem.COUNT <> 1) THEN
        ERROR(Text001, TradeItem.TABLECAPTION, TradeItem."GTIN Code (Item)", INSBOUPurchOrderRespLine.GTIN);
      TradeItem.FINDFIRST;
      EXIT(TradeItem."Item Code");
    END;

    PROCEDURE PurchaseHeaderPresent@1100528303() : Boolean;
    VAR
      PurchaseHeader@1100528300 : Record 38;
    BEGIN
      EXIT(PurchaseHeader.GET(DetermineDocumentType, BuyersOrderNumber));
    END;

    PROCEDURE DetermineDocumentType@1100528305() : Integer;
    VAR
      PurchaseHeader@1100528300 : Record 38;
    BEGIN
      IF (BuyersOrderNumber = '') THEN
        EXIT(-1);
      PurchaseHeader.SETRANGE("No.", BuyersOrderNumber);
      IF (PurchaseHeader.COUNT <> 1) THEN BEGIN
        EXIT(PurchaseHeader."Document Type"::Order);
      END;
      PurchaseHeader.FINDFIRST;
      EXIT(PurchaseHeader."Document Type");
    END;

    LOCAL PROCEDURE CreateTradeItem@1100525000(VendorNo@1100525001 : Code[20];INSBOUPurchOrderRespLine@1100525002 : Record 11072659);
    VAR
      ItemSetup@1100525003 : Record 11012300;
      TradeItem@1100525000 : Record 11012317;
      "2baManagement"@1100525004 : Codeunit 11012306;
    BEGIN
      IF (TradeItem.GET(VendorNo, INSBOUPurchOrderRespLine.SuppliersTradeItemIdentificati)) THEN
        EXIT;

      ItemSetup.GET;
      TradeItem.INIT;
      TradeItem.Vendor := VendorNo;
      TradeItem."Item Code" := INSBOUPurchOrderRespLine.SuppliersTradeItemIdentificati;
      TradeItem.INSERT;

      TradeItem."GTIN Code (Item)" := INSBOUPurchOrderRespLine.GTIN;
      TradeItem."Item Description" := COPYSTR(INSBOUPurchOrderRespLine.TradeItemDescription, 1, MAXSTRLEN(TradeItem."Item Description"));
      CASE ItemSetup."Adjust Item Description" OF
        ItemSetup."Adjust Item Description"::ToUpper: BEGIN
          TradeItem."Item Description" := UPPERCASE(TradeItem."Item Description");
        END;
        ItemSetup."Adjust Item Description"::ToLower: BEGIN
          TradeItem."Item Description" := LOWERCASE(TradeItem."Item Description");
        END;
      END;
      IF TradeItem."Search Argument" = '' THEN
        TradeItem."Search Argument" := COPYSTR(TradeItem."Item Description", 1, MAXSTRLEN(TradeItem."Search Argument"));

      TradeItem."Application Unit Quantity" := 1;
      TradeItem."Starting Date Price" := TODAY;
      TradeItem."Gross Price" := INSBOUPurchOrderRespLine.GrossPrice;
      TradeItem."Quantity Per Price" := 1;

      IF (TradeItem."Cost Object" = '') OR (NOT TradeItem."Cost Object changed Manually") THEN
        TradeItem."Cost Object" := TradeItem.GetDefaultCostObject(FALSE);
      TradeItem.Expired := FALSE;
      TradeItem.MODIFY;

      "2baManagement".UpdatePriceHistory(TradeItem);
    END;

    BEGIN
    END.
  }
}

