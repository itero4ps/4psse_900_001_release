OBJECT Table 11072674 Plant Maint. Scheme Version
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceSetup@1100528800 : Record 11012800;
             BEGIN
               ServiceSetup.GET();
               CheckKeyFieldsLevel();
             END;

    OnDelete=VAR
               PlantMaintenanceSchemeVersionLine@1100528800 : Record 11072675;
             BEGIN
               PlantMaintenanceSchemeVersionLine.SETRANGE(Level, Level);
               PlantMaintenanceSchemeVersionLine.SETRANGE("Plant Type", "Plant Type");
               PlantMaintenanceSchemeVersionLine.SETRANGE("Level Code", "Level Code");
               PlantMaintenanceSchemeVersionLine.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
               PlantMaintenanceSchemeVersionLine.SETRANGE("Index Date", "Index Date");
               PlantMaintenanceSchemeVersionLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               CheckKeyFieldsLevel();
             END;

    CaptionML=ENU=Plant Maint. Scheme Version;
    LookupPageID=Page11125951;
    DrillDownPageID=Page11125951;
  }
  FIELDS
  {
    { 10  ;   ;Level               ;Option        ;CaptionML=ENU=Level;
                                                   OptionCaptionML=ENU=Maintenance Scheme,Plant Type,Plant Number;
                                                   OptionString=Maintenance Scheme,Plant Type,Plant Number }
    { 15  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type".Code;
                                                   CaptionML=ENU=Plant Type }
    { 20  ;   ;Level Code          ;Code20        ;TableRelation=IF (Level=CONST(Maintenance Scheme)) "Plant Maintenance Scheme".Code
                                                                 ELSE IF (Level=CONST(Plant Type)) "Plant Type".Code
                                                                 ELSE IF (Level=CONST(Plant Number)) "Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   CaptionML=ENU=Level Code;
                                                   NotBlank=Yes }
    { 30  ;   ;Level Code Description OLD;Text50  ;OnValidate=BEGIN
                                                                TESTFIELD("Level Code Description OLD");
                                                              END;

                                                   CaptionML=ENU=Level Code Description OLD }
    { 40  ;   ;Maintenance Scheme  ;Code20        ;TableRelation="Plant Maintenance Scheme";
                                                   OnValidate=VAR
                                                                PlantTypeMaintenanceScheme@1100525000 : Record 11072677;
                                                              BEGIN
                                                                IF "Maintenance Scheme" <> '' THEN BEGIN
                                                                  IF Level =  Level::"Plant Number" THEN
                                                                    PlantTypeMaintenanceScheme.GET("Plant Type", '', "Maintenance Scheme");
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupMaintnanceScheme(TRUE);
                                                            END;

                                                   CaptionML=ENU=Maintenance Scheme;
                                                   NotBlank=Yes }
    { 50  ;   ;Maintenance Scheme Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Maintenance Scheme".Description WHERE (Code=FIELD(Maintenance Scheme)));
                                                   CaptionML=ENU=Maintenance Scheme Description;
                                                   Editable=No }
    { 60  ;   ;Index Date          ;Date          ;CaptionML=ENU=Index Date;
                                                   NotBlank=Yes }
    { 70  ;   ;Cancelled           ;Boolean       ;CaptionML=ENU=Cancelled }
  }
  KEYS
  {
    {    ;Level,Plant Type,Level Code,Maintenance Scheme,Index Date;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    LOCAL PROCEDURE CheckKeyFieldsLevel@1100525001();
    BEGIN
      CASE Level OF
        Level::"Maintenance Scheme":
          BEGIN
            TESTFIELD("Plant Type", '');
            TESTFIELD("Level Code", "Maintenance Scheme")
          END;
        Level::"Plant Type":
          BEGIN
            TESTFIELD("Plant Type");
            TESTFIELD("Level Code", "Plant Type")
          END;
        Level::"Plant Number":
          TESTFIELD("Plant Type");
      END;
    END;

    PROCEDURE LookupMaintnanceScheme@1100525000(EditableMode@1100525002 : Boolean);
    VAR
      PlantTypeMaintenanceScheme@1100525001 : Record 11072677;
      PlantMaintenanceScheme@1100525000 : Record 11072673;
    BEGIN
      IF Level =  Level::"Plant Number" THEN BEGIN
        PlantTypeMaintenanceScheme.SETRANGE("Plant Type", "Plant Type");
        PlantTypeMaintenanceScheme.SETRANGE("Plant Number", '');
        IF PlantTypeMaintenanceScheme.FINDSET THEN BEGIN
          REPEAT
            IF PlantMaintenanceScheme.GET(PlantTypeMaintenanceScheme."Maintenance Scheme") THEN
              PlantMaintenanceScheme.MARK(TRUE);
          UNTIL PlantTypeMaintenanceScheme.NEXT = 0;
        END;
        PlantMaintenanceScheme.MARKEDONLY(TRUE);
      END;
      "Maintenance Scheme" := PlantMaintenanceScheme.Code;
      IF NOT EditableMode THEN
        PAGE.RUNMODAL(PAGE::"Plant Maintenance Schemes", PlantMaintenanceScheme)
      ELSE BEGIN
        IF PAGE.RUNMODAL(PAGE::"Plant Maintenance Schemes", PlantMaintenanceScheme) = ACTION::LookupOK THEN
          VALIDATE("Maintenance Scheme", PlantMaintenanceScheme.Code);
      END;
    END;

    PROCEDURE GetLevelCodeDescription@1100528803() : Text;
    VAR
      PlantMaintenanceScheme@1100528803 : Record 11072673;
      PlantType@1100528802 : Record 11012551;
      PlantNumber@1100528801 : Record 11012552;
    BEGIN
      CASE Level OF
        Level::"Maintenance Scheme":
          BEGIN
            IF PlantMaintenanceScheme.GET("Level Code") THEN
              EXIT(PlantMaintenanceScheme.Description);
          END;
        Level::"Plant Type":
          BEGIN
            IF PlantType.GET("Level Code") THEN
              EXIT(PlantType.Description);
          END;
        Level::"Plant Number":
          BEGIN
            IF PlantNumber.GET("Plant Type", "Level Code") THEN
              EXIT(PlantNumber.Description);
          END;
      END;
      EXIT('');
    END;

    PROCEDURE NewerVersionExists@1100528600() : Boolean;
    VAR
      PlantMaintenanceSchemeVersion@1100528600 : Record 11072674;
    BEGIN
      PlantMaintenanceSchemeVersion := Rec;
      PlantMaintenanceSchemeVersion.SETRECFILTER;
      PlantMaintenanceSchemeVersion.SETFILTER("Index Date", '>%1', "Index Date");
      PlantMaintenanceSchemeVersion.SETRANGE(Cancelled, FALSE);
      EXIT(NOT PlantMaintenanceSchemeVersion.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

