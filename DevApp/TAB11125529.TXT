OBJECT Table 11125529 Plant Request (SMF)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Project No.");
               IF "No." = '' THEN BEGIN
                 SiteMgtSetupRec.GET;
                 SiteMgtSetupRec.TESTFIELD("Plant Request Nos.");
                 NoSeriesMgt.InitSeries(SiteMgtSetupRec."Plant Request Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               InitRecord();
             END;

    OnModify=BEGIN
               TESTFIELD(Status, Status::Open);
             END;

    OnDelete=VAR
               lvReqLineRec@1100485002 : Record 11125530;
             BEGIN
               lvReqLineRec.SETRANGE("Request No.", "No.");
               lvReqLineRec.DELETEALL;
             END;

    OnRename=BEGIN
               TESTFIELD(Status, Status::Open);
             END;

    CaptionML=ENU=Plant Request (Site Manager Form);
    LookupPageID=Page11125533;
    DrillDownPageID=Page11125533;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  SiteMgtSetupRec.GET;
                                                                  NoSeriesMgt.TestManual(SiteMgtSetupRec."Plant Request Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 15  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=ENU=Project No.;
                                                   Editable=No }
    { 20  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                FillLocations();
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Arrival,Removal,Other Transfers;
                                                   OptionString=Arrival,Removal,OtherTransfers }
    { 25  ;   ;Plant Company       ;Text30        ;TableRelation=Company;
                                                   OnValidate=VAR
                                                                lvReqLineRec@1100525000 : Record 11125530;
                                                                PlantMgtCompRec@1100525001 : Record 11020586;
                                                                TmpPlantMgtCompRec@1100525002 : TEMPORARY Record 11020586;
                                                              BEGIN
                                                                IF "Plant Company" <> '' THEN BEGIN
                                                                  IF NOT PlantMgtCompRec.GET("Plant Company") THEN
                                                                    ERROR(Text001, "Plant Company", PlantMgtCompRec.TABLECAPTION)
                                                                  ELSE BEGIN
                                                                    IF "Project No." <> '' THEN BEGIN
                                                                      PlantMgtCompRec.GetPlantCompaniesForProject("Project No.", TmpPlantMgtCompRec, '');
                                                                      IF NOT TmpPlantMgtCompRec.GET("Plant Company") THEN
                                                                        ERROR(Text002, "Project No.", FIELDCAPTION("Plant Company"), "Plant Company");
                                                                    END;
                                                                  END;
                                                                END;

                                                                IF "Plant Company" <> xRec."Plant Company" THEN BEGIN
                                                                  lvReqLineRec.SETRANGE("Request No.", "No.");
                                                                  IF NOT lvReqLineRec.ISEMPTY THEN
                                                                    ERROR(Text003, FIELDCAPTION("Plant Company"));
                                                                  FillLocations();
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupPlantCompany();
                                                            END;

                                                   CaptionML=ENU=Plant Company;
                                                   NotBlank=Yes }
    { 30  ;   ;From Location       ;Code20        ;OnValidate=BEGIN
                                                                IF "From Location" <> '' THEN BEGIN
                                                                  TESTFIELD("Plant Company");
                                                                  PlantLocRec.CHANGECOMPANY("Plant Company");
                                                                  PlantLocRec.GET("From Location");
                                                                  PlantLocRec.TESTFIELD(Blocked, FALSE);
                                                                  CASE Type OF
                                                                    Type::Arrival:
                                                                      PlantLocRec.TESTFIELD(Depot, TRUE);
                                                                    Type::Removal,
                                                                    Type::OtherTransfers:  //* Other: Only from location of the project, to another location
                                                                      BEGIN
                                                                        PlantLocRec.TESTFIELD(Depot, FALSE);
                                                                        CheckLinkedProject();
                                                                      END;
                                                                  END
                                                                END;

                                                                IF "From Location" <> xRec."From Location" THEN BEGIN
                                                                  "From Location Address Code" := '';
                                                                  IF "From Location" <> '' THEN BEGIN
                                                                    "From Location Name" := PlantLocRec.Description;
                                                                    "From Location Name 2" := PlantLocRec."Description 2";
                                                                    "From Location Address" := PlantLocRec.Address;
                                                                    "From Location Address 2" := PlantLocRec."Address 2";
                                                                    "From Location Post Code" := PlantLocRec."Post Code";
                                                                    "From Location City" := PlantLocRec.City;
                                                                    "From Location Contact" := PlantLocRec.Contact;
                                                                  END ELSE BEGIN
                                                                    "From Location Name" := '';
                                                                    "From Location Name 2" := '';
                                                                    "From Location Address" := '';
                                                                    "From Location Address 2" := '';
                                                                    "From Location Post Code" := '';
                                                                    "From Location City" := '';
                                                                    "From Location Contact" := '';
                                                                  END;
                                                                  "From Location Contact Phone No" := '';
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupFromLocation();
                                                            END;

                                                   CaptionML=ENU=From Location }
    { 40  ;   ;To Location         ;Code20        ;OnValidate=BEGIN
                                                                IF "To Location" <> '' THEN BEGIN
                                                                  TESTFIELD("Plant Company");
                                                                  PlantLocRec.CHANGECOMPANY("Plant Company");
                                                                  PlantLocRec.GET("To Location");
                                                                  PlantLocRec.TESTFIELD(Blocked, FALSE);
                                                                  CASE Type OF
                                                                    Type::Arrival:
                                                                      BEGIN
                                                                        PlantLocRec.TESTFIELD(Depot, FALSE);
                                                                        CheckLinkedProject();
                                                                      END;
                                                                    Type::Removal:
                                                                      PlantLocRec.TESTFIELD(Depot, TRUE);
                                                                    Type::OtherTransfers:  //* Other: Only from location of the project, to another location
                                                                      BEGIN
                                                                        PlantLocRec.TESTFIELD(Depot, FALSE);
                                                                      END;
                                                                  END
                                                                END;

                                                                IF "To Location" <> xRec."To Location" THEN BEGIN
                                                                  "To Location Address Code" := '';
                                                                  IF "To Location" <> '' THEN BEGIN
                                                                    "To Location Name" := PlantLocRec.Description;
                                                                    "To Location Name 2" := PlantLocRec."Description 2";
                                                                    "To Location Address" := PlantLocRec.Address;
                                                                    "To Location Address 2" := PlantLocRec."Address 2";
                                                                    "To Location City" := PlantLocRec.City;
                                                                    "To Location Contact" := PlantLocRec.Contact;
                                                                    "To Location Post Code" := PlantLocRec."Post Code";
                                                                  END ELSE BEGIN
                                                                    "To Location Name" := '';
                                                                    "To Location Name 2" := '';
                                                                    "To Location Address" := '';
                                                                    "To Location Address 2" := '';
                                                                    "To Location City" := '';
                                                                    "To Location Contact" := '';
                                                                    "To Location Post Code" := '';
                                                                  END;
                                                                  "To Location Contact Phone No" := '';
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupToLocation();
                                                            END;

                                                   CaptionML=ENU=To Location }
    { 60  ;   ;Transfer Date       ;Date          ;CaptionML=ENU=Transfer Date }
    { 70  ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=Open,,,,Requested;
                                                   OptionString=Open,,,,Requested;
                                                   Editable=No }
    { 110 ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=No }
    { 200 ;   ;From Location Address;Text50       ;CaptionML=ENU=From Location Address }
    { 210 ;   ;From Location Address 2;Text50     ;CaptionML=ENU=From Location Address 2 }
    { 220 ;   ;From Location City  ;Text30        ;CaptionML=ENU=From Location City }
    { 230 ;   ;From Location Contact;Text50       ;CaptionML=ENU=From Location Contact }
    { 235 ;   ;From Location Contact Phone No;Text30;
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=From Location Contact Phone No. }
    { 240 ;   ;From Location Post Code;Code20     ;TableRelation="Post Code";
                                                   OnValidate=BEGIN
                                                                //* Do not change company, should also be present in the current (project) company
                                                                IF PostCodeRec.GET("From Location Post Code") THEN
                                                                  "From Location City" := PostCodeRec.City;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=From Location Post Code }
    { 250 ;   ;From Location Address Code;Code10  ;TableRelation="Plant Location Address"."Address Code" WHERE (Location=FIELD(From Location));
                                                   OnValidate=BEGIN
                                                                PlantLocRec.CHANGECOMPANY("Plant Company");
                                                                LocAddressRec.CHANGECOMPANY("Plant Company");
                                                                IF ("From Location Address Code" <> '') THEN BEGIN
                                                                  PlantLocRec.GET("From Location");
                                                                  LocAddressRec.GET("From Location", "From Location Address Code");
                                                                  "From Location Name" := PlantLocRec.Description;
                                                                  "From Location Name 2" := PlantLocRec."Description 2";
                                                                  "From Location Address" := LocAddressRec.Address;
                                                                  "From Location Address 2" := LocAddressRec."Address 2";
                                                                  "From Location Post Code" := LocAddressRec."Post Code";
                                                                  "From Location City" := LocAddressRec.City;
                                                                  "From Location Contact" := LocAddressRec.Contact;
                                                                  "From Location Contact Phone No" := LocAddressRec."Phone No.";
                                                                END ELSE BEGIN
                                                                  IF ("From Location Address Code" <> xRec."From Location Address Code") AND ("From Location" <> '') THEN BEGIN
                                                                    PlantLocRec.GET("From Location");
                                                                    "From Location Name" := PlantLocRec.Description;
                                                                    "From Location Name 2" := PlantLocRec."Description 2";
                                                                    "From Location Address" := PlantLocRec.Address;
                                                                    "From Location Address 2" := PlantLocRec."Address 2";
                                                                    "From Location Post Code" := PlantLocRec."Post Code";
                                                                    "From Location City" := PlantLocRec.City;
                                                                    "From Location Contact" := PlantLocRec.Contact;
                                                                    "From Location Contact Phone No" := '';
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupFromLocAddress();
                                                            END;

                                                   CaptionML=ENU=From Location Address Code }
    { 260 ;   ;From Location Name  ;Text50        ;CaptionML=ENU=From Location Name }
    { 265 ;   ;From Location Name 2;Text50        ;CaptionML=ENU=From Location Name 2 }
    { 300 ;   ;To Location Address ;Text50        ;CaptionML=ENU=To Location Address }
    { 310 ;   ;To Location Address 2;Text50       ;CaptionML=ENU=To Location Address 2 }
    { 320 ;   ;To Location City    ;Text30        ;CaptionML=ENU=To Location City }
    { 330 ;   ;To Location Contact ;Text50        ;CaptionML=ENU=To Location Contact }
    { 335 ;   ;To Location Contact Phone No;Text30;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=To Location Contact Phone No. }
    { 340 ;   ;To Location Post Code;Code20       ;TableRelation="Post Code";
                                                   OnValidate=BEGIN
                                                                IF PostCodeRec.GET("To Location Post Code") THEN
                                                                  "To Location City" := PostCodeRec.City;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=To Location Post Code }
    { 350 ;   ;To Location Address Code;Code10    ;TableRelation="Plant Location Address"."Address Code" WHERE (Location=FIELD(To Location));
                                                   OnValidate=BEGIN
                                                                PlantLocRec.CHANGECOMPANY("Plant Company");
                                                                LocAddressRec.CHANGECOMPANY("Plant Company");
                                                                IF ("To Location Address Code" <> '') THEN BEGIN
                                                                  PlantLocRec.GET("To Location");
                                                                  LocAddressRec.GET("To Location", "To Location Address Code");
                                                                  "To Location Name" := PlantLocRec.Description;
                                                                  "To Location Name 2" := PlantLocRec."Description 2";
                                                                  "To Location Address" := LocAddressRec.Address;
                                                                  "To Location Address 2" := LocAddressRec."Address 2";
                                                                  "To Location Post Code" := LocAddressRec."Post Code";
                                                                  "To Location City" := LocAddressRec.City;
                                                                  "To Location Contact" := LocAddressRec.Contact;
                                                                  "To Location Contact Phone No" := LocAddressRec."Phone No.";
                                                                END ELSE BEGIN
                                                                  IF ("To Location Address Code" <> xRec."To Location Address Code") AND ("To Location" <> '') THEN BEGIN
                                                                    PlantLocRec.GET("To Location");
                                                                    "To Location Name" := PlantLocRec.Description;
                                                                    "To Location Name 2" := PlantLocRec."Description 2";
                                                                    "To Location Address" := PlantLocRec.Address;
                                                                    "To Location Address 2" := PlantLocRec."Address 2";
                                                                    "To Location Post Code" := PlantLocRec."Post Code";
                                                                    "To Location City" := PlantLocRec.City;
                                                                    "To Location Contact" := PlantLocRec.Contact;
                                                                    "To Location Contact Phone No" := '';
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupToLocAddress();
                                                            END;

                                                   CaptionML=ENU=To Location Address Code }
    { 360 ;   ;To Location Name    ;Text50        ;CaptionML=ENU=To Location Name }
    { 365 ;   ;To Location Name 2  ;Text50        ;CaptionML=ENU=To Location Name 2 }
    { 380 ;   ;Your Reference      ;Text35        ;CaptionML=ENU=Your Reference }
    { 400 ;   ;Plant Request No.   ;Code20        ;CaptionML=ENU=Plant Request No.;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Project No.                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SiteMgtSetupRec@1100525000 : Record 11125525;
      PlantSetupRec@1100485004 : Record 11012550;
      PostCodeRec@1100485003 : Record 225;
      PlantLocRec@1100485002 : Record 11012554;
      LocAddressRec@1100485001 : Record 11012590;
      NoSeriesMgt@1100485000 : Codeunit 396;
      Text001@1100525003 : TextConst 'ENU="''%1'' is not setup as %2. "';
      Text002@1100525002 : TextConst 'ENU=There is no plant location of project ''%1'' present in %2 ''%3''.';
      Text003@1100525001 : TextConst 'ENU=Line(s) present, modify %1 not allowed.';

    PROCEDURE AssistEdit@2(IReqXRec@11012000 : Record 11125529) : Boolean;
    VAR
      lvReqRec@1100485000 : Record 11125529;
    BEGIN
      WITH lvReqRec DO BEGIN
        lvReqRec := Rec;
        SiteMgtSetupRec.GET;
        SiteMgtSetupRec.TESTFIELD("Plant Request Nos.");
        IF NoSeriesMgt.SelectSeries(SiteMgtSetupRec."Plant Request Nos.",IReqXRec."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := lvReqRec;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE InitRecord@1100485002();
    BEGIN
      Status := Status::Open;
      FillDefaultPlantCompany();

      IF "Plant Company" <> '' THEN BEGIN
        xRec.INIT;
        FillLocations();
      END;
    END;

    PROCEDURE FillDefaultPlantCompany@1100525004();
    VAR
      PlantMgtCompRec@1100525000 : Record 11020586;
      TmpPlantMgtCompRec@1100525002 : TEMPORARY Record 11020586;
      NoOfPlantComps@1100525001 : Integer;
    BEGIN
      PlantMgtCompRec."Plant Company" := PlantMgtCompRec.GetPlantCompany(TRUE, NoOfPlantComps, '');
      IF (PlantMgtCompRec."Plant Company" <> '') AND (NoOfPlantComps = 1) THEN
        "Plant Company" := PlantMgtCompRec."Plant Company";

      IF (NoOfPlantComps > 1) AND ("Project No." <> '') THEN BEGIN
        IF PlantMgtCompRec.GetPlantCompaniesForProject("Project No.", TmpPlantMgtCompRec, '') = 1 THEN
          "Plant Company" := TmpPlantMgtCompRec."Plant Company";
        IF "Plant Company" = '' THEN BEGIN
          TmpPlantMgtCompRec.SETRANGE(Default, TRUE);
          IF TmpPlantMgtCompRec.FINDFIRST THEN
            "Plant Company" := TmpPlantMgtCompRec."Plant Company";
        END;
      END;
    END;

    PROCEDURE FillLocations@1();
    VAR
      lvDefPlantLoc@1100525000 : Code[20];
    BEGIN
      TESTFIELD("Plant Company");
      PlantSetupRec.CHANGECOMPANY("Plant Company");
      PlantSetupRec.GET;
      PlantSetupRec.SetPlantCompany("Plant Company");  //T001104.n
      CASE Type OF
        Type::Arrival:
          BEGIN
            VALIDATE("From Location",PlantSetupRec.GetRentalDepot);
            VALIDATE("To Location", '');
            lvDefPlantLoc := GetProjectDefaultPlantLoc();
            IF lvDefPlantLoc <> '' THEN
              VALIDATE("To Location", lvDefPlantLoc);
          END;
        Type::Removal:
          BEGIN
            VALIDATE("From Location",'');
            VALIDATE("To Location",PlantSetupRec.GetReturnDepot);
            lvDefPlantLoc := GetProjectDefaultPlantLoc();
            IF lvDefPlantLoc <> '' THEN
              VALIDATE("From Location", lvDefPlantLoc);
          END;
        Type::OtherTransfers:
          BEGIN
            VALIDATE("From Location",'');
            VALIDATE("To Location",'');
            lvDefPlantLoc := GetProjectDefaultPlantLoc();
            IF lvDefPlantLoc <> '' THEN
              VALIDATE("From Location", lvDefPlantLoc);
          END;
      END;
    END;

    PROCEDURE GetProjectDefaultPlantLoc@1100525006() : Code[20];
    VAR
      lvPlantLocRec@1100525001 : Record 11012554;
      lvDefPlantLoc@1100525002 : Code[20];
      lvCompany@1100525000 : Text[30];
    BEGIN
      //* Default location only when there is only 1 plant location of the project.
      IF "Plant Company" <> COMPANYNAME THEN
        lvCompany := COMPANYNAME;
      lvPlantLocRec.CHANGECOMPANY("Plant Company");
      lvPlantLocRec.SETCURRENTKEY("Company Name", "Project No.", "Service Order No.", "Employee No.");
      lvPlantLocRec.SETRANGE(Depot, FALSE);
      lvPlantLocRec.SETRANGE("Project No.", "Project No.");
      lvPlantLocRec.SETRANGE("Company Name", lvCompany);
      IF lvPlantLocRec.FINDFIRST THEN BEGIN
        lvDefPlantLoc := lvPlantLocRec.Code;
        IF lvPlantLocRec.NEXT <> 0 THEN
          lvDefPlantLoc := '';
      END;
      EXIT(lvDefPlantLoc);
    END;

    PROCEDURE GetFromLocationName@1100485000() : Text[100];
    BEGIN
      IF ("From Location" = '') AND ("From Location Name" <> '') THEN
        EXIT("From Location Name");

      IF ("From Location" <> '') THEN BEGIN
        PlantLocRec.CHANGECOMPANY("Plant Company");
        IF PlantLocRec.GET("From Location") THEN
          EXIT(PlantLocRec.Description);
      END;

      EXIT('');
    END;

    PROCEDURE GetToLocationName@1100485001() : Text[100];
    BEGIN
      IF ("To Location" = '') AND ("To Location Name" <> '') THEN
        EXIT("To Location Name");

      IF ("To Location" <> '') THEN BEGIN
        PlantLocRec.CHANGECOMPANY("Plant Company");
        IF PlantLocRec.GET("To Location") THEN
          EXIT(PlantLocRec.Description);
      END;

      EXIT('');
    END;

    PROCEDURE GetPlantRequestData@1100525003(VAR OPlantReqRec@1100525000 : Record 11020520) : Boolean;
    BEGIN
      //* Plant Request in the plant company (so not 'Site Mgt. Plant Request')
      CLEAR(OPlantReqRec);
      IF "Plant Request No." <> '' THEN BEGIN
        OPlantReqRec.CHANGECOMPANY("Plant Company");
        IF OPlantReqRec.GET("Plant Request No.") THEN
          EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    PROCEDURE CheckLinkedProject@1100525001();
    VAR
      lvCompany@1100525000 : Text[30];
    BEGIN
      PlantLocRec.TESTFIELD("Project No.");
      PlantLocRec.TESTFIELD("Project No.", "Project No.");
      IF "Plant Company" <> COMPANYNAME THEN
        lvCompany := COMPANYNAME;
      PlantLocRec.TESTFIELD("Company Name", lvCompany);
    END;

    PROCEDURE LookupPlantCompany@1100525005();
    VAR
      PlantMgtCompRec@1100525001 : Record 11020586;
      TmpPlantMgtCompRec@1100525002 : TEMPORARY Record 11020586;
      NoOfPlantComps@1100525000 : Integer;
    BEGIN
      PlantMgtCompRec.GetPlantCompany(TRUE, NoOfPlantComps, '');
      IF PlantMgtCompRec.GetPlantCompaniesForProject("Project No.", TmpPlantMgtCompRec, '') = 0 THEN BEGIN
        PlantMgtCompRec."Plant Company" := "Plant Company";
        PAGE.RUNMODAL(0, PlantMgtCompRec);
      END ELSE BEGIN
        TmpPlantMgtCompRec."Plant Company" := "Plant Company";
        IF PAGE.RUNMODAL(0, TmpPlantMgtCompRec) = ACTION::LookupOK THEN BEGIN
          IF (Status = Status::Open) AND (TmpPlantMgtCompRec."Plant Company" <> "Plant Company") THEN
            VALIDATE("Plant Company", TmpPlantMgtCompRec."Plant Company");
        END;
      END;
    END;

    PROCEDURE LookupFromLocation@1100525000();
    VAR
      lvPlantLocRec@1100525001 : Record 11012554;
      lvCompany@1100525000 : Text[30];
    BEGIN
      TESTFIELD("Plant Company");
      IF "Plant Company" <> COMPANYNAME THEN
        lvCompany := COMPANYNAME;
      lvPlantLocRec.CHANGECOMPANY("Plant Company");
      lvPlantLocRec.SETCURRENTKEY("Company Name", "Project No.", "Service Order No.", "Employee No.");
      lvPlantLocRec.FILTERGROUP(9);
      CASE Type OF
        Type::Arrival:
          lvPlantLocRec.SETRANGE(Depot, TRUE);
        Type::Removal,
        Type::OtherTransfers:  //* Other: Only from location of the project, to another location
          BEGIN
            lvPlantLocRec.SETRANGE(Depot, FALSE);
            lvPlantLocRec.SETRANGE("Project No.", "Project No.");
            lvPlantLocRec.SETRANGE("Company Name", lvCompany);
          END;
      END;
      lvPlantLocRec.FILTERGROUP(0);
      lvPlantLocRec.Code := "From Location";
      lvPlantLocRec."Project No." := "Project No.";
      lvPlantLocRec."Company Name" := lvCompany;
      IF PAGE.RUNMODAL(0, lvPlantLocRec) = ACTION::LookupOK THEN BEGIN
        IF Status = Status::Open THEN
          VALIDATE("From Location", lvPlantLocRec.Code);
      END;
    END;

    PROCEDURE LookupToLocation@1100525002();
    VAR
      lvPlantLocRec@1100525001 : Record 11012554;
      lvCompany@1100525000 : Text[30];
    BEGIN
      TESTFIELD("Plant Company");
      IF "Plant Company" <> COMPANYNAME THEN
        lvCompany := COMPANYNAME;
      lvPlantLocRec.CHANGECOMPANY("Plant Company");
      lvPlantLocRec.SETCURRENTKEY("Company Name", "Project No.", "Service Order No.", "Employee No.");
      lvPlantLocRec.FILTERGROUP(9);
      CASE Type OF
        Type::Arrival:
          BEGIN
            lvPlantLocRec.SETRANGE(Depot, FALSE);
            lvPlantLocRec.SETRANGE("Project No.", "Project No.");
            lvPlantLocRec.SETRANGE("Company Name", lvCompany);
          END;
        Type::Removal:
          lvPlantLocRec.SETRANGE(Depot, TRUE);
        Type::OtherTransfers:  //* Other: Only from location of the project, to another location
          lvPlantLocRec.SETRANGE(Depot, FALSE);
      END;
      lvPlantLocRec.FILTERGROUP(0);
      lvPlantLocRec.Code := "To Location";
      lvPlantLocRec."Project No." := "Project No.";
      lvPlantLocRec."Company Name" := lvCompany;
      IF PAGE.RUNMODAL(0, lvPlantLocRec) = ACTION::LookupOK THEN BEGIN
        IF Status = Status::Open THEN
          VALIDATE("To Location", lvPlantLocRec.Code);
      END;
    END;

    PROCEDURE LookupFromLocAddress@1100525008();
    VAR
      lvLocAddressRec@1100525000 : Record 11012590;
    BEGIN
      TESTFIELD("Plant Company");
      lvLocAddressRec.CHANGECOMPANY("Plant Company");
      lvLocAddressRec.FILTERGROUP(9);
      lvLocAddressRec.SETRANGE(Location, "From Location");
      lvLocAddressRec.FILTERGROUP(0);
      lvLocAddressRec.Location := "From Location";
      lvLocAddressRec."Address Code" := "From Location Address Code";
      IF PAGE.RUNMODAL(0, lvLocAddressRec) = ACTION::LookupOK THEN BEGIN
        IF Status = Status::Open THEN
          VALIDATE("From Location Address Code", lvLocAddressRec."Address Code");
      END;
    END;

    PROCEDURE LookupToLocAddress@1100525011();
    VAR
      lvLocAddressRec@1100525000 : Record 11012590;
    BEGIN
      TESTFIELD("Plant Company");
      lvLocAddressRec.CHANGECOMPANY("Plant Company");
      lvLocAddressRec.FILTERGROUP(9);
      lvLocAddressRec.SETRANGE(Location, "To Location");
      lvLocAddressRec.FILTERGROUP(0);
      lvLocAddressRec.Location := "To Location";
      lvLocAddressRec."Address Code" := "To Location Address Code";
      IF PAGE.RUNMODAL(0, lvLocAddressRec) = ACTION::LookupOK THEN BEGIN
        IF Status = Status::Open THEN
          VALIDATE("To Location Address Code", lvLocAddressRec."Address Code");
      END;
    END;

    BEGIN
    END.
  }
}

