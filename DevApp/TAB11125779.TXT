OBJECT Table 11125779 Glazing Service Call Assembly
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE("VAT Prod. Posting Group", UpdateVatProdPostingGrp);  //RFC 547
             END;

    CaptionML=ENU=Glazing Service Call Assembly;
    LookupPageID=Page11125790;
    DrillDownPageID=Page11125790;
  }
  FIELDS
  {
    { 1   ;   ;Glazing Service Call No.;Code20    ;TableRelation="Glazing Service Call";
                                                   CaptionML=ENU=Glazing Service Call No.;
                                                   NotBlank=Yes }
    { 2   ;   ;Glazing Service Call Line No.;Integer;
                                                   TableRelation="Glazing Service Call Line"."Line No." WHERE (Glazing Service Call No.=FIELD(Glazing Service Call No.));
                                                   OnValidate=VAR
                                                                GlazingServiceCallLine@1100525000 : Record 11125778;
                                                              BEGIN
                                                                IF NOT GlazingServiceCallLine.GET("Glazing Service Call No.", "Glazing Service Call Line No.") THEN
                                                                  GlazingServiceCallLine.INIT;

                                                                VALIDATE("Surface Area Driemaat (m2)", GlazingServiceCallLine."Surface Area Driemaat (m2)");
                                                                VALIDATE("Surface Area (m2)", GlazingServiceCallLine."Surface Area (m2)");
                                                                VALIDATE("Linear Meters", GlazingServiceCallLine."Linear Meters");
                                                                VALIDATE("Quantity (Glazing Call Line)", GlazingServiceCallLine.Quantity);
                                                                VALIDATE("Shape Surcharge %", GlazingServiceCallLine."Shape Surcharge %");
                                                              END;

                                                   CaptionML=ENU=Glazing Service Call Line No.;
                                                   NotBlank=Yes }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 10  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=VAR
                                                                Item@1100525000 : Record 27;
                                                              BEGIN
                                                                IF NOT Item.GET("Item No.") THEN
                                                                  Item.INIT
                                                                //mg.sn, 13-12-11: M31429
                                                                ELSE BEGIN
                                                                  Item.TESTFIELD(Blocked, FALSE);
                                                                END;
                                                                //mg.en, 13-12-11: M31429

                                                                Description := Item.Description;
                                                                "Description 2" := Item."Description 2";
                                                                UpdateBasicPrice(FIELDNO("Item No."));
                                                                "Standard Cost" := Item."Unit Cost";
                                                                "Global Dimension 2 Code" := Item."Global Dimension 2 Code";
                                                                VALIDATE("Glazing Application Code", Item."Glazing Application Code");
                                                                VALIDATE("Item Weight", Item.Weight);
                                                                VALIDATE("Min. Sfc. Area Driemaat (m2)", Item."Min. Sfc. Area Driemaat (m2)");

                                                                // jhoek.231110.begin
                                                                //CASE Item."Glazing Ordering Code" = '' OF
                                                                //  TRUE: "Ordering Code" := "Item No.";
                                                                //  FALSE: "Ordering Code" := Item."Glazing Ordering Code";
                                                                //END;
                                                                "Ordering Code" := Item."Glazing Ordering Code";
                                                                // jhoek.231110.end

                                                                VALIDATE("VAT Prod. Posting Group", UpdateVatProdPostingGrp);  //RFC 547
                                                                UpdateTotals;

                                                                //db, 31-08-15: function LookupItemNoWithSalesPrice from OnLookup moved to OnAssistEdit on page
                                                              END;

                                                   CaptionML=ENU=Item No. }
    { 11  ;   ;Glazing Application Code;Code20    ;TableRelation="Glazing Application";
                                                   OnValidate=VAR
                                                                GlazingApplication@1100525000 : Record 11125763;
                                                              BEGIN
                                                                IF NOT GlazingApplication.GET("Glazing Application Code") THEN
                                                                  GlazingApplication.INIT;

                                                                "Min. Breastwork Height" := GlazingApplication."Min. Breastwork Height";
                                                                "Tilted Mounting Disallowed" := GlazingApplication."Tilted Mounting Disallowed";
                                                              END;

                                                   CaptionML=ENU=Glazing Application Code;
                                                   Editable=No }
    { 12  ;   ;Min. Sfc. Area Driemaat (m2);Decimal;
                                                   CaptionML=ENU=Min. Sfc. Area Driemaat (m2);
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 15  ;   ;Ordering Code       ;Code20        ;CaptionML=ENU=Ordering Code;
                                                   Editable=No }
    { 20  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 21  ;   ;Description 2       ;Text50        ;CaptionML=ENU=Description 2 }
    { 25  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2),
                                                                                               Cost Type=CONST(Material));
                                                   CaptionML=ENU=Global Dimension 2 Code;
                                                   CaptionClass='1,1,2' }
    { 30  ;   ;Cost Driver         ;Option        ;InitValue=Surface Area;
                                                   OnValidate=BEGIN
                                                                Quantity := 0;
                                                                UpdateQuantity;
                                                              END;

                                                   CaptionML=ENU=Cost Driver;
                                                   OptionCaptionML=ENU=" ,,,,,Surface Area,,,,,Linear Meters";
                                                   OptionString=[ ,,,,,Surface Area,,,,,Linear Meters] }
    { 35  ;   ;Surface Area (m2)   ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotalWeight;
                                                              END;

                                                   CaptionML=ENU=Surface Area (m2);
                                                   Editable=No }
    { 40  ;   ;Surface Area Driemaat (m2);Decimal ;OnValidate=BEGIN
                                                                UpdateQuantity;
                                                              END;

                                                   CaptionML=ENU=Surface Area Driemaat (m2);
                                                   Editable=No }
    { 50  ;   ;Linear Meters       ;Decimal       ;OnValidate=BEGIN
                                                                UpdateQuantity;
                                                              END;

                                                   CaptionML=ENU=Linear Meters;
                                                   Editable=No }
    { 60  ;   ;Quantity (Glazing Call Line);Decimal;
                                                   OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=ENU=Quantity (Glazing Call Line);
                                                   Editable=No }
    { 70  ;   ;Item Weight         ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotalWeight;
                                                              END;

                                                   CaptionML=ENU=Item Weight;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   Editable=No }
    { 71  ;   ;Total Weight        ;Decimal       ;OnValidate=VAR
                                                                GlazingServiceCallLine@1100525000 : Record 11125778;
                                                                GlazingServiceCallAssembly@1100525001 : Record 11125779;
                                                              BEGIN
                                                              END;

                                                   CaptionML=ENU=Total Weight;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   Editable=No }
    { 100 ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO(Quantity) THEN
                                                                  IF Quantity <> 0 THEN
                                                                    TESTFIELD("Cost Driver", "Cost Driver"::" ");

                                                                UpdateBasicPrice(FIELDNO(Quantity));
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5 }
    { 110 ;   ;Unit Price          ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=ENU=Unit Price;
                                                   Editable=No }
    { 120 ;   ;Standard Cost       ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=ENU=Standard Cost;
                                                   Editable=No }
    { 125 ;   ;Nett Sales Amount   ;Decimal       ;CaptionML=ENU=Nett Sales Amount;
                                                   Editable=No }
    { 130 ;   ;Surface Surcharge % ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=ENU=Surface Surcharge %;
                                                   DecimalPlaces=0:2;
                                                   BlankZero=Yes }
    { 131 ;   ;Surface Surcharge Amt. (Sales);Decimal;
                                                   CaptionML=ENU=Surface Surcharge Amount (Sales);
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 132 ;   ;Surface Surcharge Amt. (Cost);Decimal;
                                                   CaptionML=ENU=Surface Surcharge Amt. (Cost);
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 140 ;   ;Shape Surcharge %   ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=ENU=Shape Surcharge %;
                                                   DecimalPlaces=0:2;
                                                   BlankZero=Yes }
    { 141 ;   ;Shape Surcharge Amount (Cost);Decimal;
                                                   CaptionML=ENU=Shape Surcharge Amount (Cost);
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 142 ;   ;Shape Surcharge Amount (Sales);Decimal;
                                                   CaptionML=ENU=Shape Surcharge Amount (Sales);
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 200 ;   ;Sales Amount        ;Decimal       ;CaptionML=ENU=Sales Amount;
                                                   Editable=No }
    { 210 ;   ;Standard Cost Amount;Decimal       ;CaptionML=ENU=Standard Cost Amount;
                                                   Editable=No }
    { 300 ;   ;Min. Breastwork Height;Decimal     ;CaptionML=ENU=Min. Breastwork Height;
                                                   Editable=No }
    { 310 ;   ;Tilted Mounting Disallowed;Boolean ;CaptionML=ENU=Tilted Mounting Disallowed;
                                                   Editable=No }
    { 400 ;   ;Total Cost Amount   ;Decimal       ;CaptionML=ENU=Total Cost Amount;
                                                   Editable=No }
    { 410 ;   ;Total Sales Amount  ;Decimal       ;CaptionML=ENU=Total Sales Amount;
                                                   Editable=No }
    { 1050;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group".Code;
                                                   CaptionML=ENU=VAT Prod. Posting Group;
                                                   Editable=No }
    { 1060;   ;VAT %               ;Decimal       ;CaptionML=ENU=VAT %;
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 1070;   ;Total Sales VAT Amount;Decimal     ;CaptionML=ENU=Total Sales VAT Amount;
                                                   Editable=No }
    { 1080;   ;Total Sales Amount incl. VAT;Decimal;
                                                   CaptionML=ENU=Total Sales Amount incl. VAT;
                                                   Editable=No }
    { 1090;   ;Basic Price         ;Decimal       ;OnValidate=BEGIN
                                                                UpdateUnitPrice;
                                                              END;

                                                   CaptionML=ENU=Basic Price;
                                                   Editable=No }
    { 1100;   ;Item Discount %     ;Decimal       ;OnValidate=BEGIN
                                                                UpdateUnitPrice;
                                                              END;

                                                   CaptionML=ENU=Item Discount %;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Glazing Service Call No.,Glazing Service Call Line No.,Line No.;
                                                   SumIndexFields=Sales Amount,Standard Cost Amount,Total Weight,Total Sales Amount,Total Cost Amount,Total Sales VAT Amount,Total Sales Amount incl. VAT;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Glazing Application Code                 }
    {    ;VAT Prod. Posting Group                 ;SumIndexFields=Sales Amount,Standard Cost Amount,Total Weight,Total Sales Amount,Total Cost Amount,Total Sales VAT Amount,Total Sales Amount incl. VAT }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE UpdateBasicPrice@1100525017(CalledByFieldNo@1100525000 : Integer);
    VAR
      SalesPriceCalcMgt@1100525001 : Codeunit 7000;
    BEGIN
      IF "Item No." = '' THEN  BEGIN
        VALIDATE("Basic Price", 0);
        EXIT;
      END;

      SalesPriceCalcMgt.FindGlazSvcCallAssemblyPrice(Rec,CalledByFieldNo);
    END;

    PROCEDURE UpdateQuantity@1100525001();
    BEGIN
      CASE "Cost Driver" OF
        "Cost Driver"::"Surface Area": VALIDATE(Quantity, "Surface Area Driemaat (m2)");
        "Cost Driver"::"Linear Meters": VALIDATE(Quantity, "Linear Meters");
      END;
    END;

    PROCEDURE UpdateTotals@1100525005();
    VAR
      NetCostAmount@1100525000 : Decimal;
    BEGIN
      "Nett Sales Amount" := "Unit Price" * Quantity;
      NetCostAmount := "Standard Cost" * Quantity;

      "Surface Surcharge Amt. (Cost)" := NetCostAmount * ("Surface Surcharge %" / 100);
      "Surface Surcharge Amt. (Sales)" := "Nett Sales Amount" * ("Surface Surcharge %" / 100);

      "Shape Surcharge Amount (Cost)" := NetCostAmount * ("Shape Surcharge %" / 100);
      "Shape Surcharge Amount (Sales)" := "Nett Sales Amount" * ("Shape Surcharge %" / 100);

      "Sales Amount" := "Nett Sales Amount" + "Surface Surcharge Amt. (Sales)" + "Shape Surcharge Amount (Sales)";
      "Standard Cost Amount" := NetCostAmount + "Surface Surcharge Amt. (Cost)" + "Shape Surcharge Amount (Cost)";

      "Total Cost Amount" := "Standard Cost Amount" * "Quantity (Glazing Call Line)";
      "Total Sales Amount" := "Sales Amount" * "Quantity (Glazing Call Line)";

      CalcVATAmount;  // RFC 547
    END;

    PROCEDURE UpdateTotalWeight@1100525000();
    BEGIN
      VALIDATE("Total Weight", "Surface Area (m2)"* "Item Weight");
    END;

    PROCEDURE UpdateVatProdPostingGrp@1100525018() VatProdPostingGrp : Code[20];
    VAR
      ServiceSetup@1100525001 : Record 11012800;
      ItemRec@1100525002 : Record 27;
      ServLocRec@1100525003 : Record 11012801;
      GlazingServiceCall@1100525004 : Record 11125777;
      Customer@1100525005 : Record 18;
      DimValRec@1100525006 : Record 349;
      DimMgt@1100525000 : Codeunit 408;
    BEGIN
      // RFC 547
      VatProdPostingGrp := '';
      GlazingServiceCall.GET("Glazing Service Call No.");
      ServiceSetup.GET;

      IF (ServiceSetup."Source VAT Prod. Posting Group" =
          ServiceSetup."Source VAT Prod. Posting Group"::"Item/CostObject") THEN
      BEGIN

        IF NOT ItemRec.GET("Item No.") THEN
          ItemRec.INIT;

        IF (ItemRec."VAT Prod. Posting Group" <> '') THEN
        BEGIN
          VatProdPostingGrp := ItemRec."VAT Prod. Posting Group";
        END;

        IF VatProdPostingGrp = '' THEN BEGIN
          DimMgt.GetDimValueRec(2,"Global Dimension 2 Code",DimValRec,FALSE,'');
          IF (DimValRec."VAT Prod. Posting Group" <> '') THEN
          BEGIN
            VatProdPostingGrp := DimValRec."VAT Prod. Posting Group";
          END;
        END;
      END;

      IF VatProdPostingGrp = '' THEN BEGIN
        IF ServLocRec.GET(GlazingServiceCall."Service Location No.") THEN BEGIN
          IF ServLocRec."VAT Prod. Posting Group" <> '' THEN
            VatProdPostingGrp := ServLocRec."VAT Prod. Posting Group";
        END;
      END;

      IF VatProdPostingGrp = '' THEN BEGIN
        IF Customer.GET(GlazingServiceCall."Customer No.") THEN
          VatProdPostingGrp := Customer."VAT Prod. Posting Group";
      END;

      IF VatProdPostingGrp = '' THEN BEGIN
        VatProdPostingGrp := ServiceSetup."VAT Prod. Posting Group"
      END;
    END;

    PROCEDURE CalcVATAmount@1100525019();
    VAR
      GlazingServiceCall@1100525001 : Record 11125777;
    BEGIN
      // RFC 547
      GlazingServiceCall.GET("Glazing Service Call No.");
      CalcVATAmount2(GlazingServiceCall);
    END;

    PROCEDURE CalcVATAmount2@1100525002(GlazingServiceCall@1100525001 : Record 11125777);
    VAR
      VATPostingSetup@1100525000 : Record 325;
      Currency@1100525002 : Record 4;
    BEGIN
      // RFC 547
      IF NOT VATPostingSetup.GET(GlazingServiceCall."VAT Bus. Posting Group",
                                 "VAT Prod. Posting Group")
      THEN
        VATPostingSetup.INIT;

      "VAT %" := 0;
      "Total Sales VAT Amount" := 0;
      "Total Sales Amount incl. VAT" := "Total Sales Amount";
      IF VATPostingSetup."VAT Calculation Type" = VATPostingSetup."VAT Calculation Type"::"Normal VAT" THEN BEGIN
        "VAT %" := VATPostingSetup."VAT %";
        Currency.InitRoundingPrecision;
        "Total Sales Amount incl. VAT" := ROUND(
           "Total Sales Amount" * (100 + "VAT %") /100,
           Currency."Unit-Amount Rounding Precision");
        "Total Sales VAT Amount" := "Total Sales Amount incl. VAT" - "Total Sales Amount";
      END;
    END;

    PROCEDURE LookupItemNoWithSalesPrice@1100528400();
    VAR
      GlazingServiceCall@1100528401 : Record 11125777;
      Item@1100528400 : Record 27;
    BEGIN
      //kkleermaker, 110721, M23540
      //db, 31-08-15: function was local (for lookup table field ItemNo, but now used on page)
      GlazingServiceCall.GET("Glazing Service Call No.");
      Item.LookupItemNoWithSalesPrice(
        "Item No.",
        Quantity,
        1,
        GlazingServiceCall."Customer No.",
        GlazingServiceCall.Date);
      VALIDATE("Item No.");
    END;

    PROCEDURE UpdateUnitPrice@1100528600();
    BEGIN
      VALIDATE("Unit Price", "Basic Price" * (100 - "Item Discount %") / 100);
    END;

    BEGIN
    END.
  }
}

