OBJECT Table 11125854 Timesheet Portal Template Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               TimesheetPortalTemplateLine@1100409000 : Record 11125854;
             BEGIN
               IF "Line No." = 0 THEN BEGIN
                 TimesheetPortalTemplateLine.SETRANGE(Type, Type);
                 TimesheetPortalTemplateLine.SETRANGE("CAO Code", "CAO Code");
                 IF NOT TimesheetPortalTemplateLine.FINDLAST THEN
                   TimesheetPortalTemplateLine."Line No." := 0;
                 "Line No." := TimesheetPortalTemplateLine."Line No." + 10000;
               END
             END;

    CaptionML=ENU=Timesheet Portal Template Line;
  }
  FIELDS
  {
    { 10  ;   ;CAO Code            ;Code10        ;TableRelation="Site Mngmnt. CAO's";
                                                   CaptionML=ENU=CAO Code;
                                                   NotBlank=No }
    { 20  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Type <> xRec.Type THEN BEGIN
                                                                  IF Type <> Type::Indirect THEN BEGIN
                                                                    VALIDATE("Cost Code",'');
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Project,Service,Indirect;
                                                   OptionString=Project,Service,Indirect }
    { 25  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 28  ;   ;Sequence No.        ;Integer       ;CaptionML=ENU=Sequence No. }
    { 30  ;   ;Cost Code           ;Code10        ;TableRelation="Cost Code".Code;
                                                   OnValidate=VAR
                                                                CostCode@1100409000 : Record 11012083;
                                                              BEGIN
                                                                IF ("Cost Code" <> '') AND (Type = Type::Indirect) THEN BEGIN
                                                                  CostCode.GET("Cost Code");
                                                                  CostCode.TESTFIELD(Blocked, FALSE);
                                                                  IF CostCode.Component <> '' THEN
                                                                    VALIDATE("Wage Component", CostCode.Component);
                                                                END;
                                                                CALCFIELDS("Cost Code Description");
                                                              END;

                                                   CaptionML=ENU=Cost Code }
    { 40  ;   ;Cost Code Description;Text30       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Cost Code".Description WHERE (Code=FIELD(Cost Code)));
                                                   CaptionML=ENU=Cost Code Description;
                                                   Editable=No }
    { 50  ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Cost Type=FILTER(Labor|Sundry),
                                                                                               Global Dimension No.=CONST(2));
                                                   OnValidate=VAR
                                                                DimValRec@1100409001 : Record 349;
                                                                DimMgt@1100409000 : Codeunit 408;
                                                              BEGIN
                                                                IF "Cost Object" = '' THEN
                                                                  EXIT;

                                                                DimMgt.GetDimValueRec(2, "Cost Object", DimValRec, TRUE, '');
                                                                DimValRec.TESTFIELD(Blocked, FALSE);
                                                                CALCFIELDS("Cost Object Description");
                                                                IF (DimValRec."Wage Component" <> '') AND ("Wage Component" <> DimValRec."Wage Component") THEN
                                                                  VALIDATE("Wage Component", DimValRec."Wage Component");
                                                              END;

                                                   OnLookup=VAR
                                                              DimValRec@1100409000 : Record 349;
                                                            BEGIN
                                                              DimValRec.Code := "Cost Object";
                                                              DimValRec.SETRANGE("Global Dimension No.", 2);
                                                              DimValRec.SETFILTER("Cost Type",'%1|%2|%3',
                                                                DimValRec."Cost Type"::Labor,DimValRec."Cost Type"::Subcontracting,DimValRec."Cost Type"::Sundry); //DP01010
                                                              IF PAGE.RUNMODAL(PAGE::"Cost Object List", DimValRec) = ACTION::LookupOK THEN
                                                                VALIDATE("Cost Object", DimValRec.Code);
                                                              DimValRec.RESET;
                                                            END;

                                                   CaptionML=ENU=Cost Object }
    { 60  ;   ;Cost Object Description;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value".Name WHERE (Global Dimension No.=FILTER(2),
                                                                                                    Code=FIELD(Cost Object)));
                                                   CaptionML=ENU=Cost Object Description;
                                                   Editable=No }
    { 70  ;   ;Wage Component      ;Code10        ;TableRelation="Wage Component".Code WHERE (Component Type=FILTER(Hours|Expenses));
                                                   CaptionML=ENU=Wage Component }
    { 75  ;   ;Wage Component Description;Text30  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Wage Component".Description WHERE (Code=FIELD(Wage Component)));
                                                   CaptionML=ENU=Wage Component Description }
    { 80  ;   ;Comment             ;Text50        ;CaptionML=ENU=Comment }
  }
  KEYS
  {
    {    ;CAO Code,Type,Line No.                  ;Clustered=Yes }
    {    ;CAO Code,Type,Sequence No.               }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    BEGIN
    END.
  }
}

