OBJECT Table 11126401 Subcont.Port. User
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               SubcontPortVendor@1100528100 : Record 11126400;
             BEGIN
               GuardUniqueness();

               SubcontPortVendor.GET("Vendor No.");
               "Contract Related" := SubcontPortVendor."Contract Related";
               "Call Succession" := SubcontPortVendor."Call Succession";
               "Additional Orders" := SubcontPortVendor."Additional Orders";
               "User Maintenance" := SubcontPortVendor."User Maintenance";
               "Document Management" := SubcontPortVendor."Document Management";
               //"Export Orders" := SubcontPortVendor."Export Orders";
               Attachments := SubcontPortVendor.Attachments;
               "Allow Status Changes" := SubcontPortVendor."Allow Status Changes";
             END;

    OnModify=BEGIN
               GuardUniqueness();

               IF NOT ("Contract Related" OR "Call Succession" OR "Additional Orders") THEN
                 ERROR(Text005, FIELDCAPTION("Contract Related"), FIELDCAPTION("Call Succession"),FIELDCAPTION("Additional Orders"));
             END;

    OnDelete=VAR
               SubcontrPortLocation@1100528101 : Record 11126403;
             BEGIN
               SubcontrPortLocation.SETCURRENTKEY("Vendor No.");
               SubcontrPortLocation.SETRANGE("Vendor No.", "Vendor No.");
               SubcontrPortLocation.SETRANGE(Login, Login);
               SubcontrPortLocation.DELETEALL(FALSE); //FALSE because cascade delete will go wrong otherwise.
             END;

    CaptionML=ENU=Subcontractor Portal User;
    LookupPageID=Page11126407;
    DrillDownPageID=Page11126407;
  }
  FIELDS
  {
    { 10  ;   ;Vendor No.          ;Code20        ;TableRelation="Subcont.Port. Vendor"."Vendor No.";
                                                   CaptionML=ENU=Vendor No.;
                                                   NotBlank=Yes }
    { 20  ;   ;Login               ;Text30        ;OnValidate=BEGIN
                                                                TESTFIELD(Login);
                                                              END;

                                                   CaptionML=ENU=Login;
                                                   NotBlank=Yes }
    { 30  ;   ;Password            ;Text32        ;OnValidate=BEGIN
                                                                TESTFIELD(Password);
                                                              END;

                                                   CaptionML=ENU=Password }
    { 32  ;   ;MD5 Password        ;Boolean       ;CaptionML=ENU=MD5 Password }
    { 40  ;   ;Name                ;Text50        ;CaptionML=ENU=Name }
    { 50  ;   ;E-Mail              ;Text80        ;OnValidate=BEGIN
                                                                TESTFIELD("E-Mail");
                                                              END;

                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=ENU=E-Mail }
    { 60  ;   ;Active              ;Boolean       ;CaptionML=ENU=Active }
    { 100 ;   ;Contract Related    ;Boolean       ;OnValidate=VAR
                                                                SubcontPortVendor@1100528101 : Record 11126400;
                                                              BEGIN
                                                                IF "Contract Related" THEN BEGIN
                                                                  SubcontPortVendor.GET("Vendor No.");
                                                                  SubcontPortVendor.TESTFIELD("Contract Related");
                                                                END
                                                              END;

                                                   CaptionML=ENU=Contract Related }
    { 110 ;   ;Call Succession     ;Boolean       ;OnValidate=VAR
                                                                SubcontPortVendor@1100528101 : Record 11126400;
                                                              BEGIN
                                                                IF "Call Succession" THEN BEGIN
                                                                  SubcontPortVendor.GET("Vendor No.");
                                                                  SubcontPortVendor.TESTFIELD("Call Succession");
                                                                END
                                                              END;

                                                   CaptionML=ENU=Call Succession based }
    { 120 ;   ;Additional Orders   ;Boolean       ;OnValidate=VAR
                                                                SubcontPortVendor@1100528101 : Record 11126400;
                                                              BEGIN
                                                                IF "Additional Orders" THEN BEGIN
                                                                  SubcontPortVendor.GET("Vendor No.");
                                                                  SubcontPortVendor.TESTFIELD("Additional Orders");
                                                                END
                                                              END;

                                                   CaptionML=ENU=Additional Orders }
    { 160 ;   ;User Maintenance    ;Boolean       ;OnValidate=VAR
                                                                SubcontPortVendor@1100528101 : Record 11126400;
                                                              BEGIN
                                                                IF "User Maintenance" THEN BEGIN
                                                                  SubcontPortVendor.GET("Vendor No.");
                                                                  SubcontPortVendor.TESTFIELD("User Maintenance");
                                                                END
                                                              END;

                                                   CaptionML=ENU=User Maintenance }
    { 170 ;   ;Document Management ;Boolean       ;OnValidate=VAR
                                                                SubcontPortVendor@1100528101 : Record 11126400;
                                                              BEGIN
                                                                IF "Document Management" THEN BEGIN
                                                                  SubcontPortVendor.GET("Vendor No.");
                                                                  SubcontPortVendor.TESTFIELD("Document Management");
                                                                END
                                                              END;

                                                   CaptionML=ENU=Document Management }
    { 180 ;   ;Send Mail at WO Status Update;Boolean;
                                                   CaptionML=ENU=Send Mail at WO Status Update }
    { 195 ;   ;Attachments         ;Boolean       ;OnValidate=VAR
                                                                SubcontPortVendor@1100528100 : Record 11126400;
                                                              BEGIN
                                                                IF Attachments THEN BEGIN
                                                                  SubcontPortVendor.GET("Vendor No.");
                                                                  SubcontPortVendor.TESTFIELD(Attachments);
                                                                END
                                                              END;

                                                   CaptionML=ENU=Add Attachments }
    { 200 ;   ;Allow Status Changes;Boolean       ;OnValidate=VAR
                                                                SubcontPortVendor@1100528101 : Record 11126400;
                                                              BEGIN
                                                                IF "Allow Status Changes" THEN BEGIN
                                                                  SubcontPortVendor.GET("Vendor No.");
                                                                  SubcontPortVendor.TESTFIELD("Allow Status Changes");
                                                                END
                                                              END;

                                                   CaptionML=ENU=Allow Status Changes }
    { 210 ;   ;Primary Contact No. ;Code20        ;TableRelation=Contact;
                                                   OnValidate=VAR
                                                                Contact@1001 : Record 5050;
                                                                ContactBusinessRelation@1000 : Record 5054;
                                                                SubcontPortVendor@1100528600 : Record 11126400;
                                                              BEGIN
                                                                IF "Primary Contact No." <> '' THEN BEGIN
                                                                  Contact.GET("Primary Contact No.");
                                                                  Contact.TESTFIELD(Blocked, FALSE);
                                                                  Contact.TESTFIELD(Type, Contact.Type::Person);

                                                                  ContactBusinessRelation.SETCURRENTKEY("Link to Table","No.");
                                                                  ContactBusinessRelation.SETRANGE("Link to Table", ContactBusinessRelation."Link to Table"::Vendor);
                                                                  ContactBusinessRelation.SETRANGE("No.", "Vendor No.");
                                                                  ContactBusinessRelation.FIND('-');

                                                                  SubcontPortVendor.GET("Vendor No.");
                                                                  SubcontPortVendor.CALCFIELDS("Vendor Name");
                                                                  IF Contact."Company No." <> ContactBusinessRelation."Contact No." THEN
                                                                    ERROR(Text007, Contact."No.", Contact.Name, "Vendor No.", SubcontPortVendor."Vendor Name");

                                                                  VALIDATE(Name, Contact.Name);
                                                                  VALIDATE("E-Mail", Contact."E-Mail");
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              Contact@1001 : Record 5050;
                                                              ContactBusinessRelation@1000 : Record 5054;
                                                            BEGIN
                                                              ContactBusinessRelation.SETCURRENTKEY("Link to Table","No.");
                                                              ContactBusinessRelation.SETRANGE("Link to Table", ContactBusinessRelation."Link to Table"::Vendor);
                                                              ContactBusinessRelation.SETRANGE("No.", "Vendor No.");
                                                              IF ContactBusinessRelation.FINDFIRST THEN
                                                                Contact.SETRANGE("Company No.", ContactBusinessRelation."Contact No.")
                                                              ELSE
                                                                Contact.SETRANGE("No.",'');
                                                              Contact.SETRANGE(Type, Contact.Type::Person);

                                                              IF "Primary Contact No." <> '' THEN
                                                                IF Contact.GET("Primary Contact No.") THEN ;
                                                              IF PAGE.RUNMODAL(0, Contact) = ACTION::LookupOK THEN
                                                                VALIDATE("Primary Contact No.", Contact."No.");
                                                            END;

                                                   CaptionML=ENU=Primary Contact No. }
    { 300 ;   ;All Locations Visible;Boolean      ;InitValue=Yes;
                                                   OnValidate=VAR
                                                                SubcontrPortLocation@1100528101 : Record 11126403;
                                                              BEGIN
                                                                IF Login <> '' THEN BEGIN
                                                                  SubcontrPortLocation.SETCURRENTKEY("Vendor No.");
                                                                  SubcontrPortLocation.SETRANGE("Vendor No.", "Vendor No.");
                                                                  SubcontrPortLocation.SETRANGE(Login, Login);
                                                                  IF "All Locations Visible" THEN BEGIN
                                                                    SubcontrPortLocation.DELETEALL(FALSE);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=All Locations Visible }
    { 310 ;   ;URL Link            ;Text250       ;CaptionML=ENU=URL Link;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Vendor No.,Login                        ;Clustered=Yes }
    {    ;Login                                    }
    {    ;E-Mail                                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text002@1100528101 : TextConst 'ENU=Your new password for the Subcontractor Portal';
      Text003@1100528000 : TextConst 'ENU=The new password is created.';
      Text004@1100528103 : TextConst 'ENU=Another %1 with %2 %3 already exists.';
      Text005@1100528104 : TextConst 'ENU=At least one of the following fields must be selected: %1, %2 or %3.';
      Text006@1100528600 : TextConst 'ENU=Notice: Another %1 with %2 %3 already exists.';
      Text007@1100528601 : TextConst 'ENU=Contact %1 %2 is not related to vendor %3 %4.';

    PROCEDURE GenerateAndMailPassword@1100528100();
    VAR
      passWordToMail@1100528100 : Text[30];
    BEGIN
      GeneratePassword(passWordToMail, Password, "MD5 Password");
      MODIFY;

      TESTFIELD(Login);
      TESTFIELD("E-Mail");
      MailPassword(passWordToMail, TRUE);
    END;

    PROCEDURE ResetAndMailPassword@1100528000();
    VAR
      passWordToMail@1100528000 : Text[30];
    BEGIN
      GeneratePassword(passWordToMail, Password, "MD5 Password");
      MODIFY;

      TESTFIELD(Login);
      TESTFIELD("E-Mail");
      MailPassword(passWordToMail, FALSE);
    END;

    PROCEDURE GeneratePassword@1100528101(VAR Password@1100528100 : Text[30];VAR PasswordHash@1100528101 : Text[100];VAR MD5Hash@1100528105 : Boolean);
    VAR
      StringFunctions@1100525001 : Codeunit 11012273;
    BEGIN
      StringFunctions.GeneratePasswordMD5(Password, PasswordHash, MD5Hash, PasswordLength); //#kz, 20150114
    END;

    LOCAL PROCEDURE MailPassword@1100528102(NewPassword@1100528009 : Text[30];Generate@1100528000 : Boolean);
    VAR
      SubcontPortSetup@1100528008 : Record 11126402;
      Language@1100528007 : Record 8;
      EmailOnBehalf@1100528006 : Record 11012768;
      EmailBodyText@1100528005 : Record 11012769;
      CompanyInformation@1100528004 : Record 79;
      SMTPMail@1100528003 : Codeunit 400;
      BodyText@1100528002 : Text;
      SaveLanguageId@1100528001 : Integer;
    BEGIN
      SubcontPortSetup.GET('1');

      Language.SETRANGE("Windows Language ID", WINDOWSLANGUAGE);
      Language.FINDFIRST;

      EmailOnBehalf.GET(EmailOnBehalf.Usage::"Subcontrator Portal");

      IF Generate THEN
        EmailBodyText.SETRANGE(Usage, EmailBodyText.Usage::"Receive New SCPT Password")
      ELSE
        EmailBodyText.SETRANGE(Usage, EmailBodyText.Usage::"Receive Temporary SCPT Password");
      EmailBodyText.SETRANGE("Language Code", Language.Code);
      IF (EmailBodyText.COUNT < 1) THEN
        EmailBodyText.SETRANGE("Language Code");

      CompanyInformation.GET;

      IF (NOT EmailBodyText.FINDSET) THEN
        EXIT;

      SaveLanguageId := GLOBALLANGUAGE;
      GLOBALLANGUAGE(WINDOWSLANGUAGE);
      SMTPMail.CreateMessage(
        EmailOnBehalf."Send On Behalf Of Name",
        EmailOnBehalf."Send On Behalf Of E-mail",
        "E-Mail",
        Text003,
        '',
        TRUE);
      GLOBALLANGUAGE(SaveLanguageId);

      REPEAT
        BodyText := EmailBodyText.GetSubstituteResultTextLine5(0, "Vendor No.", '', '', Login, NewPassword, '', '');
        BodyText += '<br>';
        SMTPMail.AppendBody(BodyText);
      UNTIL (EmailBodyText.NEXT = 0) OR (STRLEN(BodyText) >= 1024);

      BodyText += '<br>';
      SMTPMail.AppendBody(SubcontPortSetup."URL Site Root");

      SMTPMail.Send;
    END;

    PROCEDURE GuardUniqueness@1100528103();
    VAR
      SubcontPortUser@1100528100 : Record 11126401;
    BEGIN
      SubcontPortUser.SETCURRENTKEY(Login);
      SubcontPortUser.SETFILTER("Vendor No.", '<>%1', "Vendor No.");
      SubcontPortUser.SETRANGE(Login, Login);
      IF NOT SubcontPortUser.ISEMPTY THEN
        ERROR(Text004, TABLECAPTION, FIELDCAPTION(Login), Login);

      SubcontPortUser.RESET;
      SubcontPortUser.SETCURRENTKEY("E-Mail");
      SubcontPortUser.SETRANGE("E-Mail", "E-Mail");
      SubcontPortUser.SETFILTER(Login, '<>%1', Login);
      IF NOT SubcontPortUser.ISEMPTY THEN
        MESSAGE(Text006, TABLECAPTION, FIELDCAPTION("E-Mail"), "E-Mail");
    END;

    LOCAL PROCEDURE PasswordLength@1100525003() : Integer;
    BEGIN
      EXIT(8);
    END;

    LOCAL PROCEDURE ReplaceBackslash@1100409000(IInputString@1100409000 : Text) OutputString : Text;
    VAR
      CRLF@1100409001 : Text;
      i@1100409002 : Integer;
    BEGIN
      CRLF := '  <BR/>';
      CRLF[1] := 13;
      CRLF[2] := 10;

      FOR i := 1 TO STRLEN(IInputString) DO BEGIN
        IF IInputString[i] <> '\' THEN
          OutputString += FORMAT(IInputString[i])
        ELSE
          OutputString += CRLF;
      END;
    END;

    BEGIN
    END.
  }
}

