OBJECT Table 11126687 Empoly Warehouse Order
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               InventSetup@1100525000 : Record 313;
               NoSeriesMgt@1100525001 : Codeunit 396;
             BEGIN
               InventSetup.GET;
               IF "No." = '' THEN BEGIN
                 InventSetup.TESTFIELD("Empoly Warehouse Order Nos.");
                 NoSeriesMgt.InitSeries(InventSetup."Empoly Warehouse Order Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
               TESTFIELD("No.");

               "Order Date" := WORKDATE;
               "Order Time" := TIME;
             END;

    OnModify=BEGIN
               IF "Scanner ID" = '' THEN
                 "User ID" := USERID;
             END;

    OnDelete=VAR
               OrderLine@1100525000 : Record 11126688;
             BEGIN
               OrderLine.SETRANGE("Order No.", "No.");
               OrderLine.DELETEALL;
             END;

    CaptionML=ENU=Empoly Warehouse Order;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;CaptionML=ENU=No. }
    { 20  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=User ID }
    { 30  ;   ;Scanner ID          ;Code20        ;CaptionML=ENU=Scanner ID }
    { 40  ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=Open,Finished;
                                                   OptionString=Open,Finished }
    { 50  ;   ;Order Date          ;Date          ;CaptionML=ENU=Order Date }
    { 60  ;   ;Order Time          ;Time          ;CaptionML=ENU=Order Time }
    { 70  ;   ;Process             ;Option        ;CaptionML=ENU=Process;
                                                   OptionCaptionML=ENU=Project Delivery,Service Delivery,Phys. Inventory,Stock Transfer,Purch. Requisition,Project Retour,Service Retour;
                                                   OptionString=Project Delivery,Service Delivery,Phys. Inventory,Stock Transfer,Purch. Requisition,Project Retour,Service Retour }
    { 80  ;   ;Receiving Company   ;Text30        ;TableRelation=Company.Name;
                                                   CaptionML=ENU=Receiving Company }
    { 90  ;   ;Project No.         ;Code20        ;TableRelation=IF (Receiving Company=FILTER('')) Job.No.;
                                                   OnValidate=BEGIN
                                                                CheckCompanyIC;
                                                                IF "Project No." <> '' THEN BEGIN
                                                                  TESTFIELD("Service Order No.", '');
                                                                  ProjRec.GET("Project No.");
                                                                  ProjRec.TESTFIELD("Project Status", ProjRec."Project Status"::Production);
                                                                  CALCFIELDS("Description Project");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Project No. }
    { 91  ;   ;Description Project ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=ENU=Description Project;
                                                   Editable=No }
    { 100 ;   ;Service Order No.   ;Code20        ;TableRelation=IF (Receiving Company=FILTER('')) "Service Order".No.;
                                                   OnValidate=BEGIN
                                                                CheckCompanyIC;
                                                                IF "Service Order No." <> '' THEN BEGIN
                                                                  TESTFIELD("Project No.", '');
                                                                  ServOrderRec.GET("Service Order No.");
                                                                  ServOrderRec.TESTFIELD(Status, ServOrderRec.Status::"In Process");
                                                                  CALCFIELDS("Description Service Order");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Service Order No. }
    { 101 ;   ;Description Service Order;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order".Description WHERE (No.=FIELD(Service Order No.)));
                                                   CaptionML=ENU=Description Service Order;
                                                   Editable=No }
    { 110 ;   ;Element             ;Code10        ;TableRelation=IF (Receiving Company=FILTER('')) "Project Element".Element WHERE (Project No.=FIELD(Project No.),
                                                                                                                                    Publish in Empoly App=CONST(Yes));
                                                   OnValidate=BEGIN
                                                                CheckCompanyIC;
                                                                IF Element <> '' THEN BEGIN
                                                                  TESTFIELD("Project No.");
                                                                  ProjElemRec.GET("Project No.", Element);
                                                                  CALCFIELDS("Description Element");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Element }
    { 111 ;   ;Description Element ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Element".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                           Element=FIELD(Element)));
                                                   CaptionML=ENU=Description Element;
                                                   Editable=No }
    { 120 ;   ;Location Code       ;Code10        ;TableRelation=Location.Code;
                                                   CaptionML=ENU=Location Code }
    { 121 ;   ;Description Location;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Location.Name WHERE (Code=FIELD(Location Code)));
                                                   CaptionML=ENU=Description Location;
                                                   Editable=No }
    { 130 ;   ;To Location Code    ;Code10        ;TableRelation=Location.Code;
                                                   OnValidate=BEGIN
                                                                IF "To Location Code" <> '' THEN
                                                                   TESTFIELD(Process, Process::"Stock Transfer");
                                                              END;

                                                   CaptionML=ENU=To Location Code }
    { 131 ;   ;Description Location To;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Location.Name WHERE (Code=FIELD(To Location Code)));
                                                   CaptionML=ENU=Description Location To;
                                                   Editable=No }
    { 140 ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      LocRec@1100525006 : Record 14;
      ProjRec@1100525005 : Record 11072003;
      ProjElemRec@1100525004 : Record 11012010;
      ServOrderRec@1100525000 : Record 11012823;

    PROCEDURE CheckCompanyIC@1210190032();
    VAR
      lvCompName@1210190000 : Text[50];
    BEGIN
      IF "Receiving Company" <> '' THEN
        lvCompName := "Receiving Company"
      ELSE
        lvCompName := COMPANYNAME;

      ProjRec.CHANGECOMPANY(lvCompName);
      ProjElemRec.CHANGECOMPANY(lvCompName);
      LocRec.CHANGECOMPANY(lvCompName);
      ServOrderRec.CHANGECOMPANY(lvCompName);
    END;

    PROCEDURE LookupProject@1100525001();
    BEGIN
      CheckCompanyIC;
      ProjRec."No." := "Project No.";
      ProjRec.SETRANGE("Project Status", ProjRec."Project Status"::Production);
      IF PAGE.RUNMODAL(0, ProjRec) = ACTION::LookupOK THEN
        VALIDATE("Project No.", ProjRec."No.");
    END;

    PROCEDURE LookupElement@1100525002();
    BEGIN
      CheckCompanyIC;
      ProjElemRec."Project No." := "Project No.";
      ProjElemRec.Element := Element;
      ProjElemRec.SETRANGE("Project No.", "Project No.");
      ProjElemRec.SETRANGE("Publish in Empoly App", TRUE);
      IF PAGE.RUNMODAL(0, ProjElemRec) = ACTION::LookupOK THEN
        VALIDATE(Element, ProjElemRec.Element);
    END;

    PROCEDURE LookupServiceOrder@1100525000();
    BEGIN
      CheckCompanyIC;
      ServOrderRec."No." := "Service Order No.";
      ServOrderRec.SETRANGE(Status, ServOrderRec.Status::"In Process");
      IF PAGE.RUNMODAL(0, ServOrderRec) = ACTION::LookupOK THEN
        VALIDATE("Service Order No.", ServOrderRec."No.");
    END;

    BEGIN
    END.
  }
}

