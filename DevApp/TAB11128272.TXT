OBJECT Table 11128272 My Sub-Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS6.00,4PSSE,SEEV.RC;
  }
  PROPERTIES
  {
    CaptionML=ENU=My Sub-Project;
  }
  FIELDS
  {
    { 1   ;   ;User ID             ;Code20        ;TableRelation=Table2000000002;
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=User ID }
    { 2   ;   ;Project No.         ;Code20        ;TableRelation=Job WHERE (Status=FILTER(..4),
                                                                            Single/Main/Sub Project=CONST(Sub Project));
                                                   CaptionML=ENU=Project No.;
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=ENU=Description }
    { 4   ;   ;Project Status      ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job."Project Status" WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=ENU=Project Status;
                                                   OptionCaptionML=ENU=Estimation,Preparation,Production,Technical Finished,Administrative Finished,Finished,Archive,Archived;
                                                   OptionString=Estimation,Preparation,Production,Technical Finished,Administrative Finished,Finished,Archive,Archived }
    { 5   ;   ;Last Date Modified  ;Date          ;CaptionML=ENU=Last Date Modified }
  }
  KEYS
  {
    {    ;User ID,Project No.                     ;Clustered=Yes }
    {    ;Last Date Modified                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=Added %1 new %2';
      This@1000000000 : Record 11128272;
      UserSetup@1100409002 : Record 91;
      Project@1100409001 : Record 11072003;
      Added@1100409000 : Integer;

    PROCEDURE AddEntities@1(FilterStr@1000 : Text[250]);
    BEGIN
      Added := 0;

      Project.SETFILTER("No.",FilterStr);
      IF Project.FINDSET THEN REPEAT
        "User ID" := USERID;
        "Project No." := Project."No.";
        "Last Date Modified" := Project."Last Date Modified";
        IF INSERT THEN
          Added += 1;
      UNTIL Project.NEXT = 0;

      MESSAGE(Text001,Added,Project.TABLECAPTION);
    END;

    PROCEDURE AutoFill@1100409000();
    BEGIN
      UserSetup.GET(USERID); // delib. error
      UserSetup.TESTFIELD("Employee No.");
      Added := 0;
      Project.RESET;
      Project.SETCURRENTKEY("Last Date Modified","Project Manager","Project Status","Single/Main/Sub Project");
      //Project.SETRANGE(Estimator,UserSetup."Employee No.");
      Project.SETRANGE("Project Manager",UserSetup."Employee No.");
      Project.SETRANGE("Project Status",0,Project."Project Status"::"Administrative Finished");
      Project.SETRANGE("Single/Main/Sub Project",Project."Single/Main/Sub Project"::"Sub Project");
      IF Project.FINDSET THEN BEGIN
        This.RESET;
        This.SETRANGE("User ID",USERID);
        This.DELETEALL;
        REPEAT
          This."User ID" := USERID;
          This."Project No." := Project."No.";
          This."Last Date Modified" := Project."Last Date Modified";
          This.INSERT;
          Added += 1;
        UNTIL (Project.NEXT = 0) OR (Added > 9);
      END;
    END;

    BEGIN
    END.
  }
}

