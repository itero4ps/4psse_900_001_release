OBJECT Table 11229298 Web Service Set
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               TESTFIELD("Is Standard", FALSE);
             END;

    OnModify=BEGIN
               TESTFIELD("Is Standard", FALSE);
             END;

    OnDelete=VAR
               WebServiceSetLine@1100525000 : Record 11229309;
             BEGIN
               TESTFIELD("Is Standard", FALSE);
               WebServiceSetLine.SETRANGE("Web Service Set Code", Code);
               WebServiceSetLine.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Web Service Set;
    LookupPageID=Page11126625;
    DrillDownPageID=Page11126625;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code20        ;CaptionML=ENU=Code }
    { 20  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 30  ;   ;Is Standard         ;Boolean       ;CaptionML=ENU=Is Standard;
                                                   Editable=No }
    { 40  ;   ;Web Service Lines   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Web Service Set Line" WHERE (Web Service Set Code=FIELD(Code)));
                                                   CaptionML=ENU=Web Service Lines;
                                                   Editable=No }
    { 41  ;   ;Not Published Lines ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Web Service Set Line" WHERE (Web Service Set Code=FIELD(Code),
                                                                                                   Published=CONST(No)));
                                                   CaptionML=ENU=Not Published Lines;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE TogglePublishWebService@1100525000(VAR WebServiceSet@1100525001 : Record 11229298;Publish@1100525005 : Boolean);
    VAR
      WebServiceSetLine@1100525002 : Record 11229309;
      PossibleWebService@1100525004 : Record 11229317;
      WebService@1100525003 : Record 2000000076;
    BEGIN
      IF (NOT WebServiceSet.FINDSET) THEN
        EXIT;
      REPEAT
        WebServiceSetLine.SETRANGE("Web Service Set Code", WebServiceSet.Code);
        IF (WebServiceSetLine.FINDSET) THEN BEGIN
          REPEAT
            PossibleWebService.GET(WebServiceSetLine."Object Type", WebServiceSetLine."Service Name");
            IF (NOT Publish) THEN BEGIN
              IF (IsLastService(WebServiceSetLine)) THEN BEGIN
                IF (WebService.GET(PossibleWebService."Object Type", PossibleWebService."Service Name")) THEN BEGIN
                  WebService.DELETE;
                END;
              END;
            END ELSE BEGIN
              IF (NOT WebService.GET(PossibleWebService."Object Type", PossibleWebService."Service Name")) THEN BEGIN
                WebService.INIT;
                WebService."Object Type" := PossibleWebService."Object Type";
                WebService."Service Name" := PossibleWebService."Service Name";
                WebService.INSERT;
              END;
              WebService."Object ID" := PossibleWebService."Object Id";
              WebService.Published := Publish;
              WebService.MODIFY;
            END;
            WebServiceSetLine.Published := Publish;
            WebServiceSetLine.MODIFY;
          UNTIL (WebServiceSetLine.NEXT = 0);
        END;
        //WebServiceSet.MODIFY;
      UNTIL (WebServiceSet.NEXT = 0);
    END;

    PROCEDURE ShowWebService@1100525001(VAR WebServiceSet@1100525001 : Record 11229298);
    VAR
      WebServiceSetLine@1100525003 : Record 11229309;
      WebServiceSetLinesList@1100525002 : Page 11126626;
      WebServiceSetFilter@1100525004 : Text;
    BEGIN
      IF (NOT WebServiceSet.FINDSET) THEN
        EXIT;
      REPEAT
        IF (WebServiceSetFilter = '') THEN
          WebServiceSetFilter := WebServiceSet.Code
        ELSE
          WebServiceSetFilter += STRSUBSTNO('|%1', WebServiceSet.Code);
      UNTIL (WebServiceSet.NEXT = 0);
      WebServiceSetLine.SETFILTER("Web Service Set Code", WebServiceSetFilter);
      WebServiceSetLinesList.SETTABLEVIEW(WebServiceSetLine);
      WebServiceSetLinesList.RUNMODAL;
    END;

    LOCAL PROCEDURE IsLastService@1100525002(iWebServiceSetLine@1100525000 : Record 11229309) : Boolean;
    VAR
      WebServiceSetLine@1100525002 : Record 11229309;
    BEGIN
      WebServiceSetLine.SETFILTER("Web Service Set Code", '<>%1', iWebServiceSetLine."Web Service Set Code");
      WebServiceSetLine.SETRANGE("Object Type", iWebServiceSetLine."Object Type");
      WebServiceSetLine.SETRANGE("Service Name", iWebServiceSetLine."Service Name");
      WebServiceSetLine.SETRANGE(Published, TRUE);
      EXIT(NOT WebServiceSetLine.FINDSET);
    END;

    BEGIN
    END.
  }
}

