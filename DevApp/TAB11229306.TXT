OBJECT Table 11229306 WS Option Field Translation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    CaptionML=ENU=Web Service Option Field Translation;
  }
  FIELDS
  {
    { 10  ;   ;Table No.           ;Integer       ;CaptionML=ENU=Table No.;
                                                   Editable=No }
    { 11  ;   ;Table Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(TableData),
                                                                                         Company Name=FILTER(''),
                                                                                         ID=FIELD(Table No.)));
                                                   CaptionML=ENU=Table Name;
                                                   Editable=No }
    { 20  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  No.=FIELD(Field No.),
                                                                                  Type=CONST(Option));
                                                   CaptionML=ENU=Field No.;
                                                   Editable=No }
    { 21  ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table No.),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Name;
                                                   Editable=No }
    { 30  ;   ;Language Id         ;Integer       ;CaptionML=ENU=Language Id;
                                                   Editable=No }
    { 31  ;   ;Abbreviated Name    ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Windows Language"."Abbreviated Name" WHERE (Language ID=FIELD(Language Id)));
                                                   CaptionML=ENU=Abbreviated Name;
                                                   Editable=No }
    { 40  ;   ;Option SeqNo.       ;Integer       ;CaptionML=ENU=Option SeqNo.;
                                                   Editable=No }
    { 41  ;   ;Option Value        ;Text30        ;CaptionML=ENU=Option Value;
                                                   Editable=No }
    { 50  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
  }
  KEYS
  {
    {    ;Table No.,Field No.,Language Id,Option SeqNo.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      BLANK@1100525000 : TextConst 'ENU=_blank_';

    PROCEDURE GetOptionCaptionsFromObject@1100525000(VAR Field@1100525000 : Record 2000000041;GetAllOptions@1100525001 : Boolean);
    VAR
      TableOptionFieldTranslation@1100525002 : Record 11229306;
    BEGIN
      IF (NOT Field.FINDSET) THEN
        EXIT;
      REPEAT
        TableOptionFieldTranslation."Table No." := Field.TableNo;
        TableOptionFieldTranslation."Field No."   := Field."No.";
        TableOptionFieldTranslation.GetOptionCaptions(GetAllOptions);
      UNTIL (Field.NEXT = 0);
    END;

    PROCEDURE CheckOptionsFromObject@1100525003(VAR Field@1100525001 : Record 2000000041);
    VAR
      TableOptionFieldTranslation@1100525000 : Record 11229306;
    BEGIN
      IF (NOT Field.FINDSET) THEN
        EXIT;
      REPEAT
        TableOptionFieldTranslation."Table No." := Field.TableNo;
        TableOptionFieldTranslation."Field No."   := Field."No.";
        TableOptionFieldTranslation.CheckOptions;
      UNTIL (Field.NEXT = 0);
    END;

    PROCEDURE CheckOptions@1100525004();
    VAR
      Field@1100525001 : Record 2000000041;
      TableOptionFieldTranslation@1100525000 : Record 11229306;
    BEGIN
      Field.SETRANGE(TableNo, "Table No.");
      IF (Field.FINDSET) THEN BEGIN
        REPEAT
          IF (Field.Type <> Field.Type::Option) THEN BEGIN
            TableOptionFieldTranslation.SETRANGE("Table No.", Field.TableNo);
            TableOptionFieldTranslation.SETRANGE("Field No.", Field."No.");
            TableOptionFieldTranslation.DELETEALL;
          END ELSE BEGIN
            TableOptionFieldTranslation."Table No." := Field.TableNo;
            TableOptionFieldTranslation."Field No." := Field."No.";
            TableOptionFieldTranslation.GetOptionCaptions(FALSE);
          END;
        UNTIL (Field.NEXT = 0);
      END;
    END;

    PROCEDURE GetOptionCaptions@1100525001(GetAllOptions@1100525006 : Boolean);
    VAR
      Language@1100525000 : Record 8;
      SaveLanguageId@1100525001 : Integer;
      RecRef@1100525005 : RecordRef;
      FieldRef@1100525004 : FieldRef;
      StringFunctions@1100525003 : Codeunit 11012273;
      ENUCaptionString@1100528002 : Text;
      ENUCaptions@1100525002 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";
      Options@1100528001 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";
      Captions@1100528000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";
    BEGIN
      Language.SETFILTER("Windows Language ID", '>%1', 0);
      IF (NOT Language.FINDSET) THEN
        EXIT;

      SaveLanguageId := GLOBALLANGUAGE;
      RecRef.OPEN("Table No.");
      FieldRef := RecRef.FIELD("Field No.");
      StringFunctions.SplitString2(FieldRef.OPTIONSTRING, ',', Options);
      GLOBALLANGUAGE(1033); //ENU Captions
      ENUCaptionString := FieldRef.OPTIONCAPTION;
      StringFunctions.SplitString2(FieldRef.OPTIONCAPTION, ',', ENUCaptions);

      REPEAT
        GLOBALLANGUAGE(Language."Windows Language ID");
        StringFunctions.SplitString2(FieldRef.OPTIONCAPTION, ',', Captions);

        IF (GetAllOptions) THEN
          InsertCaptions(Language, ENUCaptions, Options, Captions)
        ELSE BEGIN
          IF (Language."Windows Language ID" = 1033) OR (ENUCaptionString <> FieldRef.OPTIONCAPTION) THEN
            InsertCaptions(Language, ENUCaptions, Options, Captions);
        END;
      UNTIL (Language.NEXT = 0);
      GLOBALLANGUAGE(SaveLanguageId);
    END;

    LOCAL PROCEDURE InsertCaptions@1100528008(Language@1100525007 : Record 8;ENUCaptions@1100525008 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";Options@1100528001 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";Captions@1100528000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList");
    VAR
      RecRef@1100525001 : RecordRef;
      FieldRef@1100525000 : FieldRef;
      Counter@1100525002 : Integer;
    BEGIN
      FOR Counter := 0 TO Options.Count - 1 DO BEGIN
        IF (NOT Options.Equals(Options.Item(Counter), '')) THEN
          InsertCaption(Language, Options, Captions, Counter);
      END;
    END;

    LOCAL PROCEDURE InsertCaption@1100528009(Language@1100525007 : Record 8;Options@1100528002 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";Captions@1100528001 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";Counter@1100528000 : Integer);
    VAR
      TableOptionFieldTranslation@1100525005 : Record 11229306;
    BEGIN
      IF (NOT TableOptionFieldTranslation.GET("Table No.", "Field No.",Language."Windows Language ID", Counter)) THEN BEGIN
        TableOptionFieldTranslation.INIT;
        TableOptionFieldTranslation."Table No." := "Table No.";
        TableOptionFieldTranslation."Field No." := "Field No.";
        TableOptionFieldTranslation."Language Id" := Language."Windows Language ID";
        TableOptionFieldTranslation."Option SeqNo." := Counter;
        TableOptionFieldTranslation.INSERT;
      END;
      IF (Options.Equals(Options.Item(Counter), ' ')) THEN
        TableOptionFieldTranslation."Option Value" := STRSUBSTNO('%1', BLANK)
      ELSE
        TableOptionFieldTranslation."Option Value" := Options.Item(Counter);
      TableOptionFieldTranslation.Description := Captions.Item(Counter);
      TableOptionFieldTranslation.MODIFY;
    END;

    BEGIN
    END.
  }
}

