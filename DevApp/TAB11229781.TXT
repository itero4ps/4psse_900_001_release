OBJECT Table 11229781 Estimate Usage
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Estimate Usage;
  }
  FIELDS
  {
    { 10  ;   ;Estimate No.        ;Code20        ;TableRelation=Estimate;
                                                   CaptionML=ENU=Estimate No.;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 15  ;   ;Description         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate.Description WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=ENU=Description;
                                                   Editable=No }
    { 20  ;   ;Estimate Lines      ;Integer       ;CaptionML=ENU=Estimate Lines;
                                                   Editable=No }
    { 30  ;   ;Recipe Lines        ;Integer       ;CaptionML=ENU=Recipe Lines;
                                                   Editable=No }
    { 40  ;   ;Empty Estimate Lines;Integer       ;CaptionML=ENU=Empty Estimate Lines;
                                                   Editable=No }
    { 50  ;   ;Empty Recipe Lines  ;Integer       ;CaptionML=ENU=Empty Recipe Lines;
                                                   Editable=No }
    { 60  ;   ;Empty Estimate Lines %;Decimal     ;CaptionML=ENU=Empty Estimate Lines %;
                                                   Editable=No }
    { 70  ;   ;Empty Recipe Lines %;Decimal       ;CaptionML=ENU=Empty Recipe Lines %;
                                                   Editable=No }
    { 80  ;   ;Total Empty Lines % ;Decimal       ;CaptionML=ENU=Total Empty Lines %;
                                                   Editable=No }
    { 90  ;   ;Label               ;Code10        ;CaptionML=ENU=Lable }
    { 100 ;   ;Status              ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate.Status WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=ENU=Status;
                                                   Editable=No }
    { 110 ;   ;Creation Date       ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate."Creation Date" WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=ENU=Creation Date;
                                                   Editable=No }
    { 120 ;   ;Responsible Employee;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate."Responsible Employee" WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=ENU=Responsible Employee;
                                                   Editable=No }
    { 130 ;   ;Modified by         ;Code50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate."Modified by" WHERE (No.=FIELD(Estimate No.)));
                                                   OnLookup=VAR
                                                              UserCode@1210190000 : Code[50];
                                                              UserMgt@1210190001 : Codeunit 418;
                                                            BEGIN
                                                              UserCode := "Modified by";
                                                              UserMgt.LookupUserID(UserCode);
                                                            END;

                                                   CaptionML=ENU=Modified by;
                                                   Editable=No }
    { 140 ;   ;Last Date Modified  ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate."Last Date Modified" WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=ENU=Last Date Modified;
                                                   Editable=No }
    { 150 ;   ;Estimator           ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate.Estimator WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=ENU=Estimator;
                                                   Editable=No }
    { 160 ;   ;Name Estimator      ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Estimator)));
                                                   CaptionML=ENU=Name Estimator;
                                                   Editable=No }
    { 170 ;   ;Fixed               ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate.Fixed WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=ENU=Fixed;
                                                   Editable=No }
    { 180 ;   ;Converted           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate.Converted WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=ENU=Converted;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Estimate No.                            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EstimateSetup@1100525000 : Record 11012150;
      Text001@1100525001 : TextConst 'ENU=Estimate    #1###### @2@@@@@@@@@@@@@';

    PROCEDURE GetCurrentUsage@1100409000();
    VAR
      Estimate@1100409000 : Record 11012151;
      EstimateLineNew@1100409001 : Record 11072072;
      EstimateLineOld@1100409002 : Record 11012154;
      RecipeLineNew@1100409003 : Record 11072075;
      RecipeLineOld@1100409004 : Record 11012156;
      EstimateUsage@1100528500 : Record 11229781;
      EstimateUsageExist@1100528501 : Boolean;
      Threshold@1100528502 : Decimal;
      NoOfRecords@1100528503 : Integer;
      Counter@1100528504 : Integer;
      Window@1100528505 : Dialog;
    BEGIN
      EstimateSetup.GET;
      Threshold := 0;

      Window.OPEN(Text001);

      Estimate.SETRANGE("RAW Estimate",FALSE);
      Estimate.SETRANGE("Estimate Project",FALSE); //T005673

      NoOfRecords := Estimate.COUNT;
      IF Estimate.FINDSET THEN
        REPEAT
          Counter := Counter + 1;
          Window.UPDATE(1,Estimate."No.");
          Window.UPDATE(2,ROUND(Counter / NoOfRecords * 10000,1));

          EstimateUsageExist := EstimateUsage.GET(Estimate."No.");

          EstimateUsage.INIT;
          EstimateUsage."Estimate No." := Estimate."No.";

          IF Estimate."No." <> EstimateSetup."Estimate Source" THEN BEGIN
            EstimateLineNew.SETRANGE("Estimate No.", Estimate."No.");
            EstimateLineOld.SETRANGE("Estimate No.", Estimate."No.");
            EstimateUsage."Estimate Lines" := EstimateLineNew.COUNT + EstimateLineOld.COUNT;

            RecipeLineNew.SETRANGE("Estimate No.", Estimate."No.");
            RecipeLineOld.SETRANGE("Estimate No.", Estimate."No.");
            EstimateUsage."Recipe Lines" := RecipeLineNew.COUNT + RecipeLineOld.COUNT;

            CountZeroLines(Estimate,EstimateUsage);

            IF EstimateUsage."Estimate Lines" <> 0 THEN
              EstimateUsage."Empty Estimate Lines %" := ROUND(EstimateUsage."Empty Estimate Lines" / EstimateUsage."Estimate Lines" * 100);
            IF EstimateUsage."Recipe Lines" <> 0 THEN
              EstimateUsage."Empty Recipe Lines %" := ROUND(EstimateUsage."Empty Recipe Lines" / EstimateUsage."Recipe Lines" * 100);
            IF (EstimateUsage."Estimate Lines" + EstimateUsage."Recipe Lines") <> 0 THEN
              EstimateUsage."Total Empty Lines %" := ROUND(
                (EstimateUsage."Empty Estimate Lines" + EstimateUsage."Empty Recipe Lines") /
                (EstimateUsage."Estimate Lines" + EstimateUsage."Recipe Lines") * 100);
          END;

          IF EstimateUsageExist THEN
            IF EstimateUsage."Total Empty Lines %" > Threshold THEN
              EstimateUsage.MODIFY
            ELSE
              EstimateUsage.DELETE
          ELSE
            IF EstimateUsage."Total Empty Lines %" > Threshold THEN
              EstimateUsage.INSERT;
        UNTIL Estimate.NEXT = 0;

      Window.CLOSE;
    END;

    LOCAL PROCEDURE CountZeroLines@1(Estimate@1100485003 : Record 11012151;VAR EstimateUsage@1100528500 : Record 11229781);
    VAR
      EstimateLineNew@1100409004 : Record 11072072;
      EstimateLineOld@1100409003 : Record 11012154;
      RecipeLineNew@1100409002 : Record 11072075;
      RecipeLineOld@1100409001 : Record 11012156;
      EmptyRecipe@1100409000 : 'ZeroHeader,ZeroLines';
    BEGIN
      EmptyRecipe := EmptyRecipe::ZeroHeader;

      //OLD
      RecipeLineOld.SETRANGE("Estimate No.", Estimate."No.");
      RecipeLineOld.SETRANGE(Quantity, 0);
      IF RecipeLineOld.FINDSET THEN
        REPEAT
          IF (RecipeLineOld.Norm <> 0) OR (RecipeLineOld."Material Price" <> 0) OR
             (RecipeLineOld."Subcontracting Price" <> 0) OR (RecipeLineOld."Plant Price" <> 0) THEN
            EstimateUsage."Empty Recipe Lines" := EstimateUsage."Empty Recipe Lines" + 1;
        UNTIL RecipeLineOld.NEXT = 0;
      RecipeLineOld.RESET;

      EstimateLineOld.SETRANGE("Estimate No.", Estimate."No.");
      EstimateLineOld.SETFILTER(Recipe, '<>%1', '');
      IF EstimateLineOld.FINDSET THEN
        REPEAT
          RecipeLineOld.SETRANGE("Estimate No.", EstimateLineOld."Estimate No.");
          RecipeLineOld.SETRANGE("Sub-Estimate No.", EstimateLineOld."Sub-Estimate No.");
          RecipeLineOld.SETRANGE("Part Group", EstimateLineOld."Part Group");
          RecipeLineOld.SETRANGE(Part, EstimateLineOld.Part);
          RecipeLineOld.SETRANGE("Estimate Line", EstimateLineOld."Line No.");
          IF RecipeLineOld.ISEMPTY THEN
            EstimateUsage."Empty Estimate Lines" := EstimateUsage."Empty Estimate Lines" + 1;
        UNTIL EstimateLineOld.NEXT = 0;

      EstimateLineOld.SETRANGE("Estimate No.", Estimate."No.");
      IF EmptyRecipe = EmptyRecipe::ZeroLines THEN
        EstimateLineOld.SETFILTER(Recipe, '%1', '')
      ELSE
        EstimateLineOld.SETRANGE(Recipe);
      EstimateLineOld.SETRANGE(Quantity, 0);
      IF EstimateLineOld.FINDSET THEN
        REPEAT
          IF (EstimateLineOld.Norm <> 0) OR (EstimateLineOld."Material Price" <> 0) OR
             (EstimateLineOld."Subcontracting Price" <> 0) OR (EstimateLineOld."Plant Price" <> 0) THEN BEGIN
            EstimateLineOld.CALCFIELDS(
              "Quantity Model Exist","Quantity Sheet Exist","Quantity Sheet Relation Exist");
            IF NOT EstimateSetup."Quantity Sheet with Relations" AND
               (NOT EstimateLineOld."Quantity Model Exist") AND
               (NOT EstimateLineOld."Quantity Sheet Exist") THEN
              EstimateUsage."Empty Estimate Lines" := EstimateUsage."Empty Estimate Lines" + 1;
            IF EstimateSetup."Quantity Sheet with Relations" AND
               (NOT EstimateLineOld."Quantity Model Exist") AND
               (NOT EstimateLineOld."Quantity Sheet Relation Exist") THEN
              EstimateUsage."Empty Estimate Lines" := EstimateUsage."Empty Estimate Lines" + 1;
          END;
        UNTIL EstimateLineOld.NEXT = 0;

      //NEW
      RecipeLineNew.SETRANGE("Estimate No.", Estimate."No.");
      RecipeLineNew.SETRANGE(Quantity, 0);
      RecipeLineNew.SETRANGE("Quantity (Sales)", 0);
      IF RecipeLineNew.FINDSET THEN
        REPEAT
          IF (RecipeLineNew.Norm <> 0) OR (RecipeLineNew."Nett Cost Price" <> 0) OR
             (RecipeLineNew."Norm (Sales)" <> 0) OR (RecipeLineNew."Nett Sales Price" <> 0) THEN
           EstimateUsage."Empty Recipe Lines" := EstimateUsage."Empty Recipe Lines" + 1;
        UNTIL RecipeLineNew.NEXT = 0;
      RecipeLineNew.RESET;

      EstimateLineNew.SETRANGE("Estimate No.", Estimate."No.");
      EstimateLineNew.SETFILTER(Recipe, '<>%1', '');
      IF EstimateLineNew.FINDSET THEN
        REPEAT
          EstimateLineNew.CALCFIELDS("Recipe Lines Exist");
          IF NOT EstimateLineNew."Recipe Lines Exist" THEN
            EstimateUsage."Empty Estimate Lines" := EstimateUsage."Empty Estimate Lines" + 1;
        UNTIL EstimateLineNew.NEXT = 0;

      EstimateLineNew.SETRANGE("Estimate No.", Estimate."No.");
      IF EmptyRecipe = EmptyRecipe::ZeroLines THEN
        EstimateLineNew.SETFILTER(Recipe, '%1', '')
      ELSE
        EstimateLineNew.SETRANGE(Recipe);
      EstimateLineNew.SETRANGE(Quantity, 0);
      EstimateLineNew.SETRANGE("Quantity (Sales)", 0);
      IF EstimateLineNew.FINDSET THEN
        REPEAT
          IF (EstimateLineNew.Norm <> 0) OR (EstimateLineNew."Nett Cost Price" <> 0) OR
             (EstimateLineNew."Norm (Sales)" <> 0) OR (EstimateLineNew."Nett Sales Price" <> 0) THEN BEGIN
            EstimateLineNew.CALCFIELDS(
              "Quantity Model Exist","Quantity Sheet Exist","Quantity Sheet Relation Exist");
            IF NOT EstimateSetup."Quantity Sheet with Relations" AND
               (NOT EstimateLineNew."Quantity Model Exist") AND
               (NOT EstimateLineNew."Quantity Sheet Exist") THEN
              EstimateUsage."Empty Estimate Lines" := EstimateUsage."Empty Estimate Lines" + 1;
            IF EstimateSetup."Quantity Sheet with Relations" AND
               (NOT EstimateLineNew."Quantity Model Exist") AND
               (NOT EstimateLineNew."Quantity Sheet Relation Exist") THEN
              EstimateUsage."Empty Estimate Lines" := EstimateUsage."Empty Estimate Lines" + 1;
          END ELSE
            IF (EstimateLineNew.Recipe <> '') AND (EmptyRecipe = EmptyRecipe::ZeroHeader) THEN
               EstimateUsage."Empty Estimate Lines" := EstimateUsage."Empty Estimate Lines" + 1;
        UNTIL EstimateLineNew.NEXT = 0;
    END;

    PROCEDURE DeleteZeroLines@1100525000(VAR EstimateUsage2@1100525009 : Record 11229781);
    VAR
      EstimateUsage@1100525000 : Record 11229781;
      EstimateLineNew@1100525006 : Record 11072072;
      EstimateLineOld@1100525005 : Record 11012154;
      NoOfRecords@1100525003 : Integer;
      Counter@1100525002 : Integer;
      Window@1100525001 : Dialog;
      UpdateEstOld@1100525004 : Codeunit 11012155;
      UpdateEstNew@1100525007 : Codeunit 11012229;
      EmptyRecipe@1100525008 : 'ZeroHeader,ZeroLines';
    BEGIN
      EstimateUsage.COPY(EstimateUsage2);

      Window.OPEN(Text001);

      NoOfRecords := EstimateUsage.COUNT;
      IF EstimateUsage.FINDSET(TRUE,FALSE) THEN
        REPEAT
          Counter := Counter + 1;
          Window.UPDATE(1,EstimateUsage."Estimate No.");
          Window.UPDATE(2,ROUND(Counter / NoOfRecords * 10000,1));

          EstimateLineOld.SETRANGE("Estimate No.", EstimateUsage."Estimate No.");
          UpdateEstOld.DeleteZeroLines(EstimateLineOld,FALSE,EmptyRecipe::ZeroHeader,FALSE,NoOfRecords=1);

          EstimateLineNew.SETRANGE("Estimate No.", EstimateUsage."Estimate No.");
          UpdateEstNew.DeleteZeroLines(EstimateLineNew,FALSE,EmptyRecipe::ZeroHeader,NoOfRecords=1);

          EstimateUsage.DELETE;
          COMMIT;
        UNTIL EstimateUsage.NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    {
      4PS, 03-02-2015, DP00717
    }
    END.
  }
}

