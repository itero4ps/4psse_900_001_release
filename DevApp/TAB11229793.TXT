OBJECT Table 11229793 Consent Rule Filter
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Consent Rule Filter;
  }
  FIELDS
  {
    { 1   ;   ;Consent Rule Line No.;Integer      ;TableRelation="Consent Rule";
                                                   CaptionML=ENU=Consent Rule Line No. }
    { 2   ;   ;Table No.           ;Integer       ;OnValidate=BEGIN
                                                                CALCFIELDS("Table Caption");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupTableNo(TRUE);
                                                            END;

                                                   CaptionML=ENU=Table No. }
    { 3   ;   ;Table Caption       ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=ENU=Table Caption;
                                                   Editable=No }
    { 4   ;   ;Field No.           ;Integer       ;OnValidate=BEGIN
                                                                CALCFIELDS("Field Caption");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupFieldNo(TRUE);
                                                            END;

                                                   CaptionML=ENU=Field No. }
    { 5   ;   ;Field Caption       ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Caption;
                                                   Editable=No }
    { 6   ;   ;Filter              ;Text250       ;CaptionML=ENU=Filter }
    { 7   ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Table No.", DATABASE::"Dimension Value");
                                                              END;

                                                   CaptionML=ENU=Dimension Code }
  }
  KEYS
  {
    {    ;Consent Rule Line No.,Table No.,Field No.,Dimension Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetApplicableTables@1100528400(VAR AllObjWithCaption@1100528400 : Record 2000000058);
    VAR
      TableIDFilter@1100528401 : Text[250];
    BEGIN
      AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Table);
      TableIDFilter :=
        FORMAT(DATABASE::Job) + '|' +
        FORMAT(DATABASE::"Service Order") + '|' +
        FORMAT(DATABASE::Vendor) + '|' +
        FORMAT(DATABASE::"Dimension Value") + '|' +
        FORMAT(DATABASE::"Purchase Header") + '|' +
        FORMAT(DATABASE::"Purch. Inv. Header") + '|' +
        FORMAT(DATABASE::"Purch. Cr. Memo Hdr.") + '|' +
        FORMAT(DATABASE::Location) + '|' +
        FORMAT(DATABASE::"Project Element") + '|' +
        FORMAT(DATABASE::"G/L Account") + '|' +
        FORMAT(DATABASE::Item) + '|' +
        FORMAT(DATABASE::"Plant Type");
      AllObjWithCaption.SETFILTER("Object ID", TableIDFilter);
    END;

    PROCEDURE LookupTableNo@1100528401(Editable@1100528402 : Boolean);
    VAR
      AllObjWithCaption@1100528401 : Record 2000000058;
      ObjectList@1100528400 : Page 358;
    BEGIN
      GetApplicableTables(AllObjWithCaption);
      ObjectList.LOOKUPMODE(Editable);
      ObjectList.SETTABLEVIEW(AllObjWithCaption);
      IF ObjectList.RUNMODAL = ACTION::LookupOK THEN
        IF Editable THEN BEGIN
          ObjectList.GETRECORD(AllObjWithCaption);
          VALIDATE("Table No.",AllObjWithCaption."Object ID");
        END;
    END;

    PROCEDURE LookupFieldNo@1100528402(Editable@1100528402 : Boolean);
    VAR
      Field@1100528401 : Record 2000000041;
      TableFieldList@1100528400 : Page 6521;
    BEGIN
      TESTFIELD("Table No.");
      Field.SETRANGE(TableNo, "Table No.");
      TableFieldList.LOOKUPMODE(Editable);
      TableFieldList.SETTABLEVIEW(Field);
      IF TableFieldList.RUNMODAL = ACTION::LookupOK THEN
        IF Editable THEN BEGIN
          TableFieldList.GETRECORD(Field);
          VALIDATE("Table No.", Field.TableNo);
          VALIDATE("Field No.", Field."No.");
        END;
    END;

    BEGIN
    {
      New table for saving filters at the advanced consent setup.
    }
    END.
  }
}

