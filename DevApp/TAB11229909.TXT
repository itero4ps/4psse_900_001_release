OBJECT Table 11229909 Invoice Proposal Batch Filter
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Table No." = 0 THEN
                 VALIDATE("Table Type");
             END;

    CaptionML=ENU=Invoice Proposal Batch Filter;
  }
  FIELDS
  {
    { 10  ;   ;Invoice Proposal Batch ID;Integer  ;TableRelation="Invoice Proposal Batch";
                                                   CaptionML=ENU=Invoice Proposal Batch ID;
                                                   Editable=No }
    { 20  ;   ;Invoice Proposal Type;Option       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Invoice Proposal Batch"."Invoice Proposal Type");
                                                   CaptionML=ENU=Invoice Proposal Type;
                                                   OptionCaptionML=ENU=Service Contract;
                                                   OptionString=Service Contract;
                                                   Description=Must be equal to T11229906;
                                                   Editable=No }
    { 25  ;   ;Batch Seq. No.      ;Code20        ;CaptionML=ENU=Batch Seq. No.;
                                                   Editable=No }
    { 30  ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 40  ;   ;Table Type          ;Option        ;OnValidate=BEGIN
                                                                CASE "Invoice Proposal Type" OF
                                                                  "Invoice Proposal Type"::"Service Contract":
                                                                    IF NOT ("Table Type" IN ["Table Type"::"Service Contract", "Table Type"::Customer]) THEN
                                                                      FIELDERROR("Table Type");
                                                                END;

                                                                CASE "Table Type" OF
                                                                  "Table Type"::Customer:
                                                                    "Table No." := DATABASE::Customer;
                                                                  "Table Type"::"Service Contract":
                                                                    "Table No." := DATABASE::"Service-Contract";
                                                                END;
                                                                VALIDATE("Field No.", 0);
                                                                ClearValueFilter;
                                                              END;

                                                   CaptionML=ENU=Table Type;
                                                   OptionCaptionML=ENU=Service Contract,Customer;
                                                   OptionString=Service Contract,Customer }
    { 50  ;   ;Table No.           ;Integer       ;OnValidate=BEGIN
                                                                ClearValueFilter;
                                                              END;

                                                   CaptionML=ENU=Table No.;
                                                   Editable=No }
    { 60  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  Class=CONST(Normal),
                                                                                  Type=FILTER(<>BLOB));
                                                   OnValidate=BEGIN
                                                                ClearValueFilter;
                                                                CALCFIELDS("Field Name", "Field Caption");
                                                              END;

                                                   OnLookup=VAR
                                                              Field@1100525000 : Record 2000000041;
                                                            BEGIN
                                                              Field.SETRANGE(TableNo, "Table No.");
                                                              Field.SETRANGE(Class, Field.Class::Normal);
                                                              Field.SETFILTER(Type, '<>%1', Field.Type::BLOB);
                                                              IF Field.GET("Table No.", "Field No.") THEN;
                                                              IF PAGE.RUNMODAL(PAGE::"Table Fields", Field) = ACTION::LookupOK THEN
                                                                VALIDATE("Field No.", Field."No.");
                                                            END;

                                                   CaptionML=ENU=Field No.;
                                                   BlankZero=Yes }
    { 70  ;   ;Value Filter        ;Text250       ;OnValidate=BEGIN
                                                                EvaluateFilter;
                                                              END;

                                                   CaptionML=ENU=Value Filter }
    { 100 ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table No.),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Name;
                                                   Editable=No }
    { 110 ;   ;Field Caption       ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Caption;
                                                   Editable=No }
    { 120 ;   ;Field Type          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Type Name" WHERE (TableNo=FIELD(Table No.),
                                                                                               No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Type;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Invoice Proposal Batch ID,Batch Seq. No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=t';

    PROCEDURE EvaluateFilter@1210190000();
    VAR
      ObjectSpecificationLine@1210190000 : Record 11071781;
      ServiceObject@1210190003 : Record 11071691;
      StandardObject@1210190004 : Record 11020334;
      ServiceLocation@1100528600 : Record 11012801;
      ServiceContract@1100528601 : Record 11012812;
      TableRef@1210190001 : RecordRef;
      FieldRef@1210190002 : FieldRef;
    BEGIN
      CASE "Table Type" OF
        "Table Type"::"Service Contract":
          BEGIN
            TableRef.OPEN("Table No.");
            FieldRef := TableRef.FIELD("Field No.");
            IF FORMAT(FieldRef.TYPE) = 'Date' THEN
              ConvertDateFilter("Value Filter");
            FieldRef.SETFILTER("Value Filter");
            "Value Filter" := FieldRef.GETFILTER;
          END;
        "Table Type"::Customer:
          BEGIN
            TableRef.OPEN("Table No.");
            FieldRef := TableRef.FIELD("Field No.");
            IF FORMAT(FieldRef.TYPE) = 'Date' THEN
              ConvertDateFilter("Value Filter");
            FieldRef.SETFILTER("Value Filter");
            "Value Filter" := FieldRef.GETFILTER;
          END;
      END;
    END;

    PROCEDURE ClearValueFilter@1210190001();
    BEGIN
      IF ("Table Type" <> xRec."Table Type") OR
        ("Table No." <> xRec."Table No.") OR
        ("Field No." <> xRec."Field No.")
      THEN
        "Value Filter" := '';
    END;

    PROCEDURE ConvertDateFilter@1210190003(VAR OValueFilter@1210190000 : Text[250]);
    VAR
      Pos@1210190001 : Integer;
    BEGIN
      Pos := STRPOS(LOWERCASE(OValueFilter), Text000);
      IF Pos <> 0 THEN BEGIN
        OValueFilter := DELSTR(OValueFilter, Pos, STRLEN(Text000));
        OValueFilter := INSSTR(OValueFilter, FORMAT(TODAY), Pos);
      END;
    END;

    BEGIN
    END.
  }
}

