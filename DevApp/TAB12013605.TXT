OBJECT Table 12013605 ExFlow Initialize Company
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF350000;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    CaptionML=ENU=ExFlow Initialize Company;
  }
  FIELDS
  {
    { 1   ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   CaptionML=ENU=Company Name;
                                                   NotBlank=Yes }
    { 50  ;   ;Line Type G/L Account;Boolean      ;CaptionML=ENU=Line Type G/L Account }
    { 51  ;   ;Line Type Item      ;Boolean       ;CaptionML=ENU=Line Type Item }
    { 52  ;   ;Line Type Fixed Asset;Boolean      ;CaptionML=ENU=Line Type Fixed Asset }
    { 53  ;   ;Line Type Item Charge;Boolean      ;CaptionML=ENU=Line Type Item Charge }
    { 60  ;   ;Use Jobs            ;Boolean       ;CaptionML=ENU=Use Jobs }
    { 61  ;   ;Sort Order Jobs     ;Integer       ;CaptionML=ENU=Sort Order Jobs }
    { 65  ;   ;Use Periodic Code (SE);Boolean     ;CaptionML=ENU=Use Periodic Code (SE Version Only) }
    { 66  ;   ;Sort Order Periodic ;Integer       ;CaptionML=ENU=Sort Order Periodic Code }
    { 70  ;   ;Use Shortcut Dimension 1;Boolean   ;OnValidate=BEGIN
                                                                IF "Use Shortcut Dimension 1" THEN
                                                                  "Sort Order 1" := GetNextSortOrder
                                                                ELSE
                                                                  "Sort Order 1" := 0;
                                                              END;

                                                   CaptionML=ENU=Use Shortcut Dimension 1 }
    { 72  ;   ;Sort Order 1        ;Integer       ;CaptionML=ENU=Sort Order 1 }
    { 75  ;   ;Use Shortcut Dimension 2;Boolean   ;OnValidate=BEGIN
                                                                IF "Use Shortcut Dimension 2" THEN
                                                                  "Sort Order 2" := GetNextSortOrder
                                                                ELSE
                                                                  "Sort Order 2" := 0;
                                                              END;

                                                   CaptionML=ENU=Use Shortcut Dimension 2 }
    { 77  ;   ;Sort Order 2        ;Integer       ;CaptionML=ENU=Sort Order 2 }
    { 80  ;   ;Use Shortcut Dimension 3;Boolean   ;OnValidate=BEGIN
                                                                IF "Use Shortcut Dimension 3" THEN
                                                                  "Sort Order 3" := GetNextSortOrder
                                                                ELSE
                                                                  "Sort Order 3" := 0;
                                                              END;

                                                   CaptionML=ENU=Use Shortcut Dimension 3 }
    { 82  ;   ;Sort Order 3        ;Integer       ;CaptionML=ENU=Sort Order 3 }
    { 85  ;   ;Use Shortcut Dimension 4;Boolean   ;OnValidate=BEGIN
                                                                IF "Use Shortcut Dimension 4" THEN
                                                                  "Sort Order 4" := GetNextSortOrder
                                                                ELSE
                                                                  "Sort Order 4" := 0;
                                                              END;

                                                   CaptionML=ENU=Use Shortcut Dimension 4 }
    { 87  ;   ;Sort Order 4        ;Integer       ;CaptionML=ENU=Sort Order 4 }
    { 90  ;   ;Use Shortcut Dimension 5;Boolean   ;OnValidate=BEGIN
                                                                IF "Use Shortcut Dimension 5" THEN
                                                                  "Sort Order 5" := GetNextSortOrder
                                                                ELSE
                                                                  "Sort Order 5" := 0;
                                                              END;

                                                   CaptionML=ENU=Use Shortcut Dimension 5 }
    { 92  ;   ;Sort Order 5        ;Integer       ;CaptionML=ENU=Sort Order 5 }
    { 95  ;   ;Use Shortcut Dimension 6;Boolean   ;OnValidate=BEGIN
                                                                IF "Use Shortcut Dimension 6" THEN
                                                                  "Sort Order 6" := GetNextSortOrder
                                                                ELSE
                                                                  "Sort Order 6" := 0;
                                                              END;

                                                   CaptionML=ENU=Use Shortcut Dimension 6 }
    { 97  ;   ;Sort Order 6        ;Integer       ;CaptionML=ENU=Sort Order 6 }
    { 100 ;   ;Use Shortcut Dimension 7;Boolean   ;OnValidate=BEGIN
                                                                IF "Use Shortcut Dimension 7" THEN
                                                                  "Sort Order 7" := GetNextSortOrder
                                                                ELSE
                                                                  "Sort Order 7" := 0;
                                                              END;

                                                   CaptionML=ENU=Use Shortcut Dimension 7 }
    { 102 ;   ;Sort Order 7        ;Integer       ;CaptionML=ENU=Sort Order 7 }
    { 105 ;   ;Use Shortcut Dimension 8;Boolean   ;OnValidate=BEGIN
                                                                IF "Use Shortcut Dimension 8" THEN
                                                                  "Sort Order 8" := GetNextSortOrder
                                                                ELSE
                                                                  "Sort Order 8" := 0;
                                                              END;

                                                   CaptionML=ENU=Use Shortcut Dimension 8 }
    { 107 ;   ;Sort Order 8        ;Integer       ;CaptionML=ENU=Sort Order 8 }
    { 150 ;   ;Create Default Coding Rules;Boolean;CaptionML=ENU=Create Default Coding Rules }
    { 200 ;   ;Create Default ExFlow Setup;Boolean;CaptionML=ENU=Create Default ExFlow Setup }
    { 201 ;   ;Path to New Invoices;Text128       ;OnValidate=BEGIN
                                                                IF "Path to New Invoices" <> '' THEN
                                                                  "Create Default ExFlow Setup" := TRUE;

                                                                EndingSlash("Path to New Invoices");
                                                              END;

                                                   CaptionML=ENU=Path to New Invoices }
    { 202 ;   ;Path to Used Invoices;Text128      ;OnValidate=BEGIN
                                                                IF "Path to Used Invoices" <> '' THEN
                                                                  "Create Default ExFlow Setup" := TRUE;

                                                                EndingSlash("Path to Used Invoices");
                                                              END;

                                                   CaptionML=ENU=Path to Processed Invoices }
    { 203 ;   ;Path to New OCR-files;Text128      ;OnValidate=BEGIN
                                                                IF "Path to New OCR-files" <> '' THEN
                                                                  "Create Default ExFlow Setup" := TRUE;

                                                                EndingSlash("Path to New OCR-files");
                                                              END;

                                                   CaptionML=ENU=Path to New OCR files }
    { 204 ;   ;Path to Attached Files;Text128     ;OnValidate=BEGIN
                                                                IF "Path to Attached Files" <> '' THEN
                                                                  "Create Default ExFlow Setup" := TRUE;

                                                                EndingSlash("Path to Attached Files");
                                                              END;

                                                   CaptionML=ENU=Path to Attached files }
    { 205 ;   ;Path to connected Att. Files;Text128;
                                                   OnValidate=BEGIN
                                                                IF "Path to connected Att. Files" <> '' THEN
                                                                  "Create Default ExFlow Setup" := TRUE;

                                                                EndingSlash("Path to connected Att. Files");
                                                              END;

                                                   CaptionML=ENU=Path to connected Attached Files }
    { 300 ;   ;Create Default Security Roles;Boolean;
                                                   CaptionML=ENU=Create Default Security Roles }
    { 1000;   ;Apply setup         ;Boolean       ;OnValidate=BEGIN
                                                                CreateDefaultValues;
                                                              END;

                                                   CaptionML=ENU=Apply setup }
  }
  KEYS
  {
    {    ;Company Name                            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100285004 : TextConst 'ENU=Sort Order for field %1 cannot be zero and it cannot have the same value as any other column!';
      Text002@1100285005 : TextConst 'ENU=Do you want to apply the setup?';
      Text003@1100285006 : TextConst 'ENU=Setup applied.';
      Text004@1100285007 : TextConst 'ENU=Operation aborted.';
      CodingRuleName0@1100285001 : TextConst 'ENU=Default Validation';
      CodingRuleName1@1100285000 : TextConst 'ENU=Default Filter';
      CodingRuleName2@1100285002 : TextConst 'ENU=Default Subset';
      CodingRuleName3@1100285003 : TextConst 'ENU=Default Deny';

    PROCEDURE CreateDefaultValues@1100285000();
    BEGIN
      IF GUIALLOWED THEN
        IF NOT CONFIRM(Text002,FALSE) THEN
          ERROR(Text004);

      CreateExColumns;
      CreateLineTypes;
      CreateCodingRules;
      CreateExFlowSetup;
      CreateStandardSecurityRole;

      DELETE;

      MESSAGE(Text003);
    END;

    PROCEDURE CreateExColumns@1100285001();
    VAR
      ExColumn@1100285000 : Record 12013661;
      Object@1100285002 : Record 2000000001;
      GLSetup@1100285003 : Record 98;
      NextID@1100285001 : Integer;
    BEGIN
      ExColumn.RESET;
      ExColumn.SETRANGE("Company Name", "Company Name");
      IF ExColumn.FINDFIRST THEN
        EXIT;

      ExColumn.DELETEALL;

      ExColumn.RESET;
      IF ExColumn.FINDLAST THEN
        NextID := ExColumn.ID + 1
      ELSE
        NextID := 1;

      IF "Line Type G/L Account" THEN
        CreateColumn("Company Name",NextID,'G_LACCOUNT','G_LACCOUNT','{0}Î{1}','LineType=1','',FALSE,TRUE,FALSE,0);
      IF "Line Type Item" THEN
        CreateColumn("Company Name",NextID,'ITEM','ITEM','{0}Î{1}','LineType=2','',FALSE,TRUE,FALSE,0);
      IF "Line Type Fixed Asset" THEN
        CreateColumn("Company Name",NextID,'FIXEDASSET','FIXEDASSET','{0}Î{1}','LineType=4','',FALSE,TRUE,FALSE,0);
      IF "Line Type Item Charge" THEN
        CreateColumn("Company Name",NextID,'CHARGE','CHARGE','{0}Î{1}','LineType=5','',FALSE,TRUE,FALSE,0);
      IF "Use Jobs" THEN
        CreateColumn("Company Name",NextID,'JOB','JOB','{0}Î{1}','','',FALSE,TRUE,FALSE,"Sort Order Jobs");

      IF "Use Periodic Code (SE)" THEN BEGIN
        Object.RESET;
        Object.SETRANGE(Type, Object.Type::Table);
        Object.SETRANGE(ID, 11205);
        IF Object.FINDFIRST THEN BEGIN
          CreateColumn("Company Name",NextID,'PERCODE','PERCODE','{0}Î{1}','','',FALSE,TRUE,FALSE,"Sort Order Periodic");
          CreateColumn("Company Name",NextID,'PERDATE','PERDATE','{0:d}','','System.DateTime',TRUE,FALSE,FALSE,"Sort Order Periodic" + 1);
        END;
      END;

      GLSetup.CHANGECOMPANY("Company Name");
      GLSetup.GET;

      TestSortOrderValues;

      IF "Use Shortcut Dimension 1" THEN
        IF GLSetup."Shortcut Dimension 1 Code" <> '' THEN
          CreateColumn("Company Name",NextID,GLSetup."Shortcut Dimension 1 Code",'DIM1','{0}Î{1}','','',FALSE,TRUE,TRUE,"Sort Order 1");
      IF "Use Shortcut Dimension 2" THEN
        IF GLSetup."Shortcut Dimension 2 Code" <> '' THEN
          CreateColumn("Company Name",NextID,GLSetup."Shortcut Dimension 2 Code",'DIM2','{0}Î{1}','','',FALSE,TRUE,TRUE,"Sort Order 2");
      IF "Use Shortcut Dimension 3" THEN
        IF GLSetup."Shortcut Dimension 3 Code" <> '' THEN
          CreateColumn("Company Name",NextID,GLSetup."Shortcut Dimension 3 Code",'DIM3','{0}Î{1}','','',FALSE,TRUE,TRUE,"Sort Order 3");
      IF "Use Shortcut Dimension 4" THEN
        IF GLSetup."Shortcut Dimension 4 Code" <> '' THEN
          CreateColumn("Company Name",NextID,GLSetup."Shortcut Dimension 4 Code",'DIM4','{0}Î{1}','','',FALSE,TRUE,TRUE,"Sort Order 4");
      IF "Use Shortcut Dimension 5" THEN
        IF GLSetup."Shortcut Dimension 5 Code" <> '' THEN
          CreateColumn("Company Name",NextID,GLSetup."Shortcut Dimension 5 Code",'DIM5','{0}Î{1}','','',FALSE,TRUE,TRUE,"Sort Order 5");
      IF "Use Shortcut Dimension 6" THEN
        IF GLSetup."Shortcut Dimension 6 Code" <> '' THEN
          CreateColumn("Company Name",NextID,GLSetup."Shortcut Dimension 6 Code",'DIM6','{0}Î{1}','','',FALSE,TRUE,TRUE,"Sort Order 6");
      IF "Use Shortcut Dimension 7" THEN
        IF GLSetup."Shortcut Dimension 7 Code" <> '' THEN
          CreateColumn("Company Name",NextID,GLSetup."Shortcut Dimension 7 Code",'DIM7','{0}Î{1}','','',FALSE,TRUE,TRUE,"Sort Order 7");
      IF "Use Shortcut Dimension 8" THEN
        IF GLSetup."Shortcut Dimension 8 Code" <> '' THEN
          CreateColumn("Company Name",NextID,GLSetup."Shortcut Dimension 8 Code",'DIM8','{0}Î{1}','','',FALSE,TRUE,TRUE,"Sort Order 8");
    END;

    LOCAL PROCEDURE TestSortOrderValues@1100285007();
    VAR
      TempInteger@1100285000 : TEMPORARY Record 2000000026;
    BEGIN
      IF "Use Shortcut Dimension 1" THEN BEGIN
        TempInteger.Number := "Sort Order 1";
        IF (TempInteger.Number = 0) OR (NOT TempInteger.INSERT) THEN
          ERROR(Text001);
      END;
      IF "Use Shortcut Dimension 2" THEN BEGIN
        TempInteger.Number := "Sort Order 2";
        IF (TempInteger.Number = 0) OR (NOT TempInteger.INSERT) THEN
          ERROR(Text001);
      END;
      IF "Use Shortcut Dimension 3" THEN BEGIN
        TempInteger.Number := "Sort Order 3";
        IF (TempInteger.Number = 0) OR (NOT TempInteger.INSERT) THEN
          ERROR(Text001);
      END;
      IF "Use Shortcut Dimension 4" THEN BEGIN
        TempInteger.Number := "Sort Order 4";
        IF (TempInteger.Number = 0) OR (NOT TempInteger.INSERT) THEN
          ERROR(Text001);
      END;
      IF "Use Shortcut Dimension 5" THEN BEGIN
        TempInteger.Number := "Sort Order 5";
        IF (TempInteger.Number = 0) OR (NOT TempInteger.INSERT) THEN
          ERROR(Text001);
      END;
      IF "Use Shortcut Dimension 6" THEN BEGIN
        TempInteger.Number := "Sort Order 6";
        IF (TempInteger.Number = 0) OR (NOT TempInteger.INSERT) THEN
          ERROR(Text001);
      END;
      IF "Use Shortcut Dimension 7" THEN BEGIN
        TempInteger.Number := "Sort Order 7";
        IF (TempInteger.Number = 0) OR (NOT TempInteger.INSERT) THEN
          ERROR(Text001);
      END;
      IF "Use Shortcut Dimension 8" THEN BEGIN
        TempInteger.Number := "Sort Order 8";
        IF (TempInteger.Number = 0) OR (NOT TempInteger.INSERT) THEN
          ERROR(Text001);
      END;
    END;

    PROCEDURE CreateLineTypes@1100285002();
    VAR
      ExLineType@1100285000 : Record 12013642;
    BEGIN
      ExLineType.RESET;
      IF ExLineType.FINDFIRST THEN
        EXIT;

      ExLineType.DELETEALL;

      ExLineType.INIT;
      ExLineType.Code := 1;
      ExLineType."Line Type" := 'G_LACCOUNT';
      IF "Line Type G/L Account" THEN
        ExLineType."Document Display Expr" := 'TRUE'
      ELSE
        ExLineType."Document Display Expr" := 'FALSE';
      ExLineType."Line Display Expr" := 'Linetype>0';
      ExLineType.Value := 1;
      ExLineType.INSERT;

      ExLineType.INIT;
      ExLineType.Code := 2;
      ExLineType."Line Type" := 'ITEM';
      IF "Line Type Item" THEN
        ExLineType."Document Display Expr" := 'TRUE'
      ELSE
        ExLineType."Document Display Expr" := 'FALSE';
      ExLineType.Value := 2;
      ExLineType.INSERT;

      ExLineType.INIT;
      ExLineType.Code := 3;
      ExLineType."Line Type" := 'FIXEDASSET';
      IF "Line Type Fixed Asset" THEN
        ExLineType."Document Display Expr" := 'TRUE'
      ELSE
        ExLineType."Document Display Expr" := 'FALSE';
      ExLineType.Value := 4;
      ExLineType.INSERT;

      ExLineType.INIT;
      ExLineType.Code := 4;
      ExLineType."Line Type" := 'CHARGE';
      IF "Line Type Item Charge" THEN
        ExLineType."Document Display Expr" := 'TRUE'
      ELSE
        ExLineType."Document Display Expr" := 'FALSE';
      ExLineType.Value := 5;
      ExLineType.INSERT;
    END;

    PROCEDURE CreateCodingRules@1100285003();
    VAR
      ExCodingRule@1100285001 : Record 12013660;
      ExCodingRuleLine@1100285000 : Record 12013663;
      NextID@1100285002 : Integer;
    BEGIN
      IF NOT "Create Default Coding Rules" THEN
        EXIT;

      ExCodingRule.RESET;
      ExCodingRule.SETRANGE("Company Name", "Company Name");
      IF ExCodingRule.FINDFIRST THEN
        EXIT;

      ExCodingRule.DELETEALL;

      ExCodingRuleLine.RESET;
      ExCodingRuleLine.SETRANGE("Company Name", "Company Name");
      ExCodingRuleLine.DELETEALL;

      ExCodingRule.RESET;
      IF ExCodingRule.FINDLAST THEN
        NextID := ExCodingRule.ID + 1
      ELSE
        NextID := 1;

      CreateCodingRule("Company Name",CodingRuleName0,0,NextID);
      CreateCodingRule("Company Name",CodingRuleName1,1,NextID);
      CreateCodingRule("Company Name",CodingRuleName2,2,NextID);
      CreateCodingRule("Company Name",CodingRuleName3,3,NextID);
    END;

    LOCAL PROCEDURE CreateColumn@1100285004(CompName@1100285001 : Text[30];VAR NextID@1100285000 : Integer;Code@1100285002 : Text[50];Source@1100285003 : Text[50];Format@1100285004 : Text[250];LineDispExpr@1100285008 : Text[250];DataType@1100285009 : Text[30];IsFreeText@1100285010 : Boolean;UseAutoComp@1100285005 : Boolean;IsDim@1100285011 : Boolean;SortOrder@1100285006 : Integer);
    VAR
      ExColumn@1100285007 : Record 12013661;
    BEGIN
      ExColumn.INIT;
      ExColumn."Company Name" := CompName;
      ExColumn.ID := NextID;
      NextID := NextID + 1;

      ExColumn.Code := Code;
      ExColumn.Source := Source;
      ExColumn.Format := Format;
      ExColumn."Line Display Expr" := LineDispExpr;
      ExColumn."Use Auto Completion" := UseAutoComp;
      ExColumn."Sort Order" := SortOrder;
      ExColumn."Data Type" := DataType;
      ExColumn."Is Free Text" := IsFreeText;
      ExColumn.Dimension := IsDim;
      ExColumn.INSERT;
    END;

    LOCAL PROCEDURE CreateCodingRule@1100285005(CompName@1100285000 : Text[30];Name@1100285002 : Text[30];Type@1100285001 : 'IsValidation,IsFilter,IsSubset,IsDeny';VAR NextID@1100285006 : Integer);
    VAR
      ExCodingRule@1100285003 : Record 12013660;
      ExCodingRuleLine@1100285004 : Record 12013663;
      ExFlow@1100285005 : Codeunit 12013601;
    BEGIN
      ExCodingRule.INIT;
      ExCodingRule.ID := NextID;
      NextID := NextID + 1;
      ExCodingRule."Company Name" := CompName;
      ExCodingRule.Name := Name;
      CASE Type OF
        Type::IsValidation: ExCodingRule."Is Validation" := TRUE;
        Type::IsFilter: ExCodingRule."Is Filter" := TRUE;
        Type::IsSubset: ExCodingRule."Is Subset" := TRUE;
        Type::IsDeny: ExCodingRule.IsDeny := TRUE;
      END;
      ExCodingRule.Active := TRUE;
      ExCodingRule.INSERT;

      ExFlow.RuleLines(ExCodingRule);

      ExCodingRuleLine.RESET;
      ExCodingRuleLine.SETRANGE("Company Name",CompName);
      ExCodingRuleLine.SETRANGE(ID, ExCodingRule.ID);
      IF ExCodingRuleLine.FINDSET THEN
        REPEAT
          IF Type = Type::IsDeny THEN BEGIN
            IF ExCodingRuleLine."Column Code" IN ['G_LACCOUNT','ITEM','CHARGE','FIXEDASSET'] THEN
              ExCodingRuleLine.VALIDATE(Empty, TRUE);
          END
          ELSE BEGIN
            ExCodingRuleLine.VALIDATE(All, TRUE);
            IF Type <> Type::IsSubset THEN
              ExCodingRuleLine.VALIDATE(Empty, TRUE);
          END;

          ExCodingRuleLine.MODIFY;
        UNTIL ExCodingRuleLine.NEXT = 0;
    END;

    PROCEDURE CreateExFlowSetup@1100285011();
    VAR
      ExFlowSetup@1100285000 : Record 12013601;
    BEGIN
      IF NOT "Create Default ExFlow Setup" THEN
        EXIT;

      IF ExFlowSetup.GET("Company Name",0) THEN
        ExFlowSetup.DELETE;

      ExFlowSetup.INIT;
      ExFlowSetup."Company Name" := "Company Name";
      ExFlowSetup."Path to New Invoices" := "Path to New Invoices";
      ExFlowSetup."Path to Used Invoices" := "Path to Used Invoices";
      ExFlowSetup."Path to New OCR-files" := "Path to New OCR-files";
      ExFlowSetup."Path to Attached Files" := "Path to Attached Files";
      ExFlowSetup."Path to connected Att. Files" := "Path to connected Att. Files";
      ExFlowSetup."Propose Line" := TRUE;
      ExFlowSetup."Blank Currency if equal to LCY" := TRUE;
      ExFlowSetup."Block Amt change Approved Line" := TRUE;
      ExFlowSetup."No. Of Days (Due Date Calc)" := 10;
      ExFlowSetup."Check Gross Amount" := TRUE;
      ExFlowSetup."Max. Diff. Gross Amount" := 1;
      ExFlowSetup."Max. Variation on Net Amount" := 1;
      ExFlowSetup."Check Final Approval" := TRUE;
      ExFlowSetup."Max. Variation on VAT Amount" := 0;
      ExFlowSetup."Check Posting Setup" := TRUE;
      ExFlowSetup."Check Dimensions" := TRUE;
      ExFlowSetup."Check Accounting Periods" := TRUE;
      ExFlowSetup."Check if no Approvers" := TRUE;
      ExFlowSetup."Check Unit Cost" := TRUE;
      ExFlowSetup."Test Amounts" := 2;
      ExFlowSetup."Max Misc. Charges (LCY)" := 100;
      ExFlowSetup."Not Posting Predefined Account" := TRUE;
      ExFlowSetup."Order Applies-to" := 1;
      ExFlowSetup."In LCY" := 10;
      ExFlowSetup."In Discount" := 10;
      ExFlowSetup."Activate Not Over" := TRUE;
      ExFlowSetup."Do Not Search Blocked Vendors" := TRUE;
      ExFlowSetup."Default Import Type" := 2;
      ExFlowSetup."Activate VendorID sync." := TRUE;
      ExFlowSetup."Force Imported Order No. Match" := TRUE;
      ExFlowSetup."Receipt No. Mand. Item" := TRUE;
      ExFlowSetup."Check If Lines Exists" := TRUE;
      ExFlowSetup.INSERT;

      CreateVendorIDFields;
    END;

    PROCEDURE CreateVendorIDFields@1100285012();
    VAR
      Field@1100285001 : Record 2000000041;
      RSSortOrder@1100285000 : Integer;
    BEGIN
      CreateVendorIDField(DATABASE::Vendor,1,RSSortOrder); // No.
      CreateVendorIDField(DATABASE::Vendor,86,RSSortOrder); // VAT Registration No.

      IF Field.GET(DATABASE::Vendor,11200) THEN
        CreateVendorIDField(DATABASE::Vendor,11200,RSSortOrder); // SE version Plus Giro

      IF Field.GET(DATABASE::Vendor,11207) THEN
        CreateVendorIDField(DATABASE::Vendor,11207,RSSortOrder); // SE version Bank Giro No

      IF Field.GET(DATABASE::Vendor,11209) THEN
        CreateVendorIDField(DATABASE::Vendor,11209,RSSortOrder); // SE version Bank account no

      IF Field.GET(DATABASE::Vendor,11620) THEN
        CreateVendorIDField(DATABASE::Vendor,11620,RSSortOrder); // AUNZ ABN

      IF Field.GET(DATABASE::Vendor,15000002) THEN
        CreateVendorIDField(DATABASE::Vendor,15000002,RSSortOrder); // NO Version Recipient Bank Account No

      IF Field.GET(DATABASE::Vendor,15000002) THEN
        CreateVendorIDField(DATABASE::Vendor,15000002,RSSortOrder); // Swedish version Plus Giro

      IF Field.GET(DATABASE::"Vendor Bank Account",12047958) THEN
        CreateVendorIDField(DATABASE::"Vendor Bank Account",12047958,RSSortOrder); // SE version Plus Giro

      IF Field.GET(DATABASE::"Vendor Bank Account",12047964) THEN
        CreateVendorIDField(DATABASE::"Vendor Bank Account",12047964,RSSortOrder); // SE version Bank Giro

      IF Field.GET(DATABASE::"Vendor Bank Account",12047967) THEN
        CreateVendorIDField(DATABASE::"Vendor Bank Account",12047967,RSSortOrder); // SE version Account
    END;

    PROCEDURE CreateVendorIDField@1100285015(TableNo@1100285003 : Integer;FieldNo@1100285002 : Integer;VAR RSSortOrder@1100285001 : Integer);
    VAR
      VendorIDField@1100285000 : Record 12013668;
    BEGIN
      VendorIDField.INIT;
      VendorIDField."Table No." := TableNo;
      VendorIDField."Field No." := FieldNo;
      VendorIDField."RS Export order" := RSSortOrder;
      VendorIDField.INSERT;
      RSSortOrder := RSSortOrder + 1;
    END;

    PROCEDURE CreateStandardSecurityRole@1100285013();
    VAR
      UserRole@1100285000 : Record 2000000004;
      Object@1100285001 : Record 2000000001;
    BEGIN
      IF NOT "Create Default Security Roles" THEN
        EXIT;

      UserRole.INIT;
      UserRole."Role ID" := 'EXFLOW-SUPER';
      UserRole.Name := 'ExF: Full access';
      IF UserRole.INSERT THEN;

      UserRole.INIT;
      UserRole."Role ID" := 'EXFLOW-POWER';
      UserRole.Name := 'Some Setup (templ/user)';
      IF UserRole.INSERT THEN;

      UserRole.INIT;
      UserRole."Role ID" := 'EXFLOW-USER';
      UserRole.Name := 'Import/Create/Change Documents';
      IF UserRole.INSERT THEN;

      Object.RESET;
      Object.SETRANGE(Type, Object.Type::TableData);
      Object.SETFILTER(ID, '%1..%2|%3..%4', 12013587, 12013686, 12057070, 12057169);
      IF Object.FINDSET THEN
        REPEAT
          CreatePermissionEntry('EXFLOW-SUPER',Object.ID,1,1,1);
          CreatePermissionEntry('EXFLOW-POWER',Object.ID,1,1,1);
          CreatePermissionEntry('EXFLOW-USER',Object.ID,1,1,1);
        UNTIL Object.NEXT = 0;

      CreatePermissionEntry('EXFLOW-USER',12013597,2,2,2);
      CreatePermissionEntry('EXFLOW-USER',12013598,2,2,2);
      CreatePermissionEntry('EXFLOW-USER',12013600,2,2,2);
      CreatePermissionEntry('EXFLOW-USER',12013601,0,0,0);
      CreatePermissionEntry('EXFLOW-USER',12013602,0,0,0);
      CreatePermissionEntry('EXFLOW-USER',12013613,2,2,2);
      CreatePermissionEntry('EXFLOW-USER',12013616,2,2,2);
      CreatePermissionEntry('EXFLOW-USER',12013619,2,2,2);
      CreatePermissionEntry('EXFLOW-USER',12013620,2,2,2);
      CreatePermissionEntry('EXFLOW-USER',12013621,2,2,2);
      CreatePermissionEntry('EXFLOW-USER',12013633,0,0,0);
      CreatePermissionEntry('EXFLOW-USER',12013634,2,2,2);
      CreatePermissionEntry('EXFLOW-USER',12013644,0,0,0);
      CreatePermissionEntry('EXFLOW-USER',12013653,2,2,2);
      CreatePermissionEntry('EXFLOW-USER',12013654,0,0,0);
      CreatePermissionEntry('EXFLOW-USER',12013669,2,2,2);
      CreatePermissionEntry('EXFLOW-USER',12013672,2,2,2);
      CreatePermissionEntry('EXFLOW-USER',12013684,2,2,2);

      CreatePermissionEntry('EXFLOW-POWER',12013597,2,2,2);
      CreatePermissionEntry('EXFLOW-POWER',12013600,2,2,2);
      CreatePermissionEntry('EXFLOW-POWER',12013613,2,2,2);
      CreatePermissionEntry('EXFLOW-POWER',12013616,2,2,2);
      CreatePermissionEntry('EXFLOW-POWER',12013619,2,2,2);
      CreatePermissionEntry('EXFLOW-POWER',12013620,2,2,2);
      CreatePermissionEntry('EXFLOW-POWER',12013621,2,2,2);
      CreatePermissionEntry('EXFLOW-POWER',12013634,2,2,2);
      CreatePermissionEntry('EXFLOW-POWER',12013672,2,2,2);
      CreatePermissionEntry('EXFLOW-POWER',12013684,2,2,2);
    END;

    PROCEDURE CreatePermissionEntry@1100285014(_RoleID@1100285005 : Code[20];_ID@1100285004 : Integer;_Insert@1100285003 : Integer;_Modify@1100285002 : Integer;_Delete@1100285001 : Integer);
    VAR
      Permission@1100285000 : Record 2000000005;
      Object@1100285006 : Record 2000000001;
    BEGIN
      IF NOT Object.GET(Object.Type::Table,'',_ID) THEN
        EXIT;

      IF Permission.GET(_RoleID,Permission."Object Type"::"Table Data",_ID) THEN BEGIN
        Permission."Read Permission" := 1; // Yes
        Permission."Insert Permission" := _Insert;
        Permission."Modify Permission" := _Modify;
        Permission."Delete Permission" := _Delete;
        Permission.MODIFY;
      END
      ELSE BEGIN
        Permission.INIT;
        Permission."Role ID" := _RoleID;
        Permission."Object Type" := Permission."Object Type"::"Table Data";
        Permission."Object ID" := _ID;
        Permission."Read Permission" := 1; // Yes
        Permission."Insert Permission" := _Insert;
        Permission."Modify Permission" := _Modify;
        Permission."Delete Permission" := _Delete;
        Permission.INSERT;
      END;
    END;

    PROCEDURE GetLineTypeValues@1100285006(VAR ExFlowInitSetup@1100285000 : Record 12013605) : Boolean;
    VAR
      ExLineType@1100285001 : Record 12013642;
    BEGIN
      ExLineType.RESET;
      IF NOT ExLineType.FINDSET THEN
        EXIT(FALSE);

      REPEAT
        IF ExLineType."Line Type" = 'G_LACCOUNT' THEN
          ExFlowInitSetup."Line Type G/L Account" := ExLineType."Document Display Expr" = 'TRUE';
        IF ExLineType."Line Type" = 'ITEM' THEN
          ExFlowInitSetup."Line Type Item" := ExLineType."Document Display Expr" = 'TRUE';
        IF ExLineType."Line Type" = 'FIXEDASSET' THEN
          ExFlowInitSetup."Line Type Fixed Asset" := ExLineType."Document Display Expr" = 'TRUE';
        IF ExLineType."Line Type" = 'CHARGE' THEN
          ExFlowInitSetup."Line Type Item Charge" := ExLineType."Document Display Expr" = 'TRUE';
      UNTIL ExLineType.NEXT = 0;

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE GetNextSortOrder@1100285008() : Integer;
    VAR
      TempInteger@1100285000 : TEMPORARY Record 2000000026;
    BEGIN
      TempInteger.RESET;
      TempInteger.DELETEALL;

      TempInteger.Number := "Sort Order 1";
      IF TempInteger.INSERT THEN;
      TempInteger.Number := "Sort Order 2";
      IF TempInteger.INSERT THEN;
      TempInteger.Number := "Sort Order 3";
      IF TempInteger.INSERT THEN;
      TempInteger.Number := "Sort Order 4";
      IF TempInteger.INSERT THEN;
      TempInteger.Number := "Sort Order 5";
      IF TempInteger.INSERT THEN;
      TempInteger.Number := "Sort Order 6";
      IF TempInteger.INSERT THEN;
      TempInteger.Number := "Sort Order 7";
      IF TempInteger.INSERT THEN;
      TempInteger.Number := "Sort Order 8";
      IF TempInteger.INSERT THEN;

      TempInteger.RESET;
      IF TempInteger.FINDLAST THEN
        EXIT(TempInteger.Number + 1)
      ELSE
        EXIT(1);
    END;

    PROCEDURE GetColumnValues@1100285009(VAR ExFlowInitSetup@1100285001 : Record 12013605) : Boolean;
    VAR
      ExColumn@1100285000 : Record 12013661;
    BEGIN
      ExFlowInitSetup."Use Shortcut Dimension 1" := FALSE;
      ExFlowInitSetup."Sort Order 1" := 0;
      ExFlowInitSetup."Use Shortcut Dimension 2" := FALSE;
      ExFlowInitSetup."Sort Order 2" := 0;
      ExFlowInitSetup."Use Shortcut Dimension 3" := FALSE;
      ExFlowInitSetup."Sort Order 3" := 0;
      ExFlowInitSetup."Use Shortcut Dimension 4" := FALSE;
      ExFlowInitSetup."Sort Order 4" := 0;
      ExFlowInitSetup."Use Shortcut Dimension 5" := FALSE;
      ExFlowInitSetup."Sort Order 5" := 0;
      ExFlowInitSetup."Use Shortcut Dimension 6" := FALSE;
      ExFlowInitSetup."Sort Order 6" := 0;
      ExFlowInitSetup."Use Shortcut Dimension 7" := FALSE;
      ExFlowInitSetup."Sort Order 7" := 0;
      ExFlowInitSetup."Use Shortcut Dimension 8" := FALSE;
      ExFlowInitSetup."Sort Order 8" := 0;

      ExColumn.RESET;
      ExColumn.SETRANGE("Company Name", ExFlowInitSetup."Company Name");
      IF ExColumn.FINDSET THEN BEGIN
        REPEAT
          IF ExColumn.Source = 'DIM1' THEN BEGIN
            ExFlowInitSetup."Use Shortcut Dimension 1" := TRUE;
            ExFlowInitSetup."Sort Order 1" := ExColumn."Sort Order";
          END;
          IF ExColumn.Source = 'DIM2' THEN BEGIN
            ExFlowInitSetup."Use Shortcut Dimension 2" := TRUE;
            ExFlowInitSetup."Sort Order 2" := ExColumn."Sort Order";
          END;
          IF ExColumn.Source = 'DIM3' THEN BEGIN
            ExFlowInitSetup."Use Shortcut Dimension 3" := TRUE;
            ExFlowInitSetup."Sort Order 3" := ExColumn."Sort Order";
          END;
          IF ExColumn.Source = 'DIM4' THEN BEGIN
            ExFlowInitSetup."Use Shortcut Dimension 4" := TRUE;
            ExFlowInitSetup."Sort Order 4" := ExColumn."Sort Order";
          END;
          IF ExColumn.Source = 'DIM5' THEN BEGIN
            ExFlowInitSetup."Use Shortcut Dimension 5" := TRUE;
            ExFlowInitSetup."Sort Order 5" := ExColumn."Sort Order";
          END;
          IF ExColumn.Source = 'DIM6' THEN BEGIN
            ExFlowInitSetup."Use Shortcut Dimension 6" := TRUE;
            ExFlowInitSetup."Sort Order 6" := ExColumn."Sort Order";
          END;
          IF ExColumn.Source = 'DIM7' THEN BEGIN
            ExFlowInitSetup."Use Shortcut Dimension 7" := TRUE;
            ExFlowInitSetup."Sort Order 7" := ExColumn."Sort Order";
          END;
          IF ExColumn.Source = 'DIM8' THEN BEGIN
            ExFlowInitSetup."Use Shortcut Dimension 8" := TRUE;
            ExFlowInitSetup."Sort Order 8" := ExColumn."Sort Order";
          END;
        UNTIL ExColumn.NEXT = 0;

        EXIT(TRUE);
      END
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE GetCodingRuleValues@1100285010(VAR ExFlowInitSetup@1100285001 : Record 12013605) : Boolean;
    VAR
      ExCodingRule@1100285000 : Record 12013660;
    BEGIN
      ExCodingRule.RESET;
      ExCodingRule.SETRANGE("Company Name", ExFlowInitSetup."Company Name");
      IF ExCodingRule.FINDFIRST THEN BEGIN
        ExFlowInitSetup."Create Default Coding Rules" := FALSE;
        EXIT(TRUE);
      END
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE GetExFlowSetup@1100285016(VAR ExFlowInitSetup@1100285000 : Record 12013605) : Boolean;
    VAR
      ExFlowSetup@1100285001 : Record 12013601;
    BEGIN
      IF ExFlowSetup.GET(ExFlowInitSetup."Company Name",0) THEN BEGIN
        ExFlowInitSetup."Path to New Invoices" := ExFlowSetup."Path to New Invoices";
        ExFlowInitSetup."Path to Used Invoices" := ExFlowSetup."Path to Used Invoices";
        ExFlowInitSetup."Path to New OCR-files" := ExFlowSetup."Path to New OCR-files";
        ExFlowInitSetup."Path to Attached Files" := ExFlowSetup."Path to Attached Files";
        ExFlowInitSetup."Path to connected Att. Files" := ExFlowSetup."Path to connected Att. Files";
        IF (ExFlowInitSetup."Path to New Invoices" <> '') OR
           (ExFlowInitSetup."Path to Used Invoices" <> '') OR
           (ExFlowInitSetup."Path to New OCR-files" <> '') OR
           (ExFlowInitSetup."Path to Attached Files" <> '') OR
           (ExFlowInitSetup."Path to connected Att. Files" <> '') THEN
              ExFlowInitSetup."Create Default ExFlow Setup" := FALSE
            ELSE
              ExFlowInitSetup."Create Default ExFlow Setup" := TRUE;
      END;

      IF ExFlowInitSetup."Create Default ExFlow Setup" THEN
        EXIT(FALSE)
      ELSE
        EXIT(TRUE);
    END;

    PROCEDURE GetShortCutCode@1100285017(VAR ExFlowInitSetup@1100285000 : Record 12013605;ShortcutDimNo@1100285002 : Integer) : Code[20];
    VAR
      GLSetup@1100285001 : Record 98;
    BEGIN
      GLSetup.CHANGECOMPANY(ExFlowInitSetup."Company Name");
      GLSetup.GET;
      CASE ShortcutDimNo OF
        1: EXIT(GLSetup."Shortcut Dimension 1 Code");
        2: EXIT(GLSetup."Shortcut Dimension 2 Code");
        3: EXIT(GLSetup."Shortcut Dimension 3 Code");
        4: EXIT(GLSetup."Shortcut Dimension 4 Code");
        5: EXIT(GLSetup."Shortcut Dimension 5 Code");
        6: EXIT(GLSetup."Shortcut Dimension 6 Code");
        7: EXIT(GLSetup."Shortcut Dimension 7 Code");
        8: EXIT(GLSetup."Shortcut Dimension 8 Code");
      END;
    END;

    PROCEDURE EndingSlash@1000000000(VAR Path@1000000000 : Text[128]);
    BEGIN
      IF Path <> '' THEN
        IF Path[STRLEN(Path)] <> '\' THEN
          Path := Path + '\';
    END;

    BEGIN
    END.
  }
}

