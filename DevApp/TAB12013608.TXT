OBJECT Table 12013608 EX Document
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF410000;
  }
  PROPERTIES
  {
    Permissions=TableData 12013608=rimd,
                TableData 12013609=rimd,
                TableData 12013610=rimd,
                TableData 12013611=rimd;
    OnInsert=BEGIN
               "Change Date" := CREATEDATETIME(TODAY,TIME);
               "Changed By" := USERID;
             END;

    OnModify=BEGIN
               "Change Date" := CREATEDATETIME(TODAY,TIME);
               "Changed By" := USERID;
             END;

    OnDelete=BEGIN
               DeleteExDoc;
             END;

    CaptionML=ENU=Document;
    LookupPageID=Page12013633;
    DrillDownPageID=Page12013633;
  }
  FIELDS
  {
    { 2   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,,,Requisition;
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,,,Requisition }
    { 3   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No. }
    { 10  ;   ;Initiator           ;Code50        ;TableRelation="EX User Group".Code WHERE (Blocked=CONST(No));
                                                   CaptionML=ENU=Assigned Purchasing Group }
    { 11  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=ENU=Vendor No. }
    { 12  ;   ;Admin Comment       ;Text180       ;CaptionML=ENU=Comments }
    { 13  ;   ;Create Date         ;Date          ;CaptionML=ENU=Create Date }
    { 14  ;   ;Due Date            ;Date          ;CaptionML=ENU=Due Date }
    { 15  ;   ;Posting Date        ;Date          ;CaptionML=ENU=Posting Date }
    { 16  ;   ;Document Date       ;Date          ;CaptionML=ENU=Document Date }
    { 17  ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=Inactive,Active,Approved,Ready for posting,On hold,Received,Invoiced,Not released;
                                                   OptionString=Inactive,Active,Approved,Ready for posting,On hold,Received,Invoiced,Not released }
    { 18  ;   ;Predefind Posting No.;Code20       ;CaptionML=ENU=Predefind Posting No. }
    { 19  ;   ;Vendor Document No. ;Code50        ;CaptionML=ENU=Vendor Document No. }
    { 20  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=ENU=Currency Code }
    { 21  ;   ;Vendor Name         ;Text50        ;CaptionML=ENU=Vendor Name }
    { 22  ;   ;Image Name          ;Text250       ;CaptionML=ENU=Image Name }
    { 23  ;   ;Rejected            ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("EX Document Line" WHERE (Document Type=FIELD(Document Type),
                                                                                               Document No.=FIELD(Document No.),
                                                                                               Flowstatus=CONST(Rejected)));
                                                   CaptionML=ENU=Rejected;
                                                   Editable=No }
    { 24  ;   ;Net Amount          ;Decimal       ;CaptionML=ENU=Net Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 25  ;   ;VAT Amount          ;Decimal       ;CaptionML=ENU=VAT Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 26  ;   ;ID                  ;BigInteger    ;AutoIncrement=Yes;
                                                   CaptionML=ENU=ID;
                                                   Description=PK }
    { 27  ;   ;Expected Receipt Date;Date         ;CaptionML=ENU=Expected Receipt Date }
    { 28  ;   ;Hold by Approver    ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("EX Document Line Approver" WHERE (Document Type=FIELD(Document Type),
                                                                                                        Document No.=FIELD(Document No.),
                                                                                                        Flowstatus=CONST(On hold)));
                                                   CaptionML=ENU=On Hold;
                                                   Editable=No }
    { 29  ;   ;Batch No.           ;Code20        ;CaptionML=ENU=Batch No.;
                                                   Editable=No }
    { 40  ;   ;Gross Amount        ;Decimal       ;CaptionML=ENU=Gross Amount;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 50  ;   ;Assigned Receipt Group;Code50      ;TableRelation="EX User Group".Code WHERE (Blocked=CONST(No));
                                                   CaptionML=ENU=Assigned Receipt Group }
    { 60  ;   ;Ex Location ID      ;Integer       ;TableRelation="EX Location".ID;
                                                   OnValidate=VAR
                                                                ExLocation@1100285000 : Record 12013613;
                                                              BEGIN
                                                                IF "Ex Location ID" = 0 THEN
                                                                  "Text Field 10" := ''
                                                                ELSE BEGIN
                                                                  ExLocation.GET("Ex Location ID");
                                                                  "Text Field 10" := ExLocation.Name;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=ExFlow Delivery Address ID }
    { 70  ;   ;Requisition Status  ;Option        ;CaptionML=ENU=Requisition Status;
                                                   OptionCaptionML=ENU=Unsubmitted,Submitted to Vendor;
                                                   OptionString=Unsubmitted,Submitted to Vendor }
    { 71  ;   ;Requisition Archive ID;BigInteger  ;CaptionML=ENU=Requisition Archive ID;
                                                   Editable=No }
    { 75  ;   ;Requisition Subtype ;Integer       ;TableRelation="Requisition subtype";
                                                   CaptionML=ENU=Requisition Subtype }
    { 76  ;   ;Requisition No. Series;Code10      ;TableRelation="No. Series";
                                                   CaptionML=ENU=Requisition No. Series;
                                                   Editable=No }
    { 80  ;   ;Web Company ID      ;BigInteger    ;CaptionML=ENU=Web Company ID }
    { 81  ;   ;Web ID              ;BigInteger    ;CaptionML=ENU=Web ID;
                                                   Description=ID+Web Company ID }
    { 500 ;   ;Project No.         ;Code20        ;CaptionML=ENU=Project No. }
    { 501 ;   ;Error Message       ;Text250       ;CaptionML=ENU=Error Message }
    { 502 ;   ;Text Field 1        ;Text100       ;CaptionML=ENU=Reference No. }
    { 503 ;   ;Text Field 2        ;Text100       ;CaptionML=ENU=Text Field 2 }
    { 504 ;   ;Date Field 1        ;Date          ;CaptionML=ENU=Date Field 1 }
    { 505 ;   ;Date Field 2        ;Date          ;CaptionML=ENU=Date Field 2 }
    { 510 ;   ;Text Field 3        ;Text100       ;CaptionML=ENU=Text Field 3 }
    { 511 ;   ;Text Field 4        ;Text100       ;CaptionML=ENU=Text Field 4 }
    { 512 ;   ;Text Field 5        ;Text100       ;CaptionML=ENU=Text Field 5 }
    { 513 ;   ;Text Field 6        ;Text100       ;CaptionML=ENU=Text Field 6 }
    { 514 ;   ;Text Field 7        ;Text100       ;CaptionML=ENU=Text Field 7 }
    { 515 ;   ;Text Field 8        ;Text100       ;CaptionML=ENU=Text Field 8 }
    { 516 ;   ;Text Field 9        ;Text100       ;CaptionML=ENU=Text Field 9 }
    { 517 ;   ;Text Field 10       ;Text100       ;CaptionML=ENU=Text Field 10 }
    { 550 ;   ;Date Field 3        ;DateTime      ;CaptionML=ENU=Date Field 3 }
    { 551 ;   ;Date Field 4        ;DateTime      ;CaptionML=ENU=Date Field 4 }
    { 560 ;   ;Decimal Field 1     ;Decimal       ;CaptionML=ENU=Decimal Field 1 }
    { 561 ;   ;Decimal Field 2     ;Decimal       ;CaptionML=ENU=Decimal Field 2 }
    { 562 ;   ;Decimal Field 3     ;Decimal       ;CaptionML=ENU=Decimal Field 3 }
    { 563 ;   ;Decimal Field 4     ;Decimal       ;CaptionML=ENU=Decimal Field 4 }
    { 600 ;   ;Approver Comments   ;BLOB          ;Compressed=No;
                                                   CaptionML=ENU=Approver Header Comments }
    { 601 ;   ;Changed By          ;Code50        ;CaptionML=ENU=Changed By;
                                                   Editable=No }
    { 602 ;   ;Change Date         ;DateTime      ;CaptionML=ENU=Change Date;
                                                   Editable=No }
    { 650 ;   ;Created By          ;Code50        ;CaptionML=ENU=Created By;
                                                   Editable=No }
    { 651 ;   ;Created Date        ;DateTime      ;CaptionML=ENU=Created Date;
                                                   Editable=No }
    { 700 ;   ;Change Jnl Processed PO;Boolean    ;CaptionML=ENU=Change Jnl Processed Purchase Order }
    { 1500;   ;Has Attachments     ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("EX Attached File" WHERE (Document Type=FIELD(Document Type),
                                                                                               Document No.=FIELD(Document No.)));
                                                   CaptionML=ENU=Has Attachments;
                                                   Editable=No }
    { 1501;   ;Has Approver Comments;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Exist("EX Document Line Approver" WHERE (Document Type=FIELD(Document Type),
                                                                                                        Document No.=FIELD(Document No.),
                                                                                                        Comment=FILTER(<>''),
                                                                                                        Auto Approved=CONST(No)));
                                                   CaptionML=ENU=Has Approver Line Comments;
                                                   Editable=No }
    { 1502;   ;Financial On Hold   ;Code3         ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Header"."On Hold" WHERE (Document Type=FIELD(Document Type),
                                                                                                         No.=FIELD(Document No.)));
                                                   CaptionML=ENU=Payment/Refund On Hold;
                                                   Editable=No }
    { 1505;   ;Shortcut Dimension 1 Code;Code20   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Header"."Shortcut Dimension 1 Code" WHERE (Document Type=FIELD(Document Type),
                                                                                                                           No.=FIELD(Document No.)));
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=ENU=Shortcut Dimension 1 Code;
                                                   Editable=No;
                                                   CaptionClass='1,2,1' }
    { 1506;   ;Shortcut Dimension 2 Code;Code20   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Header"."Shortcut Dimension 2 Code" WHERE (Document Type=FIELD(Document Type),
                                                                                                                           No.=FIELD(Document No.)));
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   CaptionML=ENU=Shortcut Dimension 2 Code;
                                                   Editable=No;
                                                   CaptionClass='1,2,2' }
    { 1510;   ;Line 1 Current Approver;Code50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("EX Document Line Approver".Approver WHERE (Document ID=FIELD(ID),
                                                                                                                  Flowstatus=CONST(Current)));
                                                   CaptionML=ENU=Current Approver (First line);
                                                   Editable=No }
    { 1515;   ;Connected Order No. ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("EX Document Line"."Order No." WHERE (Document ID=FIELD(ID),
                                                                                                            Order No.=FILTER(<>'')));
                                                   CaptionML=ENU=Connected Order No. (first);
                                                   Editable=No }
    { 3000;   ;Temp Company Name   ;Text30        ;CaptionML=ENU=Company Name;
                                                   Description=Used temporarily;
                                                   Editable=No }
    { 12013595;;Import Document No.;Integer       ;CaptionML=ENU=Import Document No. }
    { 12013650;;Vendor Document No. 2;Code50      ;CaptionML=ENU=Vendor Document No. 2 }
    { 12013720;;XML File Name      ;Text250       ;CaptionML=ENU=XML File Name }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Web ID                                   }
    {    ;Document No.                             }
    {    ;Status,Document Type                     }
    {    ;Status,Document Type,Document No.        }
    {    ;Document Type,Document No.               }
    {    ;Due Date                                 }
    {    ;Posting Date                             }
    {    ;Initiator                                }
    {    ;Assigned Receipt Group                   }
    {    ;Create Date                              }
    {    ;Vendor Name                              }
    {    ;Created By                               }
    {    ;Vendor Document No.                      }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EXF100@1100285019 : TextConst 'ENU="The document must belong to the company currently opened. "';
      EXF117@1100285010 : TextConst 'ENU=Detailed Import information does not exist.';
      EXF118@1100285000 : TextConst 'ENU=No lines with the specified filter.';

    PROCEDURE ShowApproverComments@1100285001();
    VAR
      ExFileMgt@1100285001 : Codeunit 12013602;
      ExFlowBlob@1100285003 : Record 12013591;
      TempFileName@1100285000 : Text[1024];
    BEGIN
      CALCFIELDS("Approver Comments");
      IF NOT "Approver Comments".HASVALUE THEN
        EXIT;
      TempFileName := ExFileMgt.ClientTempFileName(CREATEGUID,'.txt');
      ExFlowBlob.BLOB := "Approver Comments";
      ExFileMgt.ShowBLOB(ExFlowBlob,TempFileName);
    END;

    PROCEDURE Navigate@1100285002();
    VAR
      NavigateForm@1100285000 : Page 344;
    BEGIN
      NavigateForm.SetDoc("Posting Date", "Document No.");
      NavigateForm.RUN;
    END;

    PROCEDURE ShowCard@1100285003();
    BEGIN
      Card;
    END;

    LOCAL PROCEDURE Card@1000000000();
    VAR
      PurchHeader@1100285000 : Record 38;
      PurchInvHeader@1100285001 : Record 122;
      PurchCrMemoHeader@1100285002 : Record 124;
    BEGIN
      PurchHeader.RESET;
      PurchHeader.SETRANGE("No.","Document No.");

      CASE "Document Type" OF
        "Document Type"::Invoice :
          BEGIN
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::Invoice);
            PurchHeader.FINDFIRST;
            PAGE.RUNMODAL(51,PurchHeader);
          END;
        "Document Type"::"Credit Memo" :
          BEGIN
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::"Credit Memo");
            PurchHeader.FINDFIRST;
            PAGE.RUNMODAL(52,PurchHeader);
          END;
        "Document Type"::Order :
          BEGIN
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::Order);
            PurchHeader.FINDFIRST;
            PAGE.RUNMODAL(50,PurchHeader);
          END;
        "Document Type"::Quote :
          BEGIN
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::Quote);
            PurchHeader.FINDFIRST;
            PAGE.RUNMODAL(49,PurchHeader);
          END;
        "Document Type"::"6" :
          BEGIN
            PurchInvHeader.SETRANGE("No.", "Document No.");
            PurchInvHeader.FINDFIRST;
            PAGE.RUNMODAL(138,PurchInvHeader);
          END;
        "Document Type"::"7" :
          BEGIN
            PurchCrMemoHeader.SETRANGE("No.", "Document No.");
            PurchCrMemoHeader.FINDFIRST;
            PAGE.RUNMODAL(140,PurchCrMemoHeader);
          END;
      END;
    END;

    PROCEDURE RetrieveRecord@1100285013(VAR ExDoc@1100285004 : Record 12013608;DocType@1100285001 : Integer;DocNo@1100285002 : Code[20]) : Boolean;
    BEGIN
      WITH ExDoc DO BEGIN
        RESET;
        CLEAR(ExDoc);
        SETCURRENTKEY("Document No.");
        SETRANGE("Document Type", DocType);
        SETRANGE("Document No.", DocNo);
        EXIT(FINDFIRST);
      END;
    END;

    PROCEDURE ShowImportCard@1100285004();
    VAR
      ExfImportedPurchHead@1100285000 : Record 12013650;
    BEGIN
      IF ExfImportedPurchHead.GET("Import Document No.") THEN
        PAGE.RUNMODAL(12013635,ExfImportedPurchHead)
      ELSE
        MESSAGE(EXF117);
    END;

    PROCEDURE DeleteExDoc@1100285005();
    VAR
      ExDeletedDoc@1100285003 : Record 12013622;
      ExDocLine@1100285000 : Record 12013609;
      PurchLine@1100285005 : Record 39;
    BEGIN
      ExDeletedDoc.INIT;
      ExDeletedDoc.TRANSFERFIELDS(Rec);
      ExDeletedDoc.ID := 0;
      ExDeletedDoc."Deleted By" := USERID;
      ExDeletedDoc."Deleted Date" := CREATEDATETIME(TODAY,TIME);
      ExDeletedDoc."Old ID" := ID;
      ExDeletedDoc.INSERT;

      PurchLine.RESET;
      IF PurchLine.SETCURRENTKEY("Document No.") THEN;
      PurchLine.SETRANGE("Document Type", "Document Type");
      PurchLine.SETRANGE("Document No.", "Document No.");
      IF NOT PurchLine.ISEMPTY THEN
        PurchLine.MODIFYALL(Approve, FALSE);

      ExDocLine.RESET;
      ExDocLine.SETCURRENTKEY("Document ID");
      ExDocLine.SETRANGE("Document ID", ID);
      IF ExDocLine.FINDSET(TRUE) THEN BEGIN
        REPEAT
          ExDocLine.DeleteLine(ExDeletedDoc.ID,Rec);
        UNTIL ExDocLine.NEXT = 0;

        ExDocLine.DELETEALL;
      END;
    END;

    PROCEDURE FilterOnApprover@1100285000(VAR ExDoc@1100285007 : Record 12013608);
    VAR
      lEXUserGroup@1100285006 : Record 12013606;
      lEXDocumentLineApprover@1100285004 : Record 12013610;
      lSelectApprover@1100285003 : Page 12013647;
      lApprover@1100285002 : Code[50];
      lStatus@1100285001 : Integer;
      TempPos@1100285009 : Integer;
      lMarked@1100285000 : Boolean;
    BEGIN
      CLEAR(lMarked);
      CLEAR(lSelectApprover);

      IF NOT (lSelectApprover.RUNMODAL = ACTION::OK) THEN
        EXIT;

      lApprover := lSelectApprover.GetApprover;
      lStatus := lSelectApprover.GetStatus;
      IF NOT lEXUserGroup.GET(lApprover) THEN BEGIN
        TempPos := STRPOS(lApprover,'\');
        IF TempPos <> 0 THEN BEGIN
          lApprover := COPYSTR(lApprover,TempPos + 1);
          IF NOT lEXUserGroup.GET(lApprover) THEN
            EXIT;
        END;
      END;

      lEXDocumentLineApprover.RESET;
      lEXDocumentLineApprover.SETCURRENTKEY(Approver,"Days to Due Date");

      CASE lStatus OF
        0 : lEXDocumentLineApprover.SETRANGE(Flowstatus,lEXDocumentLineApprover.Flowstatus::Current);
        1 : lEXDocumentLineApprover.SETRANGE(Flowstatus,lEXDocumentLineApprover.Flowstatus::"Not processed");
        2 : ;
      END;

      lEXDocumentLineApprover.SETRANGE(Approver,lApprover);
      ExDoc.ID := 0;

      IF lEXDocumentLineApprover.FINDSET THEN
        REPEAT
          IF lEXDocumentLineApprover."Document ID" <> ExDoc.ID THEN BEGIN
            IF ExDoc.GET(lEXDocumentLineApprover."Document ID") THEN BEGIN
              ExDoc.MARK(TRUE);
              lMarked := TRUE;
            END;
          END;
        UNTIL lEXDocumentLineApprover.NEXT = 0;

      IF lMarked THEN
        ExDoc.MARKEDONLY(TRUE)
      ELSE
        MESSAGE(EXF118);
    END;

    PROCEDURE GetApproverDocs@1100285007(VAR TempExDoc@1100285007 : TEMPORARY Record 12013608;lApprover@1100285000 : Code[50]);
    VAR
      AppSetup@1100285008 : Record 12013601;
      lEXUserGroup@1100285006 : Record 12013606;
      lEXDocument@1100285005 : Record 12013608;
      lEXDocumentLineApprover@1100285004 : Record 12013610;
      TempPos@1100285009 : Integer;
    BEGIN
      AppSetup.GET;

      TempExDoc.RESET;
      TempExDoc.DELETEALL;

      IF NOT lEXUserGroup.GET(lApprover) THEN BEGIN
        TempPos := STRPOS(lApprover,'\');
        IF TempPos <> 0 THEN BEGIN
          lApprover := COPYSTR(lApprover,TempPos + 1);
          IF NOT lEXUserGroup.GET(lApprover) THEN
            EXIT;
        END;
      END;

      lEXDocumentLineApprover.RESET;
      lEXDocumentLineApprover.SETCURRENTKEY(Approver,"Days to Due Date");
      lEXDocumentLineApprover.SETRANGE("Document Type", 0, 5); // No posted documents
      lEXDocumentLineApprover.SETRANGE(Approver,lApprover);
      lEXDocumentLineApprover.SETRANGE(Flowstatus,lEXDocumentLineApprover.Flowstatus::Current);
      IF lEXDocumentLineApprover.FINDSET THEN
        REPEAT
          IF NOT TempExDoc.GET(lEXDocumentLineApprover."Document ID") THEN BEGIN
            lEXDocument.GET(lEXDocumentLineApprover."Document ID");
            IF NOT lEXDocument."Change Jnl Processed PO" THEN
              IF lEXDocument.Status = lEXDocument.Status::Active THEN BEGIN
                TempExDoc.INIT;
                TempExDoc.TRANSFERFIELDS(lEXDocument);
                TempExDoc.INSERT;
              END;
          END;
        UNTIL lEXDocumentLineApprover.NEXT = 0;

      TempExDoc.SETCURRENTKEY("Document Type","Document No.");
      IF TempExDoc.FINDFIRST THEN;
    END;

    PROCEDURE ApproveDoc@1100285006(VAR TempExDoc@1100285001 : TEMPORARY Record 12013608;lApprover@1100285000 : Code[50]);
    VAR
      ExDocLineApprover@1100285003 : Record 12013610;
      DocLine@1100285002 : Record 12013609;
      ExFlow@1100285004 : Codeunit 12013601;
    BEGIN
      lApprover := ExFlow.GetUserGroup(lApprover);
      IF lApprover = '' THEN
        EXIT;

      ExDocLineApprover.RESET;
      ExDocLineApprover.SETCURRENTKEY("Document No.");
      ExDocLineApprover.SETRANGE("Document Type", "Document Type");
      ExDocLineApprover.SETRANGE("Document No.", "Document No.");
      ExDocLineApprover.SETRANGE(Approver,lApprover);
      IF ExDocLineApprover.FINDSET THEN
        REPEAT
          DocLine.GET(ExDocLineApprover."Document Line ID");
          DocLine.FinishOneLine(DocLine,ExDocLineApprover);
        UNTIL ExDocLineApprover.NEXT = 0;

      GetApproverDocs(TempExDoc,USERID);
    END;

    PROCEDURE RejectDoc@1100285009(VAR TempExDoc@1100285001 : TEMPORARY Record 12013608;lApprover@1100285000 : Code[50]);
    VAR
      ExDocLineApprover@1100285003 : Record 12013610;
      DocLine@1100285002 : Record 12013609;
      ExFlow@1100285004 : Codeunit 12013601;
    BEGIN
      lApprover := ExFlow.GetUserGroup(lApprover);
      IF lApprover = '' THEN
        EXIT;

      ExDocLineApprover.RESET;
      ExDocLineApprover.SETCURRENTKEY("Document No.");
      ExDocLineApprover.SETRANGE("Document Type", "Document Type");
      ExDocLineApprover.SETRANGE("Document No.", "Document No.");
      ExDocLineApprover.SETRANGE(Approver,lApprover);
      IF ExDocLineApprover.FINDSET THEN
        REPEAT
          DocLine.GET(ExDocLineApprover."Document Line ID");
          DocLine.RejectOneLine(DocLine,ExDocLineApprover);
        UNTIL ExDocLineApprover.NEXT = 0;

      GetApproverDocs(TempExDoc,USERID);
    END;

    PROCEDURE OpenOrderNo@1100285010() : Code[20];
    VAR
      PurchHeader@1100285000 : Record 38;
    BEGIN
      CALCFIELDS("Connected Order No.");
      IF "Connected Order No." <> '' THEN BEGIN
        IF "Document Type" = "Document Type"::Invoice THEN BEGIN
          IF PurchHeader.GET(PurchHeader."Document Type"::Order,"Connected Order No.") THEN
            PAGE.RUNMODAL(50,PurchHeader);
        END
        ELSE
          IF "Document Type" = "Document Type"::"Credit Memo" THEN BEGIN
            IF PurchHeader.GET(PurchHeader."Document Type"::"Return Order","Connected Order No.") THEN
              PAGE.RUNMODAL(6640,PurchHeader);
          END;
      END;
    END;

    PROCEDURE GetPostingDesc@1100285008() : Text[50];
    VAR
      PurchHeader@1100285000 : Record 38;
      PurchInvHeader@1100285002 : Record 122;
      PurchCrMemoHeader@1100285001 : Record 124;
    BEGIN
      CASE "Document Type" OF
        "Document Type"::Invoice :
          BEGIN
            PurchHeader.RESET;
            PurchHeader.SETCURRENTKEY("No.");
            PurchHeader.SETRANGE("No.","Document No.");
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::Invoice);
            IF PurchHeader.FINDFIRST THEN
                EXIT(PurchHeader."Posting Description");
          END;
        "Document Type"::"Credit Memo" :
          BEGIN
            PurchHeader.RESET;
            PurchHeader.SETCURRENTKEY("No.");
            PurchHeader.SETRANGE("No.","Document No.");
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::"Credit Memo");
            IF PurchHeader.FINDFIRST THEN
                EXIT(PurchHeader."Posting Description");
          END;
        "Document Type"::"6" :
          BEGIN
            PurchInvHeader.RESET;
            PurchInvHeader.SETRANGE("No.", "Document No.");
            IF PurchInvHeader.FINDFIRST THEN
              EXIT(PurchInvHeader."Posting Description");
          END;
        "Document Type"::"7" :
          BEGIN
            PurchCrMemoHeader.RESET;
            PurchCrMemoHeader.SETRANGE("No.", "Document No.");
            IF PurchCrMemoHeader.FINDFIRST THEN
              EXIT(PurchCrMemoHeader."Posting Description");
          END;
      END;

      EXIT('');
    END;

    PROCEDURE GetCaption@1100285011() : Text[250];
    BEGIN
      EXIT(STRSUBSTNO('%1 %2',FORMAT("Document Type",0,0),"Document No."));
    END;

    BEGIN
    END.
  }
}

