OBJECT Table 12013611 EX Document Line Dimension
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF350000;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=VAR
               ExDocLineDim@1100285000 : Record 12013611;
             BEGIN
               ExDocLineDim.RESET;
               ExDocLineDim.SETCURRENTKEY("Document No.");
               ExDocLineDim.SETRANGE("Company Name","Company Name");
               ExDocLineDim.SETRANGE("Document Type","Document Type");
               ExDocLineDim.SETRANGE("Document No.","Document No.");
               ExDocLineDim.SETRANGE("Line No.","Line No.");
               ExDocLineDim.SETRANGE("Dimension Code","Dimension Code");
               IF ExDocLineDim.FINDFIRST THEN
                 ERROR(STRSUBSTNO(EXF48,"Dimension Code"));

               Changeable;
             END;

    OnModify=BEGIN
               Changeable;
             END;

    OnDelete=BEGIN
               Changeable;
             END;

    CaptionML=ENU=Document Line Dimension;
  }
  FIELDS
  {
    { 1   ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   CaptionML=ENU=Company Name }
    { 2   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,Posted Invoice,Posted Credit Memo,Requisition;
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,Posted Invoice,Posted Credit Memo,Requisition }
    { 3   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No. }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 5   ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=VAR
                                                                Column@1100285000 : Record 12013661;
                                                                ColumnID@1100285001 : Integer;
                                                              BEGIN
                                                                ColumnID := ExFlow.GetColumnID("Dimension Code");

                                                                IF ColumnID = 0 THEN
                                                                  ERROR(STRSUBSTNO(EXF45,"Dimension Code"));

                                                                IF ColumnID <> 0 THEN
                                                                  IF Column.GET(COMPANYNAME,ColumnID) THEN
                                                                    IF Column.Dimension THEN
                                                                      IF NOT DimMgt.CheckDim("Dimension Code") THEN
                                                                        ERROR(DimMgt.GetDimErr);

                                                                "Column ID" := ColumnID;

                                                                DocLine.RetrieveRecord(DocLine,"Company Name","Document Type","Document No.","Line No.");
                                                                "Document Line ID" := DocLine.ID;
                                                                "Document ID" := DocLine."Document ID";
                                                              END;

                                                   CaptionML=ENU=Dimension Code }
    { 10  ;   ;Dimension Value     ;Code20        ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   OnValidate=VAR
                                                                Column@1100285000 : Record 12013661;
                                                                ColumnID@1100285001 : Integer;
                                                              BEGIN
                                                                ColumnID := ExFlow.GetColumnID("Dimension Code");

                                                                IF ColumnID = 0 THEN
                                                                  ERROR(STRSUBSTNO(EXF45,"Dimension Code"));

                                                                IF ColumnID <> 0 THEN
                                                                  IF Column.GET(COMPANYNAME,ColumnID) THEN
                                                                    IF Column.Dimension THEN
                                                                      IF NOT DimMgt.CheckDimValue("Dimension Code","Dimension Value") THEN
                                                                        ERROR(DimMgt.GetDimErr);

                                                                DocLine.RetrieveRecord(DocLine,"Company Name","Document Type","Document No.","Line No.");
                                                                "Document Line ID" := DocLine.ID;
                                                                "Document ID" := DocLine."Document ID";
                                                              END;

                                                   CaptionML=ENU=Dimension Value }
    { 11  ;   ;Column ID           ;Integer       ;CaptionML=ENU=Column ID }
    { 12  ;   ;Document Line ID    ;Integer       ;CaptionML=ENU=Document Line ID }
    { 13  ;   ;ID                  ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=ENU=ID }
    { 14  ;   ;Dimension Desc.     ;Text50        ;CaptionML=ENU=Description }
    { 31  ;   ;Document ID         ;Integer       ;CaptionML=ENU=Document ID }
    { 600 ;   ;Changed By          ;Code50        ;CaptionML=ENU=Changed By;
                                                   Editable=No }
    { 601 ;   ;Change Date         ;DateTime      ;CaptionML=ENU=Change Date;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Document No.                             }
    {    ;Company Name,Document Type,Document No.,Line No.,Dimension Code }
    {    ;Document Line ID                         }
    {    ;Document ID                              }
    {    ;Dimension Code                           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DocLine@1100285001 : Record 12013609;
      ExFlow@1100285002 : Codeunit 12013601;
      DimMgt@1100285000 : Codeunit 408;
      EXF02@1100285007 : TextConst 'ENU=Dimensions can not be changed for an approved invoice!';
      EXF41@1100285006 : TextConst 'ENU=Warning! The line has been approved!\\Do you still want to update line?';
      EXF42@1100285004 : TextConst 'ENU=The Line is Ready for Posting and needs to be restarted before it can be changed!';
      EXF45@1100285005 : TextConst 'ENU=ExFlow do not use Dimension %1!';
      EXF47@1100285008 : TextConst 'ENU=Operation aborted.';
      EXF48@1100285010 : TextConst 'ENU=Dimension %1 already exists.';

    PROCEDURE Changeable@1100285001();
    VAR
      DocHead@1100285001 : Record 12013608;
      AppSetup@1100285002 : Record 12013601;
      ExFlow@1100285000 : Codeunit 12013601;
    BEGIN
      IF DocHead.RetrieveRecord(DocHead,"Company Name","Document Type","Document No.") THEN BEGIN
        IF DocHead.Status = DocHead.Status::"Ready for posting" THEN
          ERROR(EXF42);

        IF DocHead.Status > DocHead.Status::Active THEN BEGIN
          IF NOT ExFlow.IsUserSuperior(COMPANYNAME, USERID) THEN
            ERROR(EXF02);

          AppSetup.GET("Company Name",0);
          IF NOT AppSetup."Suppress Approved Message" THEN
            IF NOT CONFIRM(EXF41) THEN
              ERROR(EXF47);
        END;
      END;
    END;

    PROCEDURE RetrieveRecord@1100285000(VAR ExDocLineDim@1100285005 : Record 12013611;CompName@1100285000 : Text[30];DocType@1100285001 : Integer;DocNo@1100285002 : Code[20];LineNo@1100285003 : Integer;DimCode@1100285004 : Code[20]) : Boolean;
    BEGIN
      WITH ExDocLineDim DO BEGIN
        RESET;
        CLEAR(ExDocLineDim);
        SETCURRENTKEY("Document No.");
        SETRANGE("Company Name", CompName);
        SETRANGE("Document Type", DocType);
        SETRANGE("Document No.", DocNo);
        SETRANGE("Line No.", LineNo);
        SETRANGE("Dimension Code", DimCode);
        EXIT(FINDFIRST);
      END;
    END;

    PROCEDURE ClearEmptyRecords@1100285002();
    VAR
      ExDocLineDim@1100285000 : Record 12013611;
    BEGIN
      ExDocLineDim.SETCURRENTKEY("Document Line ID");
      ExDocLineDim.SETRANGE("Document Line ID", "Document Line ID");
      ExDocLineDim.SETRANGE("Dimension Value", '');
      IF NOT ExDocLineDim.ISEMPTY THEN
        ExDocLineDim.DELETEALL;
    END;

    BEGIN
    END.
  }
}

