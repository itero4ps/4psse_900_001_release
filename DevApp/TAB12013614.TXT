OBJECT Table 12013614 OCR Format Conversion
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF350000;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=VAR
               OCRFormatConv@1100285000 : Record 12013614;
             BEGIN
               OCRFormatConv.RESET;
               OCRFormatConv.SETCURRENTKEY("Entry No.");
               OCRFormatConv.LOCKTABLE;
               IF OCRFormatConv.FINDLAST THEN
                 "Entry No." := OCRFormatConv."Entry No." + 1
               ELSE
                 "Entry No." := 1;
             END;

    CaptionML=ENU=OCR Format Conversion;
  }
  FIELDS
  {
    { 1   ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   CaptionML=ENU=Company Name }
    { 2   ;   ;Entry No.           ;Integer       ;CaptionML=ENU=Entry No. }
    { 4   ;   ;Table               ;Option        ;OnValidate=BEGIN
                                                                IF Table <> xRec.Table THEN
                                                                  "Field No." := 0;
                                                              END;

                                                   CaptionML=ENU=Table;
                                                   OptionCaptionML=ENU=Header,Line;
                                                   OptionString=Header,Line }
    { 5   ;   ;Field No.           ;Integer       ;TableRelation=IF (Table=CONST(Header)) Field.No. WHERE (TableNo=CONST(12013648))
                                                                 ELSE IF (Table=CONST(Line)) Field.No. WHERE (TableNo=CONST(12013649));
                                                   OnValidate=VAR
                                                                Field@1100285000 : Record 2000000041;
                                                              BEGIN
                                                                CASE Table OF
                                                                  Table::Header: Field.GET(DATABASE::"Exflow Purch. Import Header","Field No.");
                                                                  Table::Line: Field.GET(DATABASE::"Exflow Purch. Import Line","Field No.");
                                                                END;

                                                                IF NOT (Field.Type IN [Field.Type::Text,Field.Type::Code]) THEN
                                                                  ERROR(Text002);
                                                              END;

                                                   CaptionML=ENU=Field No. }
    { 8   ;   ;In Example string   ;Text50        ;OnValidate=BEGIN
                                                                ConvertChars;
                                                                BuildOutFormat;
                                                              END;

                                                   CaptionML=ENU=In-Example string }
    { 10  ;   ;In Format           ;Text50        ;OnValidate=BEGIN
                                                                BuildOutFormat;
                                                              END;

                                                   CaptionML=ENU=In Format }
    { 20  ;   ;Copy from position  ;Integer       ;OnValidate=BEGIN
                                                                BuildOutFormat;
                                                              END;

                                                   CaptionML=ENU=Copy from position }
    { 21  ;   ;Length              ;Integer       ;OnValidate=BEGIN
                                                                BuildOutFormat;
                                                              END;

                                                   CaptionML=ENU=Length }
    { 25  ;   ;Delete Chars        ;Text30        ;OnValidate=BEGIN
                                                                BuildOutFormat;
                                                              END;

                                                   CaptionML=ENU=Delete Characters }
    { 30  ;   ;Add Prefix          ;Text30        ;OnValidate=BEGIN
                                                                BuildOutFormat;
                                                              END;

                                                   CaptionML=ENU=Add Prefix }
    { 40  ;   ;Blank Value         ;Boolean       ;OnValidate=BEGIN
                                                                BuildOutFormat;
                                                              END;

                                                   CaptionML=ENU=Set out value to blank }
    { 50  ;   ;Out Format          ;Text50        ;CaptionML=ENU=Out Format;
                                                   Editable=No }
    { 55  ;   ;Out Example string  ;Text50        ;OnValidate=BEGIN
                                                                ConvertChars;
                                                                BuildOutFormat;
                                                              END;

                                                   CaptionML=ENU=Out-Example string;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Company Name,Entry No.                  ;Clustered=Yes }
    {    ;Entry No.                                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100285000 : TextConst 'ENU=Use the characters "[" for alpha-numeric values and "]" for numeric.';
      Text002@1100285001 : TextConst 'ENU=The field type must be either Text or Code!';
      ImportManager@1100285002 : Codeunit 12013604;

    PROCEDURE GetHelp@1100285000() : Text[250];
    BEGIN
      EXIT(Text001);
    END;

    PROCEDURE BuildOutFormat@1100285001();
    VAR
      TempValue@1100285000 : Text[1024];
    BEGIN
      "Out Format" := "In Format";

      ImportManager.FormatConvertValue(Rec,"Out Format");
      TempValue := "In Example string";
      ImportManager.FormatConvertValue(Rec,TempValue);
      "Out Example string" := TempValue;
    END;

    PROCEDURE GetFieldName@1100285002() : Text[100];
    VAR
      Field@1100285000 : Record 2000000041;
    BEGIN
      CLEAR(Field);

      IF "Field No." = 0 THEN
        EXIT;

      CASE Table OF
        Table::Header: IF Field.GET(DATABASE::"Exflow Purch. Import Header","Field No.") THEN;
        Table::Line: IF Field.GET(DATABASE::"Exflow Purch. Import Line","Field No.") THEN;
      END;

      EXIT(Field."Field Caption");
    END;

    PROCEDURE ConvertChars@1100285003();
    BEGIN
      "In Format" := CONVERTSTR("In Example string",'0123465789',']]]]]]]]]]');
    END;

    BEGIN
    END.
  }
}

