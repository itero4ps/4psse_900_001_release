OBJECT Table 12013621 Requisition Rule
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF350000;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               "Company Name" := COMPANYNAME;
             END;

    CaptionML=ENU=Requisition Rule;
    LookupPageID=Page12057093;
    DrillDownPageID=Page12057093;
  }
  FIELDS
  {
    { 1   ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   CaptionML=ENU=Company Name }
    { 2   ;   ;Code                ;Code20        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 10  ;   ;Name                ;Text30        ;CaptionML=ENU=Name }
    { 11  ;   ;Dimension 1         ;Boolean       ;CaptionML=ENU=Dimension 1 }
    { 12  ;   ;Dimension Code 1    ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                //342417
                                                                Disabled := FALSE;
                                                                //342417

                                                                //342418
                                                                "Dimension 1" := "Dimension Code 1" <> '';
                                                                //342418
                                                              END;

                                                   CaptionML=ENU=Dimension Code 1 }
    { 13  ;   ;Dimension Value 1 Filter;Code250   ;OnValidate=BEGIN
                                                                IF ("Dimension Code 1" = '') AND ("Dimension Value 1 Filter" <> '') THEN
                                                                  ERROR(EXF01);
                                                              END;

                                                   CaptionML=ENU=Dimension Value 1 Filter }
    { 21  ;   ;Dimension 2         ;Boolean       ;CaptionML=ENU=Dimension 2 }
    { 22  ;   ;Dimension Code 2    ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                //342417
                                                                Disabled := FALSE;
                                                                //342417

                                                                //342418
                                                                "Dimension 2" := "Dimension Code 2" <> '';
                                                                //342418
                                                              END;

                                                   CaptionML=ENU=Dimension Code 2 }
    { 23  ;   ;Dimension Value 2 Filter;Code250   ;CaptionML=ENU=Dimension Value 2 Filter }
    { 31  ;   ;Created by          ;Boolean       ;CaptionML=ENU=Created by }
    { 32  ;   ;Created by Filter   ;Code250       ;OnValidate=BEGIN
                                                                //342417
                                                                Disabled := FALSE;
                                                                //342417

                                                                //342418
                                                                "Created by" := "Created by Filter" <> '';
                                                                //342418
                                                              END;

                                                   CaptionML=ENU=Created by Filter }
    { 41  ;   ;Vendor              ;Boolean       ;CaptionML=ENU=Vendor }
    { 42  ;   ;Vendor Filter       ;Code250       ;OnValidate=BEGIN
                                                                //342417
                                                                Disabled := FALSE;
                                                                //342417

                                                                //342418
                                                                Vendor := "Vendor Filter" <> '';
                                                                //342418
                                                              END;

                                                   CaptionML=ENU=Vendor Filter }
    { 60  ;   ;Purchaser Tag       ;Boolean       ;CaptionML=ENU=Purchaser }
    { 61  ;   ;Purchaser Filter    ;Code250       ;OnValidate=BEGIN
                                                                //342417
                                                                Disabled := FALSE;
                                                                //342417

                                                                //342418
                                                                "Purchaser Tag" := "Purchaser Filter" <> '';
                                                                //342418
                                                              END;

                                                   CaptionML=ENU=Purchaser Filter }
    { 80  ;   ;Vendor Posting Group Tag;Boolean   ;CaptionML=ENU=Vendor Posting Group }
    { 81  ;   ;Vendor Posting Group Filter;Code250;OnValidate=BEGIN
                                                                //342417
                                                                Disabled := FALSE;
                                                                //342417

                                                                //342418
                                                                "Vendor Posting Group Tag" := "Vendor Posting Group Filter" <> '';
                                                                //342418
                                                              END;

                                                   CaptionML=ENU=Vendor Posting Group Filter }
    { 90  ;   ;Max Amount          ;Boolean       ;CaptionML=ENU=Max Amount }
    { 91  ;   ;Max Amount Value    ;Decimal       ;CaptionML=ENU=Max Amount Value }
    { 100 ;   ;Disabled            ;Boolean       ;CaptionML=ENU=Disabled }
    { 500 ;   ;Assigned Purchasing Group;Code50   ;TableRelation="EX User Group".Code WHERE (Company Name=FIELD(Company Name),
                                                                                             Blocked=CONST(No));
                                                   OnValidate=VAR
                                                                ExFlow@1100285001 : Codeunit 12013601;
                                                                TempGroupName@1100285000 : Text[100];
                                                              BEGIN
                                                                "Assigned Purchasing Group" := ExFlow.FindUserGroup("Assigned Purchasing Group",TempGroupName,FALSE);
                                                              END;

                                                   CaptionML=ENU=Assigned Purchasing Group }
    { 510 ;   ;Assigned Receipt Group;Code50      ;TableRelation="EX User Group".Code WHERE (Company Name=FIELD(Company Name),
                                                                                             Blocked=CONST(No));
                                                   OnValidate=VAR
                                                                ExFlow@1100285001 : Codeunit 12013601;
                                                                TempGroupName@1100285000 : Text[100];
                                                              BEGIN
                                                                "Assigned Receipt Group" := ExFlow.FindUserGroup("Assigned Receipt Group",TempGroupName,FALSE);
                                                              END;

                                                   CaptionML=ENU=Assigned Receipt Group }
    { 515 ;   ;Status              ;Option        ;CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=Open,Released;
                                                   OptionString=Open,Released }
    { 520 ;   ;Set Created by as First Approv;Boolean;
                                                   CaptionML=ENU=Set Created by as First Approver }
  }
  KEYS
  {
    {    ;Company Name,Code                       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EXF01@1100285001 : TextConst 'ENU=Dimension code missing!';

    PROCEDURE FindTemplate@1100285000(PurchHeader@1100285002 : Record 38;VAR RequisitionRule@1100285000 : Record 12013621;DimCodeTab@1100285005 : ARRAY [8] OF Code[20];DimValueTab@1100285006 : ARRAY [8] OF Code[20];CreatedBy@1100285008 : Code[50];NetAmount@1100285003 : Decimal) : Boolean;
    VAR
      Hit@1100285001 : Integer;
      FilterHit@1100285004 : Integer;
      TempExPriority@1100285011 : TEMPORARY Record 12013647;
    BEGIN
      TempExPriority.RESET;
      TempExPriority.DELETEALL;

      RequisitionRule.RESET;
      RequisitionRule.SETRANGE("Company Name",COMPANYNAME);

      //340379
      RequisitionRule.SETRANGE(Disabled, FALSE);
      //340379

      IF RequisitionRule.FINDSET THEN
        REPEAT
          Hit := 0;
          FilterHit := 0;

          IF NOT RequisitionRule."Dimension 1" THEN
            Hit := Hit + 1
          ELSE
            IF DimMatch(TRUE, RequisitionRule, DimCodeTab, DimValueTab) THEN
              BEGIN
                Hit := Hit + 1;
                FilterHit := FilterHit + 1;
              END;

          IF NOT RequisitionRule."Dimension 2" THEN
            Hit := Hit + 1
          ELSE
            IF DimMatch(FALSE, RequisitionRule, DimCodeTab, DimValueTab) THEN
              BEGIN
                Hit := Hit + 1;
                FilterHit := FilterHit + 1;
              END;

          IF NOT RequisitionRule."Created by" THEN
            Hit := Hit + 1
          ELSE
            IF FirstMatch(CreatedBy, RequisitionRule) THEN
              BEGIN
                Hit := Hit + 1;
                FilterHit := FilterHit + 1;
              END;

          IF NOT RequisitionRule.Vendor THEN
            Hit := Hit + 1
          ELSE
            IF VendorMatch(PurchHeader."Buy-from Vendor No.", RequisitionRule) THEN
              BEGIN
                Hit := Hit + 1;
                FilterHit := FilterHit + 1;
              END;

          IF NOT RequisitionRule."Purchaser Tag" THEN
            Hit := Hit + 1
          ELSE
            BEGIN
              IF PurchaserMatch(PurchHeader."Purchaser Code", RequisitionRule) THEN
                BEGIN
                  Hit := Hit + 1;
                  FilterHit := FilterHit + 1;
                END;
            END;

          IF NOT RequisitionRule."Vendor Posting Group Tag" THEN
            Hit := Hit + 1
          ELSE
            BEGIN
              IF VendorPostGroupMatch(PurchHeader."Pay-to Vendor No.", RequisitionRule) THEN
                BEGIN
                  Hit := Hit + 1;
                  FilterHit := FilterHit + 1;
                END;
            END;

          //348849
          IF NOT RequisitionRule."Max Amount" THEN
            Hit := Hit + 1
          ELSE
            BEGIN
              IF AmountMatch(NetAmount, RequisitionRule) THEN
                BEGIN
                  Hit := Hit + 1;
                  FilterHit := FilterHit + 1;
                END;
            END;
          //348849

          //348849
          IF Hit = 7 THEN
          //348849
            BEGIN
              TempExPriority.INIT;
              TempExPriority."Template Code" := RequisitionRule.Code;
              TempExPriority.Hits := FilterHit;
              TempExPriority.INSERT;
            END;
        UNTIL RequisitionRule.NEXT = 0;

      TempExPriority.RESET;
      TempExPriority.SETCURRENTKEY(Hits);
      TempExPriority.ASCENDING(FALSE);
      IF TempExPriority.FIND('-') THEN
        BEGIN
          RequisitionRule.GET(COMPANYNAME, TempExPriority."Template Code");
          EXIT(TRUE)
        END
      ELSE
        BEGIN
          CLEAR(RequisitionRule);
          EXIT(FALSE)
        END;
    END;

    PROCEDURE DimMatch@1100285002(First@1100285005 : Boolean;RequisitionRule@1100285003 : Record 12013621;DimCodeTab@1100285001 : ARRAY [8] OF Code[20];DimValueTab@1100285000 : ARRAY [8] OF Code[20]) : Boolean;
    VAR
      DimValue@1100285004 : Record 349;
      i@1100285006 : Integer;
      TargetValue@1100285007 : Code[20];
    BEGIN
      i := 1;
      TargetValue := '';

      REPEAT
        IF First THEN
          BEGIN
            IF DimCodeTab[i] = RequisitionRule."Dimension Code 1" THEN
              TargetValue := DimValueTab[i];
          END
        ELSE
          BEGIN
            IF DimCodeTab[i] = RequisitionRule."Dimension Code 2" THEN
              TargetValue := DimValueTab[i];
          END;

        i := i + 1;
      UNTIL (i = 9) OR (TargetValue <> '');

      IF TargetValue = '' THEN
        EXIT(FALSE);

      DimValue.RESET;

      IF First THEN
        BEGIN
          DimValue.SETRANGE("Dimension Code", RequisitionRule."Dimension Code 1");
          DimValue.SETFILTER(Code, RequisitionRule."Dimension Value 1 Filter");
        END
      ELSE
        BEGIN
          DimValue.SETRANGE("Dimension Code", RequisitionRule."Dimension Code 2");
          DimValue.SETFILTER(Code, RequisitionRule."Dimension Value 2 Filter");
        END;

      DimValue."Dimension Code" := DimCodeTab[i-1];
      DimValue.Code := TargetValue;

      IF DimValue.FIND('=') THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE FirstMatch@1100285005(LineApprover@1100285000 : Code[50];RequisitionRule@1100285001 : Record 12013621) : Boolean;
    VAR
      UserGroup@1100285003 : Record 12013606;
    BEGIN
      UserGroup.RESET;
      UserGroup.SETRANGE("Company Name",COMPANYNAME);
      UserGroup.SETFILTER(Code,RequisitionRule."Created by Filter");

      UserGroup."Company Name" := COMPANYNAME;
      UserGroup.Code := LineApprover;

      IF UserGroup.FIND('=') THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE VendorMatch@1100285006(LineVendor@1100285001 : Code[20];RequisitionRule@1100285000 : Record 12013621) : Boolean;
    VAR
      Vend@1100285002 : Record 23;
    BEGIN
      Vend.RESET;
      Vend.SETFILTER("No.", RequisitionRule."Vendor Filter");
      Vend."No." := LineVendor;

      IF Vend.FIND('=') THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE CopyRule@1100285001(FromRule@1100285001 : Record 12013621);
    VAR
      TempRequisitionRule@1100285000 : TEMPORARY Record 12013621;
    BEGIN
      TempRequisitionRule := Rec;
      INIT;
      TRANSFERFIELDS(FromRule,FALSE);

      IF TempRequisitionRule.Name <> '' THEN
        Name := TempRequisitionRule.Name;
      MODIFY;
      GET(TempRequisitionRule."Company Name",TempRequisitionRule.Code);
    END;

    PROCEDURE PurchaserMatch@1100285008(LinePurchaser@1100285001 : Code[10];RequisitionRule@1100285000 : Record 12013621) : Boolean;
    VAR
      PurchaserTab@1100285002 : Record 13;
    BEGIN
      //336021
      PurchaserTab.RESET;
      PurchaserTab.SETFILTER(Code, RequisitionRule."Purchaser Filter");
      PurchaserTab.Code := LinePurchaser;

      IF PurchaserTab.FIND('=') THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE VendorPostGroupMatch@1100285011(PayToVendorNo@1100285001 : Code[20];RequisitionRule@1100285000 : Record 12013621) : Boolean;
    VAR
      Vendor@1100285002 : Record 23;
    BEGIN
      //342620
      Vendor.RESET;
      Vendor.SETFILTER("Vendor Posting Group", RequisitionRule."Vendor Posting Group Filter");
      Vendor."No." := PayToVendorNo;
      IF Vendor.FIND('=') THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE AmountMatch@1100285003(NetAmount@1100285002 : Decimal;RequisitionRule@1100285000 : Record 12013621) : Boolean;
    BEGIN
      //348849
      IF RequisitionRule."Max Amount Value" >= NetAmount THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE LookUpExFlow@1100285004(VAR FilterText@1100285021 : Text[1024];"Column Source"@1100285014 : Code[20]) : Boolean;
    VAR
      Dim1List@1100285001 : Page 560;
      DimValue@1100285010 : Record 349;
      FirstList@1100285015 : Page 12013618;
      First@1100285016 : Record 12013606;
      VendorList@1100285017 : Page 27;
      Vend@1100285018 : Record 23;
      PurchaserList@1100285020 : Page 14;
      Purchaser@1100285019 : Record 13;
      VendorPostingGroupList@1100285024 : Page 111;
      VendorPostingGroup@1100285023 : Record 93;
    BEGIN
      CASE "Column Source" OF
        'DIM1':
          BEGIN
            CLEAR(Dim1List);
            Dim1List.LOOKUPMODE(TRUE);
            DimValue.RESET;
            DimValue.SETRANGE("Dimension Code", "Dimension Code 1");
            Dim1List.SETTABLEVIEW(DimValue);
            IF Dim1List.RUNMODAL = ACTION::LookupOK THEN
              BEGIN
                Dim1List.GETRECORD(DimValue);
                FilterText := FilterText + DimValue.Code;
                EXIT(TRUE);
              END;
        END;

        'DIM2':
          BEGIN
            CLEAR(Dim1List);
            Dim1List.LOOKUPMODE(TRUE);
            DimValue.RESET;
            DimValue.SETRANGE("Dimension Code", "Dimension Code 2");
            Dim1List.SETTABLEVIEW(DimValue);
            IF Dim1List.RUNMODAL = ACTION::LookupOK THEN
              BEGIN
                Dim1List.GETRECORD(DimValue);
                FilterText := FilterText + DimValue.Code;
                EXIT(TRUE);
              END;
        END;

        'FIRST':
           BEGIN
             CLEAR(FirstList);
             FirstList.LOOKUPMODE(TRUE);
             First.RESET;
             FirstList.SETTABLEVIEW(First);
             IF FirstList.RUNMODAL = ACTION::LookupOK THEN
               BEGIN
                 FirstList.GETRECORD(First);
                 FilterText := FilterText + First.Code;
                 EXIT(TRUE);
               END;
           END;

        'VENDOR':
           BEGIN
             CLEAR(VendorList);
             VendorList.LOOKUPMODE(TRUE);
             Vend.RESET;
             VendorList.SETTABLEVIEW(Vend);
             IF VendorList.RUNMODAL = ACTION::LookupOK THEN
               BEGIN
                 VendorList.GETRECORD(Vend);
                 FilterText := FilterText + Vend."No.";
                 EXIT(TRUE);
               END;
           END;

        //336021.Begin
        'PURCHASER':
           BEGIN
             CLEAR(PurchaserList);
             PurchaserList.LOOKUPMODE(TRUE);
             Purchaser.RESET;
             PurchaserList.SETTABLEVIEW(Purchaser);
             IF PurchaserList.RUNMODAL = ACTION::LookupOK THEN
               BEGIN
                 PurchaserList.GETRECORD(Purchaser);
                 FilterText := FilterText + Purchaser.Code;
                 EXIT(TRUE);
               END
           END;
        //336021.End

        'VENDPOSTGROUP':
           BEGIN
             CLEAR(VendorPostingGroupList);
             VendorPostingGroupList.LOOKUPMODE(TRUE);
             VendorPostingGroup.RESET;
             VendorPostingGroupList.SETTABLEVIEW(VendorPostingGroup);
             IF VendorPostingGroupList.RUNMODAL = ACTION::LookupOK THEN
               BEGIN
                 VendorPostingGroupList.GETRECORD(VendorPostingGroup);
                 FilterText := FilterText + VendorPostingGroup.Code;
                 EXIT(TRUE);
               END
           END;
        //342620
      END;

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

