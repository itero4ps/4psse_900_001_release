OBJECT Table 12013627 EX Posted Document Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF400006,2016;
  }
  PROPERTIES
  {
    Permissions=TableData 12013609=rimd;
    OnDelete=VAR
               ExPostedDocLineApp@1100285001 : Record 12013628;
               ExPostedDocLineDim@1100285000 : Record 12013629;
             BEGIN
               ExPostedDocLineApp.SETCURRENTKEY("Document Line ID");
               ExPostedDocLineApp.SETRANGE("Document Line ID", ID);
               ExPostedDocLineApp.DELETEALL;

               ExPostedDocLineDim.SETCURRENTKEY("Document Line ID");
               ExPostedDocLineDim.SETRANGE("Document Line ID", ID);
               ExPostedDocLineDim.DELETEALL;
             END;

    CaptionML=ENU=Posted Document Line;
  }
  FIELDS
  {
    { 2   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=,,,,,,Posted Invoice,Posted Credit Memo,Archived Requisition;
                                                   OptionString=,,,,,,Posted Invoice,Posted Credit Memo,Archived Requisition }
    { 3   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No. }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 10  ;   ;Flowstatus          ;Option        ;CaptionML=ENU=ExFlow Status;
                                                   OptionCaptionML=ENU='',Active,Approved,Rejected;
                                                   OptionString='',Active,Approved,Rejected }
    { 11  ;   ;Line Type           ;Option        ;CaptionML=ENU=Line Type;
                                                   OptionCaptionML=ENU=" ,G/L Account,Item,,Fixed Asset,Charge (Item)";
                                                   OptionString=[ ,G/L Account,Item,,Fixed Asset,Charge (Item)] }
    { 12  ;   ;No.                 ;Code20        ;TableRelation=IF (Line Type=CONST(" ")) "Standard Text"
                                                                 ELSE IF (Line Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Line Type=CONST(Item)) Item
                                                                 ELSE IF (Line Type=CONST(Fixed Asset)) "Fixed Asset"
                                                                 ELSE IF (Line Type=CONST("Charge (Item)")) "Item Charge";
                                                   ValidateTableRelation=Yes;
                                                   TestTableRelation=Yes;
                                                   CaptionML=ENU=No. }
    { 13  ;   ;Description         ;Text100       ;CaptionML=ENU=Description }
    { 14  ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity;
                                                   DecimalPlaces=0:5 }
    { 15  ;   ;Direct Unit Cost    ;Decimal       ;CaptionML=ENU=Direct Unit Cost;
                                                   DecimalPlaces=2:5 }
    { 16  ;   ;Line Discount %     ;Decimal       ;InitValue=0;
                                                   CaptionML=ENU=Line Discount %;
                                                   DecimalPlaces=2:5 }
    { 18  ;   ;Line Difference     ;Boolean       ;InitValue=No;
                                                   CaptionML=ENU=Line Difference }
    { 20  ;   ;Job No.             ;Code20        ;TableRelation=Table167;
                                                   CaptionML=ENU=Job No. }
    { 21  ;   ;Amount              ;Decimal       ;CaptionML=ENU=Amount;
                                                   DecimalPlaces=2:5 }
    { 25  ;   ;Job Task No.        ;Code20        ;TableRelation=Table1001.Field2 WHERE (Field1=FIELD(Job No.));
                                                   CaptionML=ENU=Job Task No. }
    { 30  ;   ;New Line            ;Boolean       ;CaptionML=ENU=New Line }
    { 31  ;   ;Finish Comment      ;Text120       ;CaptionML=ENU=Comments }
    { 32  ;   ;Finish Code         ;Option        ;CaptionML=ENU=Finish Code;
                                                   OptionCaptionML=ENU=Complete Invoice line,Complete Invoice line and update,Complete all Invoice lines,Complete all Invoice lines and update,Hold line,Hold all lines;
                                                   OptionString=Complete Invoice line,Complete Invoice line and update,Complete all Invoice lines,Complete all Invoice lines and update,Hold line,Hold all lines }
    { 33  ;   ;Unit of Measure     ;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(No.));
                                                   CaptionML=ENU=Unit of Measure }
    { 100 ;   ;Periodic Template Code;Code10      ;TableRelation="Deferral Template";
                                                   CaptionML=ENU=Periodic Template Code }
    { 101 ;   ;Periodic Starting Date;Date        ;CaptionML=ENU=Periodic Starting Date }
    { 110 ;   ;Base Starting Date  ;Date          ;CaptionML=ENU=Base Starting Date }
    { 111 ;   ;Base Temporary Account No.;Text20  ;InitValue=<Empty>;
                                                   TableRelation="G/L Account";
                                                   CaptionML=ENU=Base Temporary Account No. }
    { 112 ;   ;Base No. of Periods ;Integer       ;InitValue=0;
                                                   CaptionML=ENU=Base No. of Periods }
    { 113 ;   ;Document ID         ;BigInteger    ;CaptionML=ENU=Document ID }
    { 114 ;   ;ID                  ;BigInteger    ;CaptionML=ENU=ID }
    { 115 ;   ;Connected Line ID   ;BigInteger    ;CaptionML=ENU=Connected Line ID }
    { 116 ;   ;Order No.           ;Code20        ;OnLookup=VAR
                                                              PurchHeader@1100285000 : Record 38;
                                                            BEGIN
                                                              IF "Order No." <> '' THEN
                                                                IF PurchHeader.GET(PurchHeader."Document Type"::Order,"Order No.") THEN
                                                                  PAGE.RUN(0,PurchHeader)
                                                                ELSE
                                                                  IF PurchHeader.GET(PurchHeader."Document Type"::"Return Order","Order No.") THEN
                                                                    PAGE.RUN(0,PurchHeader);
                                                            END;

                                                   CaptionML=ENU=Order No. }
    { 117 ;   ;Order Unit Cost     ;Decimal       ;CaptionML=ENU=Order Unit Cost;
                                                   DecimalPlaces=2:5 }
    { 118 ;   ;Order Line No.      ;Integer       ;OnLookup=VAR
                                                              PurchLine2@1100285000 : Record 39;
                                                            BEGIN
                                                            END;

                                                   CaptionML=ENU=Order Line No.;
                                                   Editable=No }
    { 500 ;   ;Requisition No.     ;Code20        ;CaptionML=ENU=Requisition No. }
    { 501 ;   ;Error Message       ;Text250       ;CaptionML=ENU=Error Message }
    { 600 ;   ;Changed By          ;Code50        ;CaptionML=ENU=Changed By;
                                                   Editable=No }
    { 601 ;   ;Change Date         ;DateTime      ;CaptionML=ENU=Change Date;
                                                   Editable=No }
    { 602 ;   ;Last Change Date Approval;DateTime ;FieldClass=FlowField;
                                                   CalcFormula=Max("EX Posted Document Line App"."Change Date" WHERE (Document Line ID=FIELD(ID)));
                                                   CaptionML=ENU=Last Changed Date Approval;
                                                   Editable=No }
    { 603 ;   ;Last Change Date Dimension;DateTime;FieldClass=FlowField;
                                                   CalcFormula=Max("EX Posted Document Line Dim"."Change Date" WHERE (Document Line ID=FIELD(ID)));
                                                   CaptionML=ENU=Last Changed Date Dimension;
                                                   Editable=No }
    { 650 ;   ;Created By          ;Code50        ;CaptionML=ENU=Created By;
                                                   Editable=No }
    { 651 ;   ;Created Date        ;DateTime      ;CaptionML=ENU=Created Date;
                                                   Editable=No }
    { 12013611;;Invoice to Customer;Boolean       ;CaptionML=ENU=Invoice to Customer;
                                                   Description=ExFlow }
    { 12013612;;Invoicing Amount   ;Decimal       ;CaptionML=ENU=Invoicing Amount;
                                                   Description=ExFlow }
    { 12013613;;Start Date for Invoicing;Date     ;CaptionML=ENU=Start Date for Invoicing;
                                                   Description=ExFlow }
    { 12013614;;Invoicing Code     ;Code10        ;TableRelation="EX Invoicing Code";
                                                   CaptionML=ENU=Invoicing Code;
                                                   Description=ExFlow }
    { 12013659;;Applied Template Rule;Code20      ;TableRelation="Template Rule".Code;
                                                   CaptionML=ENU=Applied Template Rule;
                                                   Description=ExFlow;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Document No.                             }
    {    ;Document No.,Line No.                    }
    {    ;Flowstatus,Document Type                 }
    {    ;Document Type,Document No.,Line No.      }
    {    ;Document ID                              }
    {    ;Document ID,Flowstatus                   }
    {    ;Connected Line ID                        }
    {    ;Order No.                               ;SumIndexFields=Order Unit Cost }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ExFlow@1100285002 : Codeunit 12013601;
      GLSetupShortcutDimCode@1100285024 : ARRAY [8] OF Code[20];
      HasGotGLSetup@1100285022 : Boolean;
      DoByPassControl@1100285025 : Boolean;
      Number@1100285001 : Integer;

    PROCEDURE Approved@1100285002(PostedDocLine@1100285000 : Record 12013627) : Boolean;
    VAR
      PostedDocLineApp@1100285001 : Record 12013628;
    BEGIN
      PostedDocLineApp.RESET;
      PostedDocLineApp.SETCURRENTKEY("Document Line ID","Approver Order");
      PostedDocLineApp.SETRANGE("Document Line ID",PostedDocLine.ID);
      PostedDocLineApp.SETFILTER("Approved By",'<>%1','');
      IF PostedDocLineApp.FINDFIRST THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE ShowShortcutDimCode@1100285012(VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20]);
    VAR
      PostedExDocLineDim@1100285000 : Record 12013629;
      i@1100285001 : Integer;
    BEGIN
      GetGLSetup;

      PostedExDocLineDim.RESET;
      PostedExDocLineDim.SETCURRENTKEY("Document Line ID");
      PostedExDocLineDim.SETRANGE("Document Line ID", ID);
      FOR i := 1 TO 8 DO BEGIN
        ShortcutDimCode[i] := '';
        IF GLSetupShortcutDimCode[i] <> '' THEN BEGIN
          PostedExDocLineDim.SETRANGE("Dimension Code", GLSetupShortcutDimCode[i]);
          IF PostedExDocLineDim.FINDFIRST THEN
            ShortcutDimCode[i] := PostedExDocLineDim."Dimension Value";
        END;
      END;
    END;

    PROCEDURE LookupShortcutDimCode@30(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    VAR
      DimMgt@1100285001 : Codeunit 408;
      TempShortcutDimCode@1100285000 : Code[20];
    BEGIN
      TempShortcutDimCode := ShortcutDimCode;
      DimMgt.LookupDimValueCode(FieldNumber,ShortcutDimCode);
    END;

    LOCAL PROCEDURE GetGLSetup@52();
    VAR
      GLSetup@1000 : Record 98;
    BEGIN
      IF NOT HasGotGLSetup THEN BEGIN
        GLSetup.GET;
        GLSetupShortcutDimCode[1] := GLSetup."Shortcut Dimension 1 Code";
        GLSetupShortcutDimCode[2] := GLSetup."Shortcut Dimension 2 Code";
        GLSetupShortcutDimCode[3] := GLSetup."Shortcut Dimension 3 Code";
        GLSetupShortcutDimCode[4] := GLSetup."Shortcut Dimension 4 Code";
        GLSetupShortcutDimCode[5] := GLSetup."Shortcut Dimension 5 Code";
        GLSetupShortcutDimCode[6] := GLSetup."Shortcut Dimension 6 Code";
        GLSetupShortcutDimCode[7] := GLSetup."Shortcut Dimension 7 Code";
        GLSetupShortcutDimCode[8] := GLSetup."Shortcut Dimension 8 Code";
        HasGotGLSetup := TRUE;
      END;
    END;

    PROCEDURE ShowCard@1100285014();
    BEGIN
      Card;
    END;

    LOCAL PROCEDURE Card@1100285004();
    VAR
      PurchInvHeader@1100285001 : Record 122;
      PurchCrMemoHeader@1100285002 : Record 124;
    BEGIN
      CASE "Document Type" OF
        "Document Type"::"Posted Invoice" :
          BEGIN
            PurchInvHeader.SETRANGE("No.", "Document No.");
            PAGE.RUNMODAL(138,PurchInvHeader);
          END;
        "Document Type"::"Posted Credit Memo" :
          BEGIN
            PurchCrMemoHeader.SETRANGE("No.", "Document No.");
            PAGE.RUNMODAL(140,PurchCrMemoHeader);
          END;
      END;
    END;

    PROCEDURE RetrieveRecord@1100285013(VAR PostedExDocLine@1100285004 : Record 12013627;DocType@1100285001 : Integer;DocNo@1100285002 : Code[20];LineNo@1100285003 : Integer) : Boolean;
    BEGIN
      WITH PostedExDocLine DO BEGIN
        RESET;
        CLEAR(PostedExDocLine);
        SETCURRENTKEY("Document No.");
        SETRANGE("Document Type", DocType);
        SETRANGE("Document No.", DocNo);
        SETRANGE("Line No.", LineNo);
        EXIT(FINDFIRST);
      END;
    END;

    PROCEDURE GetName@1100285015() : Text[50];
    VAR
      GLAccount@1100285000 : Record 15;
      Item@1100285001 : Record 27;
      FixedAsset@1100285002 : Record 5600;
      ItemCharge@1100285003 : Record 5800;
    BEGIN
      IF "No." = '' THEN
        EXIT('');

      CASE "Line Type" OF
        "Line Type"::"G/L Account":
          BEGIN
            IF GLAccount.GET("No.") THEN
              EXIT(GLAccount.Name);
          END;
        "Line Type"::Item:
          BEGIN
            IF Item.GET("No.") THEN
              EXIT(Item.Description);
          END;
        "Line Type"::"Fixed Asset":
          BEGIN
            IF FixedAsset.GET("No.") THEN
              EXIT(FixedAsset.Description);
          END;
        "Line Type"::"Charge (Item)":
          BEGIN
            IF ItemCharge.GET("No.") THEN
              EXIT(ItemCharge.Description);
          END;
      END;

      EXIT('');
    END;

    PROCEDURE ShowDim@1100285025();
    VAR
      PostedDocLineDim@1100285004 : Record 12013629;
    BEGIN
      PostedDocLineDim.RESET;
      PostedDocLineDim.SETCURRENTKEY("Document Line ID");
      PostedDocLineDim.SETRANGE("Document Line ID",ID);
      PAGE.RUN(12057123,PostedDocLineDim);
    END;

    PROCEDURE ShowNonDimCodes@1100285028();
    VAR
      PostedExDocLineDim@1100285000 : Record 12013629;
      ExReqToQuote@1100285001 : Codeunit 12013614;
    BEGIN
      PostedExDocLineDim.RESET;
      PostedExDocLineDim.SETCURRENTKEY("Document Line ID");
      PostedExDocLineDim.SETRANGE("Document Line ID", ID);
      // fields not updated from the web
      PostedExDocLineDim.SETRANGE("Dimension Code", 'PERCODE');
      IF PostedExDocLineDim.FINDFIRST THEN
        "Periodic Template Code" := PostedExDocLineDim."Dimension Value";

      PostedExDocLineDim.SETRANGE("Dimension Code", 'PERDATE');
      IF PostedExDocLineDim.FINDFIRST THEN
        "Periodic Starting Date" := ExReqToQuote.DateField(PostedExDocLineDim."Dimension Value");

      PostedExDocLineDim.SETRANGE("Dimension Code", 'JOB');
      IF PostedExDocLineDim.FINDFIRST THEN
        "Job No." := PostedExDocLineDim."Dimension Value";

      PostedExDocLineDim.SETRANGE("Dimension Code", 'JOBTASK');
      IF PostedExDocLineDim.FINDFIRST THEN
        "Job Task No." := PostedExDocLineDim."Dimension Value";
    END;

    PROCEDURE GetInvoiceToCustValue@1100285027() : Boolean;
    VAR
      PostedExDocLineDim@1100285001 : Record 12013629;
      TempValue@1100285000 : Boolean;
    BEGIN
      PostedExDocLineDim.RESET;
      PostedExDocLineDim.SETCURRENTKEY("Document Line ID");
      PostedExDocLineDim.SETRANGE("Document Line ID", ID);
      PostedExDocLineDim.SETRANGE("Dimension Code", 'CUSTOMERINV');
      IF PostedExDocLineDim.FINDFIRST THEN
        IF EVALUATE(TempValue,PostedExDocLineDim."Dimension Value") THEN
          EXIT(TempValue);

      EXIT(FALSE);
    END;

    PROCEDURE GetInvoicingAmtValue@1100285029() : Decimal;
    VAR
      PostedExDocLineDim@1100285000 : Record 12013629;
      TempValue@1100285001 : Decimal;
    BEGIN
      PostedExDocLineDim.RESET;
      PostedExDocLineDim.SETCURRENTKEY("Document Line ID");
      PostedExDocLineDim.SETRANGE("Document Line ID", ID);
      PostedExDocLineDim.SETRANGE("Dimension Code", 'AMOUNTINV');
      IF PostedExDocLineDim.FINDFIRST THEN
        IF EVALUATE(TempValue,PostedExDocLineDim."Dimension Value") THEN
          EXIT(TempValue);

      EXIT(0);
    END;

    PROCEDURE GetStartDateForInvValue@1100285030() : Date;
    VAR
      PostedExDocLineDim@1100285001 : Record 12013629;
      TempValue@1100285000 : Date;
    BEGIN
      PostedExDocLineDim.RESET;
      PostedExDocLineDim.SETCURRENTKEY("Document Line ID");
      PostedExDocLineDim.SETRANGE("Document Line ID", ID);
      PostedExDocLineDim.SETRANGE("Dimension Code", 'INVDATE');
      IF PostedExDocLineDim.FINDFIRST THEN
        IF EVALUATE(TempValue,PostedExDocLineDim."Dimension Value") THEN
          EXIT(TempValue);

      EXIT(0D);
    END;

    PROCEDURE GetInvCodeValue@1100285031() : Code[20];
    VAR
      PostedExDocLineDim@1100285000 : Record 12013629;
    BEGIN
      PostedExDocLineDim.RESET;
      PostedExDocLineDim.SETCURRENTKEY("Document Line ID");
      PostedExDocLineDim.SETRANGE("Document Line ID", ID);
      PostedExDocLineDim.SETRANGE("Dimension Code", 'INV_CODE');
      IF PostedExDocLineDim.FINDFIRST THEN
        EXIT(PostedExDocLineDim."Dimension Value");

      EXIT('');
    END;

    PROCEDURE GetCustomerValue@1100285033() : Code[20];
    VAR
      PostedExDocLineDim@1100285001 : Record 12013629;
    BEGIN
      PostedExDocLineDim.RESET;
      PostedExDocLineDim.SETCURRENTKEY("Document Line ID");
      PostedExDocLineDim.SETRANGE("Document Line ID", ID);
      PostedExDocLineDim.SETRANGE("Dimension Code", 'CUSTOMER');
      IF PostedExDocLineDim.FINDFIRST THEN
        EXIT(PostedExDocLineDim."Dimension Value");

      EXIT('');
    END;

    BEGIN
    END.
  }
}

