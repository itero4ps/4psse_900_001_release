OBJECT Table 12013630 Web Company ID
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF400005;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               ConvertCompToWebID;

               RecRef.GETTABLE(Rec);
               WebSetting.TableChange(RecRef);

               IF "Web enabled" THEN
                 SetWebID;
             END;

    OnModify=BEGIN
               RecRef.GETTABLE(Rec);
               WebSetting.TableChange(RecRef);

               IF "Web enabled" THEN
                 SetWebID;
             END;

    OnDelete=BEGIN
               RecRef.GETTABLE(Rec);
               WebSetting.TableChange(RecRef);
             END;

    CaptionML=ENU=Web Company ID;
  }
  FIELDS
  {
    { 1   ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   CaptionML=ENU=Company Name }
    { 5   ;   ;Web Company ID      ;BigInteger    ;CaptionML=ENU=Web Company ID;
                                                   Description=Used to make web IDs unique;
                                                   Editable=No }
    { 10  ;   ;Web enabled         ;Boolean       ;CaptionML=ENU=Company is enabled for web }
    { 50  ;   ;Test Mode (web)     ;Boolean       ;OnValidate=VAR
                                                                _WebCompID@1100285000 : Record 12013630;
                                                              BEGIN
                                                                _WebCompID.SETFILTER("Company Name", '<>%1', "Company Name");
                                                                _WebCompID.MODIFYALL("Test Mode (web)", "Test Mode (web)");
                                                              END;

                                                   CaptionML=ENU=Test mode (web) }
  }
  KEYS
  {
    {    ;Company Name                            ;Clustered=Yes }
    {    ;Web Company ID                           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      WebSetting@1100285001 : Record 12057071;
      RecRef@1100285000 : RecordRef;
      EXF001@1100285002 : TextConst 'ENU=Company %1 now enabled for ExFlow Web';
      EXF002@1100285003 : TextConst 'ENU=Company %1 now disabled for ExFlow Web';

    LOCAL PROCEDURE ConvertCompToWebID@1100285000();
    VAR
      WebCompID2@1100285000 : Record 12013630;
      Prefix@1100285002 : BigInteger;
    BEGIN
      IF "Company Name" = '' THEN
        EXIT;

      Prefix := 1000000000;
      WebCompID2.RESET;
      WebCompID2.SETCURRENTKEY("Web Company ID");
      IF NOT WebCompID2.FINDLAST THEN
        "Web Company ID" := Prefix
      ELSE
        "Web Company ID" := WebCompID2."Web Company ID" + Prefix;
    END;

    PROCEDURE WebEnable@1100285001(CompName@1100285001 : Text[30]);
    VAR
      WebCompID@1100285000 : Record 12013630;
      WebCompID2@1100285002 : Record 12013630;
    BEGIN
      IF NOT WebCompID.GET(CompName) THEN BEGIN
        WebCompID.LOCKTABLE;
        WebCompID.INIT;
        WebCompID."Company Name" := CompName;
        WebCompID."Web enabled" := TRUE;
        WebCompID2.SETRANGE("Test Mode (web)", TRUE);
        WebCompID."Test Mode (web)" := WebCompID2.FINDFIRST;
        WebCompID.INSERT(TRUE);
      END
      ELSE BEGIN
        WebCompID."Web enabled" := TRUE;
        WebCompID.MODIFY(TRUE);
      END;

      IF GUIALLOWED THEN
        MESSAGE(STRSUBSTNO(EXF001,CompName));
    END;

    PROCEDURE WebDisable@1100285003(CompName@1100285001 : Text[30]);
    VAR
      WebCompID@1100285000 : Record 12013630;
    BEGIN
      IF WebCompID.GET(CompName) THEN BEGIN
        WebCompID."Web enabled" := FALSE;
        WebCompID.MODIFY(TRUE);
      END;

      IF GUIALLOWED THEN
        MESSAGE(STRSUBSTNO(EXF002,CompName));
    END;

    LOCAL PROCEDURE SetWebID@1100285002();
    VAR
      ExDoc@1100285000 : Record 12013608;
      ExDoc2@1100285001 : Record 12013608;
      PostedExDoc@1100285003 : Record 12013626;
      PostedExDoc2@1100285002 : Record 12013626;
      TempExDoc@1100285004 : TEMPORARY Record 12013608;
      Company@1100285007 : Record 2000000006;
      DuplicateIDFound@1100285005 : Boolean;
      TempCompID@1100285006 : BigInteger;
    BEGIN
      // Web company ID should only be set when upgrading from an 4.0 to a later version
      // when upgrading from 350 then web id should be equal to ID, this because of attachments
      Company.RESET;
      Company.FINDSET;
      REPEAT
        ExDoc.RESET;
        ExDoc.CHANGECOMPANY(Company.Name);
        IF ExDoc.FINDSET THEN
          REPEAT
            IF TempExDoc.GET(ExDoc.ID) THEN
              DuplicateIDFound := TRUE;
          UNTIL (ExDoc.NEXT = 0) OR DuplicateIDFound;

        PostedExDoc.RESET;
        PostedExDoc.CHANGECOMPANY(Company.Name);
        IF PostedExDoc.FINDSET THEN
          REPEAT
            IF TempExDoc.GET(PostedExDoc.ID) THEN
              DuplicateIDFound := TRUE;
          UNTIL (PostedExDoc.NEXT = 0) OR DuplicateIDFound;
      UNTIL (Company.NEXT = 0) OR DuplicateIDFound;

      IF DuplicateIDFound THEN
        TempCompID := "Web Company ID"
      ELSE
        TempCompID := 0;

      ExDoc.RESET;
      ExDoc.CHANGECOMPANY(COMPANYNAME);
      ExDoc.SETCURRENTKEY("Web ID");
      ExDoc.SETRANGE("Web ID", 0);
      IF ExDoc.FINDSET(TRUE,TRUE) THEN
        REPEAT
          ExDoc2.GET(ExDoc.ID);
          ExDoc2."Web Company ID" := TempCompID;
          ExDoc2."Web ID" := ExDoc2.ID + ExDoc2."Web Company ID";
          ExDoc2.MODIFY;
        UNTIL ExDoc.NEXT = 0;

      PostedExDoc.RESET;
      PostedExDoc.CHANGECOMPANY(COMPANYNAME);
      PostedExDoc.SETCURRENTKEY("Web ID");
      PostedExDoc.SETRANGE("Web ID", 0);
      IF PostedExDoc.FINDSET(TRUE,TRUE) THEN
        REPEAT
          PostedExDoc2.GET(PostedExDoc.ID);
          PostedExDoc2."Web Company ID" := TempCompID;
          PostedExDoc2."Web ID" := PostedExDoc2.ID + PostedExDoc2."Web Company ID";
          PostedExDoc2.MODIFY;
        UNTIL PostedExDoc.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

