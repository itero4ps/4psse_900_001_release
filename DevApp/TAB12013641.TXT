OBJECT Table 12013641 EX User Company
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF350000;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               "Company Name" := COMPANYNAME;
               AppSetup.GET(COMPANYNAME);

               IF NOT exUser.GET("User ID") THEN
                 BEGIN
                   exUser."User ID" := "User ID";
                   exUser."Password Web" := "Password Web";
                   exUser.INSERT;
                 END;

               CheckForInvalidChars("User ID");
             END;

    OnDelete=VAR
               ExUserMgt@1100285000 : Codeunit 12013596;
             BEGIN
               ExUserMgt.DeleteUser(Rec);
             END;

    OnRename=BEGIN
               ERROR(EXF02);
             END;

    CaptionML=ENU=ExFlow User Setup;
    LookupPageID=Page12013628;
    DrillDownPageID=Page12013628;
  }
  FIELDS
  {
    { 1   ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   CaptionML=ENU=Company Name;
                                                   NotBlank=Yes }
    { 2   ;   ;User ID             ;Code50        ;TableRelation="EX User Company"."User ID";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=User ID;
                                                   NotBlank=Yes;
                                                   Description=Tablerelation for lookups in reports }
    { 10  ;   ;E-mail ExFlow       ;Text128       ;OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO("E-mail ExFlow") THEN
                                                                  IF NOT UserGroup.GET(COMPANYNAME,"User ID") THEN
                                                                    BEGIN
                                                                      UserGroup.INIT;
                                                                      UserGroup."Company Name" := COMPANYNAME;
                                                                      UserGroup.Code := "User ID";
                                                                      UserGroup."Coding Rule" := "Coding Rule ID";
                                                                      UserGroup.VALIDATE(Name, "User Name");
                                                                      UserGroup.INSERT;

                                                                      UserGroupLine.INIT;
                                                                      UserGroupLine."Company Name" := COMPANYNAME;
                                                                      UserGroupLine.Code := UserGroup.Code;
                                                                      UserGroupLine."Line No." := 10000;
                                                                      UserGroupLine."User ID" := "User ID";
                                                                      UserGroupLine."Group Name" := "User Name";

                                                                      IF UserGroupLine."Group Name" = '' THEN
                                                                        UserGroupLine."Group Name" := "User ID";

                                                                      UserGroupLine.INSERT;
                                                                    END;
                                                              END;

                                                   CaptionML=ENU=E-mail }
    { 11  ;   ;Mobil Phone No      ;Text80        ;CaptionML=ENU=Mobil Phone No. }
    { 12  ;   ;User Level          ;Option        ;CaptionML=ENU=Web User Rights Level;
                                                   OptionCaptionML=ENU=Super User,Power User,User;
                                                   OptionString=SuperUser,PowerUser,User }
    { 16  ;   ;Coding Rule ID      ;Integer       ;TableRelation="EX Coding Rule".ID;
                                                   OnValidate=VAR
                                                                CodingRule@1100285000 : Record 12013660;
                                                              BEGIN
                                                                IF (CurrFieldNo = FIELDNO("Coding Rule ID")) AND ("Coding Rule ID" <> 0) THEN BEGIN
                                                                  CodingRule.SETRANGE("Company Name", "Company Name");
                                                                  CodingRule.SETRANGE(ID, "Coding Rule ID");
                                                                  CodingRule.SETRANGE("Is Subset", TRUE);
                                                                  CodingRule.FINDFIRST;

                                                                  "Coding Rule Name" := CodingRule.Name;
                                                                END
                                                                ELSE
                                                                  IF CodingRule.GET("Company Name","Coding Rule ID") THEN
                                                                    "Coding Rule Name" := CodingRule.Name
                                                                  ELSE
                                                                    "Coding Rule Name" := '';

                                                                IF CurrFieldNo = FIELDNO("Coding Rule ID") THEN
                                                                  IF UserGroup.GET(COMPANYNAME,"User ID") THEN
                                                                    BEGIN
                                                                      UserGroup."Coding Rule" := "Coding Rule ID";
                                                                      UserGroup.MODIFY;
                                                                    END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Coding Rule ID }
    { 17  ;   ;User Name           ;Text50        ;OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO("User Name") THEN
                                                                  IF UserGroup.GET(COMPANYNAME,"User ID") THEN
                                                                    BEGIN
                                                                      UserGroup.Name := "User Name";
                                                                      UserGroup.MODIFY;
                                                                    END;
                                                              END;

                                                   CaptionML=ENU=User Name }
    { 18  ;   ;Blocked             ;Boolean       ;CaptionML=ENU=Blocked }
    { 20  ;   ;Password Web        ;Text10        ;OnValidate=BEGIN
                                                                IF COPYSTR("Password Web",1,1) = '~' THEN
                                                                  ERROR(EXF04);

                                                                AppSetup.GET(COMPANYNAME);

                                                                IF exUser.GET("User ID") THEN BEGIN
                                                                  IF "Password Web" <> '********' THEN BEGIN
                                                                    exUser."Password Web" := "Password Web";
                                                                    exUser.MODIFY;
                                                                  END;
                                                                END ELSE BEGIN
                                                                  exUser.INIT;
                                                                  exUser."User ID" := "User ID";
                                                                  exUser."Password Web" := "Password Web";
                                                                  exUser.INSERT;
                                                                END;

                                                                "Password Web" := '********';
                                                              END;

                                                   CaptionML=ENU=Password Web }
    { 21  ;   ;Dimension 1         ;Text250       ;CaptionML=ENU=Dimension 1 Filter }
    { 22  ;   ;Superior            ;Boolean       ;CaptionML=ENU=Superior }
    { 23  ;   ;Approve Order       ;Boolean       ;CaptionML=ENU=Approve Order;
                                                   Description=ExFlow Order }
    { 25  ;   ;Coding Rule Name    ;Text50        ;CaptionML=ENU=Coding Rule Name;
                                                   Editable=No }
    { 100 ;   ;Location Code (Requisition);Code10 ;TableRelation=Location WHERE (Use As In-Transit=CONST(No));
                                                   CaptionML=ENU=Location Code (Requisition) }
    { 500 ;   ;Replace with User   ;Code50        ;TableRelation="EX User Company"."User ID" WHERE (Company Name=FIELD(Company Name));
                                                   CaptionML=ENU=Replace with User }
    { 550 ;   ;Replaced with User  ;Code50        ;TableRelation="EX User Company"."User ID" WHERE (Company Name=FIELD(Company Name));
                                                   CaptionML=ENU=Replaced with User;
                                                   Editable=No }
    { 12013600;;All-Company Access to Ex Docs;Boolean;
                                                   CaptionML=ENU=All-Company Access to ExFlow Documents }
  }
  KEYS
  {
    {    ;Company Name,User ID                    ;Clustered=Yes }
    {    ;User ID                                  }
    {    ;User Name                                }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;User ID,User Name                        }
  }
  CODE
  {
    VAR
      exUser@1100285004 : Record 12013640;
      UserGroup@1100285002 : Record 12013606;
      UserGroupLine@1100285001 : Record 12013607;
      UserComp@1100285000 : Record 12013641;
      EXF02@1100285006 : TextConst 'ENU=Rename not allowed!';
      AppSetup@1100285008 : Record 12013601;
      EXF04@1100285009 : TextConst 'ENU=Password cannot start with "~" sign';
      EXF05@1100285010 : TextConst 'ENU=Invalid character %1 in the user ID!';

    PROCEDURE CreateApprovalGroups@1100285000(OneUser@1100285000 : Code[50]);
    BEGIN
      UserComp.RESET;
      UserComp.SETRANGE("Company Name",COMPANYNAME);
      UserComp.SETFILTER("E-mail ExFlow",'<>%1','');

      IF OneUser <> '' THEN
        UserComp.SETRANGE("User ID", OneUser);

      IF UserComp.FIND('-') THEN
        REPEAT
          IF NOT UserGroup.GET(COMPANYNAME,UserComp."User ID") THEN
            BEGIN
              UserGroup.INIT;
              UserGroup."Company Name" := COMPANYNAME;
              UserGroup.Code := UserComp."User ID";
              UserGroup.INSERT;

              UserGroupLine.INIT;
              UserGroupLine."Company Name" := COMPANYNAME;
              UserGroupLine.Code := UserGroup.Code;
              UserGroupLine."Line No." := 10000;
              UserGroupLine."User ID" := UserComp."User ID";
              UserGroupLine."Group Name" := UserComp."User Name";

              IF UserGroupLine."Group Name" = '' THEN
                UserGroupLine."Group Name" := UserComp."User ID";

              UserGroupLine.INSERT;
            END;

          UserGroup.VALIDATE(Name, UserComp."User Name");
          UserGroup."Coding Rule" := UserComp."Coding Rule ID";
          UserGroup.MODIFY(FALSE);
        UNTIL UserComp.NEXT = 0;
    END;

    PROCEDURE FindDimension@1100285004() : Integer;
    VAR
      GLSetup@1100285000 : Record 98;
      ExFlowSetup@1100285001 : Record 12013601;
    BEGIN
      GLSetup.GET;
      ExFlowSetup.GET(COMPANYNAME);
      ExFlowSetup.TESTFIELD("User Dimension Code");

      CASE ExFlowSetup."User Dimension Code" OF
        GLSetup."Shortcut Dimension 1 Code": EXIT(1);
        GLSetup."Shortcut Dimension 2 Code": EXIT(2);
        GLSetup."Shortcut Dimension 3 Code": EXIT(3);
        GLSetup."Shortcut Dimension 4 Code": EXIT(4);
        GLSetup."Shortcut Dimension 5 Code": EXIT(5);
        GLSetup."Shortcut Dimension 6 Code": EXIT(6);
        GLSetup."Shortcut Dimension 7 Code": EXIT(7);
        GLSetup."Shortcut Dimension 8 Code": EXIT(8);
      END;
    END;

    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    VAR
      DimMgt@1100285000 : Codeunit 408;
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE LookupShortcutDimCode@28(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    VAR
      DimMgt@1100285000 : Codeunit 408;
    BEGIN
      DimMgt.LookupDimValueCode(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE CheckForInvalidChars@1100285001(_UserID@1100285000 : Code[50]);
    VAR
      i@1100285001 : Integer;
    BEGIN
      FOR i := 1 TO STRLEN(_UserID) DO
        IF _UserID[i] IN ['/','[',']',':',';','|','=',',','+','*','?','<','>',''''] THEN
          ERROR(STRSUBSTNO(EXF05,_UserID[i]));
    END;

    BEGIN
    END.
  }
}

