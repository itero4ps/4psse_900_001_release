OBJECT Table 12013669 Import Mapping Table
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF348000;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnDelete=VAR
               DefaultMappingDimension@1100285000 : Record 12013670;
             BEGIN
               DefaultMappingDimension.RESET;
               DefaultMappingDimension.SETRANGE("Import Source Type", "Import Source Type");
               DefaultMappingDimension.SETRANGE("Import Company Name", "Import Company Name");
               DefaultMappingDimension.SETRANGE("Vendor No.", "Vendor No.");
               DefaultMappingDimension.SETRANGE("Import No.", "Import No.");
               DefaultMappingDimension.DELETEALL(TRUE);
             END;

    OnRename=VAR
               DefaultMappingDimension@1100285000 : Record 12013670;
             BEGIN
               DefaultMappingDimension.RESET;
               DefaultMappingDimension.SETRANGE("Import Source Type", xRec."Import Source Type");
               DefaultMappingDimension.SETRANGE("Import Company Name", xRec."Import Company Name");
               DefaultMappingDimension.SETRANGE("Vendor No.", xRec."Vendor No.");
               DefaultMappingDimension.SETRANGE("Import No.", xRec."Import No.");
               IF DefaultMappingDimension.FIND('-') THEN
                 REPEAT
                   DefaultMappingDimension.RENAME("Import Source Type",
                                                  "Import Company Name",
                                                  "Vendor No.",
                                                  "Import No.");
                 UNTIL DefaultMappingDimension.NEXT = 0;
             END;

    CaptionML=ENU=Import Mapping Table;
  }
  FIELDS
  {
    { 1   ;   ;Import Source Type  ;Option        ;OnValidate=BEGIN
                                                                IF "Import Source Type" = "Import Source Type"::Global THEN
                                                                  "Import Company Name" := '';
                                                                IF "Import Source Type" = "Import Source Type"::CompanyID THEN
                                                                  "Import Company Name" := COMPANYNAME;
                                                              END;

                                                   CaptionML=ENU=Import Source Type;
                                                   OptionCaptionML=ENU=Global,Company Spec.;
                                                   OptionString=Global,CompanyID }
    { 2   ;   ;Import Company Name ;Text30        ;TableRelation=Company;
                                                   CaptionML=ENU=Import Company Name;
                                                   Editable=No }
    { 3   ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor.No.;
                                                   OnValidate=BEGIN
                                                                IF ("Vendor No." <> '') AND ("Import Source Type" <> "Import Source Type"::CompanyID) THEN
                                                                  ERROR(EXF001);
                                                              END;

                                                   CaptionML=ENU=Vendor No. }
    { 4   ;   ;Import No.          ;Code100       ;CaptionML=ENU=Import No. }
    { 5   ;   ;Map To Type         ;Option        ;CaptionML=ENU=Map To Type;
                                                   OptionCaptionML=ENU=" ,G/L Account,Item,,Fixed Asset,Charge (Item)";
                                                   OptionString=[ ,G/L Account,Item,,Fixed Asset,Charge (Item)] }
    { 6   ;   ;Map To No.          ;Code20        ;TableRelation=IF (Map To Type=CONST(" ")) "Standard Text"
                                                                 ELSE IF (Map To Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Map To Type=CONST(Item)) Item
                                                                 ELSE IF (Map To Type=CONST(3)) Resource
                                                                 ELSE IF (Map To Type=CONST(Fixed Asset)) "Fixed Asset"
                                                                 ELSE IF (Map To Type=CONST("Charge (Item)")) "Item Charge";
                                                   CaptionML=ENU=Map To No. }
    { 7   ;   ;No Order Matching   ;Boolean       ;CaptionML=ENU=No Purchase Order Matching }
    { 10  ;   ;Dimensions          ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Default Mapping Dimension" WHERE (Import Source Type=FIELD(Import Source Type),
                                                                                                        Import Company Name=FIELD(Import Company Name),
                                                                                                        Vendor No.=FIELD(Vendor No.),
                                                                                                        Import No.=FIELD(Import No.)));
                                                   CaptionML=ENU=No. of Dimensions;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Import Source Type,Import Company Name,Vendor No.,Import No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EXF001@1100285000 : TextConst 'ENU=Vendor Specific Mappings may only be selected for Company specific mapping types.';

    PROCEDURE ShowDefaultDimensions@25();
    VAR
      DefaultMappingDimension@1100285000 : Record 12013670;
    BEGIN
      DefaultMappingDimension.RESET;
      DefaultMappingDimension.SETRANGE("Import Source Type", "Import Source Type");
      DefaultMappingDimension.SETRANGE("Import Company Name", "Import Company Name");
      DefaultMappingDimension.SETRANGE("Vendor No.", "Vendor No.");
      DefaultMappingDimension.SETRANGE("Import No.", "Import No.");

      PAGE.RUNMODAL(0,DefaultMappingDimension);
    END;

    BEGIN
    END.
  }
}

