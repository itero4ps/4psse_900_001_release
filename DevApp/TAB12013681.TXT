OBJECT Table 12013681 Fraudulent Vendor ID Setup
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF400000;
  }
  PROPERTIES
  {
    CaptionML=ENU=Fraudulent Vendor ID Setup;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer        }
    { 3   ;   ;Download Path       ;Text250       ;OnValidate=BEGIN
                                                                EndingSlash("Download Path");
                                                              END;

                                                   CaptionML=ENU=Download Path }
    { 4   ;   ;Latest Update (Date);Date          ;CaptionML=ENU=Latest Update (Date);
                                                   Editable=No }
    { 5   ;   ;Latest Update (Time);Time          ;CaptionML=ENU=Latest Update (Time);
                                                   Editable=No }
    { 6   ;   ;Download Type       ;Option        ;OnValidate=VAR
                                                                ExFileMgt@1100285000 : Codeunit 12013602;
                                                              BEGIN
                                                                IF ("Download Type" <> xRec."Download Type") AND ("Download Type" = "Download Type"::Automatic) THEN
                                                                  IF ExFileMgt.GetNAVVersion <> 3 THEN
                                                                    ERROR(EXF002);
                                                              END;

                                                   CaptionML=ENU=Download Type;
                                                   OptionCaptionML=ENU=Inactive,Automatic,Manual;
                                                   OptionString=Inactive,Automatic,Manual }
    { 7   ;   ;Fraudulent IDs      ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Fraudulent Vendor ID");
                                                   CaptionML=ENU=No. Fraudulent IDs;
                                                   Editable=No }
    { 8   ;   ;Error Message       ;Text250       ;CaptionML=ENU=Error Message;
                                                   Editable=No }
    { 9   ;   ;Error Count         ;Integer       ;CaptionML=ENU=Error Count;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Latest Update (Date)                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EXF002@1100285001 : TextConst 'ENU=NAV Client must be 2009 R2 or higher and executed in RTC.';
      EXF004@1100285003 : TextConst 'ENU=%1 Successfully Initialized.';

    PROCEDURE EndingSlash@1000000000(VAR Path@1000000000 : Text[128]);
    BEGIN
      IF Path <> '' THEN
        IF Path[STRLEN(Path)] <> '\' THEN
          Path := Path + '\';
    END;

    PROCEDURE GetNumberOfBlockedIDs@1100285001(CompName@1100285000 : Text[100]) : Integer;
    VAR
      VendID@1100285001 : Record 12013667;
    BEGIN
      IF CompName <> '' THEN BEGIN
        VendID.RESET;
        VendID.CHANGECOMPANY(CompName);
        VendID.SETRANGE("Fraud ID",TRUE);
        EXIT(VendID.COUNT);
      END;
    END;

    PROCEDURE InitVendIDSetup@1100285002();
    VAR
      ExAppSetup@1100285000 : Record 12013601;
      VendorIDFields@1100285001 : Record 12013668;
      VendorIDs@1100285002 : Record 12013667;
      UpdateReq@1100285003 : Boolean;
      Vendor@1100285004 : Record 23;
      VendIDmgt@1100285005 : Codeunit 12013667;
    BEGIN
      ExAppSetup.GET;
      IF NOT ExAppSetup."Activate VendorID sync." THEN BEGIN
        ExAppSetup."Activate VendorID sync." := TRUE;
        ExAppSetup.MODIFY;
        UpdateReq := TRUE;
      END;

      IF NOT VendorIDFields.GET(23,11200) THEN BEGIN
        VendorIDFields.INIT;
        VendorIDFields."Table No." := 23;
        VendorIDFields."Field No." := 11200;
        VendorIDFields.INSERT;
        UpdateReq := TRUE;
      END;

      IF NOT VendorIDFields.GET(23,11207) THEN BEGIN
        VendorIDFields.INIT;
        VendorIDFields."Table No." := 23;
        VendorIDFields."Field No." := 11207;
        VendorIDFields.INSERT;
        UpdateReq := TRUE;
      END;

      IF (Vendor.COUNT * 1.5) > VendorIDs.COUNT THEN
        UpdateReq := TRUE;

      IF UpdateReq THEN
        VendIDmgt.UpdateAllFieldIDs(TRUE);

      MESSAGE(EXF004,COMPANYNAME);
    END;

    PROCEDURE ViewBlacklistedVendIDs@1100285003();
    VAR
      VendIDs@1100285000 : Record 12013667;
      VendorIDForm@1100285001 : Page 12013678;
    BEGIN
      VendIDs.RESET;
      VendIDs.SETRANGE("Fraud ID",TRUE);
      CLEAR(VendorIDForm);
      VendorIDForm.EDITABLE := FALSE;
      VendorIDForm.SETTABLEVIEW(VendIDs);
      VendorIDForm.RUN;
    END;

    BEGIN
    END.
  }
}

