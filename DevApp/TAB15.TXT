OBJECT Table 15 G/L Account
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,4PS9.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Name;
    OnInsert=BEGIN
               //**4PS.sn
               CentralDataMgt.UpdateGLAccount(Rec,1,xRec);
               DPA_OnInsert;
               //**4PS.en

               DimMgt.UpdateDefaultDim(DATABASE::"G/L Account","No.",
                 "Global Dimension 1 Code","Global Dimension 2 Code");

               IF CostAccSetup.GET THEN
                 CostAccMgt.UpdateCostTypeFromGLAcc(Rec,xRec,0);
             END;

    OnModify=BEGIN
               //**4PS.sn
               CentralDataMgt.UpdateGLAccount(Rec,2,xRec);
               //**4PS.en

               "Last Date Modified" := TODAY;

               IF CostAccSetup.GET THEN
                 CostAccMgt.UpdateCostTypeFromGLAcc(Rec,xRec,1);
             END;

    OnDelete=VAR
               GLBudgetEntry@1000 : Record 96;
               CommentLine@1001 : Record 97;
               ExtTextHeader@1002 : Record 279;
               AnalysisViewEntry@1003 : Record 365;
               AnalysisViewBudgetEntry@1004 : Record 366;
               MoveEntries@1005 : Codeunit 361;
             BEGIN
               MoveEntries.MoveGLEntries(Rec);

               GLBudgetEntry.SETCURRENTKEY("Budget Name","G/L Account No.");
               GLBudgetEntry.SETRANGE("G/L Account No.","No.");
               GLBudgetEntry.DELETEALL(TRUE);

               CommentLine.SETRANGE("Table Name",CommentLine."Table Name"::"G/L Account");
               CommentLine.SETRANGE("No.","No.");
               CommentLine.DELETEALL;

               ExtTextHeader.SETRANGE("Table Name",ExtTextHeader."Table Name"::"G/L Account");
               ExtTextHeader.SETRANGE("No.","No.");
               ExtTextHeader.DELETEALL(TRUE);

               AnalysisViewEntry.SETRANGE("Account No.","No.");
               AnalysisViewEntry.DELETEALL;

               AnalysisViewBudgetEntry.SETRANGE("G/L Account No.","No.");
               AnalysisViewBudgetEntry.DELETEALL;

               DimMgt.DeleteDefaultDim(DATABASE::"G/L Account","No.");

               //**4PS.sn
               CentralDataMgt.RemoveGLAccount(Rec);
               //**4PS.en
             END;

    OnRename=BEGIN
               "Last Date Modified" := TODAY;

               IF CostAccSetup.READPERMISSION THEN
                 CostAccMgt.UpdateCostTypeFromGLAcc(Rec,xRec,3);
             END;

    CaptionML=ENU=G/L Account;
    LookupPageID=Page18;
    DrillDownPageID=Page16;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;AltSearchField=Search Name;
                                                   OnValidate=VAR
                                                                GLAccRec@1210190000 : Record 15;
                                                              BEGIN
                                                                //**4PS.sn
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  GLAccRec := xRec;
                                                                  IF GLAccRec.FIND THEN
                                                                    CentralDataMgt.UpdateGLAccount(Rec,3,xRec);
                                                                END;
                                                                //**4PS.en
                                                              END;

                                                   CaptionML=ENU=No.;
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text50        ;OnValidate=BEGIN
                                                                IF ("Search Name" = UPPERCASE(xRec.Name)) OR ("Search Name" = '') THEN
                                                                  "Search Name" := Name;
                                                              END;

                                                   CaptionML=ENU=Name }
    { 3   ;   ;Search Name         ;Code50        ;CaptionML=ENU=Search Name }
    { 4   ;   ;Account Type        ;Option        ;OnValidate=VAR
                                                                GLEntry@1000 : Record 17;
                                                                GLBudgetEntry@1001 : Record 96;
                                                              BEGIN
                                                                IF ("Account Type" <> "Account Type"::Posting) AND
                                                                   (xRec."Account Type" = xRec."Account Type"::Posting)
                                                                THEN BEGIN
                                                                  GLEntry.SETCURRENTKEY("G/L Account No.");
                                                                  GLEntry.SETRANGE("G/L Account No.","No.");
                                                                  IF GLEntry.FINDFIRST THEN
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Account Type"));
                                                                  GLBudgetEntry.SETCURRENTKEY("Budget Name","G/L Account No.");
                                                                  GLBudgetEntry.SETRANGE("G/L Account No.","No.");
                                                                  IF GLBudgetEntry.FINDFIRST THEN
                                                                    ERROR(
                                                                      Text001,
                                                                      FIELDCAPTION("Account Type"));
                                                                END;
                                                                Totaling := '';
                                                                IF "Account Type" = "Account Type"::Posting THEN BEGIN
                                                                  IF "Account Type" <> xRec."Account Type" THEN
                                                                    "Direct Posting" := TRUE;
                                                                END ELSE
                                                                  "Direct Posting" := FALSE;
                                                              END;

                                                   CaptionML=ENU=Account Type;
                                                   OptionCaptionML=ENU=Posting,Heading,Total,Begin-Total,End-Total;
                                                   OptionString=Posting,Heading,Total,Begin-Total,End-Total }
    { 6   ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Global Dimension 1 Code");
                                                              END;

                                                   CaptionML=ENU=Global Dimension 1 Code;
                                                   CaptionClass='1,1,1' }
    { 7   ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Global Dimension 2 Code");
                                                              END;

                                                   CaptionML=ENU=Global Dimension 2 Code;
                                                   CaptionClass='1,1,2' }
    { 9   ;   ;Income/Balance      ;Option        ;OnValidate=VAR
                                                                CostType@1000 : Record 1103;
                                                              BEGIN
                                                                IF ("Income/Balance" = "Income/Balance"::"Balance Sheet") AND ("Cost Type No." <> '') THEN BEGIN
                                                                  IF CostType.GET("No.") THEN BEGIN
                                                                    CostType."G/L Account Range" := '';
                                                                    CostType.MODIFY;
                                                                  END;
                                                                  "Cost Type No." := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Income/Balance;
                                                   OptionCaptionML=ENU=Income Statement,Balance Sheet;
                                                   OptionString=Income Statement,Balance Sheet }
    { 10  ;   ;Debit/Credit        ;Option        ;CaptionML=ENU=Debit/Credit;
                                                   OptionCaptionML=ENU=Both,Debit,Credit;
                                                   OptionString=Both,Debit,Credit }
    { 11  ;   ;No. 2               ;Code20        ;CaptionML=ENU=No. 2 }
    { 12  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Comment Line" WHERE (Table Name=CONST(G/L Account),
                                                                                           No.=FIELD(No.)));
                                                   CaptionML=ENU=Comment;
                                                   Editable=No }
    { 13  ;   ;Blocked             ;Boolean       ;CaptionML=ENU=Blocked }
    { 14  ;   ;Direct Posting      ;Boolean       ;InitValue=Yes;
                                                   CaptionML=ENU=Direct Posting }
    { 16  ;   ;Reconciliation Account;Boolean     ;AccessByPermission=TableData 270=R;
                                                   CaptionML=ENU=Reconciliation Account }
    { 17  ;   ;New Page            ;Boolean       ;CaptionML=ENU=New Page }
    { 18  ;   ;No. of Blank Lines  ;Integer       ;CaptionML=ENU=No. of Blank Lines;
                                                   MinValue=0 }
    { 19  ;   ;Indentation         ;Integer       ;CaptionML=ENU=Indentation;
                                                   MinValue=0 }
    { 26  ;   ;Last Date Modified  ;Date          ;CaptionML=ENU=Last Date Modified;
                                                   Editable=No }
    { 28  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter }
    { 29  ;   ;Global Dimension 1 Filter;Code20   ;FieldClass=FlowFilter;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=ENU=Global Dimension 1 Filter;
                                                   CaptionClass='1,3,1' }
    { 30  ;   ;Global Dimension 2 Filter;Code20   ;FieldClass=FlowFilter;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   CaptionML=ENU=Global Dimension 2 Filter;
                                                   CaptionClass='1,3,2' }
    { 31  ;   ;Balance at Date     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("G/L Entry".Amount WHERE (G/L Account No.=FIELD(No.),
                                                                                             G/L Account No.=FIELD(FILTER(Totaling)),
                                                                                             Business Unit Code=FIELD(Business Unit Filter),
                                                                                             Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                             Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                             Posting Date=FIELD(UPPERLIMIT(Date Filter))));
                                                   CaptionML=ENU=Balance at Date;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 32  ;   ;Net Change          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("G/L Entry".Amount WHERE (G/L Account No.=FIELD(No.),
                                                                                             G/L Account No.=FIELD(FILTER(Totaling)),
                                                                                             Business Unit Code=FIELD(Business Unit Filter),
                                                                                             Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                             Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                             Posting Date=FIELD(Date Filter)));
                                                   CaptionML=ENU=Net Change;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 33  ;   ;Budgeted Amount     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("G/L Budget Entry".Amount WHERE (G/L Account No.=FIELD(No.),
                                                                                                    G/L Account No.=FIELD(FILTER(Totaling)),
                                                                                                    Business Unit Code=FIELD(Business Unit Filter),
                                                                                                    Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                    Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                    Date=FIELD(Date Filter),
                                                                                                    Budget Name=FIELD(Budget Filter)));
                                                   CaptionML=ENU=Budgeted Amount;
                                                   AutoFormatType=1 }
    { 34  ;   ;Totaling            ;Text250       ;OnValidate=BEGIN
                                                                IF NOT ("Account Type" IN ["Account Type"::Total,"Account Type"::"End-Total"]) THEN
                                                                  FIELDERROR("Account Type");
                                                                CALCFIELDS(Balance);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Totaling }
    { 35  ;   ;Budget Filter       ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="G/L Budget Name";
                                                   CaptionML=ENU=Budget Filter }
    { 36  ;   ;Balance             ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("G/L Entry".Amount WHERE (G/L Account No.=FIELD(No.),
                                                                                             G/L Account No.=FIELD(FILTER(Totaling)),
                                                                                             Business Unit Code=FIELD(Business Unit Filter),
                                                                                             Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                             Global Dimension 2 Code=FIELD(Global Dimension 2 Filter)));
                                                   CaptionML=ENU=Balance;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 37  ;   ;Budget at Date      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("G/L Budget Entry".Amount WHERE (G/L Account No.=FIELD(No.),
                                                                                                    G/L Account No.=FIELD(FILTER(Totaling)),
                                                                                                    Business Unit Code=FIELD(Business Unit Filter),
                                                                                                    Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                    Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                    Date=FIELD(UPPERLIMIT(Date Filter)),
                                                                                                    Budget Name=FIELD(Budget Filter)));
                                                   CaptionML=ENU=Budget at Date;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 39  ;   ;Consol. Translation Method;Option  ;OnValidate=VAR
                                                                ConflictGLAcc@1000 : Record 15;
                                                              BEGIN
                                                                IF TranslationMethodConflict(ConflictGLAcc) THEN
                                                                  IF ConflictGLAcc.GETFILTER("Consol. Debit Acc.") <> '' THEN
                                                                    MESSAGE(
                                                                      Text002,ConflictGLAcc.TABLECAPTION,ConflictGLAcc."No.",ConflictGLAcc.FIELDCAPTION("Consol. Debit Acc."),
                                                                      ConflictGLAcc.FIELDCAPTION("Consol. Translation Method"),ConflictGLAcc."Consol. Translation Method")
                                                                  ELSE
                                                                    MESSAGE(
                                                                      Text002,ConflictGLAcc.TABLECAPTION,ConflictGLAcc."No.",ConflictGLAcc.FIELDCAPTION("Consol. Credit Acc."),
                                                                      ConflictGLAcc.FIELDCAPTION("Consol. Translation Method"),ConflictGLAcc."Consol. Translation Method");
                                                              END;

                                                   AccessByPermission=TableData 220=R;
                                                   CaptionML=ENU=Consol. Translation Method;
                                                   OptionCaptionML=ENU=Average Rate (Manual),Closing Rate,Historical Rate,Composite Rate,Equity Rate;
                                                   OptionString=Average Rate (Manual),Closing Rate,Historical Rate,Composite Rate,Equity Rate }
    { 40  ;   ;Consol. Debit Acc.  ;Code20        ;OnValidate=VAR
                                                                ConflictGLAcc@1000 : Record 15;
                                                              BEGIN
                                                                IF TranslationMethodConflict(ConflictGLAcc) THEN
                                                                  MESSAGE(
                                                                    Text002,ConflictGLAcc.TABLECAPTION,ConflictGLAcc."No.",ConflictGLAcc.FIELDCAPTION("Consol. Debit Acc."),
                                                                    ConflictGLAcc.FIELDCAPTION("Consol. Translation Method"),ConflictGLAcc."Consol. Translation Method");
                                                              END;

                                                   AccessByPermission=TableData 220=R;
                                                   CaptionML=ENU=Consol. Debit Acc. }
    { 41  ;   ;Consol. Credit Acc. ;Code20        ;OnValidate=VAR
                                                                ConflictGLAcc@1000 : Record 15;
                                                              BEGIN
                                                                IF TranslationMethodConflict(ConflictGLAcc) THEN
                                                                  MESSAGE(
                                                                    Text002,ConflictGLAcc.TABLECAPTION,ConflictGLAcc."No.",ConflictGLAcc.FIELDCAPTION("Consol. Credit Acc."),
                                                                    ConflictGLAcc.FIELDCAPTION("Consol. Translation Method"),ConflictGLAcc."Consol. Translation Method");
                                                              END;

                                                   AccessByPermission=TableData 220=R;
                                                   CaptionML=ENU=Consol. Credit Acc. }
    { 42  ;   ;Business Unit Filter;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Business Unit";
                                                   CaptionML=ENU=Business Unit Filter }
    { 43  ;   ;Gen. Posting Type   ;Option        ;CaptionML=ENU=Gen. Posting Type;
                                                   OptionCaptionML=ENU=" ,Purchase,Sale";
                                                   OptionString=[ ,Purchase,Sale] }
    { 44  ;   ;Gen. Bus. Posting Group;Code10     ;TableRelation="Gen. Business Posting Group";
                                                   OnValidate=VAR
                                                                GenBusPostingGrp@1000 : Record 250;
                                                              BEGIN
                                                                IF xRec."Gen. Bus. Posting Group" <> "Gen. Bus. Posting Group" THEN
                                                                  IF GenBusPostingGrp.ValidateVatBusPostingGroup(GenBusPostingGrp,"Gen. Bus. Posting Group") THEN
                                                                    VALIDATE("VAT Bus. Posting Group",GenBusPostingGrp."Def. VAT Bus. Posting Group");
                                                              END;

                                                   CaptionML=ENU=Gen. Bus. Posting Group }
    { 45  ;   ;Gen. Prod. Posting Group;Code10    ;TableRelation="Gen. Product Posting Group";
                                                   OnValidate=VAR
                                                                GenProdPostingGrp@1000 : Record 251;
                                                              BEGIN
                                                                IF xRec."Gen. Prod. Posting Group" <> "Gen. Prod. Posting Group" THEN
                                                                  IF GenProdPostingGrp.ValidateVatProdPostingGroup(GenProdPostingGrp,"Gen. Prod. Posting Group") THEN
                                                                    VALIDATE("VAT Prod. Posting Group",GenProdPostingGrp."Def. VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=ENU=Gen. Prod. Posting Group }
    { 46  ;   ;Picture             ;BLOB          ;CaptionML=ENU=Picture;
                                                   SubType=Bitmap }
    { 47  ;   ;Debit Amount        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("G/L Entry"."Debit Amount" WHERE (G/L Account No.=FIELD(No.),
                                                                                                     G/L Account No.=FIELD(FILTER(Totaling)),
                                                                                                     Business Unit Code=FIELD(Business Unit Filter),
                                                                                                     Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                     Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                     Posting Date=FIELD(Date Filter)));
                                                   CaptionML=ENU=Debit Amount;
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 48  ;   ;Credit Amount       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("G/L Entry"."Credit Amount" WHERE (G/L Account No.=FIELD(No.),
                                                                                                      G/L Account No.=FIELD(FILTER(Totaling)),
                                                                                                      Business Unit Code=FIELD(Business Unit Filter),
                                                                                                      Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                      Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                      Posting Date=FIELD(Date Filter)));
                                                   CaptionML=ENU=Credit Amount;
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 49  ;   ;Automatic Ext. Texts;Boolean       ;CaptionML=ENU=Automatic Ext. Texts }
    { 52  ;   ;Budgeted Debit Amount;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("G/L Budget Entry".Amount WHERE (G/L Account No.=FIELD(No.),
                                                                                                    G/L Account No.=FIELD(FILTER(Totaling)),
                                                                                                    Business Unit Code=FIELD(Business Unit Filter),
                                                                                                    Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                    Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                    Date=FIELD(Date Filter),
                                                                                                    Budget Name=FIELD(Budget Filter)));
                                                   CaptionML=ENU=Budgeted Debit Amount;
                                                   BlankNumbers=BlankNegAndZero;
                                                   AutoFormatType=1 }
    { 53  ;   ;Budgeted Credit Amount;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("G/L Budget Entry".Amount WHERE (G/L Account No.=FIELD(No.),
                                                                                                     G/L Account No.=FIELD(FILTER(Totaling)),
                                                                                                     Business Unit Code=FIELD(Business Unit Filter),
                                                                                                     Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                     Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                     Date=FIELD(Date Filter),
                                                                                                     Budget Name=FIELD(Budget Filter)));
                                                   CaptionML=ENU=Budgeted Credit Amount;
                                                   BlankNumbers=BlankNegAndZero;
                                                   AutoFormatType=1 }
    { 54  ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   CaptionML=ENU=Tax Area Code }
    { 55  ;   ;Tax Liable          ;Boolean       ;CaptionML=ENU=Tax Liable }
    { 56  ;   ;Tax Group Code      ;Code10        ;TableRelation="Tax Group";
                                                   CaptionML=ENU=Tax Group Code }
    { 57  ;   ;VAT Bus. Posting Group;Code10      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=ENU=VAT Bus. Posting Group }
    { 58  ;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=ENU=VAT Prod. Posting Group }
    { 60  ;   ;Additional-Currency Net Change;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("G/L Entry"."Additional-Currency Amount" WHERE (G/L Account No.=FIELD(No.),
                                                                                                                   G/L Account No.=FIELD(FILTER(Totaling)),
                                                                                                                   Business Unit Code=FIELD(Business Unit Filter),
                                                                                                                   Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                                   Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                                   Posting Date=FIELD(Date Filter)));
                                                   CaptionML=ENU=Additional-Currency Net Change;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 61  ;   ;Add.-Currency Balance at Date;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("G/L Entry"."Additional-Currency Amount" WHERE (G/L Account No.=FIELD(No.),
                                                                                                                   G/L Account No.=FIELD(FILTER(Totaling)),
                                                                                                                   Business Unit Code=FIELD(Business Unit Filter),
                                                                                                                   Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                                   Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                                   Posting Date=FIELD(UPPERLIMIT(Date Filter))));
                                                   CaptionML=ENU=Add.-Currency Balance at Date;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 62  ;   ;Additional-Currency Balance;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("G/L Entry"."Additional-Currency Amount" WHERE (G/L Account No.=FIELD(No.),
                                                                                                                   G/L Account No.=FIELD(FILTER(Totaling)),
                                                                                                                   Business Unit Code=FIELD(Business Unit Filter),
                                                                                                                   Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                                   Global Dimension 2 Code=FIELD(Global Dimension 2 Filter)));
                                                   CaptionML=ENU=Additional-Currency Balance;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 63  ;   ;Exchange Rate Adjustment;Option    ;AccessByPermission=TableData 4=R;
                                                   CaptionML=ENU=Exchange Rate Adjustment;
                                                   OptionCaptionML=ENU=No Adjustment,Adjust Amount,Adjust Additional-Currency Amount;
                                                   OptionString=No Adjustment,Adjust Amount,Adjust Additional-Currency Amount }
    { 64  ;   ;Add.-Currency Debit Amount;Decimal ;FieldClass=FlowField;
                                                   CalcFormula=Sum("G/L Entry"."Add.-Currency Debit Amount" WHERE (G/L Account No.=FIELD(No.),
                                                                                                                   G/L Account No.=FIELD(FILTER(Totaling)),
                                                                                                                   Business Unit Code=FIELD(Business Unit Filter),
                                                                                                                   Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                                   Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                                   Posting Date=FIELD(Date Filter)));
                                                   CaptionML=ENU=Add.-Currency Debit Amount;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 65  ;   ;Add.-Currency Credit Amount;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("G/L Entry"."Add.-Currency Credit Amount" WHERE (G/L Account No.=FIELD(No.),
                                                                                                                    G/L Account No.=FIELD(FILTER(Totaling)),
                                                                                                                    Business Unit Code=FIELD(Business Unit Filter),
                                                                                                                    Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                                    Global Dimension 2 Code=FIELD(Global Dimension 2 Filter),
                                                                                                                    Posting Date=FIELD(Date Filter)));
                                                   CaptionML=ENU=Add.-Currency Credit Amount;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 66  ;   ;Default IC Partner G/L Acc. No;Code20;
                                                   TableRelation="IC G/L Account".No.;
                                                   CaptionML=ENU=Default IC Partner G/L Acc. No }
    { 1100;   ;Cost Type No.       ;Code20        ;TableRelation="Cost Type";
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Cost Type No.;
                                                   Editable=No }
    { 1700;   ;Default Deferral Template Code;Code10;
                                                   TableRelation="Deferral Template"."Deferral Code";
                                                   CaptionML=ENU=Default Deferral Template Code }
    { 11012000;;WIP Account        ;Boolean       ;CaptionML=ENU=WIP Account }
    { 11012010;;Import Posting Information;Boolean;CaptionML=ENU=Import Posting Information }
    { 11012020;;Department Totaling;Text30        ;CaptionML=ENU=Department Totaling }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Search Name                              }
    {    ;Reconciliation Account                   }
    {    ;Gen. Bus. Posting Group                  }
    {    ;Gen. Prod. Posting Group                 }
    { No ;Consol. Debit Acc.,Consol. Translation Method }
    { No ;Consol. Credit Acc.,Consol. Translation Method }
    {    ;Name                                     }
    {    ;Account Type                             }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Name,Income/Balance,Blocked,Direct Posting }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot change %1 because there are one or more ledger entries associated with this account.';
      Text001@1001 : TextConst 'ENU=You cannot change %1 because this account is part of one or more budgets.';
      GLSetup@1002 : Record 98;
      CostAccSetup@1006 : Record 1108;
      DimMgt@1003 : Codeunit 408;
      CostAccMgt@1007 : Codeunit 1100;
      GLSetupRead@1004 : Boolean;
      Text002@1005 : TextConst 'ENU="There is another %1: %2;WHICH=refers to the same %3, but with a different %4: %5."';
      CentralDataMgt@1210190000 : Codeunit 11012010;
      DPA_Management@1100000999 : Codeunit 11012783;
      SkipModify@1100528400 : Boolean;

    PROCEDURE SetupNewGLAcc@1(OldGLAcc@1000 : Record 15;BelowOldGLAcc@1001 : Boolean);
    VAR
      OldGLAcc2@1002 : Record 15;
    BEGIN
      IF NOT BelowOldGLAcc THEN BEGIN
        OldGLAcc2 := OldGLAcc;
        OldGLAcc.COPY(Rec);
        OldGLAcc := OldGLAcc2;
        IF NOT OldGLAcc.FIND('<') THEN
          OldGLAcc.INIT;
      END;
      "Income/Balance" := OldGLAcc."Income/Balance";
    END;

    PROCEDURE CheckGLAcc@2();
    BEGIN
      TESTFIELD("Account Type","Account Type"::Posting);
      TESTFIELD(Blocked,FALSE);
    END;

    LOCAL PROCEDURE GetCurrencyCode@3() : Code[10];
    BEGIN
      IF NOT GLSetupRead THEN BEGIN
        GLSetup.GET;
        GLSetupRead := TRUE;
      END;
      EXIT(GLSetup."Additional Reporting Currency");
    END;

    LOCAL PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      DimMgt.SaveDefaultDim(DATABASE::"G/L Account","No.",FieldNumber,ShortcutDimCode);
      //MODIFY; //**4PS.o
      IF NOT SkipModify THEN //**4PS.n
        MODIFY(TRUE); //**4PS.n
    END;

    PROCEDURE TranslationMethodConflict@4(VAR GLAcc@1000 : Record 15) : Boolean;
    BEGIN
      GLAcc.RESET;
      GLAcc.SETFILTER("No.",'<>%1',"No.");
      GLAcc.SETFILTER("Consol. Translation Method",'<>%1',"Consol. Translation Method");
      IF "Consol. Debit Acc." <> '' THEN BEGIN
        IF NOT GLAcc.SETCURRENTKEY("Consol. Debit Acc.","Consol. Translation Method") THEN
          GLAcc.SETCURRENTKEY("No.");
        GLAcc.SETRANGE("Consol. Debit Acc.","Consol. Debit Acc.");
        IF GLAcc.FIND('-') THEN
          EXIT(TRUE);
        GLAcc.SETRANGE("Consol. Debit Acc.");
      END;
      IF "Consol. Credit Acc." <> '' THEN BEGIN
        IF NOT GLAcc.SETCURRENTKEY("Consol. Credit Acc.","Consol. Translation Method") THEN
          GLAcc.SETCURRENTKEY("No.");
        GLAcc.SETRANGE("Consol. Credit Acc.","Consol. Credit Acc.");
        IF GLAcc.FIND('-') THEN
          EXIT(TRUE);
        GLAcc.SETRANGE("Consol. Credit Acc.");
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE DPA_OnInsert@1100000991();
    BEGIN
      //**4PS
      IF NOT DPA_IsActive THEN
        EXIT;

      IF "Global Dimension 1 Code" <> '' THEN BEGIN
        SkipModify := TRUE;
        VALIDATE("Global Dimension 1 Code");
        SkipModify := FALSE;
      END;
    END;

    PROCEDURE DPA_GetFiltersFltrGrp@1100000997(FilterGroupNo@1100525000 : Integer) FilterText : Text[250];
    BEGIN
      //**4PS Generated Code - Do Not Change
      FILTERGROUP(FilterGroupNo);
      FilterText := COPYSTR(GETFILTERS, 1, MAXSTRLEN(FilterText));
      FILTERGROUP(0);
    END;

    PROCEDURE DPA_SetFilters@1100000998(Context@1100525000 : Text[30]) : Boolean;
    VAR
      RecRef@1100525003 : RecordRef;
      i@1100525004 : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      RecRef.GETTABLE(Rec);
      DPA_Management.SetDPAFilterFromContext(
        RecRef,
        Context,
        DPA_GetFieldNoDepartmentCode,
        DPA_GetFieldNoDepartmentFilter );

      FOR i := 10 DOWNTO 0 DO BEGIN
        RecRef.FILTERGROUP(i);
        FILTERGROUP(i);
        SETVIEW(RecRef.GETVIEW);
      END;

      EXIT(DPA_Management.IsHardFilter);
    END;

    LOCAL PROCEDURE DPA_IsActive@1100000999() : Boolean;
    VAR
      DepartmentAuthorizObject@1100526000 : Record 11012466;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DepartmentAuthorizObject.READPERMISSION THEN
        EXIT(FALSE);

      IF NOT DPA_Management.AuthorizationActive THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE DPA_GetFieldNoDepartmentFilter@1100000993() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(FIELDNO("Global Dimension 1 Filter"));
    END;

    PROCEDURE DPA_FindRecWithDPAFilterAppld@1100000989();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN BEGIN
        FIND;
        EXIT;
      END;

      IF NOT DPA_Management.HasTableHardFilter(15) THEN BEGIN
        FIND;
        EXIT;
      END;

      DPA_SetFilters('Table 15');
      IF NOT FIND THEN
        DPA_Management.RaiseDPAErrorForTable(TABLECAPTION);
    END;

    PROCEDURE DPA_ChkDepartmentCodeAllowed@1100000990(IsHardFilter@1100526000 : Boolean);
    VAR
      DimensionManagement@1100526001 : Codeunit 408;
    BEGIN
      //**4PS Generated Code - Do Not Change
      DimensionManagement.CheckDepartmAuthWithFilterType(
        DATABASE::"G/L Account", "Global Dimension 1 Code", IsHardFilter);
    END;

    PROCEDURE DPA_GetFieldNoDepartmentCode@1100000994() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(FIELDNO("Global Dimension 1 Code"));
    END;

    PROCEDURE DPA_SetDefaultDepartment@1100000996();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      DPA_Management.SetDepartmentCodeUser(TABLENAME, "Global Dimension 1 Code");
    END;

    BEGIN
    END.
  }
}

