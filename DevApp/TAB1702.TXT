OBJECT Table 1702 Deferral Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00.00.43897;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Posting Date" = 0D THEN
                 ERROR(InvalidDeferralLineDateErr);
             END;

    CaptionML=ENU=Deferral Line;
  }
  FIELDS
  {
    { 1   ;   ;Deferral Doc. Type  ;Option        ;TableRelation="Deferral Header"."Deferral Doc. Type";
                                                   CaptionML=ENU=Deferral Doc. Type;
                                                   OptionCaptionML=ENU=Purchase,Sales,G/L;
                                                   OptionString=Purchase,Sales,G/L }
    { 2   ;   ;Gen. Jnl. Template Name;Code10     ;TableRelation="Deferral Header"."Gen. Jnl. Template Name";
                                                   CaptionML=ENU=Gen. Jnl. Template Name }
    { 3   ;   ;Gen. Jnl. Batch Name;Code10        ;TableRelation="Deferral Header"."Gen. Jnl. Batch Name";
                                                   CaptionML=ENU=Gen. Jnl. Batch Name }
    { 4   ;   ;Document Type       ;Integer       ;TableRelation="Deferral Header"."Document Type";
                                                   CaptionML=ENU=Document Type }
    { 5   ;   ;Document No.        ;Code20        ;TableRelation="Deferral Header"."Document No.";
                                                   CaptionML=ENU=Document No. }
    { 6   ;   ;Line No.            ;Integer       ;TableRelation="Deferral Header"."Line No.";
                                                   CaptionML=ENU=Line No. }
    { 7   ;   ;Posting Date        ;Date          ;OnValidate=VAR
                                                                AccountingPeriod@1000 : Record 50;
                                                              BEGIN
                                                                IF GenJnlCheckLine.DateNotAllowed("Posting Date") THEN
                                                                  ERROR(InvalidPostingDateErr,"Posting Date");

                                                                AccountingPeriod.SETFILTER("Starting Date",'>=%1',"Posting Date");
                                                                IF NOT AccountingPeriod.FINDFIRST THEN
                                                                  ERROR(DeferSchedOutOfBoundsErr);
                                                              END;

                                                   CaptionML=ENU=Posting Date }
    { 8   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 9   ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                IF Amount = 0 THEN
                                                                  ERROR(ZeroAmountToDeferErr);

                                                                IF DeferralHeader.GET("Deferral Doc. Type","Gen. Jnl. Template Name","Gen. Jnl. Batch Name","Document Type","Document No.","Line No.") THEN BEGIN
                                                                  IF DeferralHeader."Amount to Defer" > 0 THEN
                                                                    IF Amount < 0 THEN
                                                                      ERROR(AmountToDeferPositiveErr);
                                                                  IF DeferralHeader."Amount to Defer" < 0 THEN
                                                                    IF Amount > 0 THEN
                                                                      ERROR(AmountToDeferNegativeErr);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 10  ;   ;Amount (LCY)        ;Decimal       ;CaptionML=ENU=Amount (LCY);
                                                   AutoFormatType=1 }
    { 11  ;   ;Currency Code       ;Code10        ;CaptionML=ENU=Currency Code }
  }
  KEYS
  {
    {    ;Deferral Doc. Type,Gen. Jnl. Template Name,Gen. Jnl. Batch Name,Document Type,Document No.,Line No.,Posting Date;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DeferralHeader@1005 : Record 1701;
      GenJnlCheckLine@1002 : Codeunit 11;
      InvalidPostingDateErr@1003 : TextConst '@@@="%1=The date passed in for the posting date.";ENU=%1 is not within the range of posting dates for your company.';
      DeferSchedOutOfBoundsErr@1001 : TextConst 'ENU=The deferral schedule falls outside the accounting periods that have been set up for the company.';
      InvalidDeferralLineDateErr@1004 : TextConst 'ENU=The posting date for this deferral schedule line is not valid.';
      ZeroAmountToDeferErr@1006 : TextConst 'ENU=The deferral amount must not equal 0.';
      AmountToDeferPositiveErr@1007 : TextConst 'ENU=The deferral amount must be positive.';
      AmountToDeferNegativeErr@1008 : TextConst 'ENU=The deferral amount must be negative.';

    BEGIN
    END.
  }
}

