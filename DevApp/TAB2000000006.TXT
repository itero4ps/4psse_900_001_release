OBJECT Table 2000000006 Company
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,4PS9.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnDelete=BEGIN
               //**4PS.sn
               CompanyRelationsCentralMgtRec.SETRANGE(Company, Name);
               CompanyRelationsCentralMgtRec.DELETEALL;

               CompanyData.SETRANGE("Company Name",Name);
               CompanyData.DELETEALL;

               DeleteWorkorders();
               DeleteResources();
               //**4PS.en
             END;

    OnRename=BEGIN
               //**4PS.sn
               ICRelation.RESET;
               ICRelation.SETRANGE("Supplying Company", xRec.Name);
               IF ICRelation.FIND('-') THEN BEGIN
                 REPEAT
                   ICRelation2 := ICRelation;
                   ICRelation2."Supplying Company" := Name;
                   ICRelation2.INSERT;
                 UNTIL ICRelation.NEXT = 0;
                 ICRelation.DELETEALL;
               END;

               ICRelation.RESET;
               ICRelation.SETRANGE("Receiving Company", xRec.Name);
               IF ICRelation.FIND('-') THEN BEGIN
                 REPEAT
                   ICRelation2 := ICRelation;
                   ICRelation2."Receiving Company" := Name;
                   ICRelation2.INSERT;
                 UNTIL ICRelation.NEXT = 0;
                 ICRelation.DELETEALL;
               END;

               ICEntry.RESET;
               ICEntry.SETRANGE("Supplying Company", xRec.Name);
               IF ICEntry.FIND('-') THEN BEGIN
                 REPEAT
                   ICEntry2 := ICEntry;
                   ICEntry2."Supplying Company" := Name;
                   ICEntry2.MODIFY;
                 UNTIL ICEntry.NEXT = 0;
               END;

               ICEntry.RESET;
               ICEntry.SETRANGE("Receiving Company", xRec.Name);
               IF ICEntry.FIND('-') THEN BEGIN
                 REPEAT
                   ICEntry2 := ICEntry;
                   ICEntry2."Receiving Company" := Name;
                   ICEntry2.MODIFY;
                 UNTIL ICEntry.NEXT = 0;
               END;

               ICEntry.RESET;
               ICEntry.SETRANGE("Post in Company", xRec.Name);
               IF ICEntry.FIND('-') THEN BEGIN
                 REPEAT
                   ICEntry2 := ICEntry;
                   ICEntry2."Post in Company" := Name;
                   ICEntry2.MODIFY;
                 UNTIL ICEntry.NEXT = 0;
               END;
               //**4PS.en
             END;

    CaptionML=ENU=Company;
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Text30        ;CaptionML=ENU=Name }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CompanyRec@11012001 : Record 2000000006;
      CompanyRelationsCentralMgtRec@1210190000 : Record 11012059;
      ICRelation@1210190001 : Record 11012057;
      ICRelation2@1210190002 : Record 11012057;
      ICEntry@1210190003 : Record 11012058;
      ICEntry2@1210190004 : Record 11012058;
      CompanyData@1100528500 : Record 11020674;

    LOCAL PROCEDURE DeleteWorkorders@1100528000();
    VAR
      WorkOrderSignature@1100528003 : Record 11229280;
      WorkOrderRefPoint@1100528002 : Record 11229282;
      WorkOrder@1100528001 : Record 11229279;
      MobileDecentralWorkOrder@1100528000 : Record 11125649;
    BEGIN
      //**4PS
      WorkOrder.SETRANGE("Source Company", Name);
      IF WorkOrder.FINDSET THEN BEGIN
        MobileDecentralWorkOrder.SETCURRENTKEY("Work Order No.");
        REPEAT
          WorkOrderSignature.SETRANGE("Work Order No.", WorkOrder."No.");
          WorkOrderSignature.DELETEALL;
          //
          WorkOrderRefPoint.SETRANGE("Work Order No.", WorkOrder."No.");
          WorkOrderRefPoint.DELETEALL;
          //
          MobileDecentralWorkOrder.SETRANGE("Work Order No.", WorkOrder."No.");
          MobileDecentralWorkOrder.DELETEALL;
          //
          WorkOrder.DELETE(FALSE);
        UNTIL WorkOrder.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE DeleteResources@1100528001();
    VAR
      Resource@1100528005 : Record 11229278;
      WorkOrder@1100528004 : Record 11229279;
      DeletedWorkOrderCache@1100528003 : Record 11229281;
      MobileDecentralWorkOrder@1100528002 : Record 11125649;
      ResourceDiscipline@1100528001 : Record 11229286;
      VisibleResourceWOP@1100528000 : Record 11229285;
    BEGIN
      //**4PS
      Resource.SETRANGE(Company, Name);
      IF Resource.FINDSET THEN BEGIN
        WorkOrder.SETCURRENTKEY("Resource No.");
        VisibleResourceWOP.SETCURRENTKEY("Resource No.");
        REPEAT
          WorkOrder.SETRANGE("Resource No.", Resource."No.");
          WorkOrder.MODIFYALL("Resource No.", '');
          //
          DeletedWorkOrderCache.SETRANGE("Resource No.", Resource."No.");
          DeletedWorkOrderCache.DELETEALL;
          //
          MobileDecentralWorkOrder.SETRANGE("Resource No. (Employee)", Resource."No.");
          MobileDecentralWorkOrder.DELETEALL;
          //
          ResourceDiscipline.SETRANGE("Resource No.", Resource."No.");
          ResourceDiscipline.DELETEALL(TRUE);
          //
          VisibleResourceWOP.SETRANGE("Resource No.", Resource."No.");
          VisibleResourceWOP.DELETEALL;
          //
          Resource.DELETE(FALSE);
        UNTIL Resource.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

