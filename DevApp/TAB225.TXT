OBJECT Table 225 Post Code
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Post Code;
    LookupPageID=Page367;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;OnValidate=VAR
                                                                PostCode@1000 : Record 225;
                                                              BEGIN
                                                                PostCode.SETCURRENTKEY("Search City");
                                                                PostCode.SETRANGE("Search City","Search City");
                                                                PostCode.SETRANGE(Code,Code);
                                                                IF PostCode.FINDFIRST THEN
                                                                  ERROR(Text000,FIELDCAPTION(Code),Code);
                                                              END;

                                                   CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 2   ;   ;City                ;Text30        ;OnValidate=VAR
                                                                PostCode@1000 : Record 225;
                                                              BEGIN
                                                                TESTFIELD(Code);
                                                                "Search City" := City;
                                                                IF xRec."Search City" <> "Search City" THEN BEGIN
                                                                  PostCode.SETCURRENTKEY("Search City");
                                                                  PostCode.SETRANGE("Search City","Search City");
                                                                  PostCode.SETRANGE(Code,Code);
                                                                  IF PostCode.FINDFIRST THEN
                                                                    ERROR(Text000,FIELDCAPTION(City),City);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=City;
                                                   NotBlank=Yes }
    { 3   ;   ;Search City         ;Code30        ;CaptionML=ENU=Search City }
    { 4   ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=ENU=Country/Region Code }
    { 5   ;   ;County              ;Text30        ;CaptionML=ENU=County }
    { 11012000;;Group              ;Code20        ;TableRelation="Post Code Group".Code;
                                                   CaptionML=ENU=Group;
                                                   Description=SERV_CONS }
    { 11012010;;Company Name       ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Post Code Group"."Company Name" WHERE (Code=FIELD(Group)));
                                                   TableRelation=Company;
                                                   CaptionML=ENU=Related Company;
                                                   Description=Central Sign In page;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code,City                               ;Clustered=Yes }
    {    ;City,Code                                }
    {    ;Search City                              }
    {    ;Country/Region Code                      }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,City,Search City,Country/Region Code,County }
    { 2   ;Brick               ;Code,City,County,Country/Region Code     }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 %2 already exists.';

    PROCEDURE ValidateCity@5(VAR City@1000 : Text[30];VAR PostCode@1001 : Code[20];VAR County@1006 : Text[30];VAR CountryCode@1005 : Code[10];UseDialog@1007 : Boolean);
    VAR
      PostCodeRec@1002 : Record 225;
      PostCodeRec2@1004 : Record 225;
      PostCodeRec3@1100485000 : Record 225;
      SearchCity@1003 : Code[32];
    BEGIN
      IF NOT GUIALLOWED THEN
        EXIT;

      IF City <> '' THEN BEGIN
        SearchCity := City;
        PostCodeRec.SETCURRENTKEY("Search City");
        IF STRPOS(SearchCity,'*') = STRLEN(SearchCity) THEN
          PostCodeRec.SETFILTER("Search City",SearchCity)
        ELSE
          PostCodeRec.SETRANGE("Search City",SearchCity);
        IF NOT PostCodeRec.FINDFIRST THEN
          //**4PS.sn
          //Some names of a city, like 's Hertogenbosch, give an error at setfilter,
          //but only a full city name will give a result at setrange;
          //test 2e time on <*SearchCity*> in behalf of amst (amsterdam/amstelveen)
          //Define Var SearchCity 2 larger than Var City
          //PostCodeRec.SETFILTER("Search City",SearchCity); //**4PS.o
        BEGIN
          IF COPYSTR(SearchCity,1,1) <> '*' THEN
            SearchCity := '*' + SearchCity;
          IF COPYSTR(SearchCity,STRLEN(SearchCity),1) <> '*' THEN
            SearchCity := SearchCity + '*';
          PostCodeRec.SETFILTER("Search City",'%1',SearchCity);
          IF NOT PostCodeRec.FIND('-') THEN
          //**4PS.en
            EXIT;
        //**4PS.sn
        END;
        PostCodeRec3.COPY(PostCodeRec);
        PostCodeRec3.SETRANGE(Code, PostCode);
        IF (PostCode <> '') AND PostCodeRec3.FINDFIRST THEN BEGIN
          City := PostCodeRec3.City;
          CountryCode := PostCodeRec3."Country/Region Code";
          County := PostCodeRec3.County;
        END ELSE BEGIN
        //**4PS.en
          PostCodeRec2.COPY(PostCodeRec);
          IF UseDialog AND (PostCodeRec2.NEXT = 1) THEN
            IF PAGE.RUNMODAL(PAGE::"Post Codes",PostCodeRec,PostCodeRec.Code) <> ACTION::LookupOK THEN
              EXIT;
          PostCode := PostCodeRec.Code;
          City := PostCodeRec.City;
          CountryCode := PostCodeRec."Country/Region Code";
          County := PostCodeRec.County;
        END; //**4PS.n
      END;
    END;

    PROCEDURE ValidatePostCode@6(VAR City@1001 : Text[30];VAR PostCode@1000 : Code[20];VAR County@1005 : Text[30];VAR CountryCode@1006 : Code[10];UseDialog@1004 : Boolean);
    VAR
      PostCodeRec@1002 : Record 225;
      PostCodeRec2@1003 : Record 225;
    BEGIN
      IF PostCode <> '' THEN BEGIN
        IF STRPOS(PostCode,'*') = STRLEN(PostCode) THEN
          PostCodeRec.SETFILTER(Code,PostCode)
        ELSE
          PostCodeRec.SETRANGE(Code,PostCode);
        IF NOT PostCodeRec.FINDFIRST THEN
        //**4PS.sn
        BEGIN
          PostCodeRec.SETFILTER(Code, '%1', COPYSTR(PostCode, 1, 4) + '*');
          IF PostCodeRec.FINDFIRST THEN
            City := PostCodeRec.City;
        //**4PS.en
          EXIT;
        END; //**4PS.n
        PostCodeRec2.COPY(PostCodeRec);
        IF UseDialog AND (PostCodeRec2.NEXT = 1) AND GUIALLOWED THEN
          IF PAGE.RUNMODAL(PAGE::"Post Codes",PostCodeRec,PostCodeRec.Code) <> ACTION::LookupOK THEN
            EXIT;
        PostCode := PostCodeRec.Code;
        City := PostCodeRec.City;
        CountryCode := PostCodeRec."Country/Region Code";
        County := PostCodeRec.County;
      END;
    END;

    PROCEDURE GetTerritoryByPostCodeGroup@1100525000(City@1100525003 : Text[50];PostCode@1100525002 : Code[20];VAR TerritoryCode@1100525004 : Code[20]);
    VAR
      PostCodeRec@1100525000 : Record 225;
      PostCodeGroupRec@1100525001 : Record 11020599;
    BEGIN
      //**4PS
      IF NOT PostCodeRec.GET(PostCode, City) THEN PostCodeRec.INIT;
      IF PostCodeRec.Group = '' THEN EXIT;

      IF NOT PostCodeGroupRec.GET(PostCodeRec.Group) THEN PostCodeGroupRec.INIT;
      IF PostCodeGroupRec.Territory = '' THEN EXIT;

      TerritoryCode := PostCodeGroupRec.Territory;  //db, 11-02-13: C005107
    END;

    BEGIN
    END.
  }
}

