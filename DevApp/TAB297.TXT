OBJECT Table 297 Issued Reminder Header
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00.00.45480,NAVSE9.FI9.DK9.NO9,4PS9.00,4PSSE;
  }
  PROPERTIES
  {
    Permissions=TableData 297=rm;
    DataCaptionFields=No.,Name;
    OnInsert=BEGIN
               //**4PS Generated Code. Do Not Change.sn05
               DPA_OnInsert;
               //**4PS Generated Code.en05
             END;

    OnDelete=BEGIN
               TESTFIELD("No. Printed");
               LOCKTABLE;
               ReminderIssue.DeleteIssuedReminderLines(Rec);

               ReminderCommentLine.SETRANGE(Type,ReminderCommentLine.Type::"Issued Reminder");
               ReminderCommentLine.SETRANGE("No.","No.");
               ReminderCommentLine.DELETEALL;
             END;

    CaptionML=ENU=Issued Reminder Header;
    LookupPageID=Page440;
    DrillDownPageID=Page440;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=ENU=No.;
                                                   NotBlank=Yes }
    { 2   ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   CaptionML=ENU=Customer No. }
    { 3   ;   ;Name                ;Text50        ;CaptionML=ENU=Name }
    { 4   ;   ;Name 2              ;Text50        ;CaptionML=ENU=Name 2 }
    { 5   ;   ;Address             ;Text50        ;CaptionML=ENU=Address }
    { 6   ;   ;Address 2           ;Text50        ;CaptionML=ENU=Address 2 }
    { 7   ;   ;Post Code           ;Code20        ;TableRelation="Post Code";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Post Code }
    { 8   ;   ;City                ;Text30        ;TableRelation="Post Code".City;
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=City }
    { 9   ;   ;County              ;Text30        ;CaptionML=ENU=County }
    { 10  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=ENU=Country/Region Code }
    { 11  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=ENU=Language Code }
    { 12  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=ENU=Currency Code }
    { 13  ;   ;Contact             ;Text50        ;CaptionML=ENU=Contact }
    { 14  ;   ;Your Reference      ;Text35        ;CaptionML=ENU=Your Reference }
    { 15  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=ENU=Shortcut Dimension 1 Code;
                                                   CaptionClass='1,2,1' }
    { 16  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   CaptionML=ENU=Shortcut Dimension 2 Code;
                                                   CaptionClass='1,2,2' }
    { 17  ;   ;Customer Posting Group;Code10      ;TableRelation="Customer Posting Group";
                                                   CaptionML=ENU=Customer Posting Group }
    { 18  ;   ;Gen. Bus. Posting Group;Code10     ;TableRelation="Gen. Business Posting Group";
                                                   CaptionML=ENU=Gen. Bus. Posting Group }
    { 19  ;   ;VAT Registration No.;Text20        ;CaptionML=ENU=VAT Registration No. }
    { 20  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=ENU=Reason Code }
    { 21  ;   ;Posting Date        ;Date          ;CaptionML=ENU=Posting Date }
    { 22  ;   ;Document Date       ;Date          ;CaptionML=ENU=Document Date }
    { 23  ;   ;Due Date            ;Date          ;CaptionML=ENU=Due Date }
    { 24  ;   ;Reminder Terms Code ;Code10        ;TableRelation="Reminder Terms";
                                                   CaptionML=ENU=Reminder Terms Code }
    { 25  ;   ;Fin. Charge Terms Code;Code10      ;TableRelation="Finance Charge Terms";
                                                   CaptionML=ENU=Fin. Charge Terms Code }
    { 26  ;   ;Interest Posted     ;Boolean       ;CaptionML=ENU=Interest Posted }
    { 27  ;   ;Additional Fee Posted;Boolean      ;CaptionML=ENU=Additional Fee Posted }
    { 28  ;   ;Reminder Level      ;Integer       ;TableRelation="Reminder Level".No. WHERE (Reminder Terms Code=FIELD(Reminder Terms Code));
                                                   CaptionML=ENU=Reminder Level }
    { 29  ;   ;Posting Description ;Text50        ;CaptionML=ENU=Posting Description }
    { 30  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Reminder Comment Line" WHERE (Type=CONST(Issued Reminder),
                                                                                                    No.=FIELD(No.)));
                                                   CaptionML=ENU=Comment;
                                                   Editable=No }
    { 31  ;   ;Remaining Amount    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Issued Reminder Line"."Remaining Amount" WHERE (Reminder No.=FIELD(No.),
                                                                                                                    Line Type=CONST(Reminder Line),
                                                                                                                    Multiple Interest Rates Entry=CONST(No)));
                                                   CaptionML=ENU=Remaining Amount;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 32  ;   ;Interest Amount     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Issued Reminder Line".Amount WHERE (Reminder No.=FIELD(No.),
                                                                                                        Type=CONST(Customer Ledger Entry),
                                                                                                        Line Type=CONST(Reminder Line),
                                                                                                        Multiple Interest Rates Entry=CONST(No)));
                                                   CaptionML=ENU=Interest Amount;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 33  ;   ;Additional Fee      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Issued Reminder Line".Amount WHERE (Reminder No.=FIELD(No.),
                                                                                                        Type=CONST(G/L Account)));
                                                   CaptionML=ENU=Additional Fee;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 34  ;   ;VAT Amount          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Issued Reminder Line"."VAT Amount" WHERE (Reminder No.=FIELD(No.),
                                                                                                              Multiple Interest Rates Entry=CONST(No)));
                                                   CaptionML=ENU=VAT Amount;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 35  ;   ;No. Printed         ;Integer       ;CaptionML=ENU=No. Printed }
    { 36  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=ENU=User ID }
    { 37  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series;
                                                   Editable=No }
    { 38  ;   ;Pre-Assigned No. Series;Code10     ;TableRelation="No. Series";
                                                   CaptionML=ENU=Pre-Assigned No. Series }
    { 39  ;   ;Pre-Assigned No.    ;Code20        ;CaptionML=ENU=Pre-Assigned No. }
    { 40  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   CaptionML=ENU=Source Code }
    { 41  ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   CaptionML=ENU=Tax Area Code }
    { 42  ;   ;Tax Liable          ;Boolean       ;CaptionML=ENU=Tax Liable }
    { 43  ;   ;VAT Bus. Posting Group;Code10      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=ENU=VAT Bus. Posting Group }
    { 44  ;   ;Add. Fee per Line   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Issued Reminder Line".Amount WHERE (Reminder No.=FIELD(No.),
                                                                                                        Type=CONST(Line Fee)));
                                                   CaptionML=ENU=Add. Fee per Line;
                                                   AutoFormatExpr="Currency Code" }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=ENU=Dimension Set ID;
                                                   Editable=No }
    { 11012001;;E-Mail Sent on     ;Date          ;CaptionML=ENU=E-Mail Sent on;
                                                   Editable=No }
    { 11012002;;E-Mail Sent by     ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              LoginManagement@1100528700 : Codeunit 418;
                                                            BEGIN
                                                              LoginManagement.LookupUserID("E-Mail Sent by");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=E-Mail Sent by;
                                                   Editable=No }
    { 11012010;;E-Mail (Reminders) ;Text80        ;OnValidate=VAR
                                                                ContactDataValidation@1100528702 : Codeunit 11012275;
                                                                RecRef@1100528701 : RecordRef;
                                                                FldRef@1100528700 : FieldRef;
                                                              BEGIN
                                                                //**4PS.sn
                                                                RecRef.GETTABLE(Rec);
                                                                FldRef := RecRef.FIELD(FIELDNO("E-Mail (Reminders)"));
                                                                ContactDataValidation.ValidateEmail(FldRef, "E-Mail (Reminders)");

                                                                IF "E-Mail (Reminders)" = '' THEN
                                                                  "Reminder per E-Mail" := FALSE;
                                                                //**4PS.en
                                                              END;

                                                   CaptionML=ENU=E-Mail (Reminders) }
    { 11012020;;Reminder per E-Mail;Boolean       ;OnValidate=BEGIN
                                                                //**4PS
                                                                IF "Reminder per E-Mail" THEN
                                                                  TESTFIELD("E-Mail (Reminders)");
                                                              END;

                                                   CaptionML=ENU=Reminder per E-Mail }
    { 11020285;;Electronic Invoicing;Option       ;OnValidate=BEGIN
                                                                //IF "Electronic Invoicing" = "Electronic Invoicing"::"E-Mail" THEN
                                                                //  TESTFIELD("E-Mail (Invoices)");
                                                              END;

                                                   CaptionML=ENU=Electronic Invoicing;
                                                   OptionCaptionML=ENU=Print,,,,,,Svefaktura (XML),OIOUBL (DK);
                                                   OptionString=Print,,E-Mail,,GLN (XML),Digipoort (XML),Svefaktura (XML),OIOUBL (DK),UBL (NO) }
    { 11126180;;Reference No.      ;Code20        ;CaptionML=ENU=Reference No.;
                                                   Description=NAVFI }
    { 11128505;;GLN                ;Code13        ;CaptionML=ENU=GLN No.;
                                                   Description=NAVNO }
    { 11128506;;Account Code NO    ;Text30        ;CaptionML=ENU=Account Code NO;
                                                   Description=NAVNO }
    { 11128512;;E-Invoice Created  ;Boolean       ;CaptionML=ENU=E-Invoice Created;
                                                   Editable=No }
    { 11128513;;E-Invoice          ;Boolean       ;CaptionML=ENU=E-Invoice;
                                                   Description=NAVNO }
    { 11128570;;EAN No.            ;Code13        ;CaptionML=ENU=EAN No.;
                                                   Description=NAVDK }
    { 11128571;;Electronic Reminder Created;Boolean;
                                                   CaptionML=ENU=Electronic Reminder Created;
                                                   Description=NAVDK;
                                                   Editable=No }
    { 11128572;;Account Code DK    ;Text30        ;CaptionML=ENU=Account Code DK;
                                                   Description=NAVDK }
    { 11128575;;Contact Phone No.  ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Contact Phone No.;
                                                   Description=NAVDK }
    { 11128576;;Contact Fax No.    ;Text30        ;CaptionML=ENU=Contact Fax No.;
                                                   Description=NAVDK }
    { 11128577;;Contact E-Mail     ;Text80        ;ExtendedDatatype=E-Mail;
                                                   CaptionML=ENU=Contact E-Mail;
                                                   Description=NAVDK }
    { 11128578;;Contact Role       ;Option        ;CaptionML=ENU=Contact Role;
                                                   OptionCaptionML=ENU=" ,,,Purchase Responsible,,,Accountant,,,Budget Responsible,,,Requisitioner";
                                                   OptionString=[ ,,,Purchase Responsible,,,Accountant,,,Budget Responsible,,,Requisitioner];
                                                   Description=NAVDK }
    { 11128590;;Payment Channel    ;Option        ;CaptionML=ENU=Payment Channel;
                                                   OptionCaptionML=ENU=" ,Payment Slip,Account Transfer,National Clearing,Direct Debit";
                                                   OptionString=[ ,Payment Slip,Account Transfer,National Clearing,Direct Debit];
                                                   Description=NAVDK }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Customer No.,Posting Date                }
    {    ;Posting Date                             }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Customer No.,Name,Posting Date       }
  }
  CODE
  {
    VAR
      IssuedReminderHeader@1000 : Record 297;
      ReminderCommentLine@1001 : Record 299;
      ReminderIssue@1002 : Codeunit 393;
      DimMgt@1003 : Codeunit 408;
      SuppresSendDialogQst@1004 : TextConst 'ENU=Do you want to suppress send dialog?';
      DPA_Management@1100000999 : Codeunit 11012783;
      Text11012001@1100285501 : TextConst 'ENU=No %1 sent, because the %2 is set to %3.';
      txtCreated@1100285500 : TextConst 'ENU=%1 reminders have been created.';

    PROCEDURE PrintRecords@1(ShowRequestForm@1000 : Boolean;SendAsEmail@1002 : Boolean;HideDialog@1003 : Boolean);
    VAR
      ReportSelection@1001 : Record 77;
      TempReportSelection@1100485000 : TEMPORARY Record 77;
      TempEmailAttachment@1100528502 : TEMPORARY Record 11229294;
      EMailSetup@1100528400 : Record 11229911;
      DocumentPrint@1100528401 : Codeunit 229;
    BEGIN
      WITH IssuedReminderHeader DO BEGIN
        COPY(Rec);
      //**4PS.so
      //ReportSelection.SETRANGE(Usage,ReportSelection.Usage::Reminder);
      //ReportSelection.SETFILTER("Report ID",'<>0');
      //ReportSelection.FIND('-');
      //REPEAT
      //  IF NOT SendAsEmail THEN
      //    REPORT.RUNMODAL(ReportSelection."Report ID",ShowRequestForm,FALSE,IssuedReminderHeader)
      //  ELSE
      //    SendReport(ReportSelection."Report ID",HideDialog,IssuedReminderHeader);
      //UNTIL ReportSelection.NEXT = 0;
      //**4PS.eo
      //**4PS.sn
        IF HideDialog THEN
          DocumentPrint.GetDefaultReportSelection(TempReportSelection.Usage::Reminder,TempReportSelection)
        ELSE
          DocumentPrint.GetReportSelection(TempReportSelection.Usage::Reminder,TempReportSelection,TRUE);
        TempReportSelection.FIND('-');
        REPEAT
          IF NOT SendAsEmail THEN
            REPORT.RUNMODAL(TempReportSelection."Report ID",ShowRequestForm,FALSE,IssuedReminderHeader)
          ELSE
            DocumentPrint.SaveAnyRecordReportAsPdf(
              IssuedReminderHeader,EMailSetup.Usage::Reminder,IssuedReminderHeader."Shortcut Dimension 1 Code",
              TempReportSelection."Report ID",TRUE,TempEmailAttachment);
        UNTIL TempReportSelection.NEXT = 0;
        IF SendAsEmail THEN
          SendReports(TempEmailAttachment,HideDialog);
      //**4PS.en
      END;
    END;

    PROCEDURE Navigate@2();
    VAR
      NavigateForm@1000 : Page 344;
    BEGIN
      NavigateForm.SetDoc("Posting Date","No.");
      NavigateForm.RUN;
    END;

    PROCEDURE FormatAddr@30(VAR AddrLines@1000 : ARRAY [8] OF Text[50]);
    VAR
      Cust@1001 : Record 18;
      FormatAddrCodeunit@1002 : Codeunit 365;
    BEGIN
      Cust.Contact := Contact;
      Cust.Name := Name;
      Cust."Name 2" := "Name 2";
      Cust.Address := Address;
      Cust."Address 2" := "Address 2";
      Cust."Post Code" := "Post Code";
      Cust.City := City;
      Cust.County := County;
      Cust."Country/Region Code" := "Country/Region Code";
      FormatAddrCodeunit.Customer(AddrLines,Cust);
    END;

    PROCEDURE IncrNoPrinted@3();
    BEGIN
      ReminderIssue.IncrNoPrinted(Rec);
    END;

    PROCEDURE ShowDimensions@4();
    BEGIN
      DimMgt.ShowDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2',TABLECAPTION,"No."));
    END;

    PROCEDURE CalculateLineFeeVATAmount@1000() : Decimal;
    VAR
      IssuedReminderLine@1000 : Record 298;
    BEGIN
      IssuedReminderLine.SETCURRENTKEY("Reminder No.",Type,"Line Type");
      IssuedReminderLine.SETRANGE("Reminder No.","No.");
      IssuedReminderLine.SETRANGE(Type,IssuedReminderLine.Type::"Line Fee");
      IssuedReminderLine.CALCSUMS("VAT Amount");
      EXIT(IssuedReminderLine."VAT Amount");
    END;

    LOCAL PROCEDURE SendReport@8(ReportId@1002 : Integer;HideDialog@1000 : Boolean;VAR IssuedReminderHeaderSource@1005 : Record 297);
    VAR
      IssuedReminderHeaderSend@1006 : Record 297;
      DocumentMailing@1003 : Codeunit 260;
      FileManagement@1001 : Codeunit 419;
      ServerAttachmentFilePath@1004 : Text[250];
    BEGIN
      IF (NOT HideDialog) AND (IssuedReminderHeaderSource.COUNT > 1) THEN
        IF CONFIRM(SuppresSendDialogQst) THEN
          HideDialog := TRUE;
      IF IssuedReminderHeader.FINDSET THEN
        REPEAT
          IssuedReminderHeaderSend.COPY(IssuedReminderHeaderSource);
          IssuedReminderHeaderSend.SETRECFILTER;
          ServerAttachmentFilePath :=
            COPYSTR(FileManagement.ServerTempFileName('pdf'),1,MAXSTRLEN(ServerAttachmentFilePath));
          REPORT.SAVEASPDF(ReportId,ServerAttachmentFilePath,IssuedReminderHeaderSend);
          COMMIT;
          DocumentMailing.EmailFileFromIssuedReminder(IssuedReminderHeaderSend,ServerAttachmentFilePath,HideDialog);
        UNTIL IssuedReminderHeader.NEXT = 0;
    END;

    LOCAL PROCEDURE SendReports@1100528500(VAR TempEmailAttachment@1100528502 : TEMPORARY Record 11229294;HideDialog@1100528501 : Boolean);
    VAR
      DocumentMailing@1100528500 : Codeunit 260;
    BEGIN
      //**4PS
      COMMIT;
      DocumentMailing.SetTempEmailAttachments(
        TempEmailAttachment);
      IF TempEmailAttachment.FINDFIRST THEN;
      DocumentMailing.EmailFileFromIssuedReminder(
        IssuedReminderHeader, TempEmailAttachment."Attachment Server File Path", HideDialog);

      IF DocumentMailing.IsSent THEN BEGIN
        IssuedReminderHeader.FIND;
        IssuedReminderHeader."E-Mail Sent by" := USERID;
        IssuedReminderHeader."E-Mail Sent on" := TODAY;
        CODEUNIT.RUN(CODEUNIT::"Issued Reminder Header-Edit", IssuedReminderHeader);
        COMMIT;
      END;
    END;

    PROCEDURE DPA_SetDefaultDpmtFromNoSeries@1100000995(xRecRef@1100525002 : RecordRef);
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      xRecRef.SETTABLE(xRec);
      DPA_Management.SetDepartmentCodeNoSeries(
        TABLENAME,
        "Shortcut Dimension 1 Code",
        "No. Series",
        xRec."No. Series" );
    END;

    LOCAL PROCEDURE DPA_OnInsert@1100000991();
    VAR
      Dimension1Code@1100528400 : Code[20];
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      Dimension1Code :=
        DPA_Management.GetNoSeriesDepartmentCode(
          "No. Series",
          xRec."No. Series",
          DATABASE::"Issued Reminder Header");

      IF Dimension1Code <> '' THEN
        "Shortcut Dimension 1 Code" := Dimension1Code;

      IF "Shortcut Dimension 1 Code" <> '' THEN
        VALIDATE("Shortcut Dimension 1 Code");
    END;

    PROCEDURE DPA_GetFieldNoDepartmentFilter@1100000993() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(0);
    END;

    PROCEDURE DPA_GetFiltersFltrGrp@1100000997(FilterGroupNo@1100525000 : Integer) FilterText : Text[250];
    BEGIN
      //**4PS Generated Code - Do Not Change
      FILTERGROUP(FilterGroupNo);
      FilterText := COPYSTR(GETFILTERS, 1, MAXSTRLEN(FilterText));
      FILTERGROUP(0);
    END;

    PROCEDURE DPA_SetFilters@1100000998(Context@1100525000 : Text[30]) : Boolean;
    VAR
      RecRef@1100525003 : RecordRef;
      i@1100525004 : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      RecRef.GETTABLE(Rec);
      DPA_Management.SetDPAFilterFromContext(
        RecRef,
        Context,
        DPA_GetFieldNoDepartmentCode,
        DPA_GetFieldNoDepartmentFilter );

      FOR i := 10 DOWNTO 0 DO BEGIN
        RecRef.FILTERGROUP(i);
        FILTERGROUP(i);
        SETVIEW(RecRef.GETVIEW(FALSE));
      END;

      EXIT(DPA_Management.IsHardFilter);
    END;

    LOCAL PROCEDURE DPA_IsActive@1100000999() : Boolean;
    VAR
      DepartmentAuthorizObject@1100526000 : Record 11012466;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DepartmentAuthorizObject.READPERMISSION THEN
        EXIT(FALSE);

      IF NOT DPA_Management.AuthorizationActive THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE DPA_FindRecWithDPAFilterAppld@1100000989();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN BEGIN
        FIND;
        EXIT;
      END;

      IF NOT DPA_Management.HasTableHardFilter(297) THEN BEGIN
        FIND;
        EXIT;
      END;

      DPA_SetFilters('Table 297');
      IF NOT FIND THEN
        DPA_Management.RaiseDPAErrorForTable(TABLECAPTION);
    END;

    PROCEDURE DPA_ChkDepartmentCodeAllowed@1100000990(IsHardFilter@1100526000 : Boolean);
    VAR
      DimensionManagement@1100526001 : Codeunit 408;
    BEGIN
      //**4PS Generated Code - Do Not Change
      DimensionManagement.CheckDepartmAuthWithFilterType(
        DATABASE::"Issued Reminder Header", "Shortcut Dimension 1 Code", IsHardFilter);
    END;

    PROCEDURE DPA_GetFieldNoDepartmentCode@1100000994() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(FIELDNO("Shortcut Dimension 1 Code"));
    END;

    PROCEDURE DPA_SetDefaultDepartment@1100000996();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      DPA_Management.SetDepartmentCodeUser(TABLENAME, "Shortcut Dimension 1 Code");
    END;

    PROCEDURE AccountCodeLineSpecified@1101100000(CountryCode@1100285500 : Code[10]) : Boolean;
    VAR
      IssuedReminderLine@1101100000 : Record 298;
    BEGIN
      //NAVDK
      //NAVNO
      IssuedReminderLine.RESET;
      IssuedReminderLine.SETRANGE("Reminder No.", "No.");
      IssuedReminderLine.SETFILTER(Type, '>%1', IssuedReminderLine.Type::" ");
      CASE CountryCode OF
        'DK': IssuedReminderLine.SETFILTER("Account Code DK", '<>%1&<>%2', '', "Account Code DK");
        'NO': IssuedReminderLine.SETFILTER("Account Code NO", '<>%1&<>%2', '', "Account Code NO");
      END;
      EXIT(NOT IssuedReminderLine.ISEMPTY);
    END;

    PROCEDURE TaxLineSpecified@1101100001() : Boolean;
    VAR
      IssuedReminderLine@1101100000 : Record 298;
    BEGIN
      IssuedReminderLine.RESET;
      IssuedReminderLine.SETRANGE("Reminder No.", "No.");
      IssuedReminderLine.SETFILTER(Type, '>%1', IssuedReminderLine.Type::" ");
      IssuedReminderLine.FIND('-');
      IssuedReminderLine.SETFILTER("VAT %", '<>%1', IssuedReminderLine."VAT %");
      EXIT(NOT IssuedReminderLine.ISEMPTY);
    END;

    PROCEDURE GetDescription@1101100005() : Text[1024];
    VAR
      AppMgt@1101100001 : Codeunit 1;
    BEGIN
      //NAVDK
      EXIT(STRSUBSTNO('%1 %2 %3%4', TABLECAPTION,FIELDCAPTION("No."), "No.", CrLf));
    END;

    LOCAL PROCEDURE CrLf@1101100009() CrLf : Text[2];
    BEGIN
      //NAVDK
      CrLf[1] := 13;
      CrLf[2] := 10;
    END;

    PROCEDURE PrintRecordsWithRequestPage@1100528400();
    VAR
      TempReportSelection@1100528401 : TEMPORARY Record 77;
      DocPrint@1100485001 : Codeunit 229;
    BEGIN
      //**4PS
      DocPrint.GetReportSelection(TempReportSelection.Usage::Reminder, TempReportSelection, TRUE);
      TempReportSelection.FINDFIRST;
      REPEAT
        REPORT.RUNMODAL(TempReportSelection."Report ID", TRUE, FALSE, Rec);
      UNTIL TempReportSelection.NEXT = 0;
    END;

    LOCAL PROCEDURE SendReminderElectronically@1100285500();
    VAR
      SelectSendOptionReminder@1100285510 : Page 11128044;
      lvIssuedReminderHeader2@1100285508 : Record 297;
      lvSalesRecSetup@1100285507 : Record 311;
      lvFilename@1100285506 : Text;
      TempReportSelection@1100285503 : TEMPORARY Record 77;
      DocPrint@1100285502 : Codeunit 229;
      ServerTempFilename@1100285501 : Text;
      FileMgmt@1100285500 : Codeunit 419;
      lvCounter@1100285000 : Integer;
    BEGIN
      //141105 ENH053 ITERO.MH

      IF (SelectSendOptionReminder.RUNMODAL <> ACTION::LookupOK) THEN
          EXIT;

      IF (SelectSendOptionReminder.GetSelectPDF)
      THEN BEGIN
         lvSalesRecSetup.GET();
         lvSalesRecSetup.TESTFIELD("Export Path Reminder PDF");
         DocPrint.GetReportSelection(TempReportSelection.Usage::Reminder, TempReportSelection, TRUE);
         TempReportSelection.FINDFIRST;
         IF (FINDSET(TRUE)) THEN REPEAT

            CLEAR(lvIssuedReminderHeader2);
            lvIssuedReminderHeader2 := Rec;
            lvIssuedReminderHeader2.SETRECFILTER;
            MARK(FALSE);  // 160522 ITERO.AC IME476 Bug fix in order to avoid perpetual loop

            REPEAT
               ServerTempFilename := FileMgmt.ServerTempFileName('pdf');
               lvFilename := lvSalesRecSetup."Export Path Reminder PDF" + '\Rem_' + lvIssuedReminderHeader2."No." + '_'+  FORMAT(TempReportSelection."Report ID") +
               FORMAT(TODAY,0,'<year4><month,2><day,2>') + '_' +FORMAT(TIME,0,'<Standard format,2>') + '.pdf';

               REPORT.SAVEASPDF(TempReportSelection."Report ID", ServerTempFilename, lvIssuedReminderHeader2);
               FileMgmt.DownloadToFile(ServerTempFilename, lvFilename);
               lvCounter +=1;   // 160522 ITERO.AC IME476
            UNTIL TempReportSelection.NEXT = 0;

            lvIssuedReminderHeader2.GET(lvIssuedReminderHeader2."No.");
            lvIssuedReminderHeader2."Electronic Reminder Created" := TRUE;
            lvIssuedReminderHeader2.MODIFY(FALSE);
         UNTIL NEXT = 0;

         //MESSAGE(txtCreated, COUNT);
         MESSAGE(txtCreated, lvCounter);  // 160522 ITERO.AC IME476 Use lvCounter instead of COUNT
      END;
    END;

    PROCEDURE SendReminderElectronic@1210190009(SelectSendOption@1100528701 : Boolean;FromPost@1100528202 : Boolean);
    VAR
      ExportSvefaktura@1100285500 : Codeunit 11128055;
      IssuedReminderHeader@1001 : Record 297;
      CreateDKElecReminders@1002 : Report 11128572;
      SelectSendOptionIssuedRem@1000 : Page 11128044;
    BEGIN
      //**4PS, DP00141, Digipoort Invoice
      IssuedReminderHeader.COPY(Rec);
      IssuedReminderHeader.SETRECFILTER;

      IF (IssuedReminderHeader."Electronic Invoicing" = IssuedReminderHeader."Electronic Invoicing"::Print) THEN
      BEGIN
        IF GUIALLOWED THEN MESSAGE(Text11012001,
          IssuedReminderHeader.TABLECAPTION, IssuedReminderHeader.FIELDCAPTION("Electronic Invoicing"),
          IssuedReminderHeader."Electronic Invoicing");
        ERROR('');
      END;

      IF SelectSendOption THEN BEGIN
        //>>4PSSE
        SelectSendOptionIssuedRem.SetSelectPDF(
          IssuedReminderHeader."Electronic Invoicing" =
          IssuedReminderHeader."Electronic Invoicing"::"Svefaktura (XML)");
        //<<4PSSE
        //>>NAVDK
        SelectSendOptionIssuedRem.SetSelectOIOUBL(
          IssuedReminderHeader."Electronic Invoicing" =
          IssuedReminderHeader."Electronic Invoicing"::"OIOUBL (DK)");
        //<<NAVDK
        IF (SelectSendOptionIssuedRem.RUNMODAL <> ACTION::LookupOK) THEN
          EXIT;
      END;

      //User must have option to stop te send option after the menu-choice in the page/form
      //For this "NOT SelectSendOption" can be used. because with selectoption the choice to stop is already there

      //>>4PSSE
      IF ((NOT SelectSendOption) AND
          (IssuedReminderHeader."Electronic Invoicing" = IssuedReminderHeader."Electronic Invoicing"::"Svefaktura (XML)")) OR
         (SelectSendOptionIssuedRem.GetSelectPDF)
      THEN BEGIN
        SendReminderElectronically;
      END;
      //<<4PSSE

      //>>4PSSE.DK
      IF ((NOT SelectSendOption) AND
          (IssuedReminderHeader."Electronic Invoicing" = IssuedReminderHeader."Electronic Invoicing"::"OIOUBL (DK)")) OR
         (SelectSendOptionIssuedRem.GetSelectOIOUBL)
      THEN BEGIN
        CreateDKElecReminders.USEREQUESTPAGE := NOT SelectSendOption;
        CreateDKElecReminders.SETTABLEVIEW(IssuedReminderHeader);
        CreateDKElecReminders.RUNMODAL;
      END;
      //<<4PSSE.DK
    END;

    BEGIN
    {
      4PS, dbeekman, 22-10-2008:
        - ReportSelection changed. cod229.GetReportSelection called to select reports to be printed
      4PS, hbk, 17-03-2009
        - Added key: "Posting date" for call 12670 (used in UpdateReminders in T11012395)
      4PS, rdblaey, 10-11-2011
        - Add field 'E-Mail created'

      ITERO.MH IME210 141105 Merged functionality for multiple reminder from 4PSNL
      ITERO.MH ENH053 141105 Added new function SendReminderElectronically
      ITERO.MH ENH053 141110 Added Modify-permission on table
      160522 ITERO.AC IME476 Bug fix in order to avoid perpetual loop in SendReminderElectronically()
      Version List=NAVW19.00.00.45480,NAVSE9.00.00.45480;
      Version List=NAVW19.00.00.45480,NAVDK9.00.00.45480;
      Version List=NAVW19.00.00.45480,NAVNO9.00.00.45480;
    }
    END.
  }
}

