OBJECT Table 3 Payment Terms
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,4PS9.00,NAVFI9.DK9,4PSSE;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnDelete=VAR
               PaymentTermsTranslation@1000 : Record 462;
             BEGIN
               WITH PaymentTermsTranslation DO BEGIN
                 SETRANGE("Payment Term",Code);
                 DELETEALL
               END;
             END;

    CaptionML=ENU=Payment Terms;
    LookupPageID=Page4;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 2   ;   ;Due Date Calculation;DateFormula   ;OnValidate=BEGIN
                                                                CheckFormulas; //**4PS.n
                                                              END;

                                                   CaptionML=ENU=Due Date Calculation }
    { 3   ;   ;Discount Date Calculation;DateFormula;
                                                   OnValidate=BEGIN
                                                                CheckFormulas; //**4PS.n
                                                              END;

                                                   CaptionML=ENU=Discount Date Calculation }
    { 4   ;   ;Discount %          ;Decimal       ;CaptionML=ENU=Discount %;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 5   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 6   ;   ;Calc. Pmt. Disc. on Cr. Memos;Boolean;
                                                   CaptionML=ENU=Calc. Pmt. Disc. on Cr. Memos }
    { 11012000;;Discount Date Calculation 2;DateFormula;
                                                   OnValidate=BEGIN
                                                                CheckFormulas;
                                                              END;

                                                   CaptionML=ENU=Discount Date Calculation 2;
                                                   Description=4PS }
    { 11012001;;Discount % 2       ;Decimal       ;CaptionML=ENU=Discount % 2;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Description=4PS }
    { 11012002;;Discount Date Calculation 3;DateFormula;
                                                   OnValidate=BEGIN
                                                                CheckFormulas;
                                                              END;

                                                   CaptionML=ENU=Discount Date Calculation 3;
                                                   Description=4PS }
    { 11012003;;Discount % 3       ;Decimal       ;CaptionML=ENU=Discount % 3;
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Description=4PS }
    { 11126181;;Disreg. Pmt. Disc. at Full Pmt;Boolean;
                                                   CaptionML=ENU=Disreg. Pmt. Disc. at Full Pmt;
                                                   Description=NAVFI }
    { 11128570;;OIOUBL Code        ;Option        ;CaptionML=ENU=OIOUBL Code;
                                                   OptionCaptionML=ENU=" ,Contract,Specific";
                                                   OptionString=[ ,Contract,Specific];
                                                   Description=NAVDK }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Description,Due Date Calculation    }
    { 2   ;Brick               ;Code,Description,Due Date Calculation    }
  }
  CODE
  {
    VAR
      Text001@1100485000 : TextConst 'ENU=Due Dates of all formulas must entered based on posting date.';

    PROCEDURE TranslateDescription@1(VAR PaymentTerms@1000 : Record 3;Language@1001 : Code[10]);
    VAR
      PaymentTermsTranslation@1002 : Record 462;
    BEGIN
      IF PaymentTermsTranslation.GET(PaymentTerms.Code,Language) THEN
        PaymentTerms.Description := PaymentTermsTranslation.Description;
    END;

    PROCEDURE CheckFormulas@1100485000();
    VAR
      RefDate@1100485000 : ARRAY [5] OF Date;
    BEGIN
      //**4PS.n
      // Startdate
      RefDate[1] := 000101D; // Take a long month (31 days as a reference month). In this case 1st jan. 2000.

      // Discount Date 1
      IF FORMAT("Discount Date Calculation") <> '' THEN
        RefDate[2] := CALCDATE("Discount Date Calculation", RefDate[1])
      ELSE
        RefDate[2] := RefDate[1];

      // Discount Date 2
      IF FORMAT("Discount Date Calculation 2") <> '' THEN
        RefDate[3] := CALCDATE("Discount Date Calculation 2", RefDate[1])
      ELSE
        RefDate[3] := RefDate[2];

      // Discount Date 3
      IF FORMAT("Discount Date Calculation 3") <> '' THEN
        RefDate[4] := CALCDATE("Discount Date Calculation 3", RefDate[1])
      ELSE
        RefDate[4] := RefDate[3];

      // Due Date
      RefDate[5] := CALCDATE("Due Date Calculation", RefDate[1]);

      IF RefDate[1] > RefDate[2] THEN
        ERROR(Text001)
      ELSE IF RefDate[2] > RefDate[3] THEN
        ERROR(Text001)
      ELSE IF RefDate[3] > RefDate[4] THEN
        ERROR(Text001)
      ELSE IF RefDate[4] > RefDate[5] THEN
        ERROR(Text001);
    END;

    BEGIN
    END.
  }
}

