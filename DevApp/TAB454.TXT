OBJECT Table 454 Approval Entry
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,DCW19.00.00.4.04,4PS9.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               "Last Date-Time Modified" := CREATEDATETIME(TODAY,TIME);
               "Last Modified By User ID" := USERID;
             END;

    CaptionML=ENU=Approval Entry;
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;CaptionML=ENU=Table ID }
    { 2   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU="Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order, ";
                                                   OptionString=[Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order, ] }
    { 3   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No. }
    { 4   ;   ;Sequence No.        ;Integer       ;CaptionML=ENU=Sequence No. }
    { 5   ;   ;Approval Code       ;Code20        ;CaptionML=ENU=Approval Code }
    { 6   ;   ;Sender ID           ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=BEGIN
                                                              UserMgt.LookupUserID("Sender ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Sender ID }
    { 7   ;   ;Salespers./Purch. Code;Code10      ;CaptionML=ENU=Salespers./Purch. Code }
    { 8   ;   ;Approver ID         ;Code50        ;TableRelation="User Setup";
                                                   OnValidate=VAR
                                                                UserSetup@1100525000 : Record 91;
                                                              BEGIN
                                                                 //**4PS.n  Call C022612
                                                                IF "Approver ID" <> '' THEN BEGIN
                                                                  UserSetup.RESET;
                                                                  UserSetup.GET("Approver ID");
                                                                  UserSetup.TESTFIELD("Approver ID"); //In the User Setup Table the field Approver ID may not be empty
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              //UserMgt.LookupUserID("Approver ID"); //**4PS.o
                                                              IF LookupUserSetupName("Approver ID") THEN;    //**4PS.n  Call C022612
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Approver ID }
    { 9   ;   ;Status              ;Option        ;OnValidate=BEGIN
                                                                IF (xRec.Status = Status::Created) AND (Status = Status::Open) THEN
                                                                  "Date-Time Sent for Approval" := CREATEDATETIME(TODAY,TIME);
                                                              END;

                                                   CaptionML=ENU=Status;
                                                   OptionCaptionML=ENU=Created,Open,Canceled,Rejected,Approved;
                                                   OptionString=Created,Open,Canceled,Rejected,Approved }
    { 10  ;   ;Date-Time Sent for Approval;DateTime;
                                                   CaptionML=ENU=Date-Time Sent for Approval }
    { 11  ;   ;Last Date-Time Modified;DateTime   ;CaptionML=ENU=Last Date-Time Modified }
    { 12  ;   ;Last Modified By User ID;Code50    ;TableRelation=User."User Name";
                                                   OnLookup=BEGIN
                                                              UserMgt.LookupUserID("Last Modified By User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Last Modified By User ID }
    { 13  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Approval Comment Line" WHERE (Table ID=FIELD(Table ID),
                                                                                                    Record ID to Approve=FIELD(Record ID to Approve)));
                                                   CaptionML=ENU=Comment;
                                                   Editable=No }
    { 14  ;   ;Due Date            ;Date          ;CaptionML=ENU=Due Date }
    { 15  ;   ;Amount              ;Decimal       ;CaptionML=ENU=Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 16  ;   ;Amount (LCY)        ;Decimal       ;CaptionML=ENU=Amount (LCY);
                                                   AutoFormatType=1 }
    { 17  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=ENU=Currency Code }
    { 18  ;   ;Approval Type       ;Option        ;CaptionML=ENU=Approval Type;
                                                   OptionCaptionML=ENU=Workflow User Group,Sales Pers./Purchaser,Approver,Advanced Approver,,,,,Consent Rules;
                                                   OptionString=Workflow User Group,Sales Pers./Purchaser,Approver,Advanced Approver,,,,,Consent Rules }
    { 19  ;   ;Limit Type          ;Option        ;CaptionML=ENU=Limit Type;
                                                   OptionCaptionML=ENU=Approval Limits,Credit Limits,Request Limits,No Limits;
                                                   OptionString=Approval Limits,Credit Limits,Request Limits,No Limits }
    { 20  ;   ;Available Credit Limit (LCY);Decimal;
                                                   CaptionML=ENU=Available Credit Limit (LCY) }
    { 21  ;   ;Pending Approvals   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Approval Entry" WHERE (Record ID to Approve=FIELD(Record ID to Approve),
                                                                                             Status=FILTER(Created|Open),
                                                                                             Workflow Step Instance ID=FIELD(Workflow Step Instance ID)));
                                                   CaptionML=ENU=Pending Approvals }
    { 22  ;   ;Record ID to Approve;RecordID      ;CaptionML=ENU=Record ID to Approve }
    { 23  ;   ;Delegation Date Formula;DateFormula;CaptionML=ENU=Delegation Date Formula }
    { 26  ;   ;Number of Approved Requests;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("Approval Entry" WHERE (Record ID to Approve=FIELD(Record ID to Approve),
                                                                                             Status=FILTER(Approved),
                                                                                             Workflow Step Instance ID=FIELD(Workflow Step Instance ID)));
                                                   CaptionML=ENU=Number of Approved Requests }
    { 27  ;   ;Number of Rejected Requests;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("Approval Entry" WHERE (Record ID to Approve=FIELD(Record ID to Approve),
                                                                                             Status=FILTER(Rejected),
                                                                                             Workflow Step Instance ID=FIELD(Workflow Step Instance ID)));
                                                   CaptionML=ENU=Number of Rejected Requests }
    { 29  ;   ;Entry No.           ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=ENU=Entry No. }
    { 30  ;   ;Workflow Step Instance ID;GUID     ;CaptionML=ENU=Workflow Step Instance ID }
    { 31  ;   ;Related to Change   ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Workflow - Record Change" WHERE (Workflow Step Instance ID=FIELD(Workflow Step Instance ID),
                                                                                                       Record ID=FIELD(Record ID to Approve)));
                                                   CaptionML=ENU=Related to Change }
    { 6085573;;Amount Incl. VAT    ;Decimal       ;CaptionML=ENU=Amount Incl. VAT }
    { 6085574;;Amount Incl. VAT (LCY);Decimal     ;CaptionML=ENU=Amount Incl. VAT (LCY) }
    { 6085575;;Original Approver ID;Code50        ;CaptionML=ENU=Original Approver ID }
    { 6085576;;Temp. Entry Type    ;Option        ;CaptionML=ENU=Temp. Entry Type;
                                                   OptionCaptionML=ENU=Normal,Out of Office Sharing,Normal Sharing;
                                                   OptionString=Normal,Out of Office Sharing,Normal Sharing }
    { 6085577;;Temp. Display Sorting;Integer      ;CaptionML=ENU=Temp. Display Sorting }
    { 6085578;;Reminder Level      ;Integer       ;CaptionML=ENU=Reminder Level }
    { 6085787;;Delegated By User ID;Code50        ;TableRelation=User."User Name";
                                                   CaptionML=ENU=Delegated By User ID }
    { 6085788;;Delegated To User ID;Code50        ;TableRelation=User."User Name";
                                                   CaptionML=ENU=Delegated To User ID }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Record ID to Approve,Workflow Step Instance ID,Sequence No. }
    {    ;Table ID,Document Type,Document No.,Sequence No.,Record ID to Approve }
    {    ;Approver ID,Status                       }
    {    ;Sender ID                                }
    {    ;Due Date                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      UserMgt@1000 : Codeunit 418;
      PageManagement@1001 : Codeunit 700;
      RecNotExistTxt@1002 : TextConst 'ENU=The record does not exist.';
      ChangeRecordDetailsTxt@1003 : TextConst 'ENU="; %1 changed from %2 to %3"';

    PROCEDURE ShowRecord@1();
    VAR
      RecRef@1000 : RecordRef;
    BEGIN
      IF NOT RecRef.GET("Record ID to Approve") THEN
        EXIT;
      RecRef.SETRECFILTER;
      PageManagement.PageRun(RecRef);
    END;

    PROCEDURE RecordCaption@4() : Text;
    VAR
      Object@1000 : Record 2000000001;
      RecRef@1002 : RecordRef;
      PageNo@1001 : Integer;
    BEGIN
      IF NOT RecRef.GET("Record ID to Approve") THEN
        EXIT;
      PageNo := PageManagement.GetPageID(RecRef);
      IF PageNo = 0 THEN
        EXIT;
      Object.GET(Object.Type::Page,'',PageNo);
      Object.CALCFIELDS(Caption);
      EXIT(STRSUBSTNO('%1 %2',Object.Caption,"Document No."));
    END;

    PROCEDURE RecordDetails@3() : Text;
    VAR
      SalesHeader@1001 : Record 36;
      PurchHeader@1000 : Record 38;
      RecRef@1002 : RecordRef;
      ChangeRecordDetails@1003 : Text;
    BEGIN
      IF NOT RecRef.GET("Record ID to Approve") THEN
        EXIT(RecNotExistTxt);

      ChangeRecordDetails := GetChangeRecordDetails;

      CASE RecRef.NUMBER OF
        DATABASE::"Sales Header":
          BEGIN
            RecRef.SETTABLE(SalesHeader);
            SalesHeader.CALCFIELDS(Amount);
            EXIT(STRSUBSTNO('%1 ; %2: %3',SalesHeader."Sell-to Customer Name",
                SalesHeader.FIELDCAPTION(Amount),SalesHeader.Amount));
          END;
        DATABASE::"Purchase Header":
          BEGIN
            RecRef.SETTABLE(PurchHeader);
            PurchHeader.CALCFIELDS(Amount);
            EXIT(STRSUBSTNO('%1 ; %2: %3',PurchHeader."Buy-from Vendor Name",
                PurchHeader.FIELDCAPTION(Amount),PurchHeader.Amount));
          END;
        ELSE
          EXIT(FORMAT("Record ID to Approve",0,1) + ChangeRecordDetails);
      END;
    END;

    PROCEDURE IsOverdue@5() : Boolean;
    BEGIN
      EXIT((Status IN [Status::Created,Status::Open]) AND ("Due Date" < TODAY));
    END;

    PROCEDURE GetCustVendorDetails@6(VAR CustVendorNo@1002 : Code[20];VAR CustVendorName@1003 : Text[50]);
    VAR
      PurchaseHeader@1000 : Record 38;
      SalesHeader@1001 : Record 36;
      Customer@1005 : Record 18;
      RecRef@1004 : RecordRef;
    BEGIN
      IF NOT RecRef.GET("Record ID to Approve") THEN
        EXIT;

      CASE "Table ID" OF
        DATABASE::"Purchase Header":
          BEGIN
            RecRef.SETTABLE(PurchaseHeader);
            CustVendorNo := PurchaseHeader."Pay-to Vendor No.";
            CustVendorName := PurchaseHeader."Pay-to Name";
          END;
        DATABASE::"Sales Header":
          BEGIN
            RecRef.SETTABLE(SalesHeader);
            CustVendorNo := SalesHeader."Bill-to Customer No.";
            CustVendorName := SalesHeader."Bill-to Name";
          END;
        DATABASE::Customer:
          BEGIN
            RecRef.SETTABLE(Customer);
            CustVendorNo := Customer."No.";
            CustVendorName := Customer.Name;
          END;
      END;
    END;

    PROCEDURE GetChangeRecordDetails@2() ChangeDetails : Text;
    VAR
      WorkflowRecordChange@1000 : Record 1525;
      OldValue@1001 : Text;
      NewValue@1002 : Text;
    BEGIN
      WorkflowRecordChange.SETRANGE("Record ID","Record ID to Approve");
      WorkflowRecordChange.SETRANGE("Workflow Step Instance ID","Workflow Step Instance ID");

      IF WorkflowRecordChange.FINDSET THEN
        REPEAT
          WorkflowRecordChange.CALCFIELDS("Field Caption");
          NewValue := WorkflowRecordChange.GetFormattedNewValue(TRUE);
          OldValue := WorkflowRecordChange.GetFormattedOldValue(TRUE);
          ChangeDetails += STRSUBSTNO(ChangeRecordDetailsTxt,WorkflowRecordChange."Field Caption",
              OldValue,NewValue);
        UNTIL WorkflowRecordChange.NEXT = 0;
    END;

    PROCEDURE LookupUserSetupName@11012003(VAR UserName@1000 : Code[50]) : Boolean;
    VAR
      UserSetup@1100525001 : Record 91;
    BEGIN
      //**4PS  Call C022612
      UserSetup.RESET;
      UserSetup.SETCURRENTKEY("User ID");
      UserSetup.FILTERGROUP(9);
      UserSetup.SETFILTER("Approver ID", '<>%1', '');
      UserSetup.FILTERGROUP(0);
      UserSetup."User ID" := UserName;

      IF NOT UserSetup.FIND('=><') THEN
        EXIT(FALSE);

      IF PAGE.RUNMODAL(0, UserSetup) = ACTION::LookupOK THEN BEGIN
        UserName := UserSetup."User ID";
        EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    BEGIN
    {
      <DC>
        Document Capture
      </DC>
    }
    END.
  }
}

