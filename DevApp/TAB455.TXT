OBJECT Table 455 Approval Comment Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "User ID" := USERID;
               "Date and Time" := CREATEDATETIME(TODAY,TIME);
               IF "Entry No." = 0 THEN
                 "Entry No." := GetNextEntryNo;
             END;

    CaptionML=ENU=Approval Comment Line;
    LookupPageID=Page660;
    DrillDownPageID=Page660;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=ENU=Entry No.;
                                                   Editable=No }
    { 2   ;   ;Table ID            ;Integer       ;CaptionML=ENU=Table ID;
                                                   Editable=No }
    { 3   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU="Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order, ";
                                                   OptionString=[Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order, ];
                                                   Editable=No }
    { 4   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No.;
                                                   Editable=No }
    { 5   ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=ENU=User ID;
                                                   Editable=No }
    { 6   ;   ;Date and Time       ;DateTime      ;CaptionML=ENU=Date and Time;
                                                   Editable=No }
    { 7   ;   ;Comment             ;Text80        ;CaptionML=ENU=Comment }
    { 8   ;   ;Record ID to Approve;RecordID      ;CaptionML=ENU=Record ID to Approve }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Table ID,Document Type,Document No.,Record ID to Approve }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    LOCAL PROCEDURE GetNextEntryNo@4() : Integer;
    VAR
      ApprovalCommentLine@1000 : Record 455;
    BEGIN
      ApprovalCommentLine.SETCURRENTKEY("Entry No.");
      IF ApprovalCommentLine.FINDLAST THEN
        EXIT(ApprovalCommentLine."Entry No." + 1);

      EXIT(1);
    END;

    PROCEDURE ShowDCComments@1100485001(IDocNo@1100485000 : Code[20]);
    VAR
      lvPurchInvoiceRec@1100485005 : Record 122;
      lvPurchCredMemRec@1100485004 : Record 124;
      lvPurchHeadRec@1100485006 : Record 38;
      lvFound@1100485003 : Boolean;
      lvDocumentType@1100485002 : Option;
      lvtableID@1100485001 : Integer;
      Comments@1100528200 : Record 455;
      TempComments@1100528201 : TEMPORARY Record 455;
      CommentsHistory@1100528202 : Record 457;
      LastLine@1100528203 : Integer;
      ListPage@1100528204 : Page 660;
    BEGIN
      //**4PS
      lvFound := FALSE;
      IF IDocNo = '' THEN
        EXIT;

      IF NOT lvFound THEN BEGIN
        lvFound := lvPurchInvoiceRec.GET(IDocNo);
        IF lvFound THEN BEGIN
          lvtableID := 122;
          lvDocumentType := "Document Type"::Invoice;
        END
      END;
      IF NOT lvFound THEN BEGIN
        lvFound := lvPurchCredMemRec.GET(IDocNo);
        IF lvFound THEN BEGIN
          lvtableID := 124;
          lvDocumentType := "Document Type"::"Credit Memo";
        END;
      END;

      IF NOT lvFound THEN BEGIN
        lvFound := lvPurchHeadRec.GET(lvPurchHeadRec."Document Type"::Invoice, IDocNo);
        IF lvFound THEN BEGIN
          lvtableID := 38;
          lvDocumentType := "Document Type"::Invoice;
        END
      END;
      IF NOT lvFound THEN BEGIN
        lvFound := lvPurchHeadRec.GET(lvPurchHeadRec."Document Type"::"Credit Memo", IDocNo);
        IF lvFound THEN BEGIN
          lvtableID := 38;
          lvDocumentType := "Document Type"::"Credit Memo";
        END
      END;

      IF NOT lvFound THEN
        EXIT;

      Comments.SETCURRENTKEY("Table ID","Document Type","Document No.");
      Comments.SETRANGE("Table ID",lvtableID);
      Comments.SETRANGE("Document Type",lvDocumentType);
      Comments.SETRANGE("Document No.", IDocNo);
      IF Comments.FINDSET THEN
        REPEAT
          TempComments.TRANSFERFIELDS(Comments);
          TempComments.INSERT;
        UNTIL Comments.NEXT=0;
      LastLine := Comments."Entry No.";

      CommentsHistory.SETCURRENTKEY("Table ID","Document No.");
      CommentsHistory.SETRANGE("Table ID",lvtableID);
      CommentsHistory.SETRANGE("Document No.", IDocNo);
      IF CommentsHistory.FINDSET THEN
        REPEAT
          TempComments.TRANSFERFIELDS(Comments);
          LastLine += 1;
          TempComments."Document Type" := lvDocumentType;
          TempComments."Entry No." := LastLine;
          TempComments.INSERT;
        UNTIL CommentsHistory.NEXT=0;

      TempComments.SETRANGE("Document Type", lvDocumentType);
      IF PAGE.RUNMODAL(PAGE::"Purch. Approval Comment List", TempComments) = ACTION::LookupOK THEN;
    END;

    BEGIN
    END.
  }
}

