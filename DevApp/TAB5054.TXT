OBJECT Table 5054 Contact Business Relation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ContBusRel@1000 : Record 5054;
               Cont@1001 : Record 5050;
             BEGIN
               IF "No." <> '' THEN BEGIN
                 ContBusRel.SETRANGE("Contact No.","Contact No.");
                 ContBusRel.SETRANGE("Link to Table","Link to Table");
                 IF ContBusRel.FINDFIRST THEN
                   ERROR(
                     Text000,
                     Cont.TABLECAPTION,"Contact No.",TABLECAPTION,"Link to Table",ContBusRel."No.");

                 ContBusRel.RESET;
                 ContBusRel.SETCURRENTKEY("Link to Table","No.");
                 ContBusRel.SETRANGE("Link to Table","Link to Table");
                 ContBusRel.SETRANGE("No.","No.");
                 IF ContBusRel.FINDFIRST THEN
                   ERROR(
                     Text000,
                     "Link to Table","No.",TABLECAPTION,Cont.TABLECAPTION,ContBusRel."Contact No.");

                 ContBusRel.RESET;
                 ContBusRel.SETRANGE("Contact No.","Contact No.");
                 ContBusRel.SETRANGE("Business Relation Code","Business Relation Code");
                 ContBusRel.SETRANGE("No.",'');
                 ContBusRel.DELETEALL;
               END;

               TouchContact("Contact No.");
               //**4PS.sn
               CentralDataMgt.UpdateBusinessRelation(Rec,1,xRec);
               //**4PS.en
             END;

    OnModify=BEGIN
               TouchContact("Contact No.");
               //**4PS.sn
               CentralDataMgt.UpdateBusinessRelation(Rec,2,xRec);
               //**4PS.en
             END;

    OnDelete=BEGIN
               TouchContact("Contact No.");
               //**4PS.sn
               CentralDataMgt.UpdateBusinessRelation(Rec,4,xRec);
               //**4PS.en
             END;

    OnRename=BEGIN
               IF xRec."Contact No." = "Contact No." THEN
                 TouchContact("Contact No.")
               ELSE BEGIN
                 TouchContact("Contact No.");
                 TouchContact(xRec."Contact No.");
               END;
             END;

    CaptionML=ENU=Contact Business Relation;
    LookupPageID=Page5062;
    DrillDownPageID=Page5061;
  }
  FIELDS
  {
    { 1   ;   ;Contact No.         ;Code20        ;TableRelation=Contact;
                                                   OnValidate=BEGIN
                                                                IF "Contact No." <> '' THEN
                                                                  VALIDATE("Business Relation Code");
                                                              END;

                                                   CaptionML=ENU=Contact No.;
                                                   NotBlank=Yes }
    { 2   ;   ;Business Relation Code;Code10      ;TableRelation="Business Relation";
                                                   OnValidate=VAR
                                                                RMSetup@1000 : Record 5079;
                                                                Cust@1001 : Record 18;
                                                                Vend@1002 : Record 23;
                                                                BankAcc@1003 : Record 270;
                                                              BEGIN
                                                                IF ("No." = '') AND
                                                                   ("Contact No." <> '') AND
                                                                   ("Business Relation Code" <> '') AND
                                                                   (CurrFieldNo <> 0)
                                                                THEN BEGIN
                                                                  RMSetup.GET;
                                                                  IF "Business Relation Code" = RMSetup."Bus. Rel. Code for Customers" THEN
                                                                    ERROR(Text001,
                                                                      FIELDCAPTION("Business Relation Code"),"Business Relation Code",
                                                                      Cont.TABLECAPTION,Cust.TABLECAPTION);
                                                                  IF "Business Relation Code" = RMSetup."Bus. Rel. Code for Vendors" THEN
                                                                    ERROR(Text001,
                                                                      FIELDCAPTION("Business Relation Code"),"Business Relation Code",
                                                                      Cont.TABLECAPTION,Vend.TABLECAPTION);
                                                                  IF "Business Relation Code" = RMSetup."Bus. Rel. Code for Bank Accs." THEN
                                                                    ERROR(Text001,
                                                                      FIELDCAPTION("Business Relation Code"),"Business Relation Code",
                                                                      Cont.TABLECAPTION,BankAcc.TABLECAPTION);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Business Relation Code;
                                                   NotBlank=Yes }
    { 3   ;   ;Link to Table       ;Option        ;CaptionML=ENU=Link to Table;
                                                   OptionCaptionML=ENU=" ,Customer,Vendor,Bank Account,Employee,Employer";
                                                   OptionString=[ ,Customer,Vendor,Bank Account,Employee,Employer] }
    { 4   ;   ;No.                 ;Code20        ;TableRelation=IF (Link to Table=CONST(Customer)) Customer
                                                                 ELSE IF (Link to Table=CONST(Vendor)) Vendor
                                                                 ELSE IF (Link to Table=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Link to Table=CONST(Employer)) Employer
                                                                 ELSE IF (Link to Table=CONST(Employee)) Employee;
                                                   CaptionML=ENU=No. }
    { 5   ;   ;Business Relation Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Business Relation".Description WHERE (Code=FIELD(Business Relation Code)));
                                                   CaptionML=ENU=Business Relation Description;
                                                   Editable=No }
    { 6   ;   ;Contact Name        ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=ENU=Contact Name;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Contact No.,Business Relation Code      ;Clustered=Yes }
    {    ;Link to Table,No.                        }
    {    ;Link to Table,Contact No.                }
    {    ;Business Relation Code                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 %2 already has a %3 with %4 %5.';
      Text001@1001 : TextConst 'ENU=%1 %2 is used when a %3 is linked with a %4.';
      Cont@1002 : Record 5050;
      CentralDataMgt@1210190003 : Codeunit 11012010;

    LOCAL PROCEDURE TouchContact@1(ContactNo@1000 : Code[20]);
    BEGIN
      Cont.LOCKTABLE;
      //Cont.GET(ContactNo); 4PS.o
      IF Cont.GET(ContactNo) THEN BEGIN //**4PS.n
        Cont."Last Date Modified" := TODAY;
        Cont.MODIFY;
      END; //**4PS.n
    END;

    BEGIN
    {
      4PS, 11-01-11, call C-026486, Field Contact No.: tablerelation changed:  WHERE (Type=CONST(Company)) removed
    }
    END.
  }
}

