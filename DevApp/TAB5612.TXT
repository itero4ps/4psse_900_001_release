OBJECT Table 5612 FA Depreciation Book
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,4PS9.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5601=r,
                TableData 11072085=r;
    OnInsert=BEGIN
               "Acquisition Date" := 0D;
               "G/L Acquisition Date" := 0D;
               "Last Acquisition Cost Date" := 0D;
               "Last Salvage Value Date" := 0D;
               "Last Depreciation Date" := 0D;
               "Last Write-Down Date" := 0D;
               "Last Appreciation Date" := 0D;
               "Last Custom 1 Date" := 0D;
               "Last Custom 2 Date" := 0D;
               "Disposal Date" := 0D;
               "Last Maintenance Date" := 0D;
               LOCKTABLE;
               FA.LOCKTABLE;
               DeprBook.LOCKTABLE;
               FA.GET("FA No.");
               DeprBook.GET("Depreciation Book Code");
               Description := FA.Description;
               "Main Asset/Component" := FA."Main Asset/Component";
               "Component of Main Asset" := FA."Component of Main Asset";
               IF ("No. of Depreciation Years" <> 0) OR ("No. of Depreciation Months" <> 0) THEN
                 DeprBook.TESTFIELD("Fiscal Year 365 Days",FALSE);

               //**4PS.sn
               IF "Main Depr. Book" THEN
                 ValidateMainDeprBook(0, FALSE);  //* 0=Insert
               //**4PS.en
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
               LOCKTABLE;
               DeprBook.LOCKTABLE;
               DeprBook.GET("Depreciation Book Code");
               IF ("No. of Depreciation Years" <> 0) OR ("No. of Depreciation Months" <> 0) THEN
                 DeprBook.TESTFIELD("Fiscal Year 365 Days",FALSE);
             END;

    OnDelete=BEGIN
               FAMoveEntries.MoveFAEntries(Rec);

               //**4PS.sn
               IF "Main Depr. Book" THEN
                 ValidateMainDeprBook(2, TRUE);  //* 2=Delete
               //**4PS.en
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=ENU=FA Depreciation Book;
  }
  FIELDS
  {
    { 1   ;   ;FA No.              ;Code20        ;TableRelation="Fixed Asset";
                                                   CaptionML=ENU=FA No.;
                                                   NotBlank=Yes }
    { 2   ;   ;Depreciation Book Code;Code10      ;TableRelation="Depreciation Book";
                                                   OnValidate=VAR
                                                                PlantDefFAPostGrp@1210190000 : Code[10];
                                                              BEGIN
                                                                //**4PS.sn
                                                                IF ("Depreciation Book Code" <> xRec."Depreciation Book Code") AND ("Depreciation Book Code" <> '') THEN BEGIN
                                                                  DeprBook.GET("Depreciation Book Code");
                                                                  IF gSpecialFAPostingGroup <> '' THEN
                                                                    VALIDATE("FA Posting Group", gSpecialFAPostingGroup)  //DP00640
                                                                  ELSE BEGIN
                                                                    IF DeprBook."Default FA Posting Group" <> '' THEN
                                                                      VALIDATE("FA Posting Group", DeprBook."Default FA Posting Group")
                                                                    ELSE BEGIN
                                                                      PlantDefFAPostGrp := GetPlantDefaultFAPostingGrp();
                                                                      IF PlantDefFAPostGrp <> '' THEN
                                                                        VALIDATE("FA Posting Group", PlantDefFAPostGrp);
                                                                    END;
                                                                  END;
                                                                  VALIDATE("Depreciation Method", DeprBook."Default Depreciation Method");
                                                                  VALIDATE("Straight-Line %", DeprBook."Default Straight-Line %");
                                                                  VALIDATE("Declining-Balance %", DeprBook."Default Declining-Balance %");
                                                                END;
                                                                //**4PS.en
                                                              END;

                                                   CaptionML=ENU=Depreciation Book Code;
                                                   NotBlank=Yes }
    { 3   ;   ;Depreciation Method ;Option        ;OnValidate=VAR
                                                                FAPostingGrp@1100530000 : Record 5606;
                                                              BEGIN
                                                                ModifyDeprFields;
                                                                CASE "Depreciation Method" OF
                                                                  "Depreciation Method"::"Straight-Line":
                                                                    BEGIN
                                                                      "Declining-Balance %" := 0;
                                                                      "Depreciation Table Code" := '';
                                                                      "First User-Defined Depr. Date" := 0D;
                                                                      "Use DB% First Fiscal Year" := FALSE;
                                                                    END;
                                                                  "Depreciation Method"::"Declining-Balance 1",
                                                                  "Depreciation Method"::"Declining-Balance 2":
                                                                    BEGIN
                                                                      "Straight-Line %" := 0;
                                                                      "No. of Depreciation Years" := 0;
                                                                      "No. of Depreciation Months" := 0;
                                                                      "Fixed Depr. Amount" := 0;
                                                                      "Depreciation Ending Date" := 0D;
                                                                      "Depreciation Table Code" := '';
                                                                      "First User-Defined Depr. Date" := 0D;
                                                                      "Use DB% First Fiscal Year" := FALSE;
                                                                    END;
                                                                  "Depreciation Method"::"DB1/SL",
                                                                  "Depreciation Method"::"DB2/SL":
                                                                    BEGIN
                                                                      "Depreciation Table Code" := '';
                                                                      "First User-Defined Depr. Date" := 0D;
                                                                    END;
                                                                  "Depreciation Method"::"User-Defined":
                                                                    BEGIN
                                                                      "Straight-Line %" := 0;
                                                                      "No. of Depreciation Years" := 0;
                                                                      "No. of Depreciation Months" := 0;
                                                                      "Fixed Depr. Amount" := 0;
                                                                      "Depreciation Ending Date" := 0D;
                                                                      "Declining-Balance %" := 0;
                                                                      "Use DB% First Fiscal Year" := FALSE;
                                                                      //**4PS.sn
                                                                      IF ("Depreciation Method" <> xRec."Depreciation Method") AND ("FA Posting Group" <> '') THEN BEGIN
                                                                        FAPostingGrp.GET("FA Posting Group");
                                                                        IF FAPostingGrp."Def. Depreciation Table Code" <> '' THEN
                                                                          VALIDATE("Depreciation Table Code", FAPostingGrp."Def. Depreciation Table Code");
                                                                      END;
                                                                      //**4PS.en
                                                                    END;
                                                                  "Depreciation Method"::Manual:
                                                                    BEGIN
                                                                      "Straight-Line %" := 0;
                                                                      "No. of Depreciation Years" := 0;
                                                                      "No. of Depreciation Months" := 0;
                                                                      "Fixed Depr. Amount" := 0;
                                                                      "Depreciation Ending Date" := 0D;
                                                                      "Declining-Balance %" := 0;
                                                                      "Depreciation Table Code" := '';
                                                                      "First User-Defined Depr. Date" := 0D;
                                                                      "Use DB% First Fiscal Year" := FALSE;
                                                                    END;
                                                                END;
                                                                TestHalfYearConventionMethod;
                                                              END;

                                                   CaptionML=ENU=Depreciation Method;
                                                   OptionCaptionML=ENU=Straight-Line,Declining-Balance 1,Declining-Balance 2,DB1/SL,DB2/SL,User-Defined,Manual;
                                                   OptionString=Straight-Line,Declining-Balance 1,Declining-Balance 2,DB1/SL,DB2/SL,User-Defined,Manual }
    { 4   ;   ;Depreciation Starting Date;Date    ;OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                                CalcDeprPeriod;
                                                              END;

                                                   CaptionML=ENU=Depreciation Starting Date;
                                                   Editable=Yes }
    { 5   ;   ;Straight-Line %     ;Decimal       ;OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                                IF ("Straight-Line %" <> 0) AND NOT LinearMethod THEN
                                                                  DeprMethodError;
                                                                AdjustLinearMethod("No. of Depreciation Years","Fixed Depr. Amount");
                                                              END;

                                                   CaptionML=ENU=Straight-Line %;
                                                   DecimalPlaces=2:8;
                                                   MinValue=0 }
    { 6   ;   ;No. of Depreciation Years;Decimal  ;OnValidate=VAR
                                                                DeprBook2@1000 : Record 5611;
                                                              BEGIN
                                                                DeprBook2.GET("Depreciation Book Code");
                                                                DeprBook2.TESTFIELD("Fiscal Year 365 Days",FALSE);

                                                                TESTFIELD("Depreciation Starting Date");
                                                                ModifyDeprFields;
                                                                IF ("No. of Depreciation Years" <> 0) AND NOT LinearMethod THEN
                                                                  DeprMethodError;

                                                                "No. of Depreciation Months" := ROUND("No. of Depreciation Years" * 12,0.00000001);
                                                                AdjustLinearMethod("Straight-Line %","Fixed Depr. Amount");
                                                                "Depreciation Ending Date" := CalcEndingDate;
                                                              END;

                                                   CaptionML=ENU=No. of Depreciation Years;
                                                   DecimalPlaces=2:8;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 7   ;   ;No. of Depreciation Months;Decimal ;OnValidate=VAR
                                                                DeprBook2@1000 : Record 5611;
                                                              BEGIN
                                                                DeprBook2.GET("Depreciation Book Code");
                                                                DeprBook2.TESTFIELD("Fiscal Year 365 Days",FALSE);

                                                                TESTFIELD("Depreciation Starting Date");
                                                                ModifyDeprFields;
                                                                IF ("No. of Depreciation Months" <> 0) AND NOT LinearMethod THEN
                                                                  DeprMethodError;

                                                                "No. of Depreciation Years" := ROUND("No. of Depreciation Months" / 12,0.00000001);
                                                                AdjustLinearMethod("Straight-Line %","Fixed Depr. Amount");
                                                                "Depreciation Ending Date" := CalcEndingDate;
                                                              END;

                                                   CaptionML=ENU=No. of Depreciation Months;
                                                   DecimalPlaces=2:8;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 8   ;   ;Fixed Depr. Amount  ;Decimal       ;OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                                IF ("Fixed Depr. Amount" <> 0) AND NOT LinearMethod THEN
                                                                  DeprMethodError;
                                                                AdjustLinearMethod("Straight-Line %","No. of Depreciation Years");
                                                              END;

                                                   CaptionML=ENU=Fixed Depr. Amount;
                                                   MinValue=0;
                                                   AutoFormatType=1 }
    { 9   ;   ;Declining-Balance % ;Decimal       ;OnValidate=BEGIN
                                                                IF "Declining-Balance %" >= 100 THEN
                                                                  FIELDERROR("Declining-Balance %",Text001);
                                                                ModifyDeprFields;
                                                                IF ("Declining-Balance %" <> 0) AND NOT DecliningMethod THEN
                                                                  DeprMethodError;
                                                              END;

                                                   CaptionML=ENU=Declining-Balance %;
                                                   DecimalPlaces=2:8;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 10  ;   ;Depreciation Table Code;Code10     ;TableRelation="Depreciation Table Header";
                                                   OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                                IF ("Depreciation Table Code" <> '') AND NOT UserDefinedMethod THEN
                                                                  DeprMethodError;
                                                              END;

                                                   CaptionML=ENU=Depreciation Table Code }
    { 11  ;   ;Final Rounding Amount;Decimal      ;OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                              END;

                                                   CaptionML=ENU=Final Rounding Amount;
                                                   MinValue=0;
                                                   AutoFormatType=1 }
    { 12  ;   ;Ending Book Value   ;Decimal       ;OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                              END;

                                                   CaptionML=ENU=Ending Book Value;
                                                   MinValue=0;
                                                   AutoFormatType=1 }
    { 13  ;   ;FA Posting Group    ;Code10        ;TableRelation="FA Posting Group";
                                                   OnValidate=VAR
                                                                FAPostingGrp@1100530000 : Record 5606;
                                                              BEGIN
                                                                ModifyDeprFields;

                                                                //**4PS.sn
                                                                IF ("FA Posting Group" <> xRec."FA Posting Group") AND ("FA Posting Group" <> '') AND UserDefinedMethod THEN BEGIN
                                                                  FAPostingGrp.GET("FA Posting Group");
                                                                  IF FAPostingGrp."Def. Depreciation Table Code" <> '' THEN
                                                                    VALIDATE("Depreciation Table Code", FAPostingGrp."Def. Depreciation Table Code");
                                                                END;
                                                                //**4PS.en
                                                              END;

                                                   CaptionML=ENU=FA Posting Group }
    { 14  ;   ;Depreciation Ending Date;Date      ;OnValidate=BEGIN
                                                                TESTFIELD("Depreciation Starting Date");
                                                                IF ("Depreciation Ending Date" <> 0D) AND NOT LinearMethod THEN
                                                                  DeprMethodError;
                                                                ModifyDeprFields;
                                                                CalcDeprPeriod;
                                                              END;

                                                   CaptionML=ENU=Depreciation Ending Date }
    { 15  ;   ;Acquisition Cost    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FA Ledger Entry".Amount WHERE (FA No.=FIELD(FA No.),
                                                                                                   Depreciation Book Code=FIELD(Depreciation Book Code),
                                                                                                   FA Posting Category=CONST(" "),
                                                                                                   FA Posting Type=CONST(Acquisition Cost),
                                                                                                   FA Posting Date=FIELD(FA Posting Date Filter)));
                                                   CaptionML=ENU=Acquisition Cost;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 16  ;   ;Depreciation        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FA Ledger Entry".Amount WHERE (FA No.=FIELD(FA No.),
                                                                                                   Depreciation Book Code=FIELD(Depreciation Book Code),
                                                                                                   FA Posting Category=CONST(" "),
                                                                                                   FA Posting Type=CONST(Depreciation),
                                                                                                   FA Posting Date=FIELD(FA Posting Date Filter)));
                                                   CaptionML=ENU=Depreciation;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 17  ;   ;Book Value          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FA Ledger Entry".Amount WHERE (FA No.=FIELD(FA No.),
                                                                                                   Depreciation Book Code=FIELD(Depreciation Book Code),
                                                                                                   Part of Book Value=CONST(Yes),
                                                                                                   FA Posting Date=FIELD(FA Posting Date Filter)));
                                                   CaptionML=ENU=Book Value;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 18  ;   ;Proceeds on Disposal;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FA Ledger Entry".Amount WHERE (FA No.=FIELD(FA No.),
                                                                                                   Depreciation Book Code=FIELD(Depreciation Book Code),
                                                                                                   FA Posting Category=CONST(" "),
                                                                                                   FA Posting Type=CONST(Proceeds on Disposal),
                                                                                                   FA Posting Date=FIELD(FA Posting Date Filter)));
                                                   CaptionML=ENU=Proceeds on Disposal;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 19  ;   ;Gain/Loss           ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FA Ledger Entry".Amount WHERE (FA No.=FIELD(FA No.),
                                                                                                   Depreciation Book Code=FIELD(Depreciation Book Code),
                                                                                                   FA Posting Category=CONST(" "),
                                                                                                   FA Posting Type=CONST(Gain/Loss),
                                                                                                   FA Posting Date=FIELD(FA Posting Date Filter)));
                                                   CaptionML=ENU=Gain/Loss;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 20  ;   ;Write-Down          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FA Ledger Entry".Amount WHERE (FA No.=FIELD(FA No.),
                                                                                                   Depreciation Book Code=FIELD(Depreciation Book Code),
                                                                                                   FA Posting Category=CONST(" "),
                                                                                                   FA Posting Type=CONST(Write-Down),
                                                                                                   FA Posting Date=FIELD(FA Posting Date Filter)));
                                                   CaptionML=ENU=Write-Down;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 21  ;   ;Appreciation        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FA Ledger Entry".Amount WHERE (FA No.=FIELD(FA No.),
                                                                                                   Depreciation Book Code=FIELD(Depreciation Book Code),
                                                                                                   FA Posting Category=CONST(" "),
                                                                                                   FA Posting Type=CONST(Appreciation),
                                                                                                   FA Posting Date=FIELD(FA Posting Date Filter)));
                                                   CaptionML=ENU=Appreciation;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 22  ;   ;Custom 1            ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FA Ledger Entry".Amount WHERE (FA No.=FIELD(FA No.),
                                                                                                   Depreciation Book Code=FIELD(Depreciation Book Code),
                                                                                                   FA Posting Category=CONST(" "),
                                                                                                   FA Posting Type=CONST(Custom 1),
                                                                                                   FA Posting Date=FIELD(FA Posting Date Filter)));
                                                   CaptionML=ENU=Custom 1;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 23  ;   ;Custom 2            ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FA Ledger Entry".Amount WHERE (FA No.=FIELD(FA No.),
                                                                                                   Depreciation Book Code=FIELD(Depreciation Book Code),
                                                                                                   FA Posting Category=CONST(" "),
                                                                                                   FA Posting Type=CONST(Custom 2),
                                                                                                   FA Posting Date=FIELD(FA Posting Date Filter)));
                                                   CaptionML=ENU=Custom 2;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 24  ;   ;Depreciable Basis   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FA Ledger Entry".Amount WHERE (FA No.=FIELD(FA No.),
                                                                                                   Depreciation Book Code=FIELD(Depreciation Book Code),
                                                                                                   Part of Depreciable Basis=CONST(Yes),
                                                                                                   FA Posting Date=FIELD(FA Posting Date Filter)));
                                                   CaptionML=ENU=Depreciable Basis;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 25  ;   ;Salvage Value       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FA Ledger Entry".Amount WHERE (FA No.=FIELD(FA No.),
                                                                                                   Depreciation Book Code=FIELD(Depreciation Book Code),
                                                                                                   FA Posting Category=CONST(" "),
                                                                                                   FA Posting Type=CONST(Salvage Value),
                                                                                                   FA Posting Date=FIELD(FA Posting Date Filter)));
                                                   CaptionML=ENU=Salvage Value;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 26  ;   ;Book Value on Disposal;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FA Ledger Entry".Amount WHERE (FA No.=FIELD(FA No.),
                                                                                                   Depreciation Book Code=FIELD(Depreciation Book Code),
                                                                                                   FA Posting Category=CONST(Disposal),
                                                                                                   FA Posting Type=CONST(Book Value on Disposal),
                                                                                                   FA Posting Date=FIELD(FA Posting Date Filter)));
                                                   CaptionML=ENU=Book Value on Disposal;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 27  ;   ;Maintenance         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Maintenance Ledger Entry".Amount WHERE (FA No.=FIELD(FA No.),
                                                                                                            Depreciation Book Code=FIELD(Depreciation Book Code),
                                                                                                            Maintenance Code=FIELD(Maintenance Code Filter),
                                                                                                            FA Posting Date=FIELD(FA Posting Date Filter)));
                                                   CaptionML=ENU=Maintenance;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 28  ;   ;Maintenance Code Filter;Code10     ;FieldClass=FlowFilter;
                                                   TableRelation=Maintenance;
                                                   CaptionML=ENU=Maintenance Code Filter }
    { 29  ;   ;FA Posting Date Filter;Date        ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=FA Posting Date Filter }
    { 30  ;   ;Acquisition Date    ;Date          ;CaptionML=ENU=Acquisition Date;
                                                   Editable=No }
    { 31  ;   ;G/L Acquisition Date;Date          ;CaptionML=ENU=G/L Acquisition Date;
                                                   Editable=No }
    { 32  ;   ;Disposal Date       ;Date          ;CaptionML=ENU=Disposal Date;
                                                   Editable=No }
    { 33  ;   ;Last Acquisition Cost Date;Date    ;CaptionML=ENU=Last Acquisition Cost Date;
                                                   Editable=No }
    { 34  ;   ;Last Depreciation Date;Date        ;CaptionML=ENU=Last Depreciation Date;
                                                   Editable=No }
    { 35  ;   ;Last Write-Down Date;Date          ;CaptionML=ENU=Last Write-Down Date;
                                                   Editable=No }
    { 36  ;   ;Last Appreciation Date;Date        ;CaptionML=ENU=Last Appreciation Date;
                                                   Editable=No }
    { 37  ;   ;Last Custom 1 Date  ;Date          ;CaptionML=ENU=Last Custom 1 Date;
                                                   Editable=No }
    { 38  ;   ;Last Custom 2 Date  ;Date          ;CaptionML=ENU=Last Custom 2 Date;
                                                   Editable=No }
    { 39  ;   ;Last Salvage Value Date;Date       ;CaptionML=ENU=Last Salvage Value Date;
                                                   Editable=No }
    { 40  ;   ;FA Exchange Rate    ;Decimal       ;OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                              END;

                                                   CaptionML=ENU=FA Exchange Rate;
                                                   DecimalPlaces=4:4;
                                                   MinValue=0 }
    { 41  ;   ;Fixed Depr. Amount below Zero;Decimal;
                                                   OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                                "Depr. below Zero %" := 0;
                                                                IF "Fixed Depr. Amount below Zero" > 0 THEN BEGIN
                                                                  DeprBook.GET("Depreciation Book Code");
                                                                  DeprBook.TESTFIELD("Allow Depr. below Zero",TRUE);
                                                                  TESTFIELD("Use FA Ledger Check",TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Fixed Depr. Amount below Zero;
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=1 }
    { 42  ;   ;Last Date Modified  ;Date          ;CaptionML=ENU=Last Date Modified;
                                                   Editable=No }
    { 43  ;   ;First User-Defined Depr. Date;Date ;OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                                IF ("First User-Defined Depr. Date" <> 0D) AND NOT UserDefinedMethod THEN
                                                                  DeprMethodError;
                                                              END;

                                                   CaptionML=ENU=First User-Defined Depr. Date }
    { 44  ;   ;Use FA Ledger Check ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF NOT "Use FA Ledger Check" THEN BEGIN
                                                                  DeprBook.GET("Depreciation Book Code");
                                                                  DeprBook.TESTFIELD("Use FA Ledger Check",FALSE);
                                                                  TESTFIELD("Fixed Depr. Amount below Zero",0);
                                                                  TESTFIELD("Depr. below Zero %",0);
                                                                END;
                                                                ModifyDeprFields;
                                                              END;

                                                   CaptionML=ENU=Use FA Ledger Check }
    { 45  ;   ;Last Maintenance Date;Date         ;CaptionML=ENU=Last Maintenance Date;
                                                   Editable=No }
    { 46  ;   ;Depr. below Zero %  ;Decimal       ;OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                                "Fixed Depr. Amount below Zero" := 0;
                                                                IF "Depr. below Zero %" > 0 THEN BEGIN
                                                                  DeprBook.GET("Depreciation Book Code");
                                                                  DeprBook.TESTFIELD("Allow Depr. below Zero",TRUE);
                                                                  TESTFIELD("Use FA Ledger Check",TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Depr. below Zero %;
                                                   DecimalPlaces=2:8;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 47  ;   ;Projected Disposal Date;Date       ;CaptionML=ENU=Projected Disposal Date }
    { 48  ;   ;Projected Proceeds on Disposal;Decimal;
                                                   CaptionML=ENU=Projected Proceeds on Disposal;
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=1 }
    { 50  ;   ;Depr. Starting Date (Custom 1);Date;OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                              END;

                                                   CaptionML=ENU=Depr. Starting Date (Custom 1) }
    { 51  ;   ;Depr. Ending Date (Custom 1);Date  ;OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                              END;

                                                   CaptionML=ENU=Depr. Ending Date (Custom 1) }
    { 52  ;   ;Accum. Depr. % (Custom 1);Decimal  ;OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                              END;

                                                   CaptionML=ENU=Accum. Depr. % (Custom 1);
                                                   DecimalPlaces=2:8;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   BlankZero=Yes }
    { 53  ;   ;Depr. This Year % (Custom 1);Decimal;
                                                   CaptionML=ENU=Depr. This Year % (Custom 1);
                                                   DecimalPlaces=2:8;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   BlankZero=Yes }
    { 54  ;   ;Property Class (Custom 1);Option   ;OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                              END;

                                                   CaptionML=ENU=Property Class (Custom 1);
                                                   OptionCaptionML=ENU=" ,Personal Property,Real Property";
                                                   OptionString=[ ,Personal Property,Real Property] }
    { 55  ;   ;Description         ;Text50        ;CaptionML=ENU=Description;
                                                   Editable=No }
    { 56  ;   ;Main Asset/Component;Option        ;CaptionML=ENU=Main Asset/Component;
                                                   OptionCaptionML=ENU=" ,Main Asset,Component";
                                                   OptionString=[ ,Main Asset,Component];
                                                   Editable=No }
    { 57  ;   ;Component of Main Asset;Code20     ;TableRelation="Fixed Asset";
                                                   CaptionML=ENU=Component of Main Asset;
                                                   Editable=No }
    { 58  ;   ;FA Add.-Currency Factor;Decimal    ;CaptionML=ENU=FA Add.-Currency Factor;
                                                   DecimalPlaces=0:15;
                                                   MinValue=0 }
    { 59  ;   ;Use Half-Year Convention;Boolean   ;OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                                TestHalfYearConventionMethod;
                                                              END;

                                                   CaptionML=ENU=Use Half-Year Convention }
    { 60  ;   ;Use DB% First Fiscal Year;Boolean  ;OnValidate=BEGIN
                                                                IF "Use DB% First Fiscal Year" THEN
                                                                  IF NOT (("Depreciation Method" = "Depreciation Method"::"DB1/SL") OR
                                                                          ("Depreciation Method" = "Depreciation Method"::"DB2/SL"))
                                                                  THEN
                                                                    DeprMethodError;
                                                              END;

                                                   CaptionML=ENU=Use DB% First Fiscal Year }
    { 61  ;   ;Temp. Ending Date   ;Date          ;CaptionML=ENU=Temp. Ending Date }
    { 62  ;   ;Temp. Fixed Depr. Amount;Decimal   ;CaptionML=ENU=Temp. Fixed Depr. Amount;
                                                   AutoFormatType=1 }
    { 63  ;   ;Ignore Def. Ending Book Value;Boolean;
                                                   OnValidate=BEGIN
                                                                ModifyDeprFields;
                                                              END;

                                                   CaptionML=ENU=Ignore Def. Ending Book Value }
    { 11012000;;FA Entry-Acq. Cost Present;Boolean;FieldClass=FlowField;
                                                   CalcFormula=Exist("FA Ledger Entry" WHERE (FA No.=FIELD(FA No.),
                                                                                              Depreciation Book Code=FIELD(Depreciation Book Code),
                                                                                              FA Posting Category=CONST(" "),
                                                                                              FA Posting Type=CONST(Acquisition Cost),
                                                                                              FA Posting Date=FIELD(FA Posting Date Filter)));
                                                   CaptionML=ENU=FA Entry-Acquisition Cost present;
                                                   Description=TableFilter (Where) from CalcFormula should be equal to CalcFormula of field 15 'Acquisition Cost'';
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 11012010;;Main Depr. Book    ;Boolean       ;OnValidate=BEGIN
                                                                //**4PS
                                                                IF "Main Depr. Book" THEN
                                                                  TESTFIELD("Depreciation Book Code");
                                                                ValidateMainDeprBook(1, TRUE);  //* 1=Modify
                                                              END;

                                                   CaptionML=ENU=Main Depr. Book }
  }
  KEYS
  {
    {    ;FA No.,Depreciation Book Code           ;Clustered=Yes }
    {    ;Depreciation Book Code,FA No.            }
    {    ;Depreciation Book Code,Component of Main Asset,Main Asset/Component }
    {    ;Main Asset/Component,Depreciation Book Code }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot rename a %1.';
      Text001@1001 : TextConst 'ENU=must not be 100';
      Text002@1002 : TextConst 'ENU=%1 is later than %2.';
      Text003@1003 : TextConst 'ENU=must not be %1';
      Text004@1004 : TextConst 'ENU=untitled';
      FA@1005 : Record 5600;
      DeprBook@1006 : Record 5611;
      FAMoveEntries@1007 : Codeunit 5623;
      FADateCalc@1008 : Codeunit 5617;
      DepreciationCalc@1009 : Codeunit 5616;
      Text11012001@1100530000 : TextConst 'ENU=%1 ''%2'' is already setup as %3, setup %1 ''%4'' as %3.\ \Are you sure?';
      Text11012002@1100530001 : TextConst 'ENU=No %1 setup as %2.\ \Are you sure?';
      Text11012003@1100530002 : TextConst 'ENU=Delete %1  last or first setup another %2 as %1.';
      gSpecialFAPostingGroup@1100409000 : Code[10];

    LOCAL PROCEDURE AdjustLinearMethod@1(VAR Amount1@1000 : Decimal;VAR Amount2@1001 : Decimal);
    BEGIN
      Amount1 := 0;
      Amount2 := 0;
      IF "No. of Depreciation Years" = 0 THEN BEGIN
        "No. of Depreciation Months" := 0;
        "Depreciation Ending Date" := 0D;
      END;
    END;

    LOCAL PROCEDURE ModifyDeprFields@2();
    BEGIN
      IF ("Last Depreciation Date" > 0D) OR
         ("Last Write-Down Date" > 0D) OR
         ("Last Appreciation Date" > 0D) OR
         ("Last Custom 1 Date" > 0D) OR
         ("Last Custom 2 Date" > 0D) OR
         ("Disposal Date" > 0D)
      THEN BEGIN
        DeprBook.GET("Depreciation Book Code");
        DeprBook.TESTFIELD("Allow Changes in Depr. Fields",TRUE);
      END;
    END;

    PROCEDURE CalcDeprPeriod@4();
    VAR
      DeprBook2@1000 : Record 5611;
    BEGIN
      IF "Depreciation Starting Date" = 0D THEN BEGIN
        "Depreciation Ending Date" := 0D;
        "No. of Depreciation Years" := 0;
        "No. of Depreciation Months" := 0;
      END;
      IF ("Depreciation Starting Date" = 0D) OR ("Depreciation Ending Date" = 0D) THEN BEGIN
        "No. of Depreciation Years" := 0;
        "No. of Depreciation Months" := 0;
      END ELSE BEGIN
        IF "Depreciation Starting Date" > "Depreciation Ending Date" THEN
          ERROR(
            Text002,
            FIELDCAPTION("Depreciation Starting Date"),FIELDCAPTION("Depreciation Ending Date"));
        DeprBook2.GET("Depreciation Book Code");
        IF DeprBook2."Fiscal Year 365 Days" THEN BEGIN
          "No. of Depreciation Months" := 0;
          "No. of Depreciation Years" := 0;
        END;
        IF NOT DeprBook2."Fiscal Year 365 Days" THEN BEGIN
          "No. of Depreciation Months" :=
            DepreciationCalc.DeprDays("Depreciation Starting Date","Depreciation Ending Date",FALSE) / 30;
          "No. of Depreciation Months" := ROUND("No. of Depreciation Months",0.00000001);
          "No. of Depreciation Years" := ROUND("No. of Depreciation Months" / 12,0.00000001);
        END;
        "Straight-Line %" := 0;
        "Fixed Depr. Amount" := 0;
      END;
    END;

    LOCAL PROCEDURE CalcEndingDate@3() : Date;
    VAR
      EndingDate@1000 : Date;
    BEGIN
      IF "No. of Depreciation Years" = 0 THEN
        EXIT(0D);
      EndingDate := FADateCalc.CalculateDate(
          "Depreciation Starting Date",ROUND("No. of Depreciation Years" * 360,1),FALSE);
      EndingDate := DepreciationCalc.Yesterday(EndingDate,FALSE);
      IF EndingDate < "Depreciation Starting Date" THEN
        EndingDate := "Depreciation Starting Date";
      EXIT(EndingDate);
    END;

    PROCEDURE GetExchangeRate@5() : Decimal;
    VAR
      DeprBook@1000 : Record 5611;
    BEGIN
      DeprBook.GET("Depreciation Book Code");
      IF NOT DeprBook."Use FA Exch. Rate in Duplic." THEN
        EXIT(0);
      IF "FA Exchange Rate" > 0 THEN
        EXIT("FA Exchange Rate");
      EXIT(DeprBook."Default Exchange Rate");
    END;

    PROCEDURE LinearMethod@9() : Boolean;
    BEGIN
      //**4PS changed to global function
      EXIT(
        "Depreciation Method" IN
        ["Depreciation Method"::"Straight-Line",
         "Depreciation Method"::"DB1/SL",
         "Depreciation Method"::"DB2/SL"]);
    END;

    LOCAL PROCEDURE DecliningMethod@10() : Boolean;
    BEGIN
      EXIT(
        "Depreciation Method" IN
        ["Depreciation Method"::"Declining-Balance 1",
         "Depreciation Method"::"Declining-Balance 2",
         "Depreciation Method"::"DB1/SL",
         "Depreciation Method"::"DB2/SL"]);
    END;

    PROCEDURE UserDefinedMethod@11() : Boolean;
    BEGIN
      //**4PS changed to global function
      EXIT("Depreciation Method" = "Depreciation Method"::"User-Defined");
    END;

    LOCAL PROCEDURE TestHalfYearConventionMethod@8() : Boolean;
    BEGIN
      IF "Depreciation Method" IN
         ["Depreciation Method"::"Declining-Balance 2",
          "Depreciation Method"::"DB2/SL",
          "Depreciation Method"::"User-Defined"]
      THEN
        TESTFIELD("Use Half-Year Convention",FALSE);
    END;

    LOCAL PROCEDURE DeprMethodError@7();
    BEGIN
      FIELDERROR("Depreciation Method",STRSUBSTNO(Text003,"Depreciation Method"));
    END;

    PROCEDURE Caption@6() : Text[120];
    VAR
      FA@1000 : Record 5600;
      DeprBook@1001 : Record 5611;
    BEGIN
      IF "FA No." = '' THEN
        EXIT(Text004);
      FA.GET("FA No.");
      DeprBook.GET("Depreciation Book Code");
      EXIT(
        STRSUBSTNO(
          '%1 %2 %3 %4',"FA No.",FA.Description,"Depreciation Book Code",DeprBook.Description));
    END;

    PROCEDURE ShowBookValueAfterDisposal@12();
    VAR
      TempFALedgEntry@1000 : TEMPORARY Record 5601;
      FALedgEntry@1001 : Record 5601;
    BEGIN
      IF "Disposal Date" > 0D THEN BEGIN
        CLEAR(TempFALedgEntry);
        TempFALedgEntry.DELETEALL;
        TempFALedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");
        DepreciationCalc.SetFAFilter(FALedgEntry,"FA No.","Depreciation Book Code",FALSE);
        WITH FALedgEntry DO BEGIN
          IF FIND('-') THEN
            REPEAT
              IF (("FA Posting Category" = "FA Posting Category"::Disposal) AND
                  ("FA Posting Type" <> "FA Posting Type"::"Book Value on Disposal") AND
                  ("FA Posting Type" <> "FA Posting Type"::"Salvage Value")) OR
                 "Part of Book Value"
              THEN BEGIN
                TempFALedgEntry := FALedgEntry;
                TempFALedgEntry.INSERT;
              END;
            UNTIL NEXT = 0;
          TempFALedgEntry.SETRANGE("FA No.",TempFALedgEntry."FA No.");
          TempFALedgEntry.SETRANGE("Depreciation Book Code",TempFALedgEntry."Depreciation Book Code");
          PAGE.RUN(0,TempFALedgEntry);
        END;
      END ELSE BEGIN
        FALedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","Part of Book Value","FA Posting Date");
        FALedgEntry.SETRANGE("FA No.","FA No.");
        FALedgEntry.SETRANGE("Depreciation Book Code","Depreciation Book Code");
        FALedgEntry.SETRANGE("Part of Book Value",TRUE);
        PAGE.RUN(0,FALedgEntry);
      END;
    END;

    PROCEDURE CalcBookValue@1001();
    BEGIN
      IF "Disposal Date" > 0D THEN
        "Book Value" := 0
      ELSE
        CALCFIELDS("Book Value");
    END;

    PROCEDURE SetBookValueFiltersOnFALedgerEntry@13(VAR FALedgEntry@1000 : Record 5601);
    BEGIN
      FALedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","Part of Book Value","FA Posting Date");
      FALedgEntry.SETRANGE("FA No.","FA No.");
      FALedgEntry.SETRANGE("Depreciation Book Code","Depreciation Book Code");
      FALedgEntry.SETRANGE("Part of Book Value",TRUE);
    END;

    PROCEDURE ValidateMainDeprBook@1100530000(UpdateMode@1100530001 : 'Insert,Modify,Delete';ConfirmMode@1100530002 : Boolean);
    VAR
      FADeprBook@1100530000 : Record 5612;
    BEGIN
      //**4PS
      FADeprBook.SETRANGE("FA No.", "FA No.");
      FADeprBook.SETFILTER("Depreciation Book Code", '<>%1', "Depreciation Book Code");
      CASE UpdateMode OF
        UpdateMode::Insert:
          BEGIN
            IF "Main Depr. Book" THEN BEGIN
              FADeprBook.SETRANGE("Main Depr. Book", TRUE);
              IF FADeprBook.FINDFIRST THEN
                "Main Depr. Book" := FALSE;
            END;
          END;
        UpdateMode::Modify:
          BEGIN
            IF "Main Depr. Book" THEN BEGIN
              FADeprBook.SETRANGE("Main Depr. Book", TRUE);
              IF FADeprBook.FINDFIRST THEN BEGIN
                IF ConfirmMode THEN BEGIN
                  IF NOT CONFIRM(Text11012001, FALSE,
                    FADeprBook.FIELDCAPTION("Depreciation Book Code"), FADeprBook."Depreciation Book Code",
                    FADeprBook.FIELDCAPTION("Main Depr. Book"), "Depreciation Book Code")
                  THEN
                    ERROR('');
                END;
                FADeprBook."Main Depr. Book" := FALSE;
                FADeprBook.MODIFY;
              END;
            END ELSE BEGIN
              IF ConfirmMode AND xRec."Main Depr. Book" THEN BEGIN
                FADeprBook.SETRANGE("Main Depr. Book", TRUE);
                IF FADeprBook.ISEMPTY THEN BEGIN
                  IF NOT CONFIRM(Text11012002, FALSE, FADeprBook.TABLECAPTION, FADeprBook.FIELDCAPTION("Main Depr. Book")) THEN
                    ERROR('');
                END;
              END;
            END;
          END;
        UpdateMode::Delete:
          BEGIN
            IF ConfirmMode AND "Main Depr. Book" THEN BEGIN
              FADeprBook.SETRANGE("Main Depr. Book", TRUE);
              IF FADeprBook.ISEMPTY THEN BEGIN
                FADeprBook.SETRANGE("Main Depr. Book");
                IF NOT FADeprBook.ISEMPTY THEN
                  ERROR(Text11012003, FIELDCAPTION("Main Depr. Book"), FIELDCAPTION("Depreciation Book Code"));
              END;
            END;
          END;
      END;
    END;

    PROCEDURE GetPlantDefaultFAPostingGrp@1210190000() : Code[10];
    VAR
      PlantType@1210190000 : Record 11012551;
      PlantNo@1210190001 : Record 11012552;
      PlantPostingGrp@1210190002 : Record 11012553;
      Vend@1210190003 : Record 23;
    BEGIN
      //**4PS (FA 26081)
      IF "FA No." = '' THEN
        EXIT('');
      IF NOT FA.GET("FA No.") THEN
        EXIT('');

      FA.CALCFIELDS("Plant Type", "Plant No.");
      IF (FA."Plant Type" = '') OR (FA."Plant No." = '') THEN
        EXIT('');

      IF NOT PlantType.GET(FA."Plant Type") THEN
        EXIT('');
      IF PlantType."Posting Group Internal" = '' THEN
        EXIT('');

      IF NOT PlantNo.GET(FA."Plant Type", FA."Plant No.") THEN
        EXIT('');
      IF PlantNo.Vendor <> '' THEN BEGIN
        IF Vend.GET(PlantNo.Vendor) THEN
          IF Vend."Vendor Posting Group" <> '' THEN
            IF PlantPostingGrp.GET(PlantType."Posting Group Internal", Vend."Vendor Posting Group") THEN
              EXIT(PlantPostingGrp."Def. FA Posting Group");
      END;

      IF PlantPostingGrp.GET(PlantType."Posting Group Internal", '') THEN
        EXIT(PlantPostingGrp."Def. FA Posting Group");

      EXIT('');
    END;

    PROCEDURE SetSpecialFAPostingGroup@1100409000(SpecialFAPostingGroup@1100409000 : Code[10]);
    BEGIN
      //**4PS DP00640
      gSpecialFAPostingGroup := SpecialFAPostingGroup;
    END;

    BEGIN
    END.
  }
}

