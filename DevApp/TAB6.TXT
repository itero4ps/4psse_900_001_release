OBJECT Table 6 Customer Price Group
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,4PS9.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               lvCustRentalRateRec@1100525000 : Record 11012541;
               PlantCostLine@1100409000 : Record 11012591;
               PlantCostLinePrice@1100409001 : Record 11072664;
             BEGIN
               UpdateSalesPrices(FALSE);

               //**4PS.sn
               lvCustRentalRateRec.SETRANGE("Customer Price Group", Code);
               lvCustRentalRateRec.DELETEALL;

               //DP00671.sn
               PlantCostLinePrice.SETRANGE("Cost Level", PlantCostLinePrice."Cost Level"::CustomerPriceGroup);
               PlantCostLinePrice.SETRANGE(Code, Code);
               PlantCostLinePrice.DELETEALL;

               PlantCostLine.SETRANGE("Cost Level", PlantCostLine."Cost Level"::CustomerPriceGroup);
               PlantCostLine.SETRANGE(Code, Code);
               PlantCostLine.DELETEALL;
               //DP00671.en
               //**4PS.en
             END;

    OnRename=BEGIN
               UpdateSalesPrices(TRUE);
             END;

    CaptionML=ENU=Customer Price Group;
    LookupPageID=Page7;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 2   ;   ;Price Includes VAT  ;Boolean       ;OnValidate=VAR
                                                                SalesSetup@1000 : Record 311;
                                                              BEGIN
                                                                IF "Price Includes VAT" THEN BEGIN
                                                                  SalesSetup.GET;
                                                                  IF SalesSetup."VAT Bus. Posting Gr. (Price)" <> '' THEN
                                                                    VALIDATE("VAT Bus. Posting Gr. (Price)",SalesSetup."VAT Bus. Posting Gr. (Price)");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Price Includes VAT }
    { 5   ;   ;Allow Invoice Disc. ;Boolean       ;InitValue=Yes;
                                                   CaptionML=ENU=Allow Invoice Disc. }
    { 6   ;   ;VAT Bus. Posting Gr. (Price);Code10;TableRelation="VAT Business Posting Group";
                                                   CaptionML=ENU=VAT Bus. Posting Gr. (Price) }
    { 10  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 7001;   ;Allow Line Disc.    ;Boolean       ;InitValue=Yes;
                                                   CaptionML=ENU=Allow Line Disc. }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Description,Allow Invoice Disc.,Allow Line Disc. }
  }
  CODE
  {

    LOCAL PROCEDURE UpdateSalesPrices@2(CreateNewSalesPrice@1002 : Boolean);
    VAR
      SalesPrice@1001 : Record 7002;
      NewSalesPrice@1000 : Record 7002;
    BEGIN
      SalesPrice.SETRANGE("Sales Type",SalesPrice."Sales Type"::"Customer Price Group");
      SalesPrice.SETRANGE("Sales Code",xRec.Code);
      IF CreateNewSalesPrice THEN
        IF SalesPrice.FINDSET THEN
          REPEAT
            NewSalesPrice := SalesPrice;
            NewSalesPrice."Sales Code" := Code;
            NewSalesPrice.INSERT(TRUE);
          UNTIL SalesPrice.NEXT = 0;
      SalesPrice.DELETEALL(TRUE);
    END;

    BEGIN
    END.
  }
}

