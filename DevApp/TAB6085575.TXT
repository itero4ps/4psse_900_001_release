OBJECT Table 6085575 DC Document Category
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.4.04,4PS9.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnInsert=BEGIN
               "Export PDF File" := "Export PDF File"::"Yes - Always Create New";
               "Document Category GUID" := CREATEGUID;
             END;

    OnDelete=VAR
               Template@1000000001 : Record 6085579;
               SourceExcl@1160040002 : Record 6085598;
               IdentField@1160040001 : Record 6085759;
               GroupLang@1000000000 : Record 6085576;
               DocSep@1000000003 : Record 6085577;
               RecIDMgt@1160040000 : Codeunit 6085604;
               CODC@1160040003 : Codeunit 6085617;
             BEGIN
               Template.SETRANGE("Category Code",Code);
               IF NOT Template.ISEMPTY THEN
                 ERROR(Text007,TABLECAPTION,Code);

               GroupLang.SETRANGE("Document Category Code",Code);
               GroupLang.DELETEALL(TRUE);

               DocSep.SETRANGE("Document Category Code",Code);
               DocSep.DELETEALL(TRUE);

               SourceExcl.SETRANGE("Document Category Code",Code);
               SourceExcl.DELETEALL(TRUE);

               IdentField.SETRANGE("Document Category Code",Code);
               IdentField.DELETEALL(TRUE);

               RecIDMgt.DeleteTableFilter("Document Category GUID");

               CODC.DeleteCategory(Rec);
             END;

    CaptionML=ENU=Document Category;
    LookupPageID=Page6085582;
    DrillDownPageID=Page6085582;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 3   ;   ;Source Table No.    ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                CheckSourceTable;
                                                              END;

                                                   CaptionML=ENU=Source Table No.;
                                                   BlankZero=Yes }
    { 4   ;   ;Source Table Name   ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Source Table No.)));
                                                   CaptionML=ENU=Source Table Name;
                                                   Editable=No }
    { 5   ;   ;Source Type         ;Option        ;CaptionML=ENU=Source Type;
                                                   OptionCaptionML=ENU=" ,Vendor,Contact,Customer,Job,Item,Fixed Asset,Employee";
                                                   OptionString=[ ,Vendor,Contact,Customer,Job,Item,Fixed Asset,Employee] }
    { 6   ;   ;No. of Open Documents;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count("DC Document" WHERE (Document Category Code=FIELD(Code),
                                                                                          Status=CONST(Open)));
                                                   CaptionML=ENU=No. of Open Documents;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 7   ;   ;Process New Files Automatic;Boolean;InitValue=Yes;
                                                   CaptionML=ENU=Process New Files Automatically }
    { 8   ;   ;No. of Templates    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count(Template WHERE (Category Code=FIELD(Code)));
                                                   CaptionML=ENU=No. of Templates;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 9   ;   ;No. of Documents    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("DC Document" WHERE (Document Category Code=FIELD(Code)));
                                                   CaptionML=ENU=No. of Documents;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 10  ;   ;No. of Registered Documents;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("DC Document" WHERE (Document Category Code=FIELD(Code),
                                                                                          Status=CONST(Registered)));
                                                   CaptionML=ENU=No. of Registered Documents;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 11  ;   ;No. of Rejected Documents;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("DC Document" WHERE (Document Category Code=FIELD(Code),
                                                                                          Status=CONST(Rejected)));
                                                   CaptionML=ENU=No. of Rejected Documents;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 12  ;   ;Export PDF File     ;Option        ;InitValue=Yes - Keep Existing Or Create New;
                                                   CaptionML=ENU=Export PDF File;
                                                   OptionCaptionML=ENU=No,Yes - Keep Existing Or Create New,Yes - Always Create New;
                                                   OptionString=No,Yes - Keep Existing Or Create New,Yes - Always Create New }
    { 13  ;   ;Process Scanned Files;Boolean      ;InitValue=Yes;
                                                   CaptionML=ENU=Process Scanned Files }
    { 14  ;   ;Process PDF Files   ;Boolean       ;InitValue=Yes;
                                                   CaptionML=ENU=Process PDF Files }
    { 15  ;   ;PDF Format          ;Option        ;InitValue=PDF/A;
                                                   CaptionML=ENU=PDF Format;
                                                   OptionCaptionML=ENU=PDF,PDF/A;
                                                   OptionString=PDF,PDF/A }
    { 16  ;   ;TIFF Image Resolution;Integer      ;InitValue=100;
                                                   CaptionML=ENU=TIFF Image Resolution;
                                                   MinValue=0 }
    { 17  ;   ;No. of Document Separators;Integer ;FieldClass=FlowField;
                                                   CalcFormula=Count("Document Category Doc. Sep." WHERE (Document Category Code=FIELD(Code)));
                                                   CaptionML=ENU=No. of Document Separator;
                                                   Editable=No }
    { 18  ;   ;Distribution Group  ;Boolean       ;OnValidate=BEGIN
                                                                IF "Distribution Group" THEN BEGIN
                                                                  CALCFIELDS("No. of Document Separators");
                                                                  IF "No. of Document Separators" > 0 THEN
                                                                    ERROR(Text006,FIELDCAPTION("Distribution Group"));
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Distribution Group }
    { 19  ;   ;Scanning Allowed    ;Boolean       ;CaptionML=ENU=Scanning Allowed }
    { 20  ;   ;E-mail Connection Endpoint;Code10  ;TableRelation="Connection Endpoint";
                                                   OnValidate=VAR
                                                                ConnEndpoint@161024012 : Record 6085620;
                                                                DocCat@1160040000 : Record 6085575;
                                                              BEGIN
                                                                IF "E-mail Connection Endpoint" = '' THEN
                                                                  EXIT;

                                                                DocCat.SETRANGE("E-mail Connection Endpoint","E-mail Connection Endpoint");
                                                                DocCat.SETFILTER(Code,'<>%1',Code);
                                                                IF DocCat.FINDFIRST THEN
                                                                  ERROR(Text016,"E-mail Connection Endpoint",DocCat.FIELDCAPTION("E-mail Connection Endpoint"),TABLECAPTION);

                                                                ConnEndpoint.GET("E-mail Connection Endpoint");
                                                                IF ConnEndpoint.Protocol = ConnEndpoint.Protocol::IMAP THEN BEGIN
                                                                  ConnEndpoint.TESTFIELD("Protocol Port");
                                                                  ConnEndpoint.TESTFIELD("Server Address");
                                                                  ConnEndpoint.TESTFIELD(Username);
                                                                  ConnEndpoint.TESTFIELD(Password);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=E-mail Connection Endpoint }
    { 21  ;   ;E-mail Sender Filter;Text250       ;OnValidate=BEGIN
                                                                TESTFIELD("E-mail Connection Endpoint");
                                                              END;

                                                   CaptionML=ENU=E-mail Sender Filter }
    { 22  ;   ;E-mail Recipient Filter;Text250    ;OnValidate=BEGIN
                                                                TESTFIELD("E-mail Connection Endpoint");
                                                              END;

                                                   CaptionML=ENU=E-mail Recipient Filter }
    { 23  ;   ;E-mail Subject Filter;Text250      ;OnValidate=BEGIN
                                                                TESTFIELD("E-mail Connection Endpoint");
                                                              END;

                                                   CaptionML=ENU=E-mail Subject Filter }
    { 24  ;   ;No. of Excluded Sources;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Document Category Source Excl." WHERE (Document Category Code=FIELD(Code)));
                                                   CaptionML=ENU=No. of Excluded Sources;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 25  ;   ;Allow Drag-and-Drop ;Boolean       ;CaptionML=ENU=Allow Drag-and-Drop }
    { 26  ;   ;Source Field No.    ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table No.));
                                                   OnValidate=VAR
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Source Field No." = xRec."Source Field No." THEN
                                                                  EXIT;

                                                                RecIDMgt.CheckDocument(Code,FIELDCAPTION("Source Field No."));

                                                                IF NOT RecIDMgt.PartOfKey("Source Table No.","Source Field No.") THEN
                                                                  ERROR(Text013,FIELDCAPTION("Source Field No."),FIELDCAPTION("Source Table No."));
                                                              END;

                                                   CaptionML=ENU=Source Field No.;
                                                   BlankZero=Yes }
    { 27  ;   ;Source Field No. (Name);Integer    ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table No.));
                                                   OnValidate=VAR
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Source Field No. (Name)" = 0 THEN
                                                                  "Source Field No. (Name)" := "Source Field No.";
                                                              END;

                                                   CaptionML=ENU=Source Field No. (Name);
                                                   BlankZero=Yes }
    { 28  ;   ;No. of Source Table Filters;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("Table Filter Field" WHERE (Table Filter GUID=FIELD(Document Category GUID)));
                                                   CaptionML=ENU=No. of Source Table Filters;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 29  ;   ;Destination Header Table No.;Integer;
                                                   TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                Template@161024013 : Record 6085579;
                                                                TemplField@161024012 : Record 6085580;
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Destination Header Table No." = xRec."Destination Header Table No." THEN
                                                                  EXIT;

                                                                Template.SETRANGE("Category Code",Code);
                                                                IF Template.FINDSET THEN
                                                                  REPEAT
                                                                    TemplField.SETRANGE("Template No.",Template."No.");
                                                                    TemplField.SETFILTER("Destination Header Field No.",'<>%1',0);
                                                                    IF NOT TemplField.ISEMPTY THEN
                                                                      ERROR(Text015,FIELDCAPTION("Destination Header Table No."),
                                                                        TemplField.TABLECAPTION,TemplField.FIELDCAPTION(TemplField."Destination Header Field No."));
                                                                  UNTIL Template.NEXT = 0;
                                                              END;

                                                   CaptionML=ENU=Destination Header Table No.;
                                                   BlankZero=Yes }
    { 30  ;   ;Destination Line Table No.;Integer ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                Template@161024013 : Record 6085579;
                                                                TemplField@161024012 : Record 6085580;
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Destination Line Table No." = xRec."Destination Line Table No." THEN
                                                                  EXIT;

                                                                Template.SETRANGE("Category Code",Code);
                                                                IF Template.FINDSET THEN
                                                                  REPEAT
                                                                    TemplField.SETRANGE("Template No.",Template."No.");
                                                                    TemplField.SETFILTER("Destination Line Field No.",'<>%1',0);
                                                                    IF NOT TemplField.ISEMPTY THEN
                                                                      ERROR(Text015,FIELDCAPTION("Destination Line Table No."),
                                                                        TemplField.TABLECAPTION,TemplField.FIELDCAPTION(TemplField."Destination Line Field No."));
                                                                  UNTIL Template.NEXT = 0;
                                                              END;

                                                   CaptionML=ENU=Destination Line Table No.;
                                                   BlankZero=Yes }
    { 31  ;   ;Archive E-mails on Document;Boolean;CaptionML=ENU=Archive E-mails on Document }
    { 32  ;   ;Source Search Field No.;Integer    ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table No.));
                                                   OnValidate=VAR
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Source Field No." = xRec."Source Field No." THEN
                                                                  EXIT;
                                                              END;

                                                   CaptionML=ENU=Source Search Field No.;
                                                   BlankZero=Yes }
    { 33  ;   ;TIFF Image Color Mode;Option       ;CaptionML=ENU=TIFF Image Color Mode;
                                                   OptionCaptionML=ENU=Black & White,Gray,Color;
                                                   OptionString=Black & White,Gray,Color }
    { 50  ;   ;Codeunit ID: Reopen ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=ENU=Codeunit ID: Reopen;
                                                   BlankZero=Yes }
    { 51  ;   ;Codeunit Name: Reopen;Text30       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(Codeunit),
                                                                                         ID=FIELD(Codeunit ID: Reopen)));
                                                   CaptionML=ENU=Codeunit Name: Reopen;
                                                   Editable=No }
    { 52  ;   ;No. of Identification Fields;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Document Category Ident. Field" WHERE (Document Category Code=FIELD(Code)));
                                                   CaptionML=ENU=No. of Identification Fields;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 53  ;   ;Document Category GUID;GUID        ;CaptionML=ENU=Document Category GUID }
    { 54  ;   ;Register without Template;Boolean  ;CaptionML=ENU=Register without Template }
    { 55  ;   ;Register Documents Automatic.;Boolean;
                                                   CaptionML=ENU=Register Documents Automatically }
    { 56  ;   ;Codeunit ID: Show Reg. Doc.;Integer;TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=ENU=Codeunit ID: Show Reg. Doc.;
                                                   BlankZero=Yes }
    { 57  ;   ;Codeunit Name: Show Reg. Doc.;Text30;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(Codeunit),
                                                                                         ID=FIELD(Codeunit ID: Show Reg. Doc.)));
                                                   CaptionML=ENU=Codeunit Name: Show Reg. Doc.;
                                                   Editable=No }
    { 58  ;   ;Codeunit ID: Get Doc. Status;Integer;
                                                   TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=ENU=Codeunit ID: Get Doc. Status;
                                                   BlankZero=Yes }
    { 59  ;   ;Codeunit Name: Get Doc. Status;Text30;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(Codeunit),
                                                                                         ID=FIELD(Codeunit ID: Get Doc. Status)));
                                                   CaptionML=ENU=Codeunit Name: Get Doc. Status;
                                                   Editable=No }
    { 60  ;   ;No. of files for OCR;Integer       ;CaptionML=ENU=No. of files for OCR;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 61  ;   ;No. of files for Import;Integer    ;CaptionML=ENU=No. of files for import;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 62  ;   ;No. of files with Error;Integer    ;CaptionML=ENU=No. of PDF-files with error;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 64  ;   ;Split on Barcode    ;Boolean       ;CaptionML=ENU=Split on Barcode }
    { 65  ;   ;Split Barcode Text  ;Text250       ;CaptionML=ENU=Split on Barcode Text }
    { 66  ;   ;Split on Blank Page ;Boolean       ;CaptionML=ENU=Split on Blank Page }
    { 67  ;   ;Split on Source ID  ;Boolean       ;CaptionML=ENU=Split on Source ID }
    { 68  ;   ;Auto Move to Company;Boolean       ;CaptionML=ENU=Auto Move to Company }
    { 70  ;   ;Codeunit ID: Get File Count;Integer;TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=ENU=Codeunit ID: Get File Count;
                                                   BlankZero=Yes }
    { 71  ;   ;Codeunit Name: Get File Count;Text30;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(Codeunit),
                                                                                         ID=FIELD(Codeunit ID: Get File Count)));
                                                   CaptionML=ENU=Codeunit Name: Get File Count;
                                                   Editable=No }
    { 72  ;   ;Codeunit ID: Doc. Journal;Integer  ;TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=ENU=Codeunit ID: Document Journal;
                                                   BlankZero=Yes }
    { 73  ;   ;Codeunit Name: Doc. Journal;Text30 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(Codeunit),
                                                                                         ID=FIELD(Codeunit ID: Doc. Journal)));
                                                   CaptionML=ENU=Codeunit Name: Document Journal;
                                                   Editable=No }
    { 74  ;   ;Codeunit ID: Import Files;Integer  ;TableRelation=Object.ID WHERE (Type=CONST(Codeunit));
                                                   CaptionML=ENU=Codeunit ID: Import Files;
                                                   BlankZero=Yes }
    { 75  ;   ;Codeunit Name: Import Files;Text30 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Object.Name WHERE (Type=CONST(Codeunit),
                                                                                         ID=FIELD(Codeunit ID: Import Files)));
                                                   CaptionML=ENU=Codeunit Name: Import Files;
                                                   Editable=No }
    { 76  ;   ;Split on Sep. Fields;Boolean       ;CaptionML=ENU=Split on Separator Fields }
    { 77  ;   ;Archive Documents in Cloud OCR;Option;
                                                   CaptionML=ENU=Archive Documents in Cloud OCR;
                                                   OptionCaptionML=ENU=No,One Day,One Week,One Month,One Year,Permanent;
                                                   OptionString=No,One Day,One Week,One Month,One Year,Permanent }
    { 11012001;;Company Identification Fields;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Category Company Caption" WHERE (Document Category=FIELD(Code)));
                                                   CaptionML=ENU=Company Identification Fields;
                                                   BlankZero=Yes;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1160040000 : TextConst 'ENU=You are changing %1, which will impact imported documents. Would you like to continue?';
      Text002@1000000001 : TextConst 'ENU=There are no open documents in this group.';
      Text006@1000000005 : TextConst 'ENU=No Document Separators can be created on a %1.\\Documents placed in a %1 will automatically be distributed to the correct categories, based on the Document Separator patterns of the individual categories.';
      Text007@1000000006 : TextConst 'ENU=You cannot delete %1 %2 because there is at least one template for this category.';
      Text008@1000000007 : TextConst 'ENU=You cannot delete %1 %2 because there is at least one document for this category.';
      Text010@161024013 : TextConst 'ENU=%1 - %2';
      Text012@161024015 : TextConst 'ENU=Please delete all identification fields before you change %1.';
      Text013@1160040001 : TextConst 'ENU=The %1 is not part of the primary key of the %2.';
      Text014@1160040002 : TextConst 'ENU=The %1 cannot be changed because there is at least one template with a value for this category.';
      Text015@161024016 : TextConst 'ENU=The %1 cannot be changed because it has one or more %2 with %3 assigned.';
      Text016@1160040003 : TextConst 'ENU=%1 is already used as %2 in another %3.';
      ForceUpdate@1160040004 : Boolean;

    PROCEDURE RegisterOpenDoc@1000000002();
    VAR
      Document@1000000000 : Record 6085590;
    BEGIN
      Document.SETCURRENTKEY(Status);
      Document.SETRANGE(Status,Document.Status::Open);
      Document.SETRANGE("Document Category Code",Code);
      IF NOT Document.FINDFIRST THEN
        ERROR(Text002);

      IF Document.FINDSET THEN
        REPEAT
          Document.MARK(TRUE);
        UNTIL Document.NEXT = 0;

      Document.FINDFIRST;
      Document.SETRANGE("Document Category Code");
      Document.MARKEDONLY(TRUE);
      PAGE.RUN(PAGE::"Document Card",Document);
    END;

    PROCEDURE DrillDownOpenDocWithImage@1000000004();
    VAR
      Document@1000000000 : Record 6085590;
    BEGIN
      DrillDownDocWithImage(Document.Status::Open);
    END;

    PROCEDURE DrillDownDocWithImage@1000000003(Status@1000000001 : Integer);
    VAR
      Document@1000000000 : Record 6085590;
    BEGIN
      Document.SETCURRENTKEY("Document Category Code",Status);
      Document.SETRANGE("Document Category Code",Code);
      Document.SETRANGE(Status,Status);
      PAGE.RUN(PAGE::"Document List With Image",Document);
    END;

    PROCEDURE CheckSourceTable@1160040000();
    VAR
      Template@1160040004 : Record 6085579;
      IdentField@1160040006 : Record 6085759;
      TableFilterField@1160040001 : Record 6085762;
      DCMgt@1160040003 : Codeunit 6085603;
      RecIDMgt@1160040000 : Codeunit 6085604;
    BEGIN
      IF "Source Table No." > 0 THEN
        DCMgt.IsLogActive("Source Table No.",TRUE);

      IF "Source Table No." = xRec."Source Table No." THEN
        EXIT;

      RecIDMgt.CheckDocument(Code,FIELDCAPTION("Source Table No."));

      Template.SETRANGE("Category Code",Code);
      Template.SETFILTER("Source Record ID Tree ID",'<>%1',0);
      IF NOT Template.ISEMPTY THEN
        ERROR(Text014,FIELDCAPTION("Source Table No."));

      IdentField.SETRANGE("Document Category Code",Code);
      IF NOT IdentField.ISEMPTY THEN
        ERROR(Text012);

      RecIDMgt.DeleteTableFilter("Document Category GUID");
      CLEAR("Document Category GUID");
      "Source Field No." := RecIDMgt.GetFirstKeyField("Source Table No.");
      "Source Field No. (Name)" := "Source Field No.";
      MODIFY;
    END;

    PROCEDURE UpdateCounters@1160040001();
    BEGIN
      IF "Codeunit ID: Get File Count" > 0 THEN
        CODEUNIT.RUN("Codeunit ID: Get File Count",Rec)
      ELSE
        CODEUNIT.RUN(CODEUNIT::"Update DC Doc. Counters",Rec);
    END;

    PROCEDURE OpenDocJournal@1();
    VAR
      Document@1000 : Record 6085590;
    BEGIN
      IF "Codeunit ID: Doc. Journal" > 0 THEN
        CODEUNIT.RUN("Codeunit ID: Doc. Journal")
      ELSE BEGIN
        Document.FILTERGROUP := 2;
        Document.SETRANGE("Document Category Code",Code);
        Document.FILTERGROUP := 0;

        PAGE.RUN(PAGE::"Document List With Image",Document);
      END;
    END;

    PROCEDURE GetCategoryPath@1160040003(FileType@1160040003 : 'TIFForOCR,PDFForOCR,ImportFiles,ErrorFiles') : Text[1024];
    VAR
      DCSetup@1160040000 : Record 6085573;
      Path@1160040001 : Text[1024];
    BEGIN
      DCSetup.GET;

      IF DCSetup."Company Code" <> '' THEN
        Path := '\' + DCSetup."Company Code" + ' - ' + Code
      ELSE
        Path := '\' + Code;

       CASE FileType OF
         FileType::TIFForOCR :
           EXIT(DCSetup."Scanned File Path for OCR" + Path);
         FileType::PDFForOCR :
           EXIT(DCSetup."PDF File Path for OCR" + Path);
         FileType::ImportFiles :
           EXIT(DCSetup."File Path for OCR-proc. files" + Path);
         FileType::ErrorFiles :
           EXIT(DCSetup."PDF File Path for OCR" + Path + '\ERROR');
       END;
    END;

    PROCEDURE OpenStatusDocument@2(Status@1000 : 'OCR,Import,Error');
    VAR
      TempDoc@1160040001 : TEMPORARY Record 6085772;
      Files@1160040003 : Record 2000000022;
      FileSysMgt@1160040002 : Codeunit 6085584;
      CODC@1160040006 : Codeunit 6085617;
      DocCounters@1160040007 : Codeunit 6085618;
      EntryNo@1160040005 : Integer;
      FilePath@1160040004 : Text[1024];
    BEGIN
      IF CODC.IsCloudActive(FALSE) THEN
        CODC.GetStatusDocuments(Rec,Status,TempDoc);

      CASE Status OF
        Status::OCR:
          FilePath := GetCategoryPath(0);
        Status::Import:
          FilePath := GetCategoryPath(2);
        Status::Error:
          FilePath := GetCategoryPath(3);
      END;

      IF FileSysMgt.DirectoryExists(FilePath) THEN BEGIN
        IF TempDoc.FINDLAST THEN
          EntryNo := TempDoc."Entry No.";

        Files.SETRANGE(Path,FilePath);
        Files.SETRANGE("Is a file",TRUE);
        Files.SETFILTER(Name,'@*.pdf');
        IF Files.FINDSET THEN
          REPEAT
            EntryNo += 1;
            TempDoc.INIT;
            TempDoc."Entry No." := EntryNo;
            TempDoc."File Name" := Files.Name;
            TempDoc."File Path" := Files.Path;
            TempDoc."Date/Time" := CREATEDATETIME(Files.Date,Files.Time);
            TempDoc.Status := Status;
            TempDoc.INSERT;
          UNTIL Files.NEXT = 0;
      END;

      PAGE.RUNMODAL(PAGE::"Display Document List",TempDoc);

      DocCounters.SetForceUpdate(TRUE);
    END;

    BEGIN
    END.
  }
}

