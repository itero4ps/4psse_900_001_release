OBJECT Table 6085731 Purch. Allocation Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.4.00,4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TestStatus;
             END;

    OnModify=BEGIN
               TestStatus;
             END;

    OnDelete=BEGIN
               TestStatus;
             END;

    CaptionML=ENU=Purch. Allocation Line;
  }
  FIELDS
  {
    { 1   ;   ;Document No.        ;Code20        ;TableRelation="Purch. Allocation Header";
                                                   CaptionML=ENU=Document No. }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 4   ;   ;G/L Account No.     ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=VAR
                                                                GLAccount@161024012 : Record 15;
                                                                SourceCodeSetup@6085575 : Record 242;
                                                                DimMgt@6085573 : Codeunit 408;
                                                                TableID@6085578 : ARRAY [10] OF Integer;
                                                                No@6085577 : ARRAY [10] OF Code[20];
                                                                TempCode@6085576 : Code[20];
                                                              BEGIN
                                                                GLAccount.GET("G/L Account No.");
                                                                GLAccount.CheckGLAcc;
                                                                "Gen. Prod. Posting Group" := GLAccount."Gen. Prod. Posting Group";
                                                                VALIDATE("VAT Prod. Posting Group",GLAccount."VAT Prod. Posting Group");

                                                                GetPurchAllocHeader;
                                                                SourceCodeSetup.GET;
                                                                TableID[1] := DATABASE::"G/L Account";
                                                                No[1] := "G/L Account No.";
                                                                "Dimension Set ID" := DimMgt.GetDefaultDimID(
                                                                  TableID,No,SourceCodeSetup.Purchases,TempCode,TempCode,PurchAllocHeader."Dimension Set ID",DATABASE::Vendor);
                                                              END;

                                                   CaptionML=ENU=G/L Account No. }
    { 5   ;   ;Gen. Bus. Posting Group;Code10     ;TableRelation="Gen. Business Posting Group";
                                                   OnValidate=BEGIN
                                                                IF xRec."Gen. Bus. Posting Group" <> "Gen. Bus. Posting Group" THEN
                                                                  IF GenBusPostingGrp.ValidateVatBusPostingGroup(GenBusPostingGrp,"Gen. Bus. Posting Group") THEN
                                                                    VALIDATE("VAT Bus. Posting Group",GenBusPostingGrp."Def. VAT Bus. Posting Group");

                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=ENU=Gen. Bus. Posting Group }
    { 6   ;   ;Gen. Prod. Posting Group;Code10    ;TableRelation="Gen. Product Posting Group";
                                                   OnValidate=BEGIN
                                                                TestStatus;
                                                                IF xRec."Gen. Prod. Posting Group" <> "Gen. Prod. Posting Group" THEN
                                                                  IF GenProdPostingGrp.ValidateVatProdPostingGroup(GenProdPostingGrp,"Gen. Prod. Posting Group") THEN
                                                                    VALIDATE("VAT Prod. Posting Group",GenProdPostingGrp."Def. VAT Prod. Posting Group");

                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=ENU=Gen. Prod. Posting Group }
    { 7   ;   ;VAT Bus. Posting Group;Code10      ;TableRelation="VAT Business Posting Group";
                                                   OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=ENU=VAT Bus. Posting Group }
    { 8   ;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group";
                                                   OnValidate=BEGIN
                                                                TestStatus;
                                                                IF NOT VATPostingSetup.GET("VAT Bus. Posting Group","VAT Prod. Posting Group") THEN
                                                                  CLEAR(VATPostingSetup);
                                                                "VAT %" := VATPostingSetup."VAT %";
                                                                "VAT Calculation Type" := VATPostingSetup."VAT Calculation Type";
                                                                "VAT Identifier" := VATPostingSetup."VAT Identifier";

                                                                CASE "VAT Calculation Type" OF
                                                                  "VAT Calculation Type"::"Reverse Charge VAT",
                                                                  "VAT Calculation Type"::"Sales Tax":
                                                                    "VAT %" := 0;
                                                                  "VAT Calculation Type"::"Full VAT":
                                                                    BEGIN
                                                                      VATPostingSetup.TESTFIELD("Purchase VAT Account");
                                                                      TESTFIELD("G/L Account No.",VATPostingSetup."Purchase VAT Account");
                                                                    END;
                                                                END;

                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=ENU=VAT Prod. Posting Group }
    { 9   ;   ;VAT Calculation Type;Option        ;CaptionML=ENU=VAT Calculation Type;
                                                   OptionCaptionML=ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   Editable=No }
    { 10  ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                GetPurchAllocHeader;
                                                                GetPurchHeader;

                                                                "VAT Difference" := 0;
                                                                Amount := ROUND(Amount,Currency."Amount Rounding Precision");
                                                                CASE "VAT Calculation Type" OF
                                                                  "VAT Calculation Type"::"Normal VAT",
                                                                  "VAT Calculation Type"::"Reverse Charge VAT":
                                                                    BEGIN
                                                                      "VAT Base Amount" :=
                                                                        ROUND(Amount * (1 - PurchHeader."VAT Base Discount %" / 100),Currency."Amount Rounding Precision");
                                                                      "Amount Including VAT" :=
                                                                        ROUND(Amount + "VAT Base Amount" * "VAT %" / 100,Currency."Amount Rounding Precision");
                                                                    END;
                                                                  "VAT Calculation Type"::"Full VAT":
                                                                    IF Amount <> 0 THEN
                                                                      FIELDERROR(Amount,
                                                                        STRSUBSTNO(
                                                                          Text011,FIELDCAPTION("VAT Calculation Type"),
                                                                          "VAT Calculation Type"));
                                                                  "VAT Calculation Type"::"Sales Tax":
                                                                    ERROR(Text001,FIELDCAPTION("VAT Calculation Type"),"VAT Calculation Type");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Amount;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 11  ;   ;Amount Including VAT;Decimal       ;CaptionML=ENU=Amount Including VAT;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 12  ;   ;VAT %               ;Decimal       ;CaptionML=ENU=VAT %;
                                                   Editable=No }
    { 13  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=ENU=Shortcut Dimension 1 Code;
                                                   Editable=No;
                                                   CaptionClass='1,2,1' }
    { 14  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   CaptionML=ENU=Shortcut Dimension 2 Code;
                                                   Editable=No;
                                                   CaptionClass='1,2,2' }
    { 15  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=ENU=Currency Code;
                                                   Editable=No }
    { 16  ;   ;VAT Difference      ;Decimal       ;OnValidate=BEGIN
                                                                "Amount Including VAT" := "Amount Including VAT" + "VAT Difference";
                                                              END;

                                                   CaptionML=ENU=VAT Difference;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 17  ;   ;VAT Identifier      ;Code10        ;CaptionML=ENU=VAT Identifier;
                                                   Editable=No }
    { 18  ;   ;VAT Base Amount     ;Decimal       ;CaptionML=ENU=VAT Base Amount;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 19  ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   CaptionML=ENU=Dimension Set ID }
    { 20  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 11012000;;Job No.            ;Code20        ;TableRelation=Job;
                                                   CaptionML=ENU=Job No. }
    { 11012010;;Quantity           ;Decimal       ;CaptionML=ENU=Quantity }
    { 11012020;;Unit of Measure Code;Code10       ;CaptionML=ENU=Unit of Measure Code }
    { 11012030;;Purchase Action    ;Code20        ;CaptionML=ENU=Purchase Action }
  }
  KEYS
  {
    {    ;Document No.,Line No.                   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PurchAllocHeader@161024015 : Record 6085730;
      GenBusPostingGrp@161024013 : Record 250;
      GenProdPostingGrp@161024012 : Record 251;
      VATPostingSetup@161024014 : Record 325;
      Currency@161024016 : Record 4;
      Text001@161024017 : TextConst 'ENU="%1 = %2 is not supported."';
      PurchHeader@161024018 : Record 38;
      Text011@161024019 : TextConst 'ENU=" must be 0 when %1 is %2"';

    PROCEDURE GetPurchAllocHeader@161024014();
    BEGIN
      IF PurchAllocHeader."No." <> "Document No." THEN
        IF NOT PurchAllocHeader.GET("Document No.") THEN
          CLEAR(PurchAllocHeader);
    END;

    PROCEDURE GetPurchHeader@161024016();
    BEGIN
      GetPurchAllocHeader;
      IF NOT PurchHeader.GET(PurchAllocHeader."Document Type",PurchAllocHeader."Document No.") THEN
        CLEAR(PurchHeader);
    END;

    PROCEDURE SetupNewLine@161024015();
    BEGIN
      GetPurchAllocHeader;
      "Gen. Bus. Posting Group" := PurchAllocHeader."Gen. Bus. Posting Group";
      "VAT Bus. Posting Group" := PurchAllocHeader."VAT Bus. Posting Group";
      "Currency Code" := PurchAllocHeader."Currency Code";
    END;

    PROCEDURE TestStatus@161024017();
    BEGIN
      GetPurchAllocHeader;
      PurchAllocHeader.TESTFIELD(Status,PurchAllocHeader.Status::Open);
    END;

    PROCEDURE ShowDimensions@25();
    VAR
      PurchAllocHeader@1160040000 : Record 6085730;
      DimMgt@6085573 : Codeunit 408;
    BEGIN
      TESTFIELD("Document No.");
      TESTFIELD("Line No.");

      GetPurchAllocHeader;

      IF PurchAllocHeader.Status = PurchAllocHeader.Status::Open THEN
        "Dimension Set ID" :=
          DimMgt.EditDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2',"Document No.","Line No."))
      ELSE
        DimMgt.ShowDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2',"Document No.","Line No."));
    END;

    BEGIN
    {
      4PS: New Fields: "Job No.", Quantity, "Unit of Measure Code", "Purchase Action"
    }
    END.
  }
}

