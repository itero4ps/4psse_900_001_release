OBJECT Table 6085757 Record Barcode
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.3.50.02;
  }
  PROPERTIES
  {
    OnInsert=VAR
               DCSetup@1160040001 : Record 6085573;
               RecBarcode@1160040000 : Record 6085757;
               NoSeriesMgt@1160040002 : Codeunit 396;
             BEGIN
               RecBarcode.LOCKTABLE;
               IF RecBarcode.FINDLAST THEN
                 "Entry No." := RecBarcode."Entry No." + 1
               ELSE
                 "Entry No." := 1;

               IF Barcode = '' THEN BEGIN
                 DCSetup.GET;
                 DCSetup.TESTFIELD("Barcode Nos.");
                 NoSeriesMgt.InitSeries(DCSetup."Barcode Nos.",DCSetup."Barcode Nos.",WORKDATE,Barcode,DCSetup."Barcode Nos.");
               END;
             END;

    CaptionML=ENU=Record Barcode;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=ENU=Entry No. }
    { 2   ;   ;Source Record ID Tree ID;Integer   ;TableRelation="Record ID Tree";
                                                   CaptionML=ENU=Record ID }
    { 3   ;   ;Barcode             ;Code50        ;CaptionML=ENU=Barcode }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Source Record ID Tree ID                 }
    {    ;Barcode                                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1160040000 : TextConst 'ENU=Barcode %1 already exist for %2.';

    PROCEDURE GetBarcode@1160040000(VAR RecRef@1160040000 : RecordRef) : Code[50];
    VAR
      RecBarcode@1160040002 : Record 6085757;
      DCMgt@1160040003 : Codeunit 6085603;
      RecIDMgt@1160040001 : Codeunit 6085604;
    BEGIN
      RecBarcode.SETCURRENTKEY("Source Record ID Tree ID");
      RecBarcode.SETRANGE("Source Record ID Tree ID",RecIDMgt.GetRecIDTreeID(RecRef,TRUE));
      IF NOT RecBarcode.FINDFIRST THEN BEGIN
        DCMgt.IsLogActive(RecRef.NUMBER,TRUE);
        RecBarcode.INIT;
        RecBarcode.VALIDATE("Source Record ID Tree ID",RecIDMgt.GetRecIDTreeID(RecRef,TRUE));
        RecBarcode.INSERT(TRUE);
      END;

      EXIT(RecBarcode.Barcode);
    END;

    PROCEDURE SaveBarcode@1160040001(VAR RecRef@1160040000 : RecordRef;NewBarcode@1160040001 : Code[50]) : Code[50];
    VAR
      RecBarcode@1160040002 : Record 6085757;
      DCMgt@1160040003 : Codeunit 6085603;
      RecIDMgt@1160040004 : Codeunit 6085604;
    BEGIN
      RecBarcode.SETCURRENTKEY(Barcode);
      RecBarcode.SETRANGE(Barcode,NewBarcode);
      IF NOT RecBarcode.ISEMPTY THEN
        ERROR(Text001,NewBarcode,RecIDMgt.GetRecIDTreeID(RecRef,TRUE))
      ELSE BEGIN
        DCMgt.IsLogActive(RecRef.NUMBER,TRUE);
        RecBarcode.INIT;
        RecBarcode.VALIDATE("Source Record ID Tree ID",RecIDMgt.GetRecIDTreeID(RecRef,TRUE));
        RecBarcode.VALIDATE(Barcode,NewBarcode);
        RecBarcode.INSERT(TRUE);
      END;

      EXIT(RecBarcode.Barcode);
    END;

    BEGIN
    END.
  }
}

