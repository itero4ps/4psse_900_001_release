OBJECT Table 6085762 Table Filter Field
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Table Filter Field;
  }
  FIELDS
  {
    { 1   ;   ;Table Filter GUID   ;GUID          ;CaptionML=ENU=Table Filter GUID }
    { 2   ;   ;Table No.           ;Integer       ;CaptionML=ENU=Table No. }
    { 3   ;   ;Field No.           ;Integer       ;CaptionML=ENU=Field No. }
    { 4   ;   ;Value (Text)        ;Text250       ;CaptionML=ENU=Value (Text) }
    { 5   ;   ;Value (Integer)     ;Integer       ;CaptionML=ENU=Value (Integer) }
    { 6   ;   ;Value (Date)        ;Date          ;CaptionML=ENU=Value (Date) }
    { 7   ;   ;Value (Decimal)     ;Decimal       ;CaptionML=ENU=Value (Decimal) }
    { 8   ;   ;Value (Boolean)     ;Boolean       ;CaptionML=ENU=Value (Boolean) }
    { 9   ;   ;Filter Type         ;Option        ;CaptionML=ENU=Filter Type;
                                                   OptionCaptionML=ENU=Fixed Filter,Document Field;
                                                   OptionString=Fixed Filter,Document Field }
    { 10  ;   ;Template No.        ;Code20        ;TableRelation=Template;
                                                   CaptionML=ENU=Template No. }
    { 11  ;   ;Template Field Type ;Option        ;TableRelation="Template Field".Type WHERE (Template No.=FIELD(Template No.));
                                                   CaptionML=ENU=Template Field Type;
                                                   OptionCaptionML=ENU=Header,Line;
                                                   OptionString=Header,Line }
    { 12  ;   ;Template Field Code ;Code20        ;TableRelation="Template Field".Code WHERE (Template No.=FIELD(Template No.),
                                                                                              Type=FIELD(Template Field Type));
                                                   CaptionML=ENU=Template Field Code }
  }
  KEYS
  {
    {    ;Table Filter GUID,Field No.             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1160040000 : TextConst 'ENU=Field type %1 not supported for Table filtering.';

    PROCEDURE SetValues@161024012(VAR Value@1160040000 : Text[250];VAR FilterType@1160040009 : 'Fixed Filter,Document Field';TemplateNo@1160040010 : Code[20];TempFieldType@1160040011 : Integer) : Boolean;
    VAR
      Field@1160040001 : Record 2000000041;
      CaptureMgt@1160040004 : Codeunit 6085576;
      i@1160040002 : Integer;
    BEGIN
      "Value (Text)" := '';
      "Value (Integer)" := 0;
      "Value (Date)" := 0D;
      "Value (Decimal)" := 0;
      "Value (Boolean)" := FALSE;
      "Template No." := '';
      "Template Field Type" := 0;
      "Template Field Code" := '';

      IF FilterType = FilterType::"Fixed Filter" THEN BEGIN
        Field.GET("Table No.","Field No.");

        CASE Field.Type OF
          Field.Type::Code:
            VALIDATE("Value (Text)",UPPERCASE(Value));
          Field.Type::Text:
            VALIDATE("Value (Text)",Value);
          Field.Type::Date:
            IF EVALUATE("Value (Date)",Value) THEN;
          Field.Type::Decimal:
            IF EVALUATE("Value (Decimal)",Value) THEN;
          Field.Type::Boolean:
            IF EVALUATE("Value (Boolean)",Value) THEN;
          Field.Type::Integer:
            IF EVALUATE("Value (Integer)",Value) THEN;
          Field.Type::Option:
            BEGIN
              i := CaptureMgt.OptionToInt("Table No.","Field No.",Value);
              IF i = -2 THEN
                EXIT(FALSE)
              ELSE IF i > -1 THEN
                "Value (Integer)" := i
              ELSE
                IF NOT EVALUATE("Value (Integer)",Value) THEN
                  EXIT(FALSE);
              Value := FORMAT("Value (Integer)");
            END;
          ELSE
            ERROR(Text001,FORMAT(Field.Type));
        END;
      END ELSE BEGIN
        "Template No." := TemplateNo;
        "Template Field Type" := TempFieldType;
        "Template Field Code" := Value;
      END;

      "Filter Type" := FilterType;
      GetValues(Value,FilterType);

      EXIT(TRUE);
    END;

    PROCEDURE GetValues@161024013(VAR Value@1160040007 : Text[250];VAR FilterType@1160040006 : 'Fixed Filter,Document Field');
    VAR
      Field@1160040002 : Record 2000000041;
      CaptureMgt@1160040004 : Codeunit 6085576;
      RecRef@1160040001 : RecordRef;
      FieldRef@1160040000 : FieldRef;
    BEGIN
      Value := '';
      FilterType := "Filter Type";

      IF "Filter Type" = "Filter Type"::"Fixed Filter" THEN BEGIN
        IF NOT Field.GET("Table No.","Field No.") THEN
          EXIT;

        CASE Field.Type OF
          Field.Type::Text,Field.Type::Code:
            Value := "Value (Text)";
          Field.Type::Date:
            Value := FORMAT(FORMAT("Value (Date)"));
          Field.Type::Decimal:
            Value := FORMAT(FORMAT("Value (Decimal)"));
          Field.Type::Boolean:
            Value := FORMAT(FORMAT("Value (Boolean)"));
          Field.Type::Integer:
            Value := FORMAT(FORMAT("Value (Integer)"));
          Field.Type::Option:
            Value := FORMAT("Value (Integer)");
          ELSE
            ERROR(Text001,FORMAT(FieldRef.TYPE));
        END
      END ELSE
        Value := "Template Field Code";
    END;

    PROCEDURE IntToOptionText@1160040000(TableNo@1160040001 : Integer;FieldNo@1160040002 : Integer;OptionValue@1160040000 : Text[30]) : Text[250];
    VAR
      RecRef@1160040004 : RecordRef;
      FieldRef@1160040003 : FieldRef;
      OptionValueAsInt@1160040005 : Integer;
    BEGIN
      IF NOT EVALUATE(OptionValueAsInt,OptionValue) THEN
        EXIT;

      IF OptionValueAsInt = -1 THEN
        EXIT;

      RecRef.OPEN(TableNo);
      FieldRef := RecRef.FIELD(FieldNo);
      EXIT(FORMAT(SELECTSTR(OptionValueAsInt+1,FieldRef.OPTIONCAPTION)));
    END;

    BEGIN
    END.
  }
}

