OBJECT Table 6085767 Purchase Header DC Info.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.00.01,4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Purchase Header DC Info.;
  }
  FIELDS
  {
    { 1   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order }
    { 2   ;   ;No.                 ;Code20        ;CaptionML=ENU=No. }
    { 3   ;   ;Approval Flow Code  ;Code10        ;TableRelation="Approval Flow";
                                                   OnValidate=VAR
                                                                PurchHeader@1160040000 : Record 38;
                                                              BEGIN
                                                                PurchHeader.GET("Document Type","No.");

                                                                PurchHeader.TESTFIELD(Status,PurchHeader.Status::Open);
                                                              END;

                                                   CaptionML=ENU=Approval Flow Code }
  }
  KEYS
  {
    {    ;Document Type,No.                       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetApprovalFlowCode@1160040002(PurchHeader@1160040000 : Record 38) : Code[10];
    VAR
      PurchHeaderDCInfo@1160040002 : Record 6085767;
    BEGIN
      IF PurchHeaderDCInfo.GET(PurchHeader."Document Type",PurchHeader."No.") THEN
        EXIT(PurchHeaderDCInfo."Approval Flow Code");
    END;

    PROCEDURE LookupApprovalFlowCode@1160040000(VAR Text@1160040001 : Text[1024]) : Boolean;
    VAR
      ApprovalFlow@1160040003 : Record 6085725;
    BEGIN
      IF ApprovalFlow.GET(Text) THEN;

      IF PAGE.RUNMODAL(0,ApprovalFlow) = ACTION::LookupOK THEN BEGIN
        Text := ApprovalFlow.Code;
        EXIT(TRUE);
      END;
    END;

    PROCEDURE UpdateApprovalFlowCode@1160040005(PurchHeader@1160040000 : Record 38;NewCode@1160040001 : Code[10]);
    VAR
      PurchHeaderDCInfo@1160040002 : Record 6085767;
    BEGIN
      PurchHeader.TESTFIELD(Status,PurchHeader.Status::Open);

      IF NewCode = '' THEN BEGIN
        IF PurchHeaderDCInfo.GET(PurchHeader."Document Type",PurchHeader."No.") THEN
          PurchHeaderDCInfo.DELETE;
        EXIT;
      END;

      IF NOT PurchHeaderDCInfo.GET(PurchHeader."Document Type",PurchHeader."No.") THEN BEGIN
        PurchHeaderDCInfo."Document Type" := PurchHeader."Document Type";
        PurchHeaderDCInfo."No." := PurchHeader."No.";
        PurchHeaderDCInfo.INSERT;
      END;

      PurchHeaderDCInfo."Approval Flow Code" := NewCode;
      PurchHeaderDCInfo.MODIFY;
    END;

    PROCEDURE TransferToPurchInvHeader@1160040001(PurchHeader@1160040001 : Record 38;PurchInvHeader@1160040000 : Record 122);
    VAR
      PurchHeaderDCInfo@1160040002 : Record 6085767;
      PurchInvHeaderDCInfo@1160040003 : Record 6085769;
    BEGIN
      IF NOT PurchHeaderDCInfo.GET(PurchHeader."Document Type",PurchHeader."No.") THEN
        EXIT;

      PurchInvHeaderDCInfo."No." := PurchInvHeader."No.";
      PurchInvHeaderDCInfo."Approval Flow Code" := PurchHeaderDCInfo."Approval Flow Code";
      PurchInvHeaderDCInfo.INSERT;
    END;

    PROCEDURE TransferToPurchCrMemoHdr@1160040003(PurchHeader@1160040001 : Record 38;PurchCrMemoHdr@1160040000 : Record 124);
    VAR
      PurchHeaderDCInfo@1160040002 : Record 6085767;
      PurchCrMemoHdrDCInfo@1160040003 : Record 6085770;
    BEGIN
      IF NOT PurchHeaderDCInfo.GET(PurchHeader."Document Type",PurchHeader."No.") THEN
        EXIT;

      PurchCrMemoHdrDCInfo."No." := PurchCrMemoHdr."No.";
      PurchCrMemoHdrDCInfo."Approval Flow Code" := PurchHeaderDCInfo."Approval Flow Code";
      PurchCrMemoHdrDCInfo.INSERT;
    END;

    PROCEDURE UpdateApprovalFlowCodeDirect@1100528200(PurchHeader@1160040000 : Record 38;NewCode@1160040001 : Code[10]);
    VAR
      PurchHeaderDCInfo@1160040002 : Record 6085767;
    BEGIN
      //**4PS
      IF NOT (PurchHeader.Status IN [PurchHeader.Status::Open, PurchHeader.Status::Released]) THEN
        PurchHeader.TESTFIELD(Status,PurchHeader.Status::Open);

      IF NewCode = '' THEN BEGIN
        IF PurchHeaderDCInfo.GET(PurchHeader."Document Type",PurchHeader."No.") THEN
          PurchHeaderDCInfo.DELETE;
        EXIT;
      END;


      IF NOT PurchHeaderDCInfo.GET(PurchHeader."Document Type",PurchHeader."No.") THEN BEGIN
        PurchHeaderDCInfo."Document Type" := PurchHeader."Document Type";
        PurchHeaderDCInfo."No." := PurchHeader."No.";
        PurchHeaderDCInfo.INSERT;
      END;

      PurchHeaderDCInfo."Approval Flow Code" := NewCode;
      PurchHeaderDCInfo.MODIFY;
    END;

    BEGIN
    END.
  }
}

