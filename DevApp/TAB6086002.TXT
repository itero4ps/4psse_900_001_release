OBJECT Table 6086002 Continia User Setup
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.05;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Continia User ID");
             END;

    OnDelete=VAR
               Company@1160040000 : Record 2000000006;
               ContiniaUser@1160040001 : Record 6086001;
               ContiniaUserSetup@1160040003 : Record 6086002;
               AppvlSharing@1160040006 : Record 6085749;
               ApprovalFlowLine@1160040005 : Record 6085726;
               ApprovalUser@1160040004 : Record 6085746;
               UserSetup@1160040007 : Record 91;
               ExistInOtherCompany@1160040002 : Boolean;
             BEGIN
               Company.SETFILTER(Name,'<>%1',COMPANYNAME);
               IF Company.FINDSET THEN BEGIN
                 ExistInOtherCompany := TRUE;
                 REPEAT
                   ContiniaUserSetup.CHANGECOMPANY(Company.Name);
                   ContiniaUserSetup.SETRANGE("Continia User ID","Continia User ID");
                   ExistInOtherCompany := NOT ContiniaUserSetup.ISEMPTY;
                 UNTIL (Company.NEXT = 0) OR ExistInOtherCompany;
               END;

               IF NOT ExistInOtherCompany THEN
                 IF NOT ContiniaUserDeleteSuspended THEN
                   IF ContiniaUser.GET("Continia User ID") THEN BEGIN
                     ContiniaUser.SuspendContiniaUserSetupDelete(TRUE);
                     ContiniaUser.DELETE(TRUE);
                   END;

               AppvlSharing.SETRANGE("Owner User ID","Continia User ID");
               AppvlSharing.DELETEALL(TRUE);
               AppvlSharing.SETRANGE("Owner User ID");

               AppvlSharing.SETCURRENTKEY("Shared to User ID");
               AppvlSharing.SETRANGE("Shared to User ID","Continia User ID");
               AppvlSharing.DELETEALL(TRUE);

               ApprovalFlowLine.SETRANGE("Approver ID","Continia User ID");
               ApprovalFlowLine.DELETEALL(TRUE);

               ApprovalUser.SETRANGE("User ID","Continia User ID");
               ApprovalUser.DELETEALL(TRUE);

               IF NOT UserSetupDeleteSuspended THEN
                 IF UserSetup.GET("Continia User ID") THEN
                   IF UserSetup.DELETE(TRUE) THEN;
             END;

    OnRename=BEGIN
               ERROR(Text005,TABLECAPTION);
             END;

    CaptionML=ENU=Continia User Setup;
    LookupPageID=Page6086051;
    DrillDownPageID=Page6086051;
  }
  FIELDS
  {
    { 1   ;   ;Continia User ID    ;Code50        ;TableRelation="Continia User";
                                                   CaptionML=ENU=Continia User ID;
                                                   NotBlank=Yes }
    { 2   ;   ;Web Approval User   ;Boolean       ;OnValidate=VAR
                                                                ContiniaUser@1160040000 : Record 6086001;
                                                              BEGIN
                                                                ContiniaUser.GET("Continia User ID");
                                                                ContiniaUser.TESTFIELD("E-Mail");
                                                                ContiniaUser."Send Welcome E-mail" := ContiniaUser.IsWebApprovalUser(FALSE) OR "Web Approval User";
                                                                ContiniaUser.MODIFY(TRUE);
                                                              END;

                                                   CaptionML=ENU=Web Approval User }
    { 3   ;   ;Expense Management User;Boolean    ;OnValidate=VAR
                                                                ContiniaUser@1160040000 : Record 6086001;
                                                              BEGIN
                                                                ContiniaUser.GET("Continia User ID");
                                                                ContiniaUser.TESTFIELD("E-Mail");
                                                              END;

                                                   CaptionML=ENU=Expense Management User }
    { 5   ;   ;Employee No.        ;Code50        ;TableRelation=Employee;
                                                   OnValidate=VAR
                                                                Employee@1160040000 : Record 5200;
                                                                ContiniaUser@1160040001 : Record 6086001;
                                                              BEGIN
                                                                IF "Employee No." <> '' THEN BEGIN
                                                                  IF (GetName = '') OR (GetEmail = '') THEN BEGIN
                                                                    Employee.GET("Employee No.");
                                                                    IF (GetName = '') AND (Employee.FullName <> '') THEN
                                                                      UpdateContiniaUserField(ContiniaUser.FIELDNO(Name),Employee.FullName);

                                                                    IF (GetEmail = '') AND (Employee."E-Mail" <> '') THEN
                                                                      UpdateContiniaUserField(ContiniaUser.FIELDNO("E-Mail"),Employee."E-Mail");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Employee No. }
    { 10  ;   ;Can Edit Posting Lines;Boolean     ;OnValidate=VAR
                                                                ContiniaUserList@1160040000 : Record 6086003;
                                                              BEGIN
                                                                IF "Can Edit Posting Lines" THEN BEGIN
                                                                  IF "G/L Account List Code" = '' THEN BEGIN
                                                                    ContiniaUserList.SETRANGE(Type,ContiniaUserList.Type::"G/L Account");
                                                                    IF ContiniaUserList.FINDFIRST THEN
                                                                      "G/L Account List Code" := ContiniaUserList.Code;
                                                                  END;

                                                                  TESTFIELD("G/L Account List Code");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Can Edit Posting Lines }
    { 11  ;   ;Document Search     ;Option        ;OnValidate=VAR
                                                                Setup@161024012 : Record 6085573;
                                                                PurchInvHeader@161024013 : Record 122;
                                                                PurchCrMemoHeader@161024014 : Record 124;
                                                                PostedApprEntry@161024016 : Record 456;
                                                                WebDocSearch@161024017 : Record 6086005;
                                                                WebSearchMgnt@161024015 : Codeunit 6085748;
                                                                Window@161024018 : Dialog;
                                                              BEGIN
                                                                IF "Document Search" = "Document Search"::"Own documents only" THEN BEGIN
                                                                  Window.OPEN(STRSUBSTNO(Text002,WebDocSearch.TABLECAPTION));
                                                                  Setup.GET;
                                                                  Setup.TESTFIELD("Maintain User Document Search");

                                                                  PostedApprEntry.SETFILTER("Table ID",'%1|%2',DATABASE::"Purch. Inv. Header",DATABASE::"Purch. Cr. Memo Hdr.");
                                                                  PostedApprEntry.SETRANGE(Status,PostedApprEntry.Status::Approved);
                                                                  PostedApprEntry.SETRANGE("Approver ID","Continia User ID");
                                                                  IF PostedApprEntry.FINDSET THEN
                                                                    REPEAT
                                                                      IF PostedApprEntry."Table ID" = DATABASE::"Purch. Inv. Header" THEN BEGIN
                                                                        IF PurchInvHeader.GET(PostedApprEntry."Document No.") THEN BEGIN
                                                                          IF NOT PurchInvHeader.MARK THEN BEGIN
                                                                            WebSearchMgnt.UpdateWebSearchFromInvoice(PurchInvHeader);
                                                                            PurchInvHeader.MARK(TRUE);
                                                                          END;
                                                                        END;
                                                                      END ELSE IF PostedApprEntry."Table ID" = DATABASE::"Purch. Cr. Memo Hdr." THEN BEGIN
                                                                        IF PurchCrMemoHeader.GET(PostedApprEntry."Document No.") THEN BEGIN
                                                                          IF NOT PurchCrMemoHeader.MARK THEN BEGIN
                                                                            WebSearchMgnt.UpdateWebSearchFromCrMemo(PurchCrMemoHeader);
                                                                            PurchCrMemoHeader.MARK(TRUE);
                                                                          END;
                                                                        END;
                                                                      END;
                                                                    UNTIL PostedApprEntry.NEXT = 0;

                                                                  Window.CLOSE;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Document Search;
                                                   OptionCaptionML=ENU=All documents,Own documents only;
                                                   OptionString=All documents,Own documents only }
    { 13  ;   ;Comment             ;Text250       ;CaptionML=ENU=Comment }
    { 15  ;   ;G/L Account List Code;Code10       ;TableRelation="Continia User List".Code WHERE (Type=CONST(G/L Account));
                                                   CaptionML=ENU=G/L Account List Code }
    { 16  ;   ;Item List Code      ;Code10        ;TableRelation="Continia User List".Code WHERE (Type=CONST(Item));
                                                   CaptionML=ENU=Item List Code }
    { 17  ;   ;Fixed Asset List Code;Code10       ;TableRelation="Continia User List".Code WHERE (Type=CONST(Fixed Asset));
                                                   CaptionML=ENU=Fixed Asset List Code }
    { 18  ;   ;Item Charge List Code;Code10       ;TableRelation="Continia User List".Code WHERE (Type=CONST(Item Charge));
                                                   CaptionML=ENU=Item Charge List Code }
    { 19  ;   ;Web Dimension 1 List Code;Code10   ;OnValidate=BEGIN
                                                                ValidateWebDimListCode(1,"Web Dimension 1 List Code");
                                                              END;

                                                   CaptionML=ENU=Web Dimension 1 List Code;
                                                   CaptionClass=GetWebDimCaption(1) }
    { 20  ;   ;Web Dimension 2 List Code;Code10   ;OnValidate=BEGIN
                                                                ValidateWebDimListCode(2,"Web Dimension 2 List Code");
                                                              END;

                                                   CaptionML=ENU=Web Dimension 2 List Code;
                                                   CaptionClass=GetWebDimCaption(2) }
    { 21  ;   ;Web Dimension 3 List Code;Code10   ;OnValidate=BEGIN
                                                                ValidateWebDimListCode(3,"Web Dimension 3 List Code");
                                                              END;

                                                   CaptionML=ENU=Web Dimension 3 List Code;
                                                   CaptionClass=GetWebDimCaption(3) }
    { 22  ;   ;Web Dimension 4 List Code;Code10   ;OnValidate=BEGIN
                                                                ValidateWebDimListCode(4,"Web Dimension 4 List Code");
                                                              END;

                                                   CaptionML=ENU=Web Dimension 4 List Code;
                                                   CaptionClass=GetWebDimCaption(4) }
    { 23  ;   ;Web Dimension 5 List Code;Code10   ;OnValidate=BEGIN
                                                                ValidateWebDimListCode(5,"Web Dimension 5 List Code");
                                                              END;

                                                   CaptionML=ENU=Web Dimension 5 List Code;
                                                   CaptionClass=GetWebDimCaption(5) }
    { 24  ;   ;Web Dimension 6 List Code;Code10   ;OnValidate=BEGIN
                                                                ValidateWebDimListCode(6,"Web Dimension 6 List Code");
                                                              END;

                                                   CaptionML=ENU=Web Dimension 6 List Code;
                                                   CaptionClass=GetWebDimCaption(6) }
    { 25  ;   ;Web Dimension 7 List Code;Code10   ;OnValidate=BEGIN
                                                                ValidateWebDimListCode(7,"Web Dimension 7 List Code");
                                                              END;

                                                   CaptionML=ENU=Web Dimension 7 List Code;
                                                   CaptionClass=GetWebDimCaption(7) }
    { 26  ;   ;Web Dimension 8 List Code;Code10   ;OnValidate=BEGIN
                                                                ValidateWebDimListCode(8,"Web Dimension 8 List Code");
                                                              END;

                                                   CaptionML=ENU=Web Dimension 8 List Code;
                                                   CaptionClass=GetWebDimCaption(8) }
    { 27  ;   ;Job List Code       ;Code10        ;TableRelation="Continia User List".Code WHERE (Type=CONST(Job));
                                                   CaptionML=ENU=Job List Code }
    { 30  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=VAR
                                                                Vendor@1160040000 : Record 23;
                                                                ContiniaUser@1160040001 : Record 6086001;
                                                              BEGIN
                                                                IF "Vendor No." <> '' THEN BEGIN
                                                                  IF GetName = '' THEN BEGIN
                                                                    CALCFIELDS("Vendor Name");
                                                                    IF "Vendor Name" <> '' THEN
                                                                      UpdateContiniaUserField(ContiniaUser.FIELDNO(Name),"Vendor Name");
                                                                  END;

                                                                  IF GetEmail = '' THEN BEGIN
                                                                    Vendor.GET("Vendor No.");
                                                                    IF Vendor."E-Mail" <> '' THEN
                                                                      UpdateContiniaUserField(ContiniaUser.FIELDNO("E-Mail"),Vendor."E-Mail");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Vendor No. }
    { 31  ;   ;Vendor Name         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=ENU=Vendor Name;
                                                   Editable=No }
    { 32  ;   ;Shared to this user ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Approval Sharing" WHERE (Shared to User ID=FIELD(Continia User ID)));
                                                   CaptionML=ENU=Shared to this user;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 33  ;   ;Shared to other users;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count("Approval Sharing" WHERE (Owner User ID=FIELD(Continia User ID)));
                                                   CaptionML=ENU=Shared to other users;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 37  ;   ;Expense User Group  ;Code20        ;TableRelation="Expense User Group";
                                                   CaptionML=ENU=Expense User Group }
    { 39  ;   ;Expense Reminder Code;Code10       ;TableRelation="Expense Reminder Terms";
                                                   CaptionML=ENU=Expense Reminder Code }
    { 40  ;   ;No. of Purch. Doc. for Appr.;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Approval Entry" WHERE (Table ID=FILTER(38),
                                                                                             Approver ID=FIELD(Continia User ID),
                                                                                             Status=CONST(Open)));
                                                   CaptionML=ENU=No. of Purch. Doc. for Approval;
                                                   Editable=No }
    { 41  ;   ;Expense Amount Approval Limit;Decimal;
                                                   OnValidate=BEGIN
                                                                IF "Unlimited Expense Approval" AND ("Expense Amount Approval Limit" <> 0) THEN
                                                                  ERROR(Text003,FIELDCAPTION("Expense Amount Approval Limit"),FIELDCAPTION("Unlimited Expense Approval"));
                                                                IF "Expense Amount Approval Limit" < 0 THEN
                                                                  ERROR(Text004);
                                                              END;

                                                   CaptionML=ENU=Expense Amount Approval Limit;
                                                   DecimalPlaces=0:0;
                                                   BlankZero=Yes }
    { 42  ;   ;Unlimited Expense Approval;Boolean ;OnValidate=BEGIN
                                                                IF "Unlimited Expense Approval" THEN
                                                                  "Expense Amount Approval Limit" := 0;
                                                              END;

                                                   CaptionML=ENU=Unlimited Expense Approval }
    { 43  ;   ;No. of Expenses for Approval;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Approval Entry" WHERE (Table ID=FILTER(6086320),
                                                                                             Approver ID=FIELD(Continia User ID),
                                                                                             Status=CONST(Open)));
                                                   CaptionML=ENU=No. of Expenses for Approval;
                                                   Editable=No }
    { 59  ;   ;Vendor Balance (LCY);Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Detailed Vendor Ledg. Entry"."Amount (LCY)" WHERE (Vendor No.=FIELD(Vendor No.)));
                                                   CaptionML=ENU=Vendor Balance (LCY);
                                                   Editable=No;
                                                   AutoFormatType=1 }
  }
  KEYS
  {
    {    ;Continia User ID                        ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1160040001 : TextConst 'ENU=%1 List Code';
      Text002@1160040000 : TextConst 'ENU=Updating %1.';
      Text003@1160040003 : TextConst 'ENU=You cannot have both a %1 and %2.';
      Text004@1160040002 : TextConst 'ENU=You cannot have approval limits less than zero.';
      Text005@1160040004 : TextConst 'ENU=You cannot rename a %1.';
      ContiniaUserDeleteSuspended@1160040005 : Boolean;
      UserSetupDeleteSuspended@1160040006 : Boolean;

    PROCEDURE ValidateContiniaUserID@1160040008(NewUserID@1160040001 : Code[50]);
    VAR
      ContiniaUser@1160040000 : Record 6086001;
    BEGIN
      IF NOT ContiniaUser.GET(NewUserID) THEN BEGIN
        ContiniaUser.INIT;
        ContiniaUser.VALIDATE("User ID",NewUserID);
        ContiniaUser.INSERT(TRUE);
      END;

      IF "Continia User ID" <> '' THEN
        RENAME(NewUserID)
      ELSE
        VALIDATE("Continia User ID",NewUserID);
    END;

    PROCEDURE LookupContiniaUser@1160040007(VAR Text@1160040001 : Text[50]) : Boolean;
    VAR
      ContiniaUserMgt@1160040002 : Codeunit 6085781;
    BEGIN
      EXIT(ContiniaUserMgt.LookupUser(Text,TRUE,TRUE));
    END;

    PROCEDURE GetWebDimCaption@161024012(WebDimNo@161024012 : Integer) : Text[50];
    VAR
      DCSetup@161024013 : Record 6085573;
      Dimension@161024015 : Record 348;
      DimCode@161024014 : Code[20];
    BEGIN
      IF NOT DCSetup.GET THEN
        EXIT;

      CASE WebDimNo OF
        1:
          DimCode := DCSetup."Web: Dimension 1 Code (Lines)";
        2:
          DimCode := DCSetup."Web: Dimension 2 Code (Lines)";
        3:
          DimCode := DCSetup."Web: Dimension 3 Code (Lines)";
        4:
          DimCode := DCSetup."Web: Dimension 4 Code (Lines)";
        5:
          DimCode := DCSetup."Web: Dimension 5 Code (Lines)";
        6:
          DimCode := DCSetup."Web: Dimension 6 Code (Lines)";
        7:
          DimCode := DCSetup."Web: Dimension 7 Code (Lines)";
        8:
          DimCode := DCSetup."Web: Dimension 8 Code (Lines)";
      END;

      IF Dimension.GET(DimCode) THEN
        EXIT(STRSUBSTNO(Text001,Dimension.Name));
    END;

    PROCEDURE ValidateWebDimListCode@161024017(WebDimNo@161024012 : Integer;VAR Code@161024016 : Code[10]);
    VAR
      UserList@161024017 : Record 6086003;
    BEGIN
      IF Code = '' THEN
        EXIT;

      FilterWebDimList(WebDimNo,UserList);
      UserList.SETRANGE(Code,Code);
      UserList.FINDFIRST
    END;

    PROCEDURE LookupWebDimListCode@161024013(WebDimNo@161024012 : Integer;VAR Code@161024016 : Code[10]) : Boolean;
    VAR
      UserList@161024017 : Record 6086003;
    BEGIN
      FilterWebDimList(WebDimNo,UserList);
      UserList.SETRANGE(Code,Code);
      IF UserList.FINDFIRST THEN;
      UserList.SETRANGE(Code);

      IF PAGE.RUNMODAL(0,UserList) = ACTION::LookupOK THEN BEGIN
        Code := UserList.Code;
        EXIT(TRUE);
      END;
    END;

    PROCEDURE FilterWebDimList@161024014(WebDimNo@161024012 : Integer;VAR UserList@161024017 : Record 6086003) : Text[50];
    VAR
      DCSetup@161024013 : Record 6085573;
      DimCode@161024014 : Code[20];
    BEGIN
      DCSetup.GET;

      CASE WebDimNo OF
        1:
          DimCode := DCSetup."Web: Dimension 1 Code (Lines)";
        2:
          DimCode := DCSetup."Web: Dimension 2 Code (Lines)";
        3:
          DimCode := DCSetup."Web: Dimension 3 Code (Lines)";
        4:
          DimCode := DCSetup."Web: Dimension 4 Code (Lines)";
        5:
          DimCode := DCSetup."Web: Dimension 5 Code (Lines)";
        6:
          DimCode := DCSetup."Web: Dimension 6 Code (Lines)";
        7:
          DimCode := DCSetup."Web: Dimension 7 Code (Lines)";
        8:
          DimCode := DCSetup."Web: Dimension 8 Code (Lines)";
      END;

      UserList.SETRANGE(Type,UserList.Type::"Dimension Value");
      UserList.SETRANGE("Dimension Code",DimCode);
    END;

    PROCEDURE GetSalesPurchName@1160040003() : Text[250];
    VAR
      UserSetup@1160040000 : Record 91;
      SalesPurchaser@1160040001 : Record 13;
    BEGIN
      IF UserSetup.GET("Continia User ID") THEN
        IF SalesPurchaser.GET(UserSetup."Salespers./Purch. Code") THEN
          EXIT(SalesPurchaser.Name);
    END;

    PROCEDURE DrillDownSalesPurchName@1160040009() : Text[250];
    VAR
      UserSetup@1160040002 : Record 91;
      SalesPurchaser@1160040001 : Record 13;
    BEGIN
      IF UserSetup.GET("Continia User ID") THEN;
      SalesPurchaser.SETFILTER(Code,UserSetup."Salespers./Purch. Code");
      PAGE.RUN(0,SalesPurchaser);
    END;

    PROCEDURE GetEmployeeName@1160040004() : Text[250];
    VAR
      Employee@1160040000 : Record 5200;
    BEGIN
      IF Employee.GET("Employee No.") THEN
        EXIT(Employee.FullName);
    END;

    PROCEDURE DrillDownEmployeeName@1160040000() : Text[250];
    VAR
      Employee@1160040000 : Record 5200;
    BEGIN
      Employee.SETFILTER("No.","Employee No.");
      PAGE.RUN(0,Employee);
    END;

    PROCEDURE GetName@1160040001() : Text[250];
    VAR
      ContiniaUser@1160040000 : Record 6086001;
    BEGIN
      IF ContiniaUser.GET("Continia User ID") THEN
        EXIT(ContiniaUser.Name);
    END;

    PROCEDURE GetEmail@1160040012() : Text[250];
    VAR
      ContiniaUser@1160040000 : Record 6086001;
    BEGIN
      IF ContiniaUser.GET("Continia User ID") THEN
        EXIT(ContiniaUser."E-Mail");
    END;

    PROCEDURE UpdateContiniaUserField@1160040002(FieldNo@1160040000 : Integer;Value@1160040001 : Variant);
    VAR
      ContiniaUser@1160040002 : Record 6086001;
      RecordRef@1160040004 : RecordRef;
      FieldRef@1160040003 : FieldRef;
    BEGIN
      IF NOT ContiniaUser.GET("Continia User ID") THEN BEGIN
        ContiniaUser.INIT;
        ContiniaUser.VALIDATE("User ID","Continia User ID");
        ContiniaUser.INSERT(TRUE);
      END;

      RecordRef.GETTABLE(ContiniaUser);
      FieldRef := RecordRef.FIELD(FieldNo);
      FieldRef.VALIDATE(Value);
      RecordRef.MODIFY(TRUE);
    END;

    PROCEDURE UpdateUserSetupField@1160040006(FieldNo@1160040000 : Integer;Value@1160040001 : Variant);
    VAR
      COSetup@1160040007 : Record 6086500;
      ContiniaUser@1160040006 : Record 6086001;
      UserSetup@1160040002 : Record 91;
      SalesPurchaser@1160040005 : Record 13;
      RecordRef@1160040004 : RecordRef;
      FieldRef@1160040003 : FieldRef;
    BEGIN
      IF NOT UserSetup.GET("Continia User ID") THEN BEGIN
        UserSetup.INIT;
        UserSetup."User ID" := "Continia User ID";
        UserSetup.INSERT(TRUE);
      END;

      RecordRef.GETTABLE(UserSetup);
      FieldRef := RecordRef.FIELD(FieldNo);
      IF FORMAT(FieldRef.VALUE) <> FORMAT(Value) THEN BEGIN
        FieldRef.VALIDATE(Value);
        RecordRef.MODIFY(TRUE);
      END;

      IF FieldNo = UserSetup.FIELDNO("Salespers./Purch. Code") THEN BEGIN
        IF (GetName = '') OR (GetEmail = '') THEN BEGIN
          UserSetup.GET("Continia User ID");
          IF UserSetup."Salespers./Purch. Code" <> '' THEN BEGIN
            SalesPurchaser.GET(UserSetup."Salespers./Purch. Code");
            IF (SalesPurchaser.Name <> '') AND (GetName = '') THEN
              UpdateContiniaUserField(ContiniaUser.FIELDNO(Name),SalesPurchaser.Name);
            IF (SalesPurchaser."E-Mail" <> '') AND (GetEmail = '') THEN
              UpdateContiniaUserField(ContiniaUser.FIELDNO("E-Mail"),SalesPurchaser."E-Mail");
          END;
        END;
          IF GetEmail <> '' THEN
            VALIDATE("Web Approval User",COSetup.GET AND COSetup."Enable Web Approval");
      END;
    END;

    PROCEDURE DrillDownPurchDocForApproval@1160040005();
    VAR
      ApprovalEntry@1160040000 : Record 454;
    BEGIN
      ApprovalEntry.FILTERGROUP(4);
      ApprovalEntry.SETRANGE("Table ID",DATABASE::"Purchase Header");
      ApprovalEntry.SETRANGE("Approver ID","Continia User ID");
      ApprovalEntry.SETRANGE(Status,ApprovalEntry.Status::Open);
      ApprovalEntry.FILTERGROUP(0);
      PAGE.RUN(PAGE::"Purch. Approval Req. Entries",ApprovalEntry);
    END;

    PROCEDURE SuspendContiniaUserDelete@1160040010(Suspend@1160040000 : Boolean);
    BEGIN
      ContiniaUserDeleteSuspended := Suspend;
    END;

    PROCEDURE SuspendUserSetupDelete@1160040011(Suspend@1160040000 : Boolean);
    BEGIN
      UserSetupDeleteSuspended := Suspend;
    END;

    PROCEDURE ResendWelcomeEmail@1160040013();
    VAR
      ContiniaUser@1160040000 : Record 6086001;
    BEGIN
      TESTFIELD("Web Approval User");

      ContiniaUser.GET("Continia User ID");
      ContiniaUser.ResendWelcomeEmail;
    END;

    BEGIN
    END.
  }
}

