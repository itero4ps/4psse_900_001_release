OBJECT Table 6086319 Expense Reminder
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.1.02.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Expense Reminder;
  }
  FIELDS
  {
    { 1   ;   ;Expense Entry No.   ;Integer       ;TableRelation=Expense;
                                                   CaptionML=ENU=Expense Entry No. }
    { 2   ;   ;Reminder Terms Code ;Code10        ;TableRelation="Expense Reminder Terms";
                                                   CaptionML=ENU=Reminder Terms Code;
                                                   NotBlank=Yes }
    { 3   ;   ;No.                 ;Integer       ;CaptionML=ENU=No.;
                                                   MinValue=1;
                                                   NotBlank=Yes }
    { 4   ;   ;Reminder Sent       ;Date          ;CaptionML=ENU=Reminder Sent }
  }
  KEYS
  {
    {    ;Expense Entry No.,Reminder Terms Code,No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CreateReminder@1160040000(VAR Expense@1160040000 : Record 6086320) : Text[80];
    VAR
      ContiniaUserSetup@1160040001 : Record 6086002;
      ReminderLevel@1160040002 : Record 6086318;
    BEGIN
      Expense.TESTFIELD("Employee No.");

      IF NOT ContiniaUserSetup.GET(Expense."Employee No.") THEN
        EXIT;
      SETRANGE("Expense Entry No.",Expense."Entry No.");
      SETRANGE("Reminder Terms Code",ContiniaUserSetup."Expense Reminder Code");
      IF FINDLAST THEN;
      "Expense Entry No." := Expense."Entry No.";
      "Reminder Terms Code" := ContiniaUserSetup."Expense Reminder Code";
      "No." := "No." + 1;
      "Reminder Sent" := TODAY;
      INSERT;

      ReminderLevel.SETRANGE("Reminder Terms Code",ContiniaUserSetup."Expense Reminder Code");
      ReminderLevel.SETRANGE("No.","No.");
      IF NOT ReminderLevel.FINDLAST THEN BEGIN
        ReminderLevel.SETRANGE("No.");
        IF NOT ReminderLevel.FINDLAST THEN;
      END;

      EXIT(ReminderLevel."Reminder Text");
    END;

    PROCEDURE ResetReminder@1160040001(Expense@1160040000 : Record 6086320);
    VAR
      ContiniaUserSetup@1160040002 : Record 6086002;
    BEGIN
      IF NOT ContiniaUserSetup.GET(Expense."Employee No.") THEN;
      SETRANGE("Expense Entry No.",Expense."Entry No.");
      SETRANGE("Reminder Terms Code",ContiniaUserSetup."Expense Reminder Code");
      IF FINDLAST THEN BEGIN
        "Reminder Sent" := TODAY;
        MODIFY(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

