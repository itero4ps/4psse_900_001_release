OBJECT Table 6086346 Configured Field
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.1.02.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               EMSetup@1160040000 : Record 6086300;
             BEGIN
               EMSetup.GET;
               EMSetup."Force Field Update in CO" := TRUE;
               EMSetup.MODIFY;
             END;

    OnModify=VAR
               EMSetup@1160040000 : Record 6086300;
             BEGIN
               EMSetup.GET;
               EMSetup."Force Field Update in CO" := TRUE;
               EMSetup.MODIFY;
             END;

    OnDelete=VAR
               EMSetup@1160040002 : Record 6086300;
               FieldType@1160040001 : Record 6086345;
               ConfigField@1160040000 : Record 6086346;
             BEGIN
               EMSetup.GET;
               EMSetup."Force Field Update in CO" := TRUE;
               EMSetup.MODIFY;

               IF NOT FieldType.GET("Field Code") THEN
                 EXIT;

               IF FieldType.IsRequiredSystemField THEN
                 ERROR(NotAllowedToDeleteSysFieldErr);

               ConfigField.SETRANGE(Type,Type);
               ConfigField.SETFILTER("Field Code",'<>%1',"Field Code");
               IF ConfigField.FINDSET THEN
                 REPEAT
                   FieldType.GET(ConfigField."Field Code");
                   IF FieldType.GetParentFieldTypeCode = "Field Code" THEN
                     ERROR(UnableToDeleteErr,FieldType.TABLECAPTION,FIELDCAPTION("Field Code"),FieldType.Code);
                 UNTIL ConfigField.NEXT = 0;
             END;

    OnRename=BEGIN
               ERROR(UnableToRenameErr,TABLECAPTION);
             END;

    CaptionML=ENU=Configured Field;
  }
  FIELDS
  {
    { 1   ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Expense,Mileage,Transaction;
                                                   OptionString=Expense,Mileage,Transaction }
    { 2   ;   ;Field Order         ;Integer       ;CaptionML=ENU=Field Order }
    { 10  ;   ;Field Code          ;Code20        ;TableRelation="Field Type";
                                                   OnValidate=VAR
                                                                FieldType@1160040001 : Record 6086345;
                                                                ConfigField@1160040000 : Record 6086346;
                                                              BEGIN
                                                                IF NOT FieldType.GET("Field Code") THEN
                                                                  EXIT;

                                                                IF FieldType.GetParentFieldTypeCode = '' THEN
                                                                  EXIT;

                                                                ConfigField.SETRANGE(Type,Type);
                                                                ConfigField.SETRANGE("Field Code",FieldType.GetParentFieldTypeCode);
                                                                IF ConfigField.ISEMPTY THEN
                                                                  ERROR(RelatedFieldMissingErr,"Field Code",FieldType.TABLECAPTION,FieldType.GetParentFieldTypeCode,"Field Code");
                                                              END;

                                                   CaptionML=ENU=Field Code;
                                                   NotBlank=Yes }
    { 11  ;   ;Field Description   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Field Type".Description WHERE (Code=FIELD(Field Code)));
                                                   CaptionML=ENU=Field Description;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Type,Field Order                        ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      RelatedFieldMissingErr@1160040000 : TextConst 'ENU=%1 has a relationship to %2 %3 which must also be added before you can add %4.';
      UnableToDeleteErr@1160040001 : TextConst 'ENU=%1 cannot be deleted as there is a relationship to %2 %3.';
      UnableToRenameErr@1160040002 : TextConst 'ENU=You cannot rename a %1.';
      NotAllowedToDeleteSysFieldErr@1160040003 : TextConst 'ENU=You are not allowed to delete a system field.';

    BEGIN
    END.
  }
}

