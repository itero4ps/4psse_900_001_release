OBJECT Table 6086350 Bank Mapping Rule
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.1.02.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               Oldrules@1160040000 : Record 6086350;
             BEGIN
               IF "Rule No." = 0 THEN
                 IF Oldrules.FINDLAST THEN
                   "Rule No." := Oldrules."Rule No." + 10;
             END;

    CaptionML=ENU=Bank Mapping Rule;
    LookupPageID=Page6086350;
    DrillDownPageID=Page6086350;
  }
  FIELDS
  {
    { 1   ;   ;Rule No.            ;Integer       ;CaptionML=ENU=Rule No. }
    { 2   ;   ;Continia User ID    ;Code50        ;TableRelation="Continia User Setup";
                                                   CaptionML=ENU=Continia User ID }
    { 10  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=CONST(6086330));
                                                   OnLookup=VAR
                                                              FieldRec@1160040000 : Record 2000000041;
                                                              Fields@1160040001 : Page 7702;
                                                            BEGIN
                                                              FieldRec.SETRANGE(TableNo,DATABASE::"Bank Transaction");
                                                              IF "Field No." <> 0 THEN BEGIN
                                                                FieldRec.SETRANGE("No.","Field No.");
                                                                IF FieldRec.FINDFIRST THEN;
                                                                FieldRec.SETRANGE("No.");
                                                              END;
                                                              Fields.SETRECORD(FieldRec);
                                                              Fields.SETTABLEVIEW(FieldRec);
                                                              Fields.LOOKUPMODE := TRUE;

                                                              IF Fields.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Fields.GETRECORD(FieldRec);
                                                                "Field No." := FieldRec."No.";
                                                                IF NOT MODIFY THEN
                                                                  INSERT;
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Field No. }
    { 11  ;   ;Value               ;Text30        ;CaptionML=ENU=Value }
    { 12  ;   ;Field Name          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=CONST(6086330),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=ENU=Field Name;
                                                   Editable=No }
    { 20  ;   ;Expense Type Code   ;Code20        ;TableRelation="Expense Type";
                                                   CaptionML=ENU=Expense Type Code }
  }
  KEYS
  {
    {    ;Rule No.                                ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE UseBankMappingRules@1160040000(VAR Transaction@1160040000 : Record 6086330);
    BEGIN
      Transaction.TESTFIELD("Matched to Expense",FALSE);

      IF Transaction."Expense Type" = '' THEN BEGIN
        SETRANGE("Continia User ID",Transaction."Employee No.");
        IF Rec.FINDFIRST THEN
          REPEAT
            UseBankMappingRule(Transaction);
          UNTIL (Rec.NEXT <= 0) OR (Transaction."Expense Type" <> '');
        SETRANGE("Continia User ID");
        IF Rec.FINDFIRST THEN
          REPEAT
            UseBankMappingRule(Transaction);
          UNTIL (Rec.NEXT <= 0) OR (Transaction."Expense Type" <> '');
      END;
    END;

    LOCAL PROCEDURE UseBankMappingRule@1160040001(VAR Transaction@1160040000 : Record 6086330);
    VAR
      RecRef@1160040002 : RecordRef;
      FieldRef@1160040001 : FieldRef;
    BEGIN
      RecRef.GETTABLE(Transaction);
      IF "Field No." <> 0 THEN BEGIN
        FieldRef := RecRef.FIELD("Field No.");
        IF STRPOS(UPPERCASE(FORMAT(FieldRef.VALUE)),UPPERCASE(Value)) <> 0 THEN BEGIN
          Transaction."Expense Type" := "Expense Type Code";
          Transaction.MODIFY;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

