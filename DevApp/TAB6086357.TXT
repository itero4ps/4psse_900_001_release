OBJECT Table 6086357 Expense Allocation Dimension
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.1.02.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TestNotPosted;
             END;

    OnModify=BEGIN
               TestNotPosted;
             END;

    OnDelete=VAR
               GLSetup@1160040000 : Record 98;
               ExpenseAllocation@1160040001 : Record 6086321;
             BEGIN
               TestNotPosted;

               GLSetup.GET;
               IF "Dimension Code" IN [GLSetup."Global Dimension 1 Code",GLSetup."Global Dimension 2 Code"] THEN BEGIN
                 ExpenseAllocation.GET("Expense Allocation Entry No.");

                 IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                   ExpenseAllocation."Global Dimension 1 Code" := '';

                 IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                   ExpenseAllocation."Global Dimension 2 Code" := '';

                 ExpenseAllocation.MODIFY;
               END;
             END;

    OnRename=BEGIN
               ERROR(Text002,TABLECAPTION);
             END;

    CaptionML=ENU=Expense Allocation Dimensions;
    LookupPageID=Page6086368;
    DrillDownPageID=Page6086368;
  }
  FIELDS
  {
    { 1   ;   ;Expense Allocation Entry No.;Integer;
                                                   TableRelation="Expense Allocation";
                                                   CaptionML=ENU=Expense Allocation Entry No. }
    { 2   ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   CaptionML=ENU=Dimension Code }
    { 3   ;   ;Dimension Value Code;Code20        ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   OnValidate=VAR
                                                                GLSetup@1160040000 : Record 98;
                                                                ExpenseAllocation@1160040002 : Record 6086321;
                                                              BEGIN
                                                                GLSetup.GET;

                                                                ExpenseAllocation.GET("Expense Allocation Entry No.");

                                                                IF "Dimension Code" IN [GLSetup."Global Dimension 1 Code",GLSetup."Global Dimension 2 Code"] THEN BEGIN
                                                                  IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                                                                    ExpenseAllocation."Global Dimension 1 Code" := "Dimension Value Code";

                                                                  IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                                                                    ExpenseAllocation."Global Dimension 2 Code" := "Dimension Value Code";
                                                                END;

                                                                ExpenseAllocation.Modified := TRUE;
                                                                ExpenseAllocation.MODIFY;
                                                              END;

                                                   CaptionML=ENU=Dimension Value Code;
                                                   NotBlank=Yes }
  }
  KEYS
  {
    {    ;Expense Allocation Entry No.,Dimension Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PendingExpUserErr@1160040003 : TextConst 'ENU="You cannot make changes to %1 when the %2 %3 = %4."';
      SpecifyParentFieldErr@1160040000 : TextConst 'ENU=You must first specify a value for %1.';
      Text002@1160040002 : TextConst 'ENU=You cannot rename a %1.';

    LOCAL PROCEDURE TestNotPosted@37();
    VAR
      Expense@1160040000 : Record 6086320;
      ExpenseAllocation@1160040001 : Record 6086321;
    BEGIN
      ExpenseAllocation.GET("Expense Allocation Entry No.");
      Expense.GET(ExpenseAllocation."Expense Entry No.");
      Expense.TESTFIELD(Posted,FALSE);
    END;

    BEGIN
    END.
  }
}

