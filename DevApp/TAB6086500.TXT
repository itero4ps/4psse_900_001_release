OBJECT Table 6086500 Continia Online Setup
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.04;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Company GUID" := CREATEGUID;
             END;

    CaptionML=ENU=Continia Online Setup;
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=ENU=Primary Key }
    { 3   ;   ;Client ID           ;Code20        ;OnValidate=BEGIN
                                                                IF "Client ID" <> '' THEN
                                                                  ValidateSetup(COMPANYNAME,"Client ID");
                                                              END;

                                                   CaptionML=ENU=Client ID;
                                                   CharAllowed=0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ+- }
    { 4   ;   ;Client Password     ;Text50        ;CaptionML=ENU=Client Password }
    { 7   ;   ;Company GUID        ;GUID          ;CaptionML=ENU=Company GUID;
                                                   Editable=No }
    { 8   ;   ;Windows Web Service URL;Text250    ;CaptionML=ENU=Windows Web Service URL }
    { 9   ;   ;Database Web Service URL;Text250   ;CaptionML=ENU=Database Web Service URL }
    { 10  ;   ;Local Domain Name   ;Code50        ;CaptionML=ENU=Local Domain Name }
    { 11  ;   ;Continia Dev. Environment;Boolean  ;OnValidate=BEGIN
                                                                IF "Continia Dev. Environment" THEN
                                                                  IF NOT CONFIRM(Text001,FALSE,FIELDCAPTION("Continia Dev. Environment")) THEN
                                                                    ERROR('');
                                                              END;

                                                   CaptionML=ENU=Continia Development Environment }
    { 12  ;   ;Tenant              ;Text80        ;CaptionML=ENU=Tenant }
    { 13  ;   ;Enable Web Approval ;Boolean       ;OnValidate=BEGIN
                                                                IF NOT "Enable Web Approval" THEN
                                                                  EXIT;

                                                                //TESTFIELD("Web Site Url");
                                                                TESTFIELD("Default Language Code");
                                                                TESTFIELD("Default Language Name");
                                                                TESTFIELD("Default Units Formatting");
                                                                TESTFIELD("Default Time Zone");

                                                                CODEUNIT.RUN(CODEUNIT::"Create DC Web Services");
                                                              END;

                                                   CaptionML=ENU=Enable Web Approval }
    { 14  ;   ;Web Site Url        ;Text80        ;CaptionML=ENU=Web Site Url }
    { 15  ;   ;Default Language Name;Text20       ;InitValue=en-GB;
                                                   OnValidate=VAR
                                                                Company@161024012 : Record 2000000006;
                                                                COSetup@161024013 : Record 6086500;
                                                              BEGIN
                                                                IF "Default Language Name" = '' THEN
                                                                  EXIT;

                                                                IF NOT CONFIRM(Text005,TRUE,FIELDCAPTION("Default Language Name")) THEN
                                                                  EXIT;

                                                                IF Company.FINDSET THEN
                                                                  REPEAT
                                                                    IF Company.Name <> COMPANYNAME THEN BEGIN
                                                                      COSetup.CHANGECOMPANY(Company.Name);
                                                                      IF COSetup.GET THEN BEGIN
                                                                        IF COSetup."Enable Web Approval" THEN BEGIN
                                                                          COSetup."Default Language Name" := "Default Language Name";
                                                                          COSetup.MODIFY;
                                                                        END;
                                                                      END;
                                                                    END;
                                                                  UNTIL Company.NEXT = 0;
                                                              END;

                                                   OnLookup=VAR
                                                              TempDisplayTable@1160040003 : TEMPORARY Record 6086007;
                                                              XmlDoc@1160040002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
                                                              XMLNodeList@1160040005 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
                                                              COMgt@1160040004 : Codeunit 6086500;
                                                              i@1160040000 : Integer;
                                                            BEGIN
                                                              COMgt.ExecuteRequestWithoutToken("Web Site Url" + '/serverdata/languages',HttpActions::GET,TRUE,XmlDoc);
                                                              XMLNodeList := XmlDoc.DocumentElement.SelectNodes('/languages/language');

                                                              IF XMLNodeList.Count = 0 THEN
                                                                ERROR(Text006);

                                                              FOR i := 0 TO XMLNodeList.Count - 1 DO BEGIN
                                                                TempDisplayTable."Display Sort Order" := i;
                                                                COMgt.GetAttribAsText(XMLNodeList,'name',i,TempDisplayTable.Name);
                                                                COMgt.GetAttribAsText(XMLNodeList,'displayName',i,TempDisplayTable.Description);
                                                                TempDisplayTable.INSERT;
                                                              END;

                                                              TempDisplayTable.SETCURRENTKEY("Display Sort Order");
                                                              IF NOT TempDisplayTable.GET("Default Language Name") THEN
                                                                TempDisplayTable.FINDFIRST;

                                                              IF PAGE.RUNMODAL(PAGE::"Web Languages",TempDisplayTable) = ACTION::LookupOK THEN
                                                                VALIDATE("Default Language Name",TempDisplayTable.Name);
                                                            END;

                                                   CaptionML=ENU=Default User Language Name }
    { 16  ;   ;Default Units Formatting;Text20    ;InitValue=en-GB;
                                                   OnValidate=VAR
                                                                Company@161024013 : Record 2000000006;
                                                                COSetup@1160040000 : Record 6086500;
                                                              BEGIN
                                                                IF "Default Units Formatting" = '' THEN
                                                                  EXIT;

                                                                IF NOT CONFIRM(Text005,TRUE,FIELDCAPTION("Default Units Formatting")) THEN
                                                                  EXIT;

                                                                IF Company.FINDSET THEN
                                                                  REPEAT
                                                                    IF Company.Name <> COMPANYNAME THEN BEGIN
                                                                      COSetup.CHANGECOMPANY(Company.Name);
                                                                      IF COSetup.GET THEN BEGIN
                                                                        IF COSetup."Enable Web Approval" THEN BEGIN
                                                                          COSetup."Default Units Formatting" := "Default Units Formatting";
                                                                          COSetup.MODIFY;
                                                                        END;
                                                                      END;
                                                                    END;
                                                                  UNTIL Company.NEXT = 0;
                                                              END;

                                                   OnLookup=VAR
                                                              TempDisplayTable@161024015 : TEMPORARY Record 6086007;
                                                              XmlDoc@161024012 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
                                                              XMLNodeList@1160040001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
                                                              COMgt@1160040000 : Codeunit 6086500;
                                                              i@161024013 : Integer;
                                                            BEGIN
                                                              COMgt.ExecuteRequestWithoutToken("Web Site Url" + '/serverdata/numberanddatecultures',HttpActions::GET,TRUE,XmlDoc);
                                                              XMLNodeList := XmlDoc.DocumentElement.SelectNodes('/cultures/culture');

                                                              IF XMLNodeList.Count = 0 THEN
                                                                ERROR(Text006);

                                                              FOR i := 0 TO XMLNodeList.Count - 1 DO BEGIN
                                                                TempDisplayTable."Display Sort Order" := i;
                                                                COMgt.GetAttribAsText(XMLNodeList,'name',i,TempDisplayTable.Name);
                                                                COMgt.GetAttribAsText(XMLNodeList,'displayName',i,TempDisplayTable.Description);
                                                                TempDisplayTable.INSERT;
                                                              END;

                                                              TempDisplayTable.SETCURRENTKEY("Display Sort Order");
                                                              IF NOT TempDisplayTable.GET("Default Units Formatting") THEN
                                                                TempDisplayTable.FINDFIRST;

                                                              IF PAGE.RUNMODAL(PAGE::"User Formatting List",TempDisplayTable) = ACTION::LookupOK THEN
                                                                VALIDATE("Default Units Formatting",TempDisplayTable.Name);
                                                            END;

                                                   CaptionML=ENU=Default Units Formatting }
    { 17  ;   ;Default Time Zone   ;Text50        ;InitValue=UTC;
                                                   OnValidate=VAR
                                                                Company@161024013 : Record 2000000006;
                                                                COSetup@1160040000 : Record 6086500;
                                                              BEGIN
                                                                IF "Default Time Zone"  = '' THEN
                                                                  EXIT;

                                                                IF NOT CONFIRM(Text005,TRUE,FIELDCAPTION("Default Time Zone")) THEN
                                                                  EXIT;

                                                                IF Company.FINDSET THEN
                                                                  REPEAT
                                                                    IF Company.Name <> COMPANYNAME THEN BEGIN
                                                                      COSetup.CHANGECOMPANY(Company.Name);
                                                                      IF COSetup.GET THEN BEGIN
                                                                        IF COSetup."Enable Web Approval" THEN BEGIN
                                                                          COSetup."Default Time Zone" := "Default Time Zone";
                                                                          COSetup.MODIFY;
                                                                        END;
                                                                      END;
                                                                    END;
                                                                  UNTIL Company.NEXT = 0;
                                                              END;

                                                   OnLookup=VAR
                                                              TempDisplayTable@161024015 : TEMPORARY Record 6086007;
                                                              XmlDoc@161024014 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
                                                              XMLNodeList@1160040001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
                                                              COMgt@1160040000 : Codeunit 6086500;
                                                              i@161024012 : Integer;
                                                            BEGIN
                                                              COMgt.ExecuteRequestWithoutToken("Web Site Url" + '/serverdata/timezones',HttpActions::GET,TRUE,XmlDoc);
                                                              XMLNodeList := XmlDoc.DocumentElement.SelectNodes('/timezones/timezone');

                                                              IF XMLNodeList.Count = 0 THEN
                                                                ERROR(Text006);

                                                              FOR i := 0 TO XMLNodeList.Count - 1 DO BEGIN
                                                                TempDisplayTable."Display Sort Order" := i;
                                                                COMgt.GetAttribAsText(XMLNodeList,'id',i,TempDisplayTable.Name);
                                                                COMgt.GetAttribAsText(XMLNodeList,'displayName',i,TempDisplayTable.Description);
                                                                TempDisplayTable.INSERT;
                                                              END;

                                                              TempDisplayTable.SETCURRENTKEY("Display Sort Order");
                                                              IF NOT TempDisplayTable.GET("Default Time Zone") THEN
                                                                TempDisplayTable.FINDFIRST;

                                                              IF PAGE.RUNMODAL(PAGE::"Time Zone List",TempDisplayTable) = ACTION::LookupOK THEN
                                                                VALIDATE("Default Time Zone",TempDisplayTable.Name);
                                                            END;

                                                   CaptionML=ENU=Default Time Zone }
    { 18  ;   ;Allow Forward and Approve;Boolean  ;InitValue=Yes;
                                                   CaptionML=ENU=Allow Forward and Approve }
    { 19  ;   ;Allow Forward w/o Approva;Boolean  ;InitValue=Yes;
                                                   CaptionML=ENU=Allow Forward w/o Approval }
    { 20  ;   ;Allow Forward and Return;Boolean   ;InitValue=Yes;
                                                   CaptionML=ENU=Allow Forward and return for Approval }
    { 21  ;   ;Use Continia Online Web Portal;Boolean;
                                                   OnValidate=BEGIN
                                                                IF "Use Continia Online Web Portal" THEN
                                                                  "Web Site Url" := 'http://www.continiaonline.com';
                                                              END;

                                                   CaptionML=ENU=Use Continia Online Web Portal }
    { 22  ;   ;Default Language Code;Code10       ;TableRelation=Language;
                                                   CaptionML=ENU=Default Language Code }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1160040001 : TextConst 'ENU=The %1 on %2 and %3 must be the same.';
      Text001@1160040000 : TextConst 'ENU=Please only enable %1 if you have a specific agreement with Continia Software to do this.\\Do you want to continue?';
      Text005@1160040003 : TextConst 'ENU=The value in %1 must be the same in all companies that are using Document Capture Web Approval.\\Do you want to update the value of this field in all other companies?';
      Text006@1160040002 : TextConst 'ENU=Unable to connect to the Document Capture Web Site. Please make sure that you are running the latest version of the Document Capture Web Site.';
      HttpActions@1160040004 : 'PUT,DELETE,GET';

    PROCEDURE ValidateSetups@1160040001(CompanyToValidate@1160040000 : Text[30]) : Boolean;
    VAR
      COSetup@1160040001 : Record 6086500;
    BEGIN
      COSetup.CHANGECOMPANY(CompanyToValidate);

      IF NOT COSetup.GET THEN
        EXIT;

      IF COSetup."Client ID" = '' THEN
        EXIT;

      ValidateSetup(CompanyToValidate,COSetup."Client ID");

      EXIT(TRUE);
    END;

    PROCEDURE ValidateSetup@1160040437(CompanyToValidate@1160040000 : Text[30];ClientID@1160040001 : Code[20]);
    VAR
      Company@1160040003 : Record 2000000006;
      COSetup@1160040004 : Record 6086500;
    BEGIN
      Company.SETFILTER(Name,'<>%1',CompanyToValidate);
      IF Company.FINDSET THEN
        REPEAT
          COSetup.CHANGECOMPANY(Company.Name);
          IF COSetup.GET THEN
            IF COSetup."Client ID" <> '' THEN
              IF COSetup."Client ID" <> ClientID THEN
                ERROR(Text000,COSetup.TABLECAPTION,Company.Name,CompanyToValidate);
        UNTIL Company.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

