OBJECT Table 7004 Sales Line Discount
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW18.00,4PS9.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               CentralDataMgt@1100525000 : Codeunit 11012010;
             BEGIN
               IF "Sales Type" = "Sales Type"::"All Customers" THEN
                 "Sales Code" := ''
               ELSE
                 TESTFIELD("Sales Code");
               TESTFIELD(Code);
               //**4PS.sn, db, 21-02-11 (obsolete code)
               //IF NOT ((Type = Type::"Item Disc. Group") AND ("Sales Type" = "Sales Type"::"Customer Disc. Group")) THEN
               //  IF NOT ((Type = Type::Item) AND ("Sales Type" = "Sales Type"::Customer)) THEN
               //    MESSAGE(Text11012000);  //db, 19-05-08: M11110
               //**4PS.en

               CentralDataMgt.UpdateSalesLineDiscount(2, Rec);
             END;

    OnModify=VAR
               CentralDataMgt@1100525000 : Codeunit 11012010;
             BEGIN
               CentralDataMgt.UpdateSalesLineDiscount(2, Rec);
             END;

    OnDelete=VAR
               CentralDataMgt@1100525000 : Codeunit 11012010;
             BEGIN
               CentralDataMgt.UpdateSalesLineDiscount(4, Rec);
             END;

    OnRename=VAR
               CentralDataMgt@1100525000 : Codeunit 11012010;
             BEGIN
               IF "Sales Type" <> "Sales Type"::"All Customers" THEN
                 TESTFIELD("Sales Code");
               TESTFIELD(Code);

               CentralDataMgt.UpdateSalesLineDiscount(4, xRec);
               CentralDataMgt.UpdateSalesLineDiscount(2, Rec);
             END;

    CaptionML=ENU=Sales Line Discount;
    LookupPageID=Page7004;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Item Disc. Group)) "Item Discount Group";
                                                   OnValidate=VAR
                                                                Item@1000 : Record 27;
                                                              BEGIN
                                                                IF xRec.Code <> Code THEN BEGIN
                                                                  "Unit of Measure Code" := '';
                                                                  "Variant Code" := '';

                                                                  IF Type = Type::Item THEN
                                                                    IF Item.GET(Code) THEN
                                                                      "Unit of Measure Code" := Item."Sales Unit of Measure"
                                                                END;

                                                                "Item Description" := '';

                                                                IF Code <> '' THEN BEGIN
                                                                  CASE Type OF
                                                                    Type::Item:
                                                                      BEGIN
                                                                        Item.GET(Code);
                                                                        "Item Description" := Item.Description;
                                                                      END;
                                                                    Type::"Item Disc. Group":
                                                                      BEGIN
                                                                        ItemDiscountGr.GET(Code);
                                                                        "Item Description" := ItemDiscountGr.Description;
                                                                      END;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 2   ;   ;Sales Code          ;Code20        ;TableRelation=IF (Sales Type=CONST(Customer Disc. Group)) "Customer Discount Group"
                                                                 ELSE IF (Sales Type=CONST(Customer)) Customer
                                                                 ELSE IF (Sales Type=CONST(Campaign)) Campaign;
                                                   OnValidate=VAR
                                                                Cust@1100409000 : Record 18;
                                                              BEGIN
                                                                "Sales Code Description" := ''; //**4PS.n

                                                                IF "Sales Code" <> '' THEN
                                                                  CASE "Sales Type" OF
                                                                    //**4PS.sn
                                                                    "Sales Type"::"Customer Disc. Group":
                                                                      BEGIN
                                                                        CustDiscountGr.GET("Sales Code");
                                                                        "Sales Code Description" := CustDiscountGr.Description;
                                                                      END;
                                                                    "Sales Type"::Customer:
                                                                      BEGIN
                                                                        Cust.GET("Sales Code");
                                                                        "Sales Code Description" := Cust.Name;
                                                                      END;
                                                                    //**4PS.en
                                                                    "Sales Type"::"All Customers":
                                                                      ERROR(Text001,FIELDCAPTION("Sales Code"));
                                                                    "Sales Type"::Campaign:
                                                                      BEGIN
                                                                        Campaign.GET("Sales Code");
                                                                        "Starting Date" := Campaign."Starting Date";
                                                                        "Ending Date" := Campaign."Ending Date";
                                                                        "Sales Code Description" := Campaign.Description; //**4PS.n
                                                                      END;
                                                                  END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Sales Code }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=ENU=Currency Code }
    { 4   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF ("Starting Date" > "Ending Date") AND ("Ending Date" <> 0D) THEN
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));

                                                                //**4PS Call C003583 sn
                                                                IF "Starting Date" = 0D THEN
                                                                  IntegerOfDate := 0
                                                                ELSE
                                                                  IntegerOfDate := "Starting Date" - 010101D;
                                                                "Reverse Starting Date" := -1 * IntegerOfDate;
                                                                //**4PS Call C003583 en

                                                                IF CurrFieldNo = 0 THEN
                                                                  EXIT;
                                                                IF "Sales Type" = "Sales Type"::Campaign THEN
                                                                  ERROR(Text003,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"),FIELDCAPTION("Sales Type"),"Sales Type");
                                                              END;

                                                   CaptionML=ENU=Starting Date }
    { 5   ;   ;Line Discount %     ;Decimal       ;CaptionML=ENU=Line Discount %;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   AutoFormatType=2 }
    { 13  ;   ;Sales Type          ;Option        ;OnValidate=BEGIN
                                                                IF "Sales Type" <> xRec."Sales Type" THEN
                                                                  VALIDATE("Sales Code",'');
                                                              END;

                                                   CaptionML=ENU=Sales Type;
                                                   OptionCaptionML=ENU=Customer,Customer Disc. Group,All Customers,Campaign;
                                                   OptionString=Customer,Customer Disc. Group,All Customers,Campaign }
    { 14  ;   ;Minimum Quantity    ;Decimal       ;CaptionML=ENU=Minimum Quantity;
                                                   MinValue=0 }
    { 15  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                //**4PS Call C003583 sn
                                                                IF "Ending Date" = 0D THEN
                                                                  IntegerOfDate := 0
                                                                ELSE
                                                                  IntegerOfDate := "Ending Date" - 010101D;
                                                                "Reverse Ending Date" := -1 * IntegerOfDate;
                                                                //**4PS Call C003583 en

                                                                VALIDATE("Starting Date");

                                                                IF CurrFieldNo = 0 THEN
                                                                  EXIT;
                                                                IF "Sales Type" = "Sales Type"::Campaign THEN
                                                                  ERROR(Text003,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"),FIELDCAPTION("Sales Type"),"Sales Type");
                                                              END;

                                                   CaptionML=ENU=Ending Date }
    { 21  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF xRec.Type <> Type THEN
                                                                  VALIDATE(Code,'');
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Item,Item Disc. Group;
                                                   OptionString=Item,Item Disc. Group }
    { 5400;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE (Item No.=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::Item);
                                                              END;

                                                   CaptionML=ENU=Unit of Measure Code }
    { 5700;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE (Item No.=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::Item);
                                                              END;

                                                   CaptionML=ENU=Variant Code }
    { 11012000;;Item Description   ;Text50        ;FieldClass=Normal;
                                                   CaptionML=ENU=Item Description;
                                                   Editable=No }
    { 11012020;;Sales Code Description;Text50     ;CaptionML=ENU=Sales Code Description;
                                                   Editable=No }
    { 11012030;;Variant Code Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Item Variant".Description WHERE (Code=FIELD(Variant Code),
                                                                                                        Item No.=FIELD(Code)));
                                                   CaptionML=ENU=Variant Code Description;
                                                   Editable=No }
    { 11012040;;Currency Code Description;Text30  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Currency.Description WHERE (Code=FIELD(Currency Code)));
                                                   CaptionML=ENU=Currency Code Description;
                                                   Editable=No }
    { 11012050;;Unit of Measure Description;Text10;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Unit of Measure".Description WHERE (Code=FIELD(Unit of Measure Code)));
                                                   CaptionML=ENU=Unit of Measure Description;
                                                   Editable=No }
    { 11012060;;Reverse Starting Date;Integer     ;CaptionML=ENU=Reverse Starting Date }
    { 11012061;;Reverse Ending Date;Integer       ;CaptionML=ENU=Reverse Ending Date }
  }
  KEYS
  {
    {    ;Type,Code,Sales Type,Sales Code,Starting Date,Currency Code,Variant Code,Unit of Measure Code,Minimum Quantity;
                                                   Clustered=Yes }
    {    ;Sales Type,Sales Code,Type,Code,Starting Date,Currency Code,Variant Code,Unit of Measure Code,Minimum Quantity }
    {    ;Type,Code,Sales Type,Sales Code,Reverse Starting Date,Currency Code,Variant Code,Unit of Measure Code,Minimum Quantity }
    {    ;Sales Type,Sales Code,Type,Code,Reverse Starting Date,Currency Code,Variant Code,Unit of Measure Code,Minimum Quantity }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1001 : TextConst 'ENU=%1 cannot be after %2';
      Text001@1003 : TextConst 'ENU=%1 must be blank.';
      Campaign@1004 : Record 11072034;
      Text003@1006 : TextConst 'ENU="You can only change the %1 and %2 from the Campaign Card when %3 = %4"';
      ItemDiscountGr@1100528300 : Record 341;
      Item@1100528301 : Record 27;
      CustDiscountGr@1100528302 : Record 340;
      IntegerOfDate@1100525000 : Duration;

    BEGIN
    END.
  }
}

