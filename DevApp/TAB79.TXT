OBJECT Table 79 Company Information
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00.00.43402,NAVFI9.DK9.NO9,4PS9.00,4PSSE;
  }
  PROPERTIES
  {
    CaptionML=ENU=Company Information;
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=ENU=Primary Key }
    { 2   ;   ;Name                ;Text50        ;CaptionML=ENU=Name }
    { 3   ;   ;Name 2              ;Text50        ;CaptionML=ENU=Name 2 }
    { 4   ;   ;Address             ;Text50        ;CaptionML=ENU=Address }
    { 5   ;   ;Address 2           ;Text50        ;CaptionML=ENU=Address 2 }
    { 6   ;   ;City                ;Text30        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code".City
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=City }
    { 7   ;   ;Phone No.           ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Phone No. }
    { 8   ;   ;Phone No. 2         ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Phone No. 2 }
    { 9   ;   ;Telex No.           ;Text30        ;CaptionML=ENU=Telex No. }
    { 10  ;   ;Fax No.             ;Text30        ;CaptionML=ENU=Fax No. }
    { 11  ;   ;Giro No.            ;Text20        ;OnValidate=VAR
                                                                BA004@1100285000 : TextConst 'ENU=%1 is not correct.';
                                                              BEGIN
                                                                PurchPaySetup.GET;
                                                                IF ("Giro No." <> '') AND PurchPaySetup."Check Giro Account" THEN
                                                                  IF NOT LBManagement.SumCheck10Control("Giro No.") THEN
                                                                    ERROR(BA004,FIELDCAPTION("Giro No."));
                                                              END;

                                                   CaptionML=ENU=Giro No. }
    { 12  ;   ;Bank Name           ;Text50        ;CaptionML=ENU=Bank Name }
    { 13  ;   ;Bank Branch No.     ;Text20        ;CaptionML=ENU=Bank Branch No. }
    { 14  ;   ;Bank Account No.    ;Text30        ;CaptionML=ENU=Bank Account No. }
    { 15  ;   ;Payment Routing No. ;Text20        ;CaptionML=ENU=Payment Routing No. }
    { 17  ;   ;Customs Permit No.  ;Text10        ;CaptionML=ENU=Customs Permit No. }
    { 18  ;   ;Customs Permit Date ;Date          ;CaptionML=ENU=Customs Permit Date }
    { 19  ;   ;VAT Registration No.;Text20        ;OnValidate=VAR
                                                                VATRegNoFormat@1000 : Record 381;
                                                              BEGIN
                                                                VATRegNoFormat.Test("VAT Registration No.","Country/Region Code",'',DATABASE::"Company Information");
                                                              END;

                                                   CaptionML=ENU=VAT Registration No. }
    { 20  ;   ;Registration No.    ;Text20        ;CaptionML=ENU=Registration No. }
    { 21  ;   ;Telex Answer Back   ;Text20        ;CaptionML=ENU=Telex Answer Back }
    { 22  ;   ;Ship-to Name        ;Text50        ;CaptionML=ENU=Ship-to Name }
    { 23  ;   ;Ship-to Name 2      ;Text50        ;CaptionML=ENU=Ship-to Name 2 }
    { 24  ;   ;Ship-to Address     ;Text50        ;CaptionML=ENU=Ship-to Address }
    { 25  ;   ;Ship-to Address 2   ;Text50        ;CaptionML=ENU=Ship-to Address 2 }
    { 26  ;   ;Ship-to City        ;Text30        ;TableRelation=IF (Ship-to Country/Region Code=CONST()) "Post Code".City
                                                                 ELSE IF (Ship-to Country/Region Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Ship-to Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(
                                                                  "Ship-to City","Ship-to Post Code","Ship-to County","Ship-to Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Ship-to City }
    { 27  ;   ;Ship-to Contact     ;Text50        ;CaptionML=ENU=Ship-to Contact }
    { 28  ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE (Use As In-Transit=CONST(No));
                                                   CaptionML=ENU=Location Code }
    { 29  ;   ;Picture             ;BLOB          ;CaptionML=ENU=Picture;
                                                   SubType=Bitmap }
    { 30  ;   ;Post Code           ;Code20        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code".Code
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code".Code WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Post Code }
    { 31  ;   ;County              ;Text30        ;CaptionML=ENU=County }
    { 32  ;   ;Ship-to Post Code   ;Code20        ;TableRelation=IF (Ship-to Country/Region Code=CONST()) "Post Code".Code
                                                                 ELSE IF (Ship-to Country/Region Code=FILTER(<>'')) "Post Code".Code WHERE (Country/Region Code=FIELD(Ship-to Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(
                                                                  "Ship-to City","Ship-to Post Code","Ship-to County","Ship-to Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Ship-to Post Code }
    { 33  ;   ;Ship-to County      ;Text30        ;CaptionML=ENU=Ship-to County }
    { 34  ;   ;E-Mail              ;Text80        ;ExtendedDatatype=E-Mail;
                                                   CaptionML=ENU=E-Mail }
    { 35  ;   ;Home Page           ;Text80        ;ExtendedDatatype=URL;
                                                   CaptionML=ENU=Home Page }
    { 36  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=ENU=Country/Region Code }
    { 37  ;   ;Ship-to Country/Region Code;Code10 ;TableRelation=Country/Region;
                                                   CaptionML=ENU=Ship-to Country/Region Code }
    { 38  ;   ;IBAN                ;Code50        ;OnValidate=BEGIN
                                                                CheckIBAN(IBAN);
                                                              END;

                                                   CaptionML=ENU=IBAN }
    { 39  ;   ;SWIFT Code          ;Code20        ;CaptionML=ENU=SWIFT Code }
    { 40  ;   ;Industrial Classification;Text30   ;CaptionML=ENU=Industrial Classification }
    { 41  ;   ;IC Partner Code     ;Code20        ;AccessByPermission=TableData 410=R;
                                                   CaptionML=ENU=IC Partner Code }
    { 42  ;   ;IC Inbox Type       ;Option        ;OnValidate=BEGIN
                                                                IF "IC Inbox Type" = "IC Inbox Type"::Database THEN
                                                                  "IC Inbox Details" := '';
                                                              END;

                                                   AccessByPermission=TableData 410=R;
                                                   CaptionML=ENU=IC Inbox Type;
                                                   OptionCaptionML=ENU=File Location,Database;
                                                   OptionString=File Location,Database }
    { 43  ;   ;IC Inbox Details    ;Text250       ;OnLookup=VAR
                                                              FileMgt@1000 : Codeunit 419;
                                                              FileName@1002 : Text;
                                                              FileName2@1003 : Text;
                                                              Path@1004 : Text;
                                                            BEGIN
                                                              TESTFIELD("IC Partner Code");
                                                              CASE "IC Inbox Type" OF
                                                                "IC Inbox Type"::"File Location":
                                                                  BEGIN
                                                                    IF "IC Inbox Details" = '' THEN
                                                                      FileName := STRSUBSTNO('%1.xml',"IC Partner Code")
                                                                    ELSE
                                                                      FileName := "IC Inbox Details" + STRSUBSTNO('\%1.xml',"IC Partner Code");

                                                                    FileName2 := FileMgt.SaveFileDialog(Text001,FileName,'');
                                                                    IF FileName <> FileName2 THEN BEGIN
                                                                      Path := FileMgt.GetDirectoryName(FileName2);
                                                                      IF Path <> '' THEN
                                                                        "IC Inbox Details" := COPYSTR(Path,1,250);
                                                                    END;
                                                                  END;
                                                              END;
                                                            END;

                                                   AccessByPermission=TableData 410=R;
                                                   CaptionML=ENU=IC Inbox Details }
    { 46  ;   ;System Indicator    ;Option        ;CaptionML=ENU=System Indicator;
                                                   OptionCaptionML=ENU=None,Custom Text,Company Information,Company,Database,Company+Database,,,,,,,Company Information Name+Database,Company Information Name+Company ID+Database;
                                                   OptionString=None,Custom Text,Company Information,Company,Database,Company+Database,,,,,,,Company Information Name+Database,Company Information Name+Company ID+Database }
    { 47  ;   ;Custom System Indicator Text;Text250;
                                                   CaptionML=ENU=Custom System Indicator Text }
    { 48  ;   ;System Indicator Style;Option      ;CaptionML=ENU=System Indicator Style;
                                                   OptionCaptionML=ENU=Standard,Accent1,Accent2,Accent3,Accent4,Accent5,Accent6,Accent7,Accent8,Accent9;
                                                   OptionString=Standard,Accent1,Accent2,Accent3,Accent4,Accent5,Accent6,Accent7,Accent8,Accent9 }
    { 50  ;   ;Allow Blank Payment Info.;Boolean  ;CaptionML=ENU=Allow Blank Payment Info. }
    { 90  ;   ;GLN                 ;Code13        ;OnValidate=VAR
                                                                GLNCalculator@1000 : Codeunit 1607;
                                                              BEGIN
                                                                IF GLN <> '' THEN
                                                                  IF NOT GLNCalculator.IsValidCheckDigit13(GLN) THEN
                                                                    ERROR(GLNCheckDigitErr,FIELDCAPTION(GLN));
                                                              END;

                                                   CaptionML=ENU=GLN;
                                                   Numeric=Yes }
    { 5700;   ;Responsibility Center;Code10       ;TableRelation="Responsibility Center".Code;
                                                   ValidateTableRelation=Yes;
                                                   CaptionML=ENU=Responsibility Center }
    { 5791;   ;Check-Avail. Period Calc.;DateFormula;
                                                   AccessByPermission=TableData 27=R;
                                                   CaptionML=ENU=Check-Avail. Period Calc. }
    { 5792;   ;Check-Avail. Time Bucket;Option    ;AccessByPermission=TableData 27=R;
                                                   CaptionML=ENU=Check-Avail. Time Bucket;
                                                   OptionCaptionML=ENU=Day,Week,Month,Quarter,Year;
                                                   OptionString=Day,Week,Month,Quarter,Year }
    { 7600;   ;Base Calendar Code  ;Code10        ;TableRelation="Base Calendar";
                                                   CaptionML=ENU=Base Calendar Code }
    { 7601;   ;Cal. Convergence Time Frame;DateFormula;
                                                   InitValue=1Y;
                                                   AccessByPermission=TableData 11072014=R;
                                                   CaptionML=ENU=Cal. Convergence Time Frame }
    { 11012010;;Affiliation No.    ;Text20        ;CaptionML=ENU=Affiliation No. }
    { 11012020;;Income Tax No.     ;Text20        ;CaptionML=ENU=Income Tax No. }
    { 11012030;;FSC Certificate    ;Text20        ;CaptionML=ENU=FSC Certificate }
    { 11012040;;Due Date Tax Auth. Statement;Date ;CaptionML=ENU=Due Date Tax Authority Statement }
    { 11012050;;COC Registration No.;Text50       ;CaptionML=ENU=COC Registration No. }
    { 11012060;;Due Date COC Statement;Date       ;CaptionML=ENU=Due Date COC Statement }
    { 11012070;;B-Account          ;Text20        ;CaptionML=ENU=B-Account }
    { 11012075;;IBAN B-Account     ;Code50        ;OnValidate=BEGIN
                                                                CheckIBAN("IBAN B-Account");
                                                              END;

                                                   CaptionML=ENU=IBAN B-Account }
    { 11012080;;Phone No. Accounts Department;Text20;
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Phone No. Accounts Department }
    { 11012090;;Archive Company    ;Boolean       ;CaptionML=ENU=Archive Company }
    { 11012100;;Postal Address     ;Text50        ;CaptionML=ENU=Postal Address }
    { 11012110;;Postal Address 2   ;Text50        ;CaptionML=ENU=Postal Address 2 }
    { 11012120;;Postal City        ;Text50        ;TableRelation=IF (Postal Country Code=CONST()) "Post Code".City
                                                                 ELSE IF (Postal Country Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Postal Country Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity("Postal City","Postal Post Code","Postal County","Postal Country Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   CaptionML=ENU=Postal City }
    { 11012130;;Postal Phone No.   ;Text20        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=ENU=Post Phone No. }
    { 11012140;;Postal Post Code   ;Code20        ;TableRelation="Post Code";
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(
                                                                  "Postal City","Postal Post Code","Postal County","Postal Country Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Postal Post Code }
    { 11012145;;Postal County      ;Text30        ;CaptionML=ENU=Postal County }
    { 11012150;;Postal Country Code;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=ENU=Postal Country Code }
    { 11012160;;Postal Fax No.     ;Text20        ;CaptionML=ENU=Postal Fax No. }
    { 11012170;;Global Location Number;Code30     ;CaptionML=ENU=Global Location Number }
    { 11012180;;Language ISO Code  ;Code10        ;CaptionML=ENU=Language ISO Code }
    { 11012190;;Language of the Party;Code10      ;CaptionML=ENU=Language of the Party }
    { 11012191;;SWIFT-code G-Account;Code20       ;CaptionML=ENU=SWIFT-code G-Account }
    { 11012192;;IBAN Account-2     ;Code50        ;OnValidate=BEGIN
                                                                CheckIBAN("IBAN Account-2");
                                                              END;

                                                   CaptionML=ENU=IBAN Account-2 }
    { 11012193;;SWIFT-code Account-2;Code20       ;CaptionML=ENU=SWIFT-code Account-2 }
    { 11012194;;EORI-No.           ;Text20        ;CaptionML=ENU=EORI-No. }
    { 11012195;;Site Mgt Company Relation Code;Code10;
                                                   OnValidate=VAR
                                                                SiteMngmntHoursCompanies@1100525000 : Record 11012356;
                                                              BEGIN
                                                                IF (NOT SiteMngmntHoursCompanies.ISEMPTY) THEN BEGIN
                                                                  IF (SiteMngmntHoursCompanies.GET(COMPANYNAME)) THEN BEGIN
                                                                    SiteMngmntHoursCompanies."Relation Code" := "Site Mgt Company Relation Code";
                                                                    SiteMngmntHoursCompanies.MODIFY;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Site Mgt Company Relation Code }
    { 11012200;;Language Code Data Upgrade;Integer;TableRelation="Windows Language";
                                                   CaptionML=ENU=Language for Data Upgrade }
    { 11126170;;Company Reg. No.   ;Code20        ;CaptionML=ENU=Company Reg. No.;
                                                   Description=NAVFI }
    { 11126171;;Business Identity Code;Code20     ;CaptionML=ENU=Business Identity Code;
                                                   Description=NAVFI }
    { 11126172;;Registered Home City;Text50       ;CaptionML=ENU=Registered Home City;
                                                   Description=NAVFI }
    { 11128010;;Plus Giro No.      ;Text20        ;CaptionML=ENU=Plus Giro No.;
                                                   Description=(11200) }
    { 11128011;;Registered Office  ;Text20        ;CaptionML=ENU=Registered Office;
                                                   Description=(11201) }
    { 11128012;;Plus Giro Customer No.;Code5      ;CaptionML=ENU=Plus Giro Customer No. }
    { 11128013;;Interpay-ID        ;Code20        ;CaptionML=ENU=Interpay-ID }
    { 11128050;;Server Computer Name;Text250      ;OnLookup=VAR
                                                              ServerInstance@1100285500 : Record 2000000112;
                                                            BEGIN
                                                              //ENH032
                                                              IF PAGE.RUNMODAL(676,ServerInstance) = ACTION::LookupOK THEN BEGIN
                                                                "Server Computer Name" := ServerInstance."Server Computer Name";
                                                                "Server Instance Name" := ServerInstance."Server Instance Name";
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Server Computer Name;
                                                   Description=ENH032 }
    { 11128051;;Server Instance Name;Text250      ;TableRelation="Server Instance"."Server Instance Name";
                                                   OnLookup=VAR
                                                              ServerInstance@1100285500 : Record 2000000112;
                                                            BEGIN
                                                              //ENH032
                                                              IF PAGE.RUNMODAL(676,ServerInstance) = ACTION::LookupOK THEN BEGIN
                                                                "Server Computer Name" := ServerInstance."Server Computer Name";
                                                                "Server Instance Name" := ServerInstance."Server Instance Name";
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Server Instance Name;
                                                   Description=ENH032 }
    { 11128060;;Base is Initiated  ;Boolean       ;CaptionML=ENU=Base is Initiated;
                                                   Description=PEB0002 (12047958) }
    { 11128061;;Plus Giro Ref. Account No.;Code6  ;CaptionML=ENU=Plus Giro Ref. Account No.;
                                                   Numeric=Yes;
                                                   Description=PEB0006 (12047959) }
    { 11128501;;Enterpriseregister ;Boolean       ;CaptionML=ENU=Enterpriseregister;
                                                   Description=NAVNO }
    { 11128570;;Bank Creditor No.  ;Code8         ;OnValidate=BEGIN
                                                                IF "Bank Creditor No." = '' THEN
                                                                  EXIT;
                                                                IF STRLEN("Bank Creditor No.") <> MAXSTRLEN("Bank Creditor No.") THEN
                                                                  ERROR(STRSUBSTNO(BankCreditorNumberLengthErr,FIELDCAPTION("Bank Creditor No.")));
                                                              END;

                                                   CaptionML=ENU=Bank Creditor No.;
                                                   Numeric=Yes;
                                                   Description=NAVDK }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PostCode@1000 : Record 225;
      Text000@1001 : TextConst 'ENU=%1 may not be a valid International Bank Account Number (IBAN). Do you want to continue?';
      Text001@1002 : TextConst 'ENU=File Location for IC files';
      Text002@1003 : TextConst 'ENU=Before you can use Online Map, you must fill in the Online Map Setup window.\See Setting Up Online Map in Help.';
      BankCreditorNumberLengthErr@1060000 : TextConst 'ENU=%1 must be an 8-digit number.';
      MissingOIOUBMInfoQst@1060001 : TextConst 'ENU=You need to provide information in %1 to support OIOUBL. Do you want to update it now?';
      MissingOIOUBMInfoErr@1060002 : TextConst 'ENU=The needed information to support OIOUBL is not provided in %1.';
      NoPaymentInfoQst@1005 : TextConst '@@@="%1 = Company Information";ENU=No payment information is provided in %1. Do you want to update it now?';
      NoPaymentInfoMsg@1004 : TextConst 'ENU=No payment information is provided in %1. Review the report.';
      PurchPaySetup@1100285500 : Record 312;
      LBManagement@1100285000 : Codeunit 11128015;
      Text10600@1080000 : TextConst 'ENU=The GLN No. field does not contain a valid, 13-digit GLN  number';
      Text10601@1080001 : TextConst 'ENU=Enterpriseregister';
      GLNCheckDigitErr@1006 : TextConst 'ENU=The %1 is not valid.';
      SuppressIBANError@1100528400 : Boolean;
      IBANErrorOccurred@1100528401 : Boolean;
      BoardOfDirectorsLocCaptionLbl@1070001 : TextConst 'ENU=Board Of Directors Location (registered office)';
      BA004@1070002 : TextConst 'ENU=%1 is not correct.';
      GLSetup@1100285501 : Record 98;
      gotGLSetup@1100285502 : Boolean;

    PROCEDURE CheckIBAN@1(IBANCode@1000 : Code[100]);
    VAR
      SourceIBANCode@1100409000 : Code[100];
      Modulus97@1001 : Integer;
      I@1002 : Integer;
    BEGIN
      IF IBANCode = '' THEN
        EXIT;
      SourceIBANCode := IBANCode; //**4PS.n W055841
      IBANCode := DELCHR(IBANCode);
      Modulus97 := 97;
      IF (STRLEN(IBANCode) <= 5) OR (STRLEN(IBANCode) > 34) THEN
      //IBANError; //**4PS.o
        IBANError(SourceIBANCode); //**4PS.n W055841
      ConvertIBAN(IBANCode);
      WHILE STRLEN(IBANCode) > 6 DO
        IBANCode := CalcModulus(COPYSTR(IBANCode,1,6),Modulus97) + COPYSTR(IBANCode,7);
      EVALUATE(I,IBANCode);
      IF (I MOD Modulus97) <> 1 THEN
      //IBANError; //**4PS.o
        IBANError(SourceIBANCode); //**4PS.n W055841
      //CALL C024254 sn
      IF NOT CheckFirstFourPositionsIBAN(SourceIBANCode) THEN
        IBANError(SourceIBANCode);
      //CALL C024254 en
    END;

    PROCEDURE ConvertIBAN@4(VAR IBANCode@1000 : Code[100]);
    VAR
      I@1002 : Integer;
    BEGIN
      IBANCode := COPYSTR(IBANCode,5) + COPYSTR(IBANCode,1,4);
      I := 0;
      WHILE I < STRLEN(IBANCode) DO BEGIN
        I := I + 1;
        IF ConvertLetter(IBANCode,COPYSTR(IBANCode,I,1),I) THEN
          I := 0;
      END;
    END;

    PROCEDURE CalcModulus@3(Number@1000 : Code[10];Modulus97@1001 : Integer) : Code[10];
    VAR
      I@1002 : Integer;
    BEGIN
      EVALUATE(I,Number);
      I := I MOD Modulus97;
      IF I = 0 THEN
        EXIT('');
      EXIT(FORMAT(I));
    END;

    LOCAL PROCEDURE ConvertLetter@5(VAR IBANCode@1000 : Code[100];Letter@1001 : Code[1];LetterPlace@1002 : Integer) : Boolean;
    VAR
      Letter2@1003 : Code[2];
      SourceIBANCode@1100409000 : Code[100];
    BEGIN
      SourceIBANCode := IBANCode; //**4PS.n W055841
      IF (Letter >= 'A') AND (Letter <= 'Z') THEN BEGIN
        CASE Letter OF
          'A':
            Letter2 := '10';
          'B':
            Letter2 := '11';
          'C':
            Letter2 := '12';
          'D':
            Letter2 := '13';
          'E':
            Letter2 := '14';
          'F':
            Letter2 := '15';
          'G':
            Letter2 := '16';
          'H':
            Letter2 := '17';
          'I':
            Letter2 := '18';
          'J':
            Letter2 := '19';
          'K':
            Letter2 := '20';
          'L':
            Letter2 := '21';
          'M':
            Letter2 := '22';
          'N':
            Letter2 := '23';
          'O':
            Letter2 := '24';
          'P':
            Letter2 := '25';
          'Q':
            Letter2 := '26';
          'R':
            Letter2 := '27';
          'S':
            Letter2 := '28';
          'T':
            Letter2 := '29';
          'U':
            Letter2 := '30';
          'V':
            Letter2 := '31';
          'W':
            Letter2 := '32';
          'X':
            Letter2 := '33';
          'Y':
            Letter2 := '34';
          'Z':
            Letter2 := '35';
        END;
        IF LetterPlace = 1 THEN
          IBANCode := Letter2 + COPYSTR(IBANCode,2)
        ELSE BEGIN
          IF LetterPlace = STRLEN(IBANCode) THEN
            IBANCode := COPYSTR(IBANCode,1,LetterPlace - 1) + Letter2
          ELSE
            IBANCode :=
              COPYSTR(IBANCode,1,LetterPlace - 1) + Letter2 + COPYSTR(IBANCode,LetterPlace + 1);
        END;
        EXIT(TRUE);
      END;
      IF (Letter >= '0') AND (Letter <= '9') THEN
        EXIT(FALSE);

      //IBANError; //**4PS.o
        IBANError(SourceIBANCode); //**4PS.n W055841
    END;

    PROCEDURE IBANError@12(IBANCode@1100409000 : Code[100]);
    BEGIN
      //**4PS.sn
      IF SuppressIBANError THEN BEGIN
        IBANErrorOccurred := TRUE;
        EXIT;
      END;
      //**4PS.en
      //IF NOT CONFIRM(Text000) THEN //**4PS.o
      IF NOT CONFIRM(Text000,FALSE,IBANCode) THEN //**4PS.n W055841
        ERROR('');
    END;

    PROCEDURE DisplayMap@7();
    VAR
      MapPoint@1001 : Record 800;
      MapMgt@1000 : Codeunit 802;
    BEGIN
      IF MapPoint.FINDFIRST THEN
        MapMgt.MakeSelection(DATABASE::"Company Information",GETPOSITION)
      ELSE
        MESSAGE(Text002);
    END;

    LOCAL PROCEDURE IsPaymentInfoAvailble@10() : Boolean;
    BEGIN
      EXIT(
        (("Giro No." + IBAN + "Bank Name" + "Bank Branch No." + "Bank Account No." + "SWIFT Code") <> '') OR
        "Allow Blank Payment Info.");
    END;

    PROCEDURE GetRegistrationNumber@14() : Text;
    BEGIN
      //NAVFI
      GetGLSetup;
      IF GLSetup."Finnish localization active" THEN
        EXIT("Business Identity Code");
      //NAVFI

      EXIT("Registration No.");
    END;

    PROCEDURE GetRegistrationNumberLbl@15() : Text;
    BEGIN
      //NAVFI
      GetGLSetup;
      IF GLSetup."Finnish localization active" THEN
        EXIT(FIELDCAPTION("Business Identity Code"));
      //NAVFI

      EXIT(FIELDCAPTION("Registration No."));
    END;

    PROCEDURE GetVATRegistrationNumber@13() : Text;
    BEGIN
      EXIT("VAT Registration No.");
    END;

    PROCEDURE GetVATRegistrationNumberLbl@11() : Text;
    BEGIN
      EXIT(FIELDCAPTION("VAT Registration No."));
    END;

    PROCEDURE GetLegalOffice@16() : Text;
    BEGIN
      //NAVFI
      GetGLSetup;
      IF GLSetup."Finnish localization active" THEN
        EXIT("Registered Home City");
      //NAVFI

      EXIT("Registered Office");
    END;

    PROCEDURE GetLegalOfficeLbl@17() : Text;
    BEGIN
      //NAVFI
      GetGLSetup;
      IF GLSetup."Finnish localization active" THEN
        EXIT(FIELDCAPTION("Registered Home City"));
      //NAVFI

      EXIT(BoardOfDirectorsLocCaptionLbl);
    END;

    PROCEDURE GetCustomGiro@20() : Text;
    BEGIN
      EXIT("Plus Giro No.");
    END;

    PROCEDURE GetCustomGiroLbl@19() : Text;
    BEGIN
      EXIT(FIELDCAPTION("Plus Giro No."));
    END;

    PROCEDURE VerifyAndSetPaymentInfo@6();
    VAR
      CompanyInformationPage@1000 : Page 1;
    BEGIN
      GET;
      IF IsPaymentInfoAvailble THEN
        EXIT;
      IF CONFIRM(NoPaymentInfoQst,TRUE,TABLECAPTION) THEN BEGIN
        CompanyInformationPage.SETRECORD(Rec);
        CompanyInformationPage.EDITABLE(TRUE);
        IF CompanyInformationPage.RUNMODAL = ACTION::OK THEN
          CompanyInformationPage.GETRECORD(Rec);
      END;
      IF NOT IsPaymentInfoAvailble THEN
        MESSAGE(NoPaymentInfoMsg,TABLECAPTION);
    END;

    PROCEDURE GetSystemIndicator@8(VAR Text@1000 : Text[250];VAR Style@1001 : 'Standard,Accent1,Accent2,Accent3,Accent4,Accent5,Accent6,Accent7,Accent8,Accent9');
    VAR
      CompanyData@1100285300 : Record 11020674;
    BEGIN
      Style := "System Indicator Style";
      CASE "System Indicator" OF
        "System Indicator"::None:
          Text := '';
        "System Indicator"::"Custom Text":
          Text := "Custom System Indicator Text";
        "System Indicator"::"Company Information":
          Text := Name;
        "System Indicator"::Company:
          Text := COMPANYNAME;
        "System Indicator"::Database:
          //Text := GetDatabaseIndicatorText(FALSE);
          Text := GetDatabaseIndicatorText(FALSE,'');
        "System Indicator"::"Company+Database":
          //Text := GetDatabaseIndicatorText(TRUE);
          Text := GetDatabaseIndicatorText(TRUE,COMPANYNAME);

        "System Indicator"::"Company Information Name+Database":
          Text := GetDatabaseIndicatorText(TRUE,Name);
        "System Indicator"::"Company Information Name+Company ID+Database":
          BEGIN
            IF CompanyData.GET(COMPANYNAME) THEN
              Text := GetDatabaseIndicatorText(TRUE,Name + ' - ' + CompanyData."Company ID")
            ELSE
              Text := GetDatabaseIndicatorText(TRUE,Name);
          END;
      END
    END;

    LOCAL PROCEDURE GetDatabaseIndicatorText@9(IncludeCompany@1003 : Boolean;pCompanyName@1000000000 : Text[250]) : Text[250];
    VAR
      ActiveSession@1000 : Record 2000000110;
      Text@1002 : Text[1024];
    BEGIN
      ActiveSession.SETRANGE("Server Instance ID",SERVICEINSTANCEID);
      ActiveSession.SETRANGE("Session ID",SESSIONID);
      ActiveSession.FINDFIRST;
      Text := ActiveSession."Database Name" + ' - ' + ActiveSession."Server Computer Name";
      IF IncludeCompany THEN
      //  Text := COMPANYNAME + ' - ' + Text;
        Text := pCompanyName + ' - ' + Text;
      IF STRLEN(Text) > 250 THEN
        EXIT(COPYSTR(Text,1,247) + '...');
      EXIT(Text)
    END;

    PROCEDURE GetEnterpriseClassification@1080000() : Text[50];
    BEGIN
      //NAVNO
      IF Enterpriseregister THEN
        EXIT(Text10601);

      EXIT('');
    END;

    PROCEDURE VerifyAndSetOIOUBLInfo@1060001();
    VAR
      OIOUBLCompanyInfoSetup@1060000 : Page 11128572;
    BEGIN
      //NAVDK
      GET;
      IF IsOIOUBLInfoAvailble THEN
        EXIT;
      IF CONFIRM(MissingOIOUBMInfoQst,TRUE,TABLECAPTION) THEN BEGIN
        OIOUBLCompanyInfoSetup.SETRECORD(Rec);
        OIOUBLCompanyInfoSetup.EDITABLE(TRUE);
        IF OIOUBLCompanyInfoSetup.RUNMODAL = ACTION::OK THEN
          OIOUBLCompanyInfoSetup.GETRECORD(Rec);
        IF NOT IsOIOUBLInfoAvailble THEN
          ERROR(MissingOIOUBMInfoErr,TABLECAPTION)
      END ELSE
        ERROR(MissingOIOUBMInfoErr,TABLECAPTION);
    END;

    LOCAL PROCEDURE IsOIOUBLInfoAvailble@1060000() : Boolean;
    BEGIN
      //NAVDK
      IF "VAT Registration No." = '' THEN
        EXIT(FALSE);
      IF Name = '' THEN
        EXIT(FALSE);
      IF Address  ='' THEN
        EXIT(FALSE);
      IF City = '' THEN
        EXIT(FALSE);
      IF "Post Code" = '' THEN
        EXIT(FALSE);
      IF "Country/Region Code" = '' THEN
         EXIT(FALSE);
      IF IBAN = '' THEN
        IF "Bank Account No." = '' THEN
          EXIT(FALSE);
      IF "Bank Branch No." = '' THEN
        EXIT(FALSE);
      EXIT(TRUE);
    END;

    PROCEDURE GetCountryRegionCode@2(CountryRegionCode@1000 : Code[10]) : Code[10];
    BEGIN
      CASE CountryRegionCode OF
        '',"Country/Region Code":
          EXIT("Country/Region Code");
        ELSE
          EXIT(CountryRegionCode);
      END;
    END;

    PROCEDURE GetPaymentChannelCode@1060002() : Text[7];
    VAR
      CountryRegion@1060000 : Record 9;
    BEGIN
      //NAVDK
      CountryRegion.GET("Country/Region Code");
      CountryRegion.TESTFIELD("OIOUBL Country/Region Code");
      EXIT(CountryRegion."OIOUBL Country/Region Code" + ':BANK');
    END;

    PROCEDURE CheckFirstFourPositionsIBAN@1100525000(VAR IBANCode@1000 : Code[100]) : Boolean;
    VAR
      I@1002 : Integer;
    BEGIN
      //CALL C024254
      FOR I := 1 TO 2 DO
        IF NOT ( (UPPERCASE(COPYSTR(IBANCode,I,1)) >= 'A') AND (UPPERCASE(COPYSTR(IBANCode,I,1))  <= 'Z')) THEN
          EXIT(FALSE);

      FOR I := 3 TO 4 DO
        IF NOT (  (COPYSTR(IBANCode,I,1) >= '0') AND ( COPYSTR(IBANCode,I,1)  <= '9')) THEN
          EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE IBANIsValid@1100528403(IBANCode@1100528400 : Code[100]) : Boolean;
    BEGIN
      //**4PS
      SuppressIBANError := TRUE;
      IBANErrorOccurred := FALSE;
      CheckIBAN(IBANCode);
      EXIT(NOT IBANErrorOccurred);
    END;

    LOCAL PROCEDURE GetGLSetup@1100285501();
    BEGIN
      //4PSSE.FI
      IF NOT gotGLSetup THEN
        gotGLSetup := GLSetup.GET;
    END;

    BEGIN
    {
      NAVSE9.00.00.43402
      Version List=NAVW19.00,NAVFI9.00;
      Version List=NAVW19.00.00.45243,NAVDK9.00.00.45243;
      Version List=NAVW19.00.00.45834,NAVNO9.00.00.45834;
      160405 ITERO.DL ut”kad system-indikator
    }
    END.
  }
}

