OBJECT Table 9053 Sales Cue
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00.00.44365,4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Sales Cue;
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=ENU=Primary Key }
    { 2   ;   ;Sales Quotes - Open ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE (Document Type=FILTER(Quote),
                                                                                           Status=FILTER(Open),
                                                                                           Responsibility Center=FIELD(Responsibility Center Filter)));
                                                   CaptionML=ENU=Sales Quotes - Open;
                                                   Editable=No }
    { 3   ;   ;Sales Orders - Open ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE (Document Type=FILTER(Order),
                                                                                           Status=FILTER(Open),
                                                                                           Responsibility Center=FIELD(Responsibility Center Filter)));
                                                   AccessByPermission=TableData 110=R;
                                                   CaptionML=ENU=Sales Orders - Open;
                                                   Editable=No }
    { 4   ;   ;Ready to Ship       ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE (Document Type=FILTER(Order),
                                                                                           Status=FILTER(Released),
                                                                                           Completely Shipped=CONST(No),
                                                                                           Shipment Date=FIELD(Date Filter2),
                                                                                           Responsibility Center=FIELD(Responsibility Center Filter)));
                                                   AccessByPermission=TableData 110=R;
                                                   CaptionML=ENU=Ready to Ship;
                                                   Editable=No }
    { 5   ;   ;Delayed             ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE (Document Type=FILTER(Order),
                                                                                           Status=FILTER(Released),
                                                                                           Completely Shipped=CONST(No),
                                                                                           Shipment Date=FIELD(Date Filter),
                                                                                           Responsibility Center=FIELD(Responsibility Center Filter)));
                                                   AccessByPermission=TableData 110=R;
                                                   CaptionML=ENU=Delayed;
                                                   Editable=No }
    { 6   ;   ;Sales Return Orders - Open;Integer ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE (Document Type=FILTER(Return Order),
                                                                                           Status=FILTER(Open),
                                                                                           Responsibility Center=FIELD(Responsibility Center Filter)));
                                                   AccessByPermission=TableData 6660=R;
                                                   CaptionML=ENU=Sales Return Orders - Open;
                                                   Editable=No }
    { 7   ;   ;Sales Credit Memos - Open;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE (Document Type=FILTER(Credit Memo),
                                                                                           Status=FILTER(Open),
                                                                                           Responsibility Center=FIELD(Responsibility Center Filter)));
                                                   CaptionML=ENU=Sales Credit Memos - Open;
                                                   Editable=No }
    { 8   ;   ;Partially Shipped   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE (Document Type=FILTER(Order),
                                                                                           Status=FILTER(Released),
                                                                                           Shipped=FILTER(Yes),
                                                                                           Completely Shipped=FILTER(No),
                                                                                           Shipment Date=FIELD(Date Filter2),
                                                                                           Responsibility Center=FIELD(Responsibility Center Filter)));
                                                   AccessByPermission=TableData 110=R;
                                                   CaptionML=ENU=Partially Shipped;
                                                   Editable=No }
    { 9   ;   ;Average Days Delayed;Decimal       ;AccessByPermission=TableData 110=R;
                                                   CaptionML=ENU=Average Days Delayed;
                                                   DecimalPlaces=1:1;
                                                   Editable=No }
    { 10  ;   ;Sales Inv. - Pending Doc.Exch.;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Invoice Header" WHERE (Document Exchange Status=FILTER(Sent to Document Exchange Service|Delivery Failed)));
                                                   CaptionML=ENU=Sales Invoices - Pending Document Exchange;
                                                   Editable=No }
    { 12  ;   ;Sales CrM. - Pending Doc.Exch.;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Cr.Memo Header" WHERE (Document Exchange Status=FILTER(Sent to Document Exchange Service|Delivery Failed)));
                                                   CaptionML=ENU=Sales Credit Memos - Pending Document Exchange;
                                                   Editable=No }
    { 20  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter;
                                                   Editable=No }
    { 21  ;   ;Date Filter2        ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter 2;
                                                   Editable=No }
    { 22  ;   ;Responsibility Center Filter;Code10;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Responsibility Center Filter;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE SetRespCenterFilter@1();
    VAR
      UserSetupMgt@1000 : Codeunit 5700;
      RespCenterCode@1001 : Code[10];
    BEGIN
      RespCenterCode := UserSetupMgt.GetSalesFilter;
      IF RespCenterCode <> '' THEN BEGIN
        FILTERGROUP(2);
        SETRANGE("Responsibility Center Filter",RespCenterCode);
        FILTERGROUP(0);
      END;
    END;

    PROCEDURE CalculateAverageDaysDelayed@2() AverageDays : Decimal;
    VAR
      SalesHeader@1000 : Record 36;
      SumDelayDays@1002 : Integer;
      CountDelayedInvoices@1001 : Integer;
    BEGIN
      FilterOrders(SalesHeader,FIELDNO(Delayed));
      IF SalesHeader.FINDSET THEN BEGIN
        REPEAT
          SumDelayDays += WORKDATE - SalesHeader."Shipment Date";
          CountDelayedInvoices += 1;
        UNTIL SalesHeader.NEXT = 0;
        AverageDays := SumDelayDays / CountDelayedInvoices;
      END;
    END;

    PROCEDURE CountOrders@9(FieldNumber@1000 : Integer) : Integer;
    VAR
      CountSalesOrders@1001 : Query 9060;
      SalesOrderWithBlankShipmentDateCount@1002 : Integer;
      SalesHeader@1100525000 : Record 36;
      i@1100525001 : Integer;
    BEGIN
      CountSalesOrders.SETRANGE(Status,CountSalesOrders.Status::Released);
      CountSalesOrders.SETRANGE(Completely_Shipped,FALSE);
      FILTERGROUP(2);
      CountSalesOrders.SETFILTER(Responsibility_Center,GETFILTER("Responsibility Center Filter"));
      FILTERGROUP(0);
      //**4PS.sn C015772
      SalesHeader.DPA_SetFilters('Page 9305');
      FOR i := 10 DOWNTO 0 DO BEGIN
        SalesHeader.FILTERGROUP(i);
        IF SalesHeader.GETFILTER("Shortcut Dimension 1 Code") <> '' THEN
          CountSalesOrders.SETFILTER(Shortcut_Dimension_1_Code,SalesHeader.GETFILTER("Shortcut Dimension 1 Code"));
      END;
      //**4PS.en

      CASE FieldNumber OF
        FIELDNO("Ready to Ship"):
          BEGIN
            CountSalesOrders.SETRANGE(Ship);
            CountSalesOrders.SETFILTER(Shipment_Date,GETFILTER("Date Filter2"));
          END;
        FIELDNO("Partially Shipped"):
          BEGIN
            CountSalesOrders.SETRANGE(Shipped,TRUE);
            CountSalesOrders.SETFILTER(Shipment_Date,GETFILTER("Date Filter2"));
          END;
        FIELDNO(Delayed):
          BEGIN
            CountSalesOrders.SETRANGE(Ship);
            CountSalesOrders.SETRANGE(Shipment_Date,0D);
            CountSalesOrders.OPEN;
            CountSalesOrders.READ;
            SalesOrderWithBlankShipmentDateCount := CountSalesOrders.Count_Orders;
            CountSalesOrders.SETFILTER(Shipment_Date,GETFILTER("Date Filter"));
          END;
      END;
      CountSalesOrders.OPEN;
      CountSalesOrders.READ;
      EXIT(CountSalesOrders.Count_Orders - SalesOrderWithBlankShipmentDateCount);
    END;

    LOCAL PROCEDURE FilterOrders@11(VAR SalesHeader@1001 : Record 36;FieldNumber@1000 : Integer);
    BEGIN
      SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::Order);
      SalesHeader.SETRANGE(Status,SalesHeader.Status::Released);
      SalesHeader.SETRANGE("Completely Shipped",FALSE);
      CASE FieldNumber OF
        FIELDNO("Ready to Ship"):
          BEGIN
            SalesHeader.SETRANGE(Ship);
            SalesHeader.SETFILTER("Shipment Date",GETFILTER("Date Filter2"));
          END;
        FIELDNO("Partially Shipped"):
          BEGIN
            SalesHeader.SETRANGE(Shipped,TRUE);
            SalesHeader.SETFILTER("Shipment Date",GETFILTER("Date Filter2"));
          END;
        FIELDNO(Delayed):
          BEGIN
            SalesHeader.SETRANGE(Ship);
            SalesHeader.SETFILTER("Shipment Date",GETFILTER("Date Filter"));
            SalesHeader.FILTERGROUP(2);
            SalesHeader.SETFILTER("Shipment Date",'<>%1',0D);
            SalesHeader.FILTERGROUP(0);
          END;
      END;
      FILTERGROUP(2);
      SalesHeader.SETFILTER("Responsibility Center",GETFILTER("Responsibility Center Filter"));
      FILTERGROUP(0);
    END;

    PROCEDURE ShowOrders@3(FieldNumber@1001 : Integer);
    VAR
      SalesHeader@1000 : Record 36;
    BEGIN
      FilterOrders(SalesHeader,FieldNumber);
      PAGE.RUN(PAGE::"Sales Order List",SalesHeader);
    END;

    PROCEDURE CountCues@1100525001(FieldNumber@1100525000 : Integer) : Integer;
    VAR
      SalesHeader@1100525007 : Record 36;
    BEGIN
      //**4PS C015772
      CASE FieldNumber OF
        FIELDNO("Sales Quotes - Open"):
          BEGIN
            SalesHeader.DPA_SetFilters('Page 9300');
            SalesHeader.SetSecurityFilterOnRespCenter;
            SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::Quote);
            SalesHeader.SETRANGE(Status,SalesHeader.Status::Open);
            EXIT(SalesHeader.COUNT);
          END;
        FIELDNO("Sales Orders - Open"):
          BEGIN
            SalesHeader.DPA_SetFilters('Page 9305');
            SalesHeader.SetSecurityFilterOnRespCenter;
            SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::Order);
            SalesHeader.SETRANGE(Status,SalesHeader.Status::Open);
            EXIT(SalesHeader.COUNT);
          END;
        FIELDNO("Ready to Ship"):
          EXIT(CountOrders(FieldNumber));
        FIELDNO(Delayed):
          EXIT(CountOrders(FieldNumber));
        FIELDNO("Sales Return Orders - Open"):
          BEGIN
            SalesHeader.DPA_SetFilters('Page 9304');
            SalesHeader.SetSecurityFilterOnRespCenter;
            SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::"Return Order");
            SalesHeader.SETRANGE(Status,SalesHeader.Status::Open);
            EXIT(SalesHeader.COUNT);
          END;
        FIELDNO("Sales Credit Memos - Open"):
          BEGIN
            SalesHeader.DPA_SetFilters('Page 9302');
            SalesHeader.SetSecurityFilterOnRespCenter;
            SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::"Credit Memo");
            SalesHeader.SETRANGE(Status,SalesHeader.Status::Open);
            EXIT(SalesHeader.COUNT);
          END;
        FIELDNO("Partially Shipped"):
          EXIT(CountOrders(FieldNumber));
      END;
    END;

    BEGIN
    END.
  }
}

