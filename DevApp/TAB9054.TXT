OBJECT Table 9054 Finance Cue
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Finance Cue;
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=ENU=Primary Key }
    { 2   ;   ;Overdue Sales Documents;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Cust. Ledger Entry" WHERE (Document Type=FILTER(Invoice|Credit Memo),
                                                                                                 Due Date=FIELD(Overdue Date Filter),
                                                                                                 Open=CONST(Yes)));
                                                   CaptionML=ENU=Overdue Sales Documents }
    { 3   ;   ;Purchase Invoices Due Today;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("Vendor Ledger Entry" WHERE (Document Type=FILTER(Invoice|Credit Memo),
                                                                                                  Due Date=FIELD(Due Date Filter),
                                                                                                  Open=CONST(Yes)));
                                                   CaptionML=ENU=Purchase Invoices Due Today }
    { 4   ;   ;POs Pending Approval;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Purchase Header" WHERE (Document Type=CONST(Order),
                                                                                              Status=FILTER(Pending Approval)));
                                                   AccessByPermission=TableData 120=R;
                                                   CaptionML=ENU=POs Pending Approval }
    { 5   ;   ;SOs Pending Approval;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE (Document Type=CONST(Order),
                                                                                           Status=FILTER(Pending Approval)));
                                                   AccessByPermission=TableData 110=R;
                                                   CaptionML=ENU=SOs Pending Approval }
    { 6   ;   ;Approved Sales Orders;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE (Document Type=CONST(Order),
                                                                                           Status=FILTER(Released|Pending Prepayment)));
                                                   AccessByPermission=TableData 110=R;
                                                   CaptionML=ENU=Approved Sales Orders }
    { 7   ;   ;Approved Purchase Orders;Integer   ;FieldClass=FlowField;
                                                   CalcFormula=Count("Purchase Header" WHERE (Document Type=CONST(Order),
                                                                                              Status=FILTER(Released|Pending Prepayment)));
                                                   AccessByPermission=TableData 120=R;
                                                   CaptionML=ENU=Approved Purchase Orders }
    { 8   ;   ;Vendors - Payment on Hold;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count(Vendor WHERE (Blocked=FILTER(Payment)));
                                                   CaptionML=ENU=Vendors - Payment on Hold }
    { 9   ;   ;Purchase Return Orders;Integer     ;FieldClass=FlowField;
                                                   CalcFormula=Count("Purchase Header" WHERE (Document Type=CONST(Return Order)));
                                                   AccessByPermission=TableData 6650=R;
                                                   CaptionML=ENU=Purchase Return Orders }
    { 10  ;   ;Sales Return Orders - All;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE (Document Type=CONST(Return Order)));
                                                   AccessByPermission=TableData 6660=R;
                                                   CaptionML=ENU=Sales Return Orders - All }
    { 11  ;   ;Customers - Blocked ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count(Customer WHERE (Blocked=FILTER(<>' ')));
                                                   CaptionML=ENU=Customers - Blocked }
    { 20  ;   ;Due Date Filter     ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Due Date Filter;
                                                   Editable=No }
    { 21  ;   ;Overdue Date Filter ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Overdue Date Filter }
    { 22  ;   ;New Incoming Documents;Integer     ;FieldClass=FlowField;
                                                   CalcFormula=Count("Incoming Document" WHERE (Status=CONST(New)));
                                                   CaptionML=ENU=New Incoming Documents }
    { 23  ;   ;Approved Incoming Documents;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("Incoming Document" WHERE (Status=CONST(Released)));
                                                   CaptionML=ENU=Approved Incoming Documents }
    { 24  ;   ;OCR Pending         ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Incoming Document" WHERE (OCR Status=FILTER(Ready|Sent|Awaiting Verification)));
                                                   CaptionML=ENU=OCR Pending }
    { 25  ;   ;OCR Completed       ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Incoming Document" WHERE (OCR Status=CONST(Success)));
                                                   CaptionML=ENU=OCR Completed }
    { 26  ;   ;Requests to Approve ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Approval Entry" WHERE (Approver ID=FIELD(User ID Filter),
                                                                                             Status=FILTER(Open)));
                                                   CaptionML=ENU=Requests to Approve }
    { 27  ;   ;Requests Sent for Approval;Integer ;FieldClass=FlowField;
                                                   CalcFormula=Count("Approval Entry" WHERE (Sender ID=FIELD(User ID Filter),
                                                                                             Status=FILTER(Open)));
                                                   CaptionML=ENU=Requests Sent for Approval }
    { 28  ;   ;User ID Filter      ;Code50        ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=User ID Filter }
    { 11012000;;Intercompany Entries;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count("Intercompany Entry");
                                                   CaptionML=ENU=Intercompany Entries;
                                                   Editable=No }
    { 11012001;;Hour Lines         ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Hour Accounting" WHERE (Employee No.=FIELD(Employee No. Filter)));
                                                   CaptionML=ENU=Hour Lines;
                                                   Editable=No }
    { 11012003;;Purchase Invoices  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Purchase Header" WHERE (Document Type=CONST(Invoice)));
                                                   CaptionML=ENU=Purchase Invoices }
    { 11012004;;Purchase Inv. to approve;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("Purch. Inv. Header" WHERE (Invoice Approved=CONST(No)));
                                                   CaptionML=ENU=Purchase Inv. to approve }
    { 11012005;;Purchase Credit Memo's;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Purchase Header" WHERE (Document Type=CONST(Credit Memo)));
                                                   CaptionML=ENU=Purchase Credit Memo's }
    { 11012006;;Pur. Credit M. to approve;Integer ;FieldClass=FlowField;
                                                   CalcFormula=Count("Purch. Cr. Memo Hdr." WHERE (Credit Memo Approved=CONST(No)));
                                                   CaptionML=ENU=Pur. Credit M. to approve }
    { 11012007;;Sales Invoices     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE (Document Type=CONST(Invoice)));
                                                   CaptionML=ENU=Sales Invoices }
    { 11012008;;Sales Credit Memo's;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Sales Header" WHERE (Document Type=CONST(Credit Memo)));
                                                   CaptionML=ENU=Sales Credit Memo's }
    { 11128277;;Employee No. Filter;Code20        ;FieldClass=FlowFilter }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CountCues@1100525000(FieldNumber@1100525000 : Integer) : Integer;
    VAR
      IntercompanyEntry@1100525001 : Record 11012058;
      HourAccounting@1100525002 : Record 11012038;
      HoursConsentLine@1100525003 : Record 11020437;
      PurchaseHeader@1100525004 : Record 38;
      PurchInvHeader@1100525005 : Record 122;
      PurchCrMemoHdr@1100525006 : Record 124;
      SalesHeader@1100525007 : Record 36;
      UserSetup@1100525008 : Record 91;
      PurchSetup@1100525009 : Record 312;
    BEGIN
      //**4PS C015772
      CASE FieldNumber OF
        FIELDNO("Intercompany Entries"):
          BEGIN
            IntercompanyEntry.DPA_SetFilters('Page 11012113');
            IntercompanyEntry.SETRANGE("Post in Company",COMPANYNAME);
            EXIT(IntercompanyEntry.COUNT);
          END;
        FIELDNO("Hour Lines"):
          BEGIN
            HourAccounting.DPA_SetFilters('Page 11012039');
            IF UserSetup.GET(USERID) THEN
              IF UserSetup."Access own Hours only" THEN
                IF UserSetup."Employee No." <> '' THEN
                  HourAccounting.SETRANGE("Employee No.",UserSetup."Employee No.")
                ELSE
                  EXIT(0);
            EXIT(HourAccounting.COUNT);
          END;
        FIELDNO("Purchase Invoices"):
          BEGIN
            PurchaseHeader.DPA_SetFilters('Page 9308');
            PurchaseHeader.SetSecurityFilterOnRespCenter;
            PurchaseHeader.SetFilterOnInputBy;
            PurchaseHeader.SETRANGE("Document Type",PurchaseHeader."Document Type"::Invoice);
            EXIT(PurchaseHeader.COUNT);
          END;
        FIELDNO("Purchase Inv. to approve"):
          BEGIN
            PurchInvHeader.DPA_SetFilters('Page 11130197');
            PurchInvHeader.SETRANGE("Invoice Approved",FALSE);
            EXIT(PurchInvHeader.COUNT);
          END;
        FIELDNO("Purchase Credit Memo's"):
          BEGIN
            PurchaseHeader.DPA_SetFilters('Page 9309');
            PurchaseHeader.SetSecurityFilterOnRespCenter;
            PurchaseHeader.SetFilterOnInputBy;
            PurchaseHeader.SETRANGE("Document Type",PurchaseHeader."Document Type"::"Credit Memo");
            EXIT(PurchaseHeader.COUNT);
          END;
        FIELDNO("Pur. Credit M. to approve"):
          BEGIN
            PurchCrMemoHdr.DPA_SetFilters('Page 11130198');
            PurchCrMemoHdr.SETRANGE("Credit Memo Approved",FALSE);
            EXIT(PurchCrMemoHdr.COUNT);
          END;
        FIELDNO("Sales Invoices"):
          BEGIN
            SalesHeader.DPA_SetFilters('Page 9301');
            SalesHeader.SetSecurityFilterOnRespCenter;
            SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::Invoice);
            EXIT(SalesHeader.COUNT);
          END;
        FIELDNO("Sales Credit Memo's"):
          BEGIN
            SalesHeader.DPA_SetFilters('Page 9302');
            SalesHeader.SetSecurityFilterOnRespCenter;
            SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::"Credit Memo");
            EXIT(SalesHeader.COUNT);
          END;
      END;
    END;

    PROCEDURE GetConsentLines@1100525001() : Integer;
    VAR
      LicensePermission@1100525000 : Record 2000000043;
      ConsentLine@1100525002 : Record 11012226;
      ConsentManagement@1100525001 : Codeunit 11012132;
    BEGIN
      //**4PS
      IF LicensePermission.GET(LicensePermission."Object Type"::Codeunit, CODEUNIT::"Consent Management") THEN
        IF LicensePermission."Execute Permission" <> LicensePermission."Execute Permission"::" " THEN
            EXIT(ConsentManagement.GetNoOfConsentLines);

      EXIT(0);
    END;

    PROCEDURE GetHourConsentLines@1100525002() : Integer;
    VAR
      LicensePermission@1100525003 : Record 2000000043;
      HoursConsentLine@1100525000 : Record 11020437;
      HoursConsentManagement@1100525001 : Codeunit 11012206;
    BEGIN
      //**4PS
      IF LicensePermission.GET(LicensePermission."Object Type"::Codeunit,CODEUNIT::"Hours Consent Management") THEN
        IF LicensePermission."Execute Permission" <> LicensePermission."Execute Permission"::" " THEN
            EXIT(HoursConsentManagement.GetNoOfHoursConsentLines);
      EXIT(0);
    END;

    BEGIN
    END.
  }
}

