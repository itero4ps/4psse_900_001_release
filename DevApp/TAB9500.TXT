OBJECT Table 9500 Email Item
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Email Item;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;GUID          ;CaptionML=ENU=ID }
    { 2   ;   ;From Name           ;Text100       ;CaptionML=ENU=From Name }
    { 3   ;   ;From Address        ;Text250       ;OnValidate=VAR
                                                                MailManagement@1000 : Codeunit 9520;
                                                              BEGIN
                                                                IF "From Address" <> '' THEN
                                                                  MailManagement.CheckValidEmailAddresses("From Address");
                                                              END;

                                                   CaptionML=ENU=From Address }
    { 4   ;   ;Send to             ;Text250       ;OnValidate=BEGIN
                                                                IF "Send to" <> '' THEN
                                                                  CorrectAndValidateEmailList("Send to");
                                                              END;

                                                   CaptionML=ENU=Send to }
    { 5   ;   ;Send CC             ;Text250       ;OnValidate=BEGIN
                                                                IF "Send CC" <> '' THEN
                                                                  CorrectAndValidateEmailList("Send CC");
                                                              END;

                                                   CaptionML=ENU=Send CC }
    { 6   ;   ;Send BCC            ;Text250       ;OnValidate=BEGIN
                                                                IF "Send BCC" <> '' THEN
                                                                  CorrectAndValidateEmailList("Send BCC");
                                                              END;

                                                   CaptionML=ENU=Send BCC }
    { 7   ;   ;Subject             ;Text250       ;CaptionML=ENU=Subject }
    { 8   ;   ;Body                ;BLOB          ;CaptionML=ENU=Body }
    { 9   ;   ;Attachment File Path;Text250       ;CaptionML=ENU=Attachment File Path }
    { 10  ;   ;Attachment Name     ;Text250       ;CaptionML=ENU=Attachment Name }
    { 11  ;   ;Plaintext Formatted ;Boolean       ;CaptionML=ENU=Plaintext Formatted }
    { 11012000;;Edit in Outlook    ;Boolean       ;CaptionML=ENU=Edit in Outlook }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      TempEmailAttachment@1100528500 : TEMPORARY Record 11229294;
      MailSent@1100528501 : Boolean;

    PROCEDURE Init@1();
    BEGIN
      ID := CREATEGUID;
    END;

    PROCEDURE Send@2(HideMailDialog@1000 : Boolean);
    VAR
      MailManagement@1001 : Codeunit 9520;
    BEGIN
      MailManagement.SetTempEmailAttachments(TempEmailAttachment); //**4PS.n
      MailManagement.SendMailOrDownload(Rec,HideMailDialog);
      MailSent := MailManagement.IsSent; //**4PS.n
    END;

    PROCEDURE SetBodyText@5(Value@1000 : Text);
    VAR
      DataStream@1001 : OutStream;
      BodyText@1002 : BigText;
    BEGIN
      CLEAR(Body);
      BodyText.ADDTEXT(Value);
      Body.CREATEOUTSTREAM(DataStream);
      BodyText.WRITE(DataStream);
    END;

    PROCEDURE GetBodyText@3() Value : Text;
    VAR
      DataStream@1000 : InStream;
      BodyText@1001 : BigText;
    BEGIN
      // Note this is intended only to get the body in memory - not from the database.
      Value := '';
      IF Body.HASVALUE THEN BEGIN
        Body.CREATEINSTREAM(DataStream);
        BodyText.READ(DataStream);
        BodyText.GETSUBTEXT(Value,1);
      END;
      EXIT(Value);
    END;

    LOCAL PROCEDURE CorrectAndValidateEmailList@6(VAR EmailAddresses@1000 : Text[250]);
    VAR
      MailManagement@1001 : Codeunit 9520;
    BEGIN
      EmailAddresses := CONVERTSTR(EmailAddresses,',',';');
      EmailAddresses := DELCHR(EmailAddresses,'<>');
      MailManagement.CheckValidEmailAddresses(EmailAddresses);
    END;

    PROCEDURE SetTempEmailAttachments@1100528510(VAR TempEmailAttachment2@1100528500 : TEMPORARY Record 11229294);
    BEGIN
      //**4PS
      IF TempEmailAttachment2.FINDSET THEN
        REPEAT
          TempEmailAttachment := TempEmailAttachment2;
          TempEmailAttachment.INSERT;
        UNTIL TempEmailAttachment2.NEXT = 0;
    END;

    PROCEDURE IsSent@1100528502() : Boolean;
    BEGIN
      //**4PS
      EXIT(MailSent);
    END;

    PROCEDURE GetNoOfAttachments@1100528501() : Integer;
    BEGIN
      //**4PS
      EXIT(TempEmailAttachment.COUNT);
    END;

    PROCEDURE OnDrillDownNoOfAttachments@1100528504();
    VAR
      EMailAttachmentList@1100528500 : Page 11012134;
    BEGIN
      //**4PS
      PAGE.RUN(PAGE::"E-Mail Attachment List", TempEmailAttachment);
    END;

    BEGIN
    END.
  }
}

