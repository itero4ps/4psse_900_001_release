OBJECT XMLport 11012277 Import ZSU
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Import ZSU file;
    Direction=Import;
    DefaultFieldsValidation=No;
    PreserveWhiteSpace=Yes;
    OnPreXMLport=VAR
                   ImportZSX@1100528700 : Codeunit 11125677;
                   lvFile@1100525500 : File;
                   TempFileName@1100525501 : Text[1024];
                 BEGIN
                   IF FileType = FileType::ZSX THEN BEGIN
                     ImportZSX.ImportZSX(gProjectNo, gPrincipal, gContractNo, currXMLport.FILENAME);
                     currXMLport.BREAK;
                   END;

                   LastUpdateTime := TIME;
                   Window.OPEN(Text000);

                   TempFileName := FileMgt.UploadFileSilent(currXMLport.FILENAME);

                   IF lvFile.OPEN(TempFileName) THEN BEGIN
                     //111 is the length of tender lines in the zsu file, because the other lines have another length
                     //the calculation is not very exactly, but for big tenders it can reasonable calculated.
                     NoOfRecords :=  ROUND(lvFile.LEN /111, 1);
                     lvFile.CLOSE;
                   END ELSE BEGIN
                     NoOfRecords := 10;
                   END;

                   ERASE(TempFileName);
                 END;

    OnPostXMLport=VAR
                    lvPrinRec@1100525501 : Record 11012005;
                    lvRAWCU@1100525500 : Codeunit 11012164;
                  BEGIN
                    lvPrinRec.GET(gProjectNo,gPrincipal);
                    lvRAWCU.CalculateAKPerc(gProjectNo,gPrincipal,lvPrinRec."Surcharge %");
                    lvPrinRec.MODIFY;
                  END;

    Format=Fixed Text;
  }
  ELEMENTS
  {
    { [{B1F83BB3-9709-4C47-9C93-EBEAE6DE5DE8}];  ;Root                ;Element ;Text     }

    { [{311939BE-7B5C-436B-8CDF-BDFE571A506F}];1 ;Integer             ;Element ;Table   ;
                                                  SourceTable=Table2000000026;
                                                  SourceTableView=SORTING(Field1);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              CLEAR(Line);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 lvTempDec@1100525505 : Decimal;
                                                                                 lvTendElem@1100525504 : Code[20];
                                                                                 lvSettlementPrice@1100525503 : Decimal;
                                                                                 lvSecondLine@1100525502 : Boolean;
                                                                                 lvSkip@1100525501 : Boolean;
                                                                                 lvCharSetConversion@1100525500 : Codeunit 11012057;
                                                                               BEGIN
                                                                                 Counter := Counter + 1;
                                                                                 UpdateProgressWindow(Counter);
                                                                                 IF COPYSTR(Line, 1, 2) = '23' THEN BEGIN
                                                                                   WITH TendElemRec DO BEGIN
                                                                                     lvSkip := FALSE;
                                                                                     INIT;
                                                                                     "Project No." := gProjectNo;
                                                                                     Principal := gPrincipal;
                                                                                     "Contract No." := gContractNo;
                                                                                     lvTendElem := DELCHR(COPYSTR(Line,3,6),'<>');
                                                                                     lvSecondLine := FALSE;
                                                                                     IF lvTendElem = '' THEN BEGIN
                                                                                       lvTendElem := gLastTenderElem;
                                                                                       lvSecondLine := TRUE;
                                                                                     END;
                                                                                     IF lvTendElem = '919990' THEN
                                                                                       lvSkip := TRUE;
                                                                                     IF NOT lvSkip THEN BEGIN
                                                                                       VALIDATE("No.", lvTendElem);
                                                                                       IF NOT lvSecondLine THEN BEGIN
                                                                                         IF INSERT(TRUE) THEN;
                                                                                         gLastTenderElem := "No.";
                                                                                         Description :=  lvCharSetConversion.Ansi2Ascii(DELCHR(COPYSTR(Line,11,50),'<>'));
                                                                                         "Unit of Measure" := COPYSTR(Line,61,4);
                                                                                         //quantity
                                                                                         IF COPYSTR(Line,65,9) <> '         ' THEN BEGIN
                                                                                           IF EVALUATE(Quantity,COPYSTR(Line,65,9)) THEN;
                                                                                           Quantity := Quantity/100;
                                                                                         END ELSE BEGIN
                                                                                           Quantity := 1;  //**4PS01
                                                                                           IF COPYSTR(lvTendElem,1,1) = '9' THEN BEGIN  //db, 07-05-15: C015555
                                                                                             IF COPYSTR(Line,74,1) = '0'THEN  //subtotal
                                                                                               Quantity := 0;
                                                                                           END;
                                                                                         END;
                                                                                         CASE COPYSTR(Line,74,1) OF
                                                                                           ' ':
                                                                                             CLEAR("Settlement Type");
                                                                                           '0':
                                                                                             "Settlement Type" := "Settlement Type"::Settling;
                                                                                           '1':
                                                                                             "Settlement Type" := "Settlement Type"::"Non Settling";
                                                                                           '2':
                                                                                             "Settlement Type" := "Settlement Type"::Reduction;
                                                                                           '3':
                                                                                             "Settlement Type" := "Settlement Type"::Open;
                                                                                           '4':
                                                                                             "Settlement Type" := "Settlement Type"::Approved;
                                                                                         END;
                                                                                         lvTempDec := 0;                                                 //**4PS01.n
                                                                                         IF COPYSTR(Line,75,8) <> '        ' THEN BEGIN                  //**4PS01.n
                                                                                           //unit price
                                                                                           IF EVALUATE("Unit Price (LCY)",COPYSTR(Line,75,8)) THEN;
                                                                                           VALIDATE("Unit Price (LCY)", "Unit Price (LCY)"/100);
                                                                                         END ELSE BEGIN                                                  //**4PS01.sn
                                                                                           IF COPYSTR(Line,91,15) <> '               ' THEN BEGIN
                                                                                             //unit price not filled this is often the case with "staartposten", use total amount
                                                                                             IF EVALUATE("Total Amount (LCY)",COPYSTR(Line,91,15)) THEN;
                                                                                             IF COPYSTR(lvTendElem,1,1) = '9' THEN BEGIN  //db, 07-05-15: C015555
                                                                                               IF COPYSTR(Line,74,1) = '0'THEN
                                                                                                 IF Quantity = 0 THEN  //disable amount for subtotal if import quantity not filled
                                                                                                   "Total Amount (LCY)" := 0;
                                                                                             END;
                                                                                             VALIDATE("Total Amount (LCY)", "Total Amount (LCY)"/100);
                                                                                           END;                                                          //**4PS01.en
                                                                                         END;
                                                                                         //Settlement price
                                                                                         IF COPYSTR(Line,83,8) <> '         ' THEN BEGIN
                                                                                           lvSettlementPrice := 0;
                                                                                           lvTempDec := 0;
                                                                                           IF EVALUATE(lvSettlementPrice,COPYSTR(Line,83,8)) THEN;
                                                                                           lvSettlementPrice := lvSettlementPrice/100;
                                                                                           IF lvSettlementPrice <> 0 THEN BEGIN
                                                                                             VALIDATE("Settlement Price (LCY)", lvSettlementPrice);
                                                                                             "Deviant Settlement Price" := TRUE;
                                                                                           END;
                                                                                         END;
                                                                                         IF MODIFY THEN;
                                                                                       END ELSE BEGIN     //next lines will be imported as text
                                                                                         WriteTextLine(gLastTenderElem);
                                                                                       END;
                                                                                     END;
                                                                                   END;
                                                                                 END;

                                                                                 currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{8B08D851-F35F-46E6-9BB3-2D09295B833B}];2 ;Line                ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=120 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100528700;;Container;
                  ContainerType=ContentArea }

      { 1100528701;1;Group  ;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 1100528702;2;Field  ;
                  CaptionML=ENU=File Type;
                  SourceExpr=FileType }

    }
  }
  CODE
  {
    VAR
      Code@1100525516 : Code[2];
      TendElemRec@1100525511 : Record 11012470;
      TendElemTextRec@1100525510 : Record 11012475;
      FileMgt@1100525515 : Codeunit 419;
      gProjectNo@1100525509 : Code[20];
      gPrincipal@1100525508 : Code[20];
      gContractNo@1100525507 : Code[10];
      gLastTenderElem@1100525506 : Text[20];
      txtFileName@1100525505 : Text[500];
      Window@1100525504 : Dialog;
      Progress@1100525503 : Integer;
      NoOfRecords@1100525502 : Integer;
      Counter@1100525501 : Integer;
      LastUpdateTime@1100525500 : Time;
      Text000@1100525514 : TextConst 'ENU=Import ZSU-file @1@@@@@@@@@@@@@@@@@@@@@@\line #2#####';
      Text001@1100525513 : TextConst 'ENU=ZSU files (*.zsu)|*.zsu|All files (*.*)|*.*';
      Text002@1100525512 : TextConst 'ENU=Select a ZSU-file';
      FileType@1100528700 : 'ZSX,ZSU';

    PROCEDURE SetSelections@1100485000(IProjNo@1100485000 : Code[20];IPrincipal@1100485001 : Code[20];IContract@1100485003 : Code[10]);
    VAR
      ProjRec@1210190000 : Record 11072003;
    BEGIN
      BEGIN
        gProjectNo := IProjNo;
        ProjRec.GET(gProjectNo);
        ProjRec.TESTFIELD(RAW);
        gPrincipal := IPrincipal;
        gContractNo := IContract;
      END;
    END;

    PROCEDURE WriteTextLine@1100485001(iLastTenderElem@1100485001 : Text[20]);
    VAR
      lvText@1100485000 : Text[60];
    BEGIN
      lvText := FORMAT(TendElemRec."No.", 6) + ' ' + COPYSTR(Line,15,50);
      TendElemTextRec.INIT;
      TendElemTextRec.SETRANGE("Project No.", gProjectNo);
      TendElemTextRec.SETRANGE(Principal, gPrincipal);
      TendElemTextRec.SETRANGE("Contract No.", gContractNo);
      TendElemTextRec.SETRANGE("Tender Paragraph", iLastTenderElem);
      IF TendElemTextRec.FINDLAST THEN
        TendElemTextRec."Text Line No." += 10000
      ELSE
        TendElemTextRec."Text Line No." := 10000;
      TendElemTextRec."Project No." := gProjectNo;
      TendElemTextRec.Principal := gPrincipal;
      TendElemTextRec."Contract No." := gContractNo;
      TendElemTextRec."Tender Paragraph" := iLastTenderElem;
      TendElemTextRec.Text := lvText;
      TendElemTextRec.INSERT(TRUE);
    END;

    PROCEDURE UpdateProgressWindow@9(NewProgress@11012000 : Integer);
    BEGIN
      IF NoOfRecords > 0 THEN BEGIN
        Window.UPDATE(2,NewProgress);
        NewProgress := ROUND(NewProgress / NoOfRecords * 100,1);
        IF (NewProgress <> Progress) OR (TIME > LastUpdateTime + 5000) THEN BEGIN
          Window.UPDATE(1,100 * NewProgress);
          LastUpdateTime := TIME;
        END;
        Progress := NewProgress;
      END;
    END;

    BEGIN
    {
      CC: Dataport 11012277

      Call like:
      ImpZSU.SetSelections("Project No.", Principal, "Contract No.");
      ImpZSU.RUN;

      4PS01 JD June, 10 2008 Call C-013594 Import of tender elements without unit price didn't work
    }
    END.
  }
}

