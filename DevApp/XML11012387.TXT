OBJECT XMLport 11012387 Export Paymaster 6.0
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Export to Paymaster;
    Direction=Export;
    DefaultFieldsValidation=No;
    OnInitXMLport=VAR
                    PaymasterRecordExtensionRec@1100528400 : Record 11012384;
                  BEGIN
                    ProjSetUp.GET;

                    IF ProjSetUp."Salary Application" <> ProjSetUp."Salary Application"::Paymaster THEN
                      ERROR(Text000 +
                            Text001,
                            ProjSetUp.TABLECAPTION);

                    sep := '#';
                    PathTxt := ProjSetUp."Pathname Files to Salary";
                    UseExtension := PaymasterRecordExtensionRec.UseExtension;

                    IF ProjSetUp."Compress Hours Paymaster" THEN BEGIN
                      IF TempPostedHourLineRec.COUNT <> 0 THEN ERROR('');
                      CompressHours := TRUE;
                    END;

                    ServerTempFileName := FileMgt.ServerTempFileName('txt');
                  END;

    OnPreXMLport=BEGIN
                   TmpTxt := 'temp.txt';
                   IF ExportPRJ = TRUE THEN BEGIN
                     FileTxt := JobNew.TABLECAPTION + '.txt';
                     currXMLport.FILENAME(PathTxt + FileTxt);
                   END;
                   IF ExportHRS = TRUE THEN BEGIN
                     FileTxt := gvDocument + '.txt';
                     currXMLport.FILENAME(PathTxt + FileTxt);
                   END;
                   FirstTime := TRUE;
                   LastDocNo := '';

                   IF FileMgt.ClientFileExists(currXMLport.FILENAME) THEN
                     IF NOT FileMgt.ExistingFileMustBeReplaced(currXMLport.FILENAME, TRUE) THEN
                       ERROR('');
                 END;

    OnPostXMLport=BEGIN
                    IF ExportHRS THEN BEGIN
                      TempPostedHourLineRec.RESET;
                      IF TempPostedHourLineRec.COUNT <> 0 THEN HandleCompressedHours;
                    END;

                    WriteSeqLine('', 'F');

                    FileMgt.DownloadToFile(
                      ServerTempFileName, PathTxt + FileTxt);
                    IF EXISTS(ServerTempFileName) THEN
                      ERASE(ServerTempFileName);
                  END;

    Format=Fixed Text;
  }
  ELEMENTS
  {
    { [{9DEAF15E-5041-40BE-AAFA-FF087113939E}];  ;Root                ;Element ;Text     }

    { [{F0D757AE-2872-4F03-AA5B-543E08A35075}];1 ;Job                 ;Element ;Table   ;
                                                  VariableName=JobNew;
                                                  SourceTable=Table11072003;
                                                  SourceTableView=SORTING(Field1)
                                                                  WHERE(Field11012016=CONST(2));
                                                  Export::OnPreXMLItem=BEGIN
                                                                         IF ExportPRJ THEN BEGIN
                                                                           CompRec.RESET;
                                                                           CompRec.SETCURRENTKEY("Navision Company", Employer);
                                                                           CompRec.SETRANGE("Navision Company", COMPANYNAME);
                                                                           CompRec.FIND('-');
                                                                         END ELSE
                                                                           JobNew.SETRANGE("No.", '');
                                                                       END;

                                                  Export::OnAfterGetRecord=VAR
                                                                             RecRef@1100528400 : RecordRef;
                                                                           BEGIN
                                                                             IF ExportPRJ THEN BEGIN
                                                                               RecRef.GETTABLE(JobNew);
                                                                               XMLportStatusDialogMgt.ShowExportStatus(RecRef);

                                                                               ExportProjects;
                                                                             END ELSE
                                                                               currXMLport.SKIP;
                                                                           END;
                                                                            }

    { [{8DBF9A3E-033D-4B8A-BF85-B634B985CBC7}];2 ;BuffTxt             ;Element ;Text    ;
                                                  Width=1000 }

    { [{08092207-4C2F-4539-A68D-495F027EAB29}];1 ;PostedHourLine      ;Element ;Table   ;
                                                  VariableName=HourNew;
                                                  SourceTable=Table11012085;
                                                  SourceTableView=SORTING(Field41,Field30);
                                                  Export::OnPreXMLItem=BEGIN
                                                                         IF ExportHRS THEN BEGIN
                                                                           IF CostTypePlant THEN
                                                                             HourNew.SETFILTER("Cost Type", '%1|%2|%3', HourNew."Cost Type"::Labor, HourNew."Cost Type"::Sundry, HourNew."Cost Type"::Plant)
                                                                           ELSE
                                                                             HourNew.SETFILTER("Cost Type", '%1|%2', HourNew."Cost Type"::Labor, HourNew."Cost Type"::Sundry)
                                                                         END ELSE BEGIN
                                                                           HourNew.SETRANGE(Year, 0);
                                                                           HourNew.SETRANGE(Week, 0);
                                                                           HourNew.SETRANGE("Employee No.", '');
                                                                           HourNew.SETRANGE("Line No.");
                                                                         END;
                                                                       END;

                                                  Export::OnAfterGetRecord=VAR
                                                                             RecRef@1100528400 : RecordRef;
                                                                           BEGIN
                                                                             IF ExportHRS THEN BEGIN
                                                                               RecRef.GETTABLE(HourNew);
                                                                               XMLportStatusDialogMgt.ShowExportStatus(RecRef);
                                                                               ProcessHours;
                                                                             END ELSE
                                                                               currXMLport.SKIP;
                                                                           END;
                                                                            }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      CaptionML=ENU=Export Hour Accounting;
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      ProjSetUp@1100528426 : Record 315;
      CompRec@1100528425 : Record 11012375;
      CostCodeRec@1100528424 : Record 11012083;
      PaymasterRecordExtensionRec@1100528423 : Record 11012384;
      WageComponentRec@1100528422 : Record 11012014;
      TempPostedHourLineRec@1100528421 : TEMPORARY Record 11012085;
      FileMgt@1100528432 : Codeunit 419;
      XMLportStatusDialogMgt@1100528431 : Codeunit 11130006;
      ServerTempFileName@1100528430 : Text[250];
      PathTxt@1100528420 : Text[250];
      FileTxt@1100528419 : Text[30];
      TmpTxt@1100528418 : Text[30];
      ElementTxt@1100528416 : Text[30];
      sep@1100528415 : Text[1];
      seqfile@1100528414 : File;
      gvDocument@1100528413 : Text[250];
      gvEmployer@1100528412 : Text[250];
      ExportPRJ@1100528411 : Boolean;
      ExportHRS@1100528410 : Boolean;
      FirstTime@1100528409 : Boolean;
      UseExtension@1100528408 : Boolean;
      CompressHours@1100528407 : Boolean;
      SalaryPeriod@1100528406 : Integer;
      CostTypePlant@1100525000 : Boolean;
      NotExportedLinesOnly@1100528405 : Boolean;
      LastDocNo@1100528404 : Code[20];
      SalPerVar@1100528403 : Code[10];
      ExportDateTime@1100528402 : DateTime;
      EmplRec@1100528401 : Record 5200;
      TradeAssRec@1100528400 : Record 11012015;
      Text000@1100528429 : TextConst 'ENU=Hours will not be exported to Paymaster';
      Text001@1100528428 : TextConst 'ENU=Go to %1 to activate this function.';
      Text002@1100528427 : TextConst 'ENU=Only < and > are allowed as parameter in function outline';
      Text003@1100528417 : TextConst 'ENU=No %1 found for %2 %3.';

    PROCEDURE SetSelection@1210190000(lvAction@1210190000 : Integer;lvDocument@1210190001 : Code[20]);
    BEGIN
      gvDocument := lvDocument;

      IF lvAction = 1 THEN ExportPRJ := TRUE;
      IF lvAction = 2 THEN ExportHRS := TRUE;
    END;

    PROCEDURE SetGlobalVars@1100528400(ExportPRJ2@1100528400 : Boolean;ExportHRS2@1100528401 : Boolean;Document2@1100528402 : Text[250];Employer2@1100528403 : Text[250];SalaryPeriod2@1100528404 : Integer;CostTypePlant2@1100525000 : Boolean;NotExportedLinesOnly2@1100528405 : Boolean;SalPerVar2@1100528406 : Code[10]);
    BEGIN
      ExportPRJ := ExportPRJ2;
      ExportHRS := ExportHRS2;
      gvDocument := Document2;
      gvEmployer := Employer2;
      SalaryPeriod := SalaryPeriod2;
      CostTypePlant := CostTypePlant2;
      NotExportedLinesOnly := NotExportedLinesOnly2;
      SalPerVar := SalPerVar2;
    END;

    PROCEDURE Alphanumeric@6(Text@11012001 : Text[250]) Res@11012000 : Boolean;
    BEGIN
      EXIT(DELCHR(Text,'=',' 0123456789')<>'');
    END;

    PROCEDURE Sign@3(Value@11012001 : Decimal) Text@11012000 : Text[1];
    BEGIN
      IF Value >= 0 THEN
        EXIT('+')
      ELSE
        EXIT('-');
    END;

    PROCEDURE FillSeqLine@4(String@11012001 : Text[80];TotLength@11012002 : Integer;WithCharacter@11012003 : Text[1];TrueBool@11012004 : Char) Text@11012000 : Text[80];
    VAR
      Length@11012005 : Integer;
    BEGIN
      Length:=STRLEN(String);
      IF TotLength>Length THEN BEGIN
        CASE TrueBool OF
          '<': String:=PADSTR('',TotLength-Length,WithCharacter)+String;
          '>': String:=String+PADSTR('',TotLength-Length,WithCharacter);
          ELSE ThrowError(Text002);
        END;
      END;

      EXIT(String);
    END;

    PROCEDURE WriteSeqLine@5(lvtext@11012000 : Text[250];lvaction@11012002 : Code[1]);
    BEGIN
      IF FirstTime = TRUE THEN BEGIN
        FirstTime := FALSE;
        WriteSeqLine('', 'S');
      END;

      CASE lvaction OF
        'S':  // open file
          BEGIN
            seqfile.TEXTMODE(TRUE);
            seqfile.WRITEMODE(TRUE);
            seqfile.CREATE(ServerTempFileName);
          END;
        'W':  // write file
          BEGIN
            seqfile.WRITE(lvtext);
          END;
        'F':  // close file
          BEGIN
            seqfile.CLOSE;
          END;
      END;
    END;

    PROCEDURE ProcessHours@12();
    BEGIN
      WITH HourNew DO BEGIN
        SETCURRENTKEY("Document No.", "Employee No.");
        IF gvDocument <> '' THEN
          SETFILTER("Document No.", gvDocument);
        IF gvEmployer <> '' THEN
          SETFILTER(Employer, gvEmployer);
        IF NotExportedLinesOnly THEN
          SETRANGE(Exported, FALSE);
        SETRANGE(External, FALSE);
        SETFILTER("Supplying Company", '%1', '');
        ExportDateTime := CURRENTDATETIME;
        IF FIND('-') THEN
          REPEAT
            TestHourLine;
            IF CompressHours THEN BEGIN
              CompressAndSaveHourRec;
            END ELSE BEGIN
              IF "Component Type" = "Component Type"::Hours THEN BEGIN
                IF Monday <> 0 THEN ExportHours(Monday,1);
                IF Tuesday <> 0 THEN ExportHours(Tuesday,2);
                IF Wednesday <> 0 THEN ExportHours(Wednesday,3);
                IF Thursday <> 0 THEN ExportHours(Thursday,4);
                IF Friday <> 0 THEN ExportHours(Friday,5);
                IF Saturday <> 0 THEN ExportHours(Saturday,6);
                IF Sunday <> 0 THEN ExportHours(Sunday,7);
              END ELSE BEGIN
                ExportHours(0,0);
              END;
            END;
            Status := Status::Export;
            Exported := TRUE;
            "Exported on" := ExportDateTime;
            MODIFY;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE TestHourLine@8();
    VAR
      lvText001@1210190003 : TextConst 'ENU=%1 %2 is to long for Paymaster. Maximum length is set to %3.';
      lvText002@1210190002 : TextConst 'ENU=%1 %2 has an alphanumeric code. Paymaster allowes only numeric codes.';
    BEGIN
      WITH HourNew DO BEGIN
        TESTFIELD("Wage Component");
        ElementTxt := DELCHR(Element, '<>=', '.');

        IF STRLEN("Project No.") > 10 THEN
          ThrowError(STRSUBSTNO(lvText001, FIELDCAPTION("Project No."), "Project No.", 10));

        IF STRLEN(ElementTxt) > 10 THEN
          ThrowError(STRSUBSTNO(lvText001, FIELDCAPTION(Element), ElementTxt, 10));

        IF STRLEN("Cost Object") > 10 THEN
          ThrowError(STRSUBSTNO(lvText001, FIELDCAPTION("Cost Object"), "Cost Object", 10));

        IF STRLEN("Wage Component") > 4 THEN
          ThrowError(STRSUBSTNO(lvText001, FIELDCAPTION("Wage Component"), "Wage Component", 4));

        IF STRLEN("Employee No.") > 10 THEN
          ThrowError(STRSUBSTNO(lvText001, FIELDCAPTION("Employee No."), "Employee No.", 10));
        { 19-03-2008 (call C-011527): alphanumeric codes allowed for project and element
        IF Alphanumeric("Project No.") THEN
          ERROR(lvText002, FIELDCAPTION("Project No."), "Project No.");
        IF Alphanumeric(ElementTxt) THEN
          ERROR(lvText002, FIELDCAPTION(Element), ElementTxt);
        }

        IF Alphanumeric("Wage Component") THEN
          ThrowError(STRSUBSTNO(lvText002, FIELDCAPTION("Wage Component"), "Wage Component"));

        IF Alphanumeric("Employee No.") THEN
          ThrowError(STRSUBSTNO(lvText002, FIELDCAPTION("Employee No."), "Employee No."));
      END;
    END;

    PROCEDURE CompressAndSaveHourRec@1100485008();
    VAR
      LastLineNo@1100485000 : Integer;
    BEGIN
      WITH TempPostedHourLineRec DO BEGIN

        RESET;
        SETRANGE(Year, HourNew.Year);
        SETRANGE(Week, HourNew.Week);
        SETRANGE("Employee No.", HourNew."Employee No.");
        IF FINDLAST THEN
          LastLineNo := "Line No."
        ELSE
          LastLineNo := 0;

        RESET;
        SETRANGE(Year, HourNew.Year);
        SETRANGE(Week, HourNew.Week);
        SETRANGE(Employer, HourNew.Employer);
        SETRANGE("Employee No.", HourNew."Employee No.");
        SETRANGE("Document No.", HourNew."Document No.");
        SETRANGE("Receiving Company", HourNew."Receiving Company");
        SETRANGE("Component Type", HourNew."Component Type");
        SETRANGE("Wage Component", HourNew."Wage Component");
        SETRANGE(Type, HourNew.Type);
        SETRANGE("Cost Code", HourNew."Cost Code");
        SETRANGE("Posting Date", HourNew."Posting Date");
        IF NOT FINDLAST THEN BEGIN
          "Line No." := LastLineNo + 10000;
          Year := HourNew.Year;
          Week := HourNew.Week;
          "Employee No." := HourNew."Employee No.";

          INIT;
          Employer := HourNew.Employer;
          "Document No." := HourNew."Document No.";
          "Receiving Company" := HourNew."Receiving Company";
          "Component Type" := HourNew."Component Type";
          "Wage Component" := HourNew."Wage Component";
          Type := HourNew.Type;
          "Cost Code" := HourNew."Cost Code";
          "Posting Date" := HourNew."Posting Date";
          INSERT;
        END;
        IF "Component Type" = "Component Type"::Hours THEN BEGIN
          Monday := Monday + HourNew.Monday;
          Tuesday := Tuesday + HourNew.Tuesday;
          Wednesday := Wednesday + HourNew.Wednesday;
          Thursday := Thursday + HourNew.Thursday;
          Friday := Friday + HourNew.Friday;
          Saturday := Saturday + HourNew.Saturday;
          Sunday := Sunday + HourNew.Sunday;
        END;
        Quantity := Quantity + HourNew.Quantity;
        "Amount (LCY)" := "Amount (LCY)" + HourNew."Amount (LCY)";
        "Precalc. Surcharge Expenses" := "Precalc. Surcharge Expenses" + HourNew."Precalc. Surcharge Expenses";
        MODIFY;
      END;
    END;

    PROCEDURE HandleCompressedHours@1100485013();
    BEGIN
      WITH TempPostedHourLineRec DO BEGIN
        IF FINDSET(FALSE, FALSE) THEN BEGIN
          REPEAT
            HourNew := TempPostedHourLineRec;
            IF "Component Type" = "Component Type"::Hours THEN BEGIN
              IF Monday <> 0 THEN ExportHours(Monday,1);
              IF Tuesday <> 0 THEN ExportHours(Tuesday,2);
              IF Wednesday <> 0 THEN ExportHours(Wednesday,3);
              IF Thursday <> 0 THEN ExportHours(Thursday,4);
              IF Friday <> 0 THEN ExportHours(Friday,5);
              IF Saturday <> 0 THEN ExportHours(Saturday,6);
              IF Sunday <> 0 THEN ExportHours(Sunday,7);
            END ELSE BEGIN
              ExportHours(0,0);
            END;
          UNTIL NEXT = 0;
        END;

      END;
    END;

    PROCEDURE ExportHours@1210190002(HoursPerDay@1210190005 : Decimal;DayNo@1210190004 : Integer);
    VAR
      lvType@1210190000 : Text[30];
      lvSign@1210190002 : Text[30];
      lvPostDate@1210190007 : Text[30];
      lvOrigDate@1210190006 : Text[30];
      lvQuanTxt@1210190003 : Text[30];
      lvQuanDec@1210190001 : Decimal;
      lvProjectNo@1100485000 : Code[20];
    BEGIN
      BuffTxt := '';

      WITH HourNew DO BEGIN
        DetermineCompany(Employer, "Employee No.");
        FillLogBook("Document No.");

        CASE "Component Type" OF
          "Component Type"::Hours:
            lvQuanDec := HoursPerDay;
          "Component Type"::Expenses:
            BEGIN
              WageComponentRec.GET("Wage Component");
              IF WageComponentRec."Export Quantity i.s.o. Amount" THEN
                lvQuanDec := Quantity
              ELSE
                lvQuanDec := "Amount (LCY)" - "Precalc. Surcharge Expenses";
            END;
          "Component Type"::Days:
            lvQuanDec := Quantity;
        END;
        IF lvQuanDec < 0 THEN
          lvSign := '-'
        ELSE
          lvSign := '+';
        lvQuanTxt := FORMAT(ABS(lvQuanDec),0,'<Precision,2:><Standard format,0>');
        IF (STRPOS(lvQuanTxt, '.') > 0) AND (STRPOS(lvQuanTxt, ',') > 0) THEN BEGIN
          IF STRPOS(lvQuanTxt, '.') < STRPOS(lvQuanTxt, ',') THEN
            lvQuanTxt := DELCHR(lvQuanTxt, '=', '.')
          ELSE
            lvQuanTxt := DELCHR(lvQuanTxt, '=', ',');
        END;
        lvPostDate := FORMAT("Posting Date",0,'<Year4><Month,2><Day,2>');
        IF DayNo = 0 THEN
          lvOrigDate := lvPostDate
        ELSE
          lvOrigDate := FORMAT(DWY2DATE(DayNo, Week, Year),0,'<Year4><Month,2><Day,2>');

        lvType := '';
        IF Type = Type::Project THEN lvType := 'P';
        IF Type = Type::Service THEN lvType := 'S';
        IF Type = Type::Indirect THEN BEGIN
          lvType := 'G';
          CostCodeRec.GET(HourNew."Cost Code");
          IF NOT CostCodeRec."No Ledger Entry" THEN
            CostCodeRec.TESTFIELD("Account No.");
        END;

        BuffTxt := BuffTxt + '059' + CompRec."Company Salary Application" + CompRec.Administration +
          FillSeqLine("Employee No.",12,'0','<') + sep + '01' + sep;
        BuffTxt := BuffTxt + '2704' + DeterminePeriod(HourNew, DayNo) + sep;
      //BuffTxt := BuffTxt + '270500' + sep;
        IF SalPerVar ='' THEN
          BuffTxt := BuffTxt + '2705' + '00' + sep
        ELSE BEGIN
          IF STRLEN(SalPerVar) = 1 THEN
            SalPerVar := '0' + SalPerVar;
          BuffTxt := BuffTxt + '2705' + SalPerVar + sep
        END;
        BuffTxt := BuffTxt + '2634' + lvPostDate + sep;
        BuffTxt := BuffTxt + '260700000000' + sep;
        BuffTxt := BuffTxt + '2621' + FillSeqLine("Wage Component",4,'0','<') + sep;
        BuffTxt := BuffTxt + '2622' + lvSign + FillSeqLine(lvQuanTxt,10,'0','<') + sep;
        BuffTxt := BuffTxt + '2635' + lvOrigDate + sep;
        BuffTxt := BuffTxt + '2631' + lvType + sep;
        BuffTxt := BuffTxt + '2636' + '4PS' + sep;
        BuffTxt := BuffTxt + '2633' + DetermineDiffCompAndAdmin("Receiving Company") + sep;

        CASE Type OF
          Type::Indirect: BEGIN
            BuffTxt := BuffTxt + '2637' + CostCodeRec."Account No." + sep ;
            IF NOT CostCodeRec."No Ledger Entry" THEN BEGIN
              CASE CostCodeRec."Source Global Dim 1 Code" OF
                CostCodeRec."Source Global Dim 1 Code"::Fixed:

                    BuffTxt := BuffTxt + '2638' + CostCodeRec."Global Dimension 1 Code" + sep;
                CostCodeRec."Source Global Dim 1 Code"::"Trade Association":
                  BEGIN
                    IF EmplRec.GET ( "Employee No.") THEN
                      IF EmplRec."Trade Association"<>'' THEN
                        IF TradeAssRec.GET(EmplRec."Trade Association") THEN
                          IF TradeAssRec."Source Department Code"=  TradeAssRec."Source Department Code":: "Trade Association" THEN
                            BuffTxt := BuffTxt + '2638' + TradeAssRec."Department Code" + sep
                          ELSE
                            BuffTxt := BuffTxt + '2638' + EmplRec."Global Dimension 1 Code" + sep;
                  END;
                CostCodeRec."Source Global Dim 1 Code"::Employee:
                  BEGIN
                    IF EmplRec.GET ( "Employee No.") THEN
                      BuffTxt := BuffTxt + '2638' + EmplRec."Global Dimension 1 Code" + sep;
                  END;
              END;
            END;
          END;
          Type::Project: BEGIN
            BuffTxt := BuffTxt + '2637' + "Project No." + sep;
            IF ProjSetUp."Length Element (Extern)" > 0 THEN BEGIN
              BuffTxt := BuffTxt + '2638' + ElementTxt + sep;
              BuffTxt := BuffTxt + '2639' + "Cost Object" + sep;
            END ELSE
              BuffTxt := BuffTxt + '2638' + "Cost Object" + sep;
          END;
          Type::Service: BEGIN
            BuffTxt := BuffTxt + '2637' + "Service Order No." + sep;
            BuffTxt := BuffTxt + '2638' + "Cost Object" + sep;
            IF (HourNew."Additional Cost (Service)") THEN
              BuffTxt := BuffTxt + '2639' + '1' + sep
            ELSE
              BuffTxt := BuffTxt + '2639' + '0' + sep
          END;
          ELSE
            BuffTxt := BuffTxt + '2637' + "Project No." + sep;
        END;

        BuffTxt := BuffTxt + '3233' + sep;
      END;

      WriteSeqLine(BuffTxt, 'W');

      IF UseExtension THEN BEGIN
        IF HourNew.Type = HourNew.Type::Indirect THEN
          lvProjectNo := CostCodeRec."Account No."
        ELSE
          lvProjectNo := HourNew."Project No.";
        WriteCompRelRec(CompRec."Company Salary Application",
                        CompRec.Administration,
                        HourNew."Employee No.",
                        lvType,
                        lvProjectNo,
                        ElementTxt,
                        HourNew."Cost Object",
                        HourNew."Wage Component",
                        DeterminePeriodEndingDate(HourNew),
                        COMPANYNAME,
                        HourNew."Receiving Company");
      END;
    END;

    PROCEDURE ExportProjects@1210190001();
    VAR
      lvType1@1210190002 : Text[30];
      lvType2@1210190001 : Text[30];
      lvPostCode@1210190000 : Text[30];
    BEGIN
      BuffTxt := '';

      WITH JobNew DO BEGIN
        IF "Location Type 1" = "Location Type 1"::Inside THEN
          lvType1 := 'BI'
        ELSE
          lvType1 := 'BU';
        IF "Location Type 2" = "Location Type 2"::Home THEN
          lvType2 := '0'
        ELSE
          lvType2 := '1';
        lvPostCode := "Post Code";
        lvPostCode := DELCHR(lvPostCode, '<>=', ' ABCDEFGHIJKLMNOPQRSTUVWXYZ');

        BuffTxt := BuffTxt + '056' + CompRec."Company Salary Application" + CompRec.Administration + sep;
        BuffTxt := BuffTxt + '3603' + COPYSTR("No.",1,10) + sep;
        BuffTxt := BuffTxt + '3604' + COPYSTR("No.",11,10) + sep;
        BuffTxt := BuffTxt + '3605' + sep;
        BuffTxt := BuffTxt + '3611' + Address + sep;
        BuffTxt := BuffTxt + '3612' + lvPostCode + sep;
        BuffTxt := BuffTxt + '3613' + City + sep;
        BuffTxt := BuffTxt + '3614' + "SFB Code" + sep;
        BuffTxt := BuffTxt + '3615' + lvType1 + sep;
        BuffTxt := BuffTxt + '3616' + "Affiliation No." + sep;
        BuffTxt := BuffTxt + '3617' + FORMAT("Starting Date",0,'<Year4><Month,2><Day,2>') + sep;
        BuffTxt := BuffTxt + '3618' + FORMAT("Ending Date",0,'<Year4><Month,2><Day,2>') + sep;
        BuffTxt := BuffTxt + '3619' + lvType2 + sep;
      END;

      WriteSeqLine(BuffTxt, 'W');
    END;

    PROCEDURE FillLogBook@33(CurrDocNo@1100485000 : Code[20]);
    VAR
      ExportRec@1210190000 : Record 11012041;
      LineNo@1210190001 : Integer;
    BEGIN
      IF CurrDocNo = LastDocNo THEN EXIT;

      IF ExportRec.FIND('+') THEN
        LineNo := ExportRec."Line No." + 1
      ELSE
        LineNo := 1;

      ExportRec.INIT;
      ExportRec."Line No." := LineNo;
      ExportRec."Document No." := CurrDocNo;
      ExportRec."User ID" := USERID;
      ExportRec.Date := TODAY;
      ExportRec.Time := TIME;
      ExportRec.INSERT;

      LastDocNo := ExportRec."Document No.";
    END;

    PROCEDURE DetermineCompany@1210190004(lvEmployer@1210190000 : Code[20];lvEmployee@1210190001 : Code[20]);
    VAR
      lvText000@1210190002 : TextConst 'ENU=Company %1 with %2 %3 not present for hour accounting export in table %4.';
      EmplRec@1210190003 : Record 5200;
    BEGIN
      IF lvEmployer = '' THEN BEGIN
        EmplRec.GET(lvEmployee);
        lvEmployer := EmplRec.Employer;
      END;

      CompRec.RESET;
      CompRec.SETCURRENTKEY("Navision Company", Employer);
      CompRec.SETRANGE("Navision Company", COMPANYNAME);
      CompRec.SETRANGE(Employer, lvEmployer);
      IF NOT CompRec.FIND('-') THEN BEGIN
        ThrowError(
          STRSUBSTNO(
            lvText000,COMPANYNAME, CompRec.FIELDCAPTION(Employer), lvEmployer, CompRec.TABLECAPTION));
      END;
    END;

    PROCEDURE DeterminePeriod@1100485000(HourNew@1100485004 : Record 11012085;DayNo@1100485000 : Integer) lvPeriod : Text[30];
    VAR
      EmplRec@1100485001 : Record 5200;
      EmployerRec@1100485002 : Record 11012013;
      lvDate@1100485003 : Date;
    BEGIN
      IF SalaryPeriod > 0 THEN
        EXIT(FORMAT(SalaryPeriod));

      WITH HourNew DO BEGIN
        EmplRec.GET("Employee No.");
        IF NOT EmployerRec.GET(EmplRec.Employer) THEN
          EmployerRec."Period Type" := ProjSetUp."Hour Document No.";

        CASE EmployerRec."Period Type" OF
          EmployerRec."Period Type"::Week:
            lvPeriod := FORMAT(Week);
          EmployerRec."Period Type"::"4-Weeks":
            lvPeriod := FORMAT(((Week-1) DIV 4) + 1);
          EmployerRec."Period Type"::Month:
            BEGIN
              IF DayNo = 0 THEN
                lvDate := "Posting Date"
              ELSE
                lvDate := DWY2DATE(DayNo, Week, Year);
              lvPeriod := FORMAT(DATE2DMY(lvDate,2));
            END;
        END;
      END;

      EXIT(lvPeriod);
    END;

    PROCEDURE DeterminePeriodEndingDate@1000000001(HourNew@1100485004 : Record 11012085) rPeriodEndDate : Date;
    VAR
      EmplRec@1100485001 : Record 5200;
      EmployerRec@1100485002 : Record 11012013;
      DayNo@1000000000 : Integer;
      WeekNo@1000000001 : Integer;
      YearNo@1000000002 : Integer;
    BEGIN
      WITH HourNew DO BEGIN
        EmplRec.GET("Employee No.");
        IF NOT EmployerRec.GET(EmplRec.Employer) THEN
          EmployerRec."Period Type" := ProjSetUp."Hour Document No.";

        CASE EmployerRec."Period Type" OF
          EmployerRec."Period Type"::Week:
            rPeriodEndDate := CALCDATE('<CW>',"Posting Date")-2;
          EmployerRec."Period Type"::"4-Weeks":
            BEGIN
              DayNo := DATE2DWY("Posting Date",1);
              WeekNo := DATE2DWY("Posting Date",2);
              YearNo := DATE2DWY("Posting Date",3);
              rPeriodEndDate := DWY2DATE(7,4 * (((WeekNo-1) DIV 4) + 1),YearNo)-2;
            END;
          EmployerRec."Period Type"::Month:
            rPeriodEndDate := CALCDATE('<CM>',"Posting Date");
        END;
      END;
    END;

    PROCEDURE WriteCompRelRec@1100485001(PaymCompany@1100485000 : Code[10];PaymAdministration@1100485001 : Code[10];Employee@1100485002 : Code[20];RecType@1100485003 : Code[1];ProjectNo@1100485004 : Code[20];ElementCode@1100485005 : Text[30];CostObjectCode@1100485006 : Code[20];WageCode@1100485007 : Code[10];HourDate@1100485008 : Date;OrgCompany@1100485009 : Text[30];RecCompany@1100485010 : Text[30]);
    BEGIN
      PaymasterRecordExtensionRec.Company := PaymCompany;
      PaymasterRecordExtensionRec.Administration := PaymAdministration;
      PaymasterRecordExtensionRec.Employee := Employee;
      PaymasterRecordExtensionRec."Record Type" := RecType;
      PaymasterRecordExtensionRec."Project/ GL AccountNo" := ProjectNo;
      PaymasterRecordExtensionRec."Element Code" := ElementCode;
      PaymasterRecordExtensionRec."Cost Object Code" := CostObjectCode;
      PaymasterRecordExtensionRec."Wage Code" := WageCode;
      PaymasterRecordExtensionRec."Hour Date" := HourDate;
      PaymasterRecordExtensionRec."Org Company" := OrgCompany;
      PaymasterRecordExtensionRec."Receiving Company" := RecCompany;
      IF PaymasterRecordExtensionRec.INSERT THEN;
    END;

    LOCAL PROCEDURE DetermineDiffCompAndAdmin@1210190008(ReceivingCompany@1210190000 : Text[30]) : Text[30];
    VAR
      PaymasterCompany@1210190001 : Record 11012375;
    BEGIN
      IF ReceivingCompany = '' THEN
        EXIT('');

      PaymasterCompany.SETRANGE("Navision Company", ReceivingCompany);
      IF NOT PaymasterCompany.FINDFIRST THEN
        ThrowError(STRSUBSTNO(
          Text003,
          PaymasterCompany.TABLECAPTION,
          PaymasterCompany.FIELDCAPTION("Navision Company"),
          ReceivingCompany))
      ELSE
        EXIT(PaymasterCompany."Company Salary Application" + PaymasterCompany.Administration);
    END;

    LOCAL PROCEDURE ThrowError@1100528403(ErrorMessage@1100528400 : Text[1024]);
    BEGIN
      // When statement "Error(ErrorMessage);" is executed during export,
      // only error message "Could not process node Root." is given.
      MESSAGE(ErrorMessage);
      ERROR('');
    END;

    BEGIN
    END.
  }
}

