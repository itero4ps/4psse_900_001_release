OBJECT XMLport 11128770 Import NEN2767 Elements
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS9.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Import NEN2767 Elements;
    PreserveWhiteSpace=Yes;
    Format=Variable Text;
    FieldSeparator=[;];
  }
  ELEMENTS
  {
    { [{E6637C20-E299-4930-8033-0ECBF1517FCE}];  ;Root                ;Element ;Text     }

    { [{CD317512-A1BD-4008-995C-0BA3EE1F88AC}];1 ;NEN2767Element      ;Element ;Table   ;
                                                  SourceTable=Table11128770;
                                                  AutoSave=No;
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              ElementNo := '';
                                                                              DescriptionText := '';
                                                                              ChapterDescription := '';
                                                                              ParagraphDescription := '';
                                                                              ElementDescription := '';
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 NEN2767Element@1100528602 : Record 11128770;
                                                                                 FormatManagement@1100525000 : Codeunit 11012022;
                                                                                 NEN2767Management@1100528600 : Codeunit 11012863;
                                                                                 ElementCode@1100525001 : Code[20];
                                                                               BEGIN
                                                                                 CLEAR("NEN2767 Element");
                                                                                 WITH "NEN2767 Element" DO BEGIN
                                                                                   BuildingPartCode := NEN2767Management.NormalizeBuildingPartCode(BuildingPartCode);
                                                                                   ElementCode := ElementNo;
                                                                                   FormatManagement.FormatElem(ElementCode, 21, '');
                                                                                   FormatManagement.DetermineLevel(ElementCode, Chapter, Paragraph, Level);
                                                                                   "Building Part" := COPYSTR(BuildingPartCode, 1, MAXSTRLEN("Building Part"));
                                                                                   DetermineDescriptions(DescriptionText, ChapterDescription, ParagraphDescription, ElementDescription);

                                                                                   IF NOT NEN2767Element.GET(Chapter) AND (Level > 1) THEN BEGIN
                                                                                     NEN2767Element.VALIDATE("No.", Chapter);
                                                                                     NEN2767Element.VALIDATE(Description, COPYSTR(ChapterDescription, 1, MAXSTRLEN(Description)));
                                                                                     NEN2767Element.INSERT(TRUE);
                                                                                   END;
                                                                                   IF NOT NEN2767Element.GET(Paragraph) AND (Level > 1) THEN BEGIN
                                                                                     NEN2767Element.VALIDATE("No.", Paragraph);
                                                                                     NEN2767Element.VALIDATE(Description, COPYSTR(ParagraphDescription, 1, MAXSTRLEN(Description)));
                                                                                     NEN2767Element.INSERT(TRUE);
                                                                                   END;

                                                                                   IF NOT GET(ElementNo) THEN BEGIN
                                                                                     VALIDATE("No.", ElementNo);
                                                                                     CASE Level OF
                                                                                       0,1:
                                                                                         VALIDATE(Description, COPYSTR(ChapterDescription, 1, MAXSTRLEN(Description)));
                                                                                       2:
                                                                                         VALIDATE(Description, COPYSTR(ParagraphDescription, 1, MAXSTRLEN(Description)));
                                                                                       3:
                                                                                         VALIDATE(Description, COPYSTR(ParagraphDescription + ';' + ElementDescription, 1, MAXSTRLEN(Description)));
                                                                                     END;
                                                                                     INSERT(TRUE);
                                                                                   END;
                                                                                 END;
                                                                               END;

                                                  Export::OnAfterGetRecord=BEGIN
                                                                             WITH "NEN2767 Element" DO BEGIN
                                                                               BuildingPartCode := "Building Part";
                                                                               ElementNo := "No.";
                                                                               CASE Level OF
                                                                                 0,1:
                                                                                   DescriptionText := STRSUBSTNO('%1;%2;%3', Description, '', '');
                                                                                 2:
                                                                                   DescriptionText := STRSUBSTNO('%1;%2;%3', '', Description, '');
                                                                                 3:
                                                                                   DescriptionText := STRSUBSTNO('%1;%2', '', Description);
                                                                               END;
                                                                             END;
                                                                           END;
                                                                            }

    { [{0FD97D95-9CF4-48FD-9882-AF1F774D2053}];2 ;BuildingPartCode    ;Element ;Text     }

    { [{42421FE0-F0BE-4394-A8D4-FB0A1F01BE98}];2 ;ElementNo           ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{53F42C61-9064-471A-9A1B-2E8292BCF66D}];2 ;DescriptionText     ;Element ;Text    ;
                                                  MinOccurs=Zero }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      ChapterDescription@1100525000 : Text;
      ParagraphDescription@1100525001 : Text;
      ElementDescription@1100525002 : Text;

    LOCAL PROCEDURE DetermineDescriptions@1100525000(IDescription@1100525000 : Text;VAR OChapterDescription@1100525001 : Text;VAR OParagraphDescription@1100525002 : Text;VAR OElementDescription@1100525003 : Text);
    VAR
      RegExpManagement@1100525004 : Codeunit 11012262;
    BEGIN
      OChapterDescription := '';
      OParagraphDescription := '';
      OElementDescription := '';
      IF RegExpManagement.RegExpFindMatches(IDescription, '^([^;]*);(.*);([^;]*)$') THEN BEGIN
        OChapterDescription := RegExpManagement.GetSubMatchFirstMatch(0);
        OParagraphDescription := RegExpManagement.GetSubMatchFirstMatch(1);
        OElementDescription := RegExpManagement.GetSubMatchFirstMatch(2);
      END;
    END;

    BEGIN
    END.
  }
}

